! ============================= !
!  Nominal morphological lexica !
! ============================= !




! ========== !
! Adjectives !
! ========== !

! Here are the adjs:

!   1394 Adj
!      3 End2
!      3 End1

! They probably need their own file adj-kom-morph.txt. 
! Or, they could be treated on a par with nouns, with a small comparative twist
! on their own.
! While awaiting the decision, I simply terminate them here.

LEXICON Adj
+A:  # ;



! ===== !
! Nouns !
! ===== !


! The present N-lex lexicon looks like this:

!   8877 Noun1
!     42 NounPL
!     42 ACRON
!     36 NounPi
!     10 NounMort
!      8 NounSG
!      6 N          ! These are probably errouneous for Noun1
!      4 NounPOSS
!      4 End1       ! These may have the wrong POS (should not be N)
!      3 A
!      2 NounSUBJ
!      2 NounLA
!      1 Verb1
!      1 Noun2


LEXICON Noun1 ! Basic nouns.
+N+Sg: CASEPOSSLEX ;
+N+Pl:%>ъяс CASEPOSSLEX ;

LEXICON Noun2 ! перъянін
 Noun1 ; ! Since I don't know what Noun2 is, I send it to Noun1.
! Note that a noun like песласянін is Noun1 !

LEXICON NounLA ! ӧтила
Noun1 ; ! to be changed.

LEXICON NounSG ! морт
Noun1 ; ! to be changed.

LEXICON NounPi ! пи These have a unique  pl marker ян.
+N+Sg: CASEPOSSLEX ;        
+N+Pl:%>ян CASEPOSSLEX ;    


LEXICON NounMort ! арта_ These all end in uncerscore, and are suspicious.
# ;

LEXICON ACRON ! А*С*СР ! АТП Note the inconcistencies in the star use.
# ;

LEXICON NounPL ! бедьторъяс
# ;
! Very many plural nouns do not have this tag. We could change яс-final
! nouns from Noun1 to NounPL, I guess.

LEXICON NounPOSS ! чой
# ;

LEXICON NounSUBJ ! дыш
# ;

LEXICON NounOBJ
# ;

LEXICON NounDET
# ;





! =================== !
! Inflectional lexica !
! =================== !


LEXICON CASEPOSSLEX
 POSSFIRST ;         ! Cases  with  Px  first
 CASEFIRST ;         ! Cases  with  Cx  first
 ACCLEX ;            ! The  last  lexica  are
 APRLEX ;            ! cases  with  idiosyncratic
 CARLEX ;            ! Px--Cx  order. APRLEX is for the parallel forms
 ELALEX ;
 LOCLEX ;            ! Ill, Ine and Ins have the same Px behaviour.
 +A: DERIVEDADJ ;    ! Deriving prenominal adjectives, республикаса



LEXICON DERIVEDADJ
+Ine:%>са PredMonLex ;     
+Car:%>тӧм PredMonLex ;
+HabObj:%>а PredMonLex ;

LEXICON PredMonLex
 # ;      ! Singular predicative
 :ӧсь # ; ! Plural predicative.
                     
 
! Section for Px first
! --------------------
                    
LEXICON POSSFIRST
SGPOSSFIRST ;        ! Splitting  in  Sg  and  Pl  Px
PLPOSSFIRST ;
CASELAST  ;          ! Allowing  for  skipping  the  Px

LEXICON SGPOSSFIRST
+PxSg1:%>ӧй CASELAST;
+PxSg2:%>ыд CASELAST;
+PxSg3:%>ыз CASELAST;

LEXICON PLPOSSFIRST
+PxPl1:%>ным CASELAST;
+PxPl2:%>ныд CASELAST;
+PxPl3:%>ныз CASELAST;

LEXICON CASELAST
+Nom: K ; 
+Gen:%>лӧн K ; 
+Abl:%>лысь K ;
+Dat:%>лы K ;
+Com:%>кӧд K ;               !svoed
+Cns:%>ла K ; ! consecutive  !dostig




! Section for idiosyncratic cases
! -------------------------------

! Accusative differs from all the others, it is thus listed here.

LEXICON ACCLEX
+Acc:%>ӧс K ;
!+PxSg1+Acc:%>ӧс  K ; ! Error here! Check.
+PxSg2+Acc:%>тӧ  K ;
+PxSg3+Acc:%>сӧ  K ;
+PxPl1+Acc:%>нымӧс  K ;
+PxPl2+Acc:%>нытӧ  K ;
+PxPl3+Acc:%>нысӧ  K ;


! These are idiosyncratic parallel forms for Apr
! The ordinary forms are found in the CASEFIRST 
! lexicon below.

LEXICON APRLEX  
+PxSg1+Apr:%>ӧйлань K ;
+PxPl1+Apr:%>нымлань K ;
+PxPl2+Apr:%>ныдлань K ;
+PxPl3+Apr:%>ныслань K ;

LEXICON  ELALEX
+Ela:%>ысь K ;
+Ela:%>сь  SGPOSSLAST ;
+Ela:%>ысь  PLPOSSLAST  ;


! For caritive, Sg is idiosyncratic, but Pl is like for
! the other POSSLAST cases.

LEXICON CARLEX
+Car:%>тӧг  K ;          ! caritive     !lysh
+Car+PxSg1:%>ӧйтӧг K ;
+Car+PxSg2:%>тӧгыд K ;
+Car+PxSg3:%>тӧгыс K ;
+Car:%>тӧг PLPOSSLAST ; 


! This is the lexicon for illative, inessive and instrumental
LEXICON LOCLEX
+Ill:%>ӧ K ;                  !vstup      ! Absolutive declension
+Ine:%>ын K ;                 !mestn
+Ins:%>ӧн K ;  ! instrumental !tvor
+Ill: LOCPX ;                            ! Possessive declension
+Ine: LOCPX ;
+Ins:%>н LOCPX ;

LEXICON LOCPX
 +PxSg1:%>ам K ;
 +PxSg2:%>ад K ;
 +PxSg3:%>ас K ;
  :%>а PLPOSSLAST  ;




! The section for case first
! --------------------------

LEXICON CASEFIRST
+Apr:%>лань POSSLAST;               !pribl            ! Approximative
+Egr:%>сянь POSSLAST;               !otdal            1 Egressive
+Tra:%>ті POSSLAST;                 !pereh -ti -ӧd    ! Translative
+Prl:%>ӧд POSSLAST;                 !pereh -ti -ӧd    ! Prolative
+Ter:%>ӧдз POSSLAST;                !pred             ! Terminative

LEXICON POSSLAST
K ;                                 ! Absolutive declension
SGPOSSLAST ;                        ! Possessive declension
PLPOSSLAST ;

LEXICON SGPOSSLAST
+PxSg1:%>ым K ;
+PxSg2:%>ыд K ;
+PxSg3:%>ыс K ;

LEXICON PLPOSSLAST
+PxPl1:%>ным K ;
+PxPl2:%>ныд K ;
+PxPl3:%>ныс K ;
