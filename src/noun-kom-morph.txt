

! ============================= !
!  Noun morphological lexica !
! ============================= !
! ===== !
! Adjs  !
! ===== !


LEXICON Adj-IS_PTC-TOM ! 2011-11-15 participle = derivation type; 
+V+Der/тӧм: Adj ;

LEXICON Adj-IS_PTC-YSJ
+V+Der/ысь: Adj ;

LEXICON Adj-IS_PTC-ANA !
+V+Der/ана: Adj ;

LEXICON Adj-IS_TOM
+N+Der/тӧм: Adj ;

LEXICON Adj-IS_SUPERL !
+Superl: Adj ;

LEXICON Adj-SUPERL !
+Superl: Adj ;

LEXICON A-PRED ! ӧбиднӧ
+A+Pred: # ; !


LEXICON Adj
+A: AdjCompar ;
+A+Der/лун:%>лун Noun1 ; !Derivational suffix for A-N
+A+Der/а:%>а ADVTAG ; ! Derivation for adjective-to-adverb

! ===== !
! Nouns !
! ===== !

LEXICON Vocative
+N+Voc: # ;

LEXICON Prop ! Basic nouns.
+N+Prop+Sg: CASEPOSSLEX ;

LEXICON Prop-R-RY
+Prop: Noun1-R-RY ; ! This will bring about problems with ordering 2012-02-07

LEXICON Prop-Derivable
+N+Prop+Sur+Mal+Sg: CASEPOSSLEX ; ! Владимиров
а+N+Prop+Sur+Fem+Sg:%^FEM%>а CASEPOSSLEX ; ! Владимирова

! Prop-Derivable end  in: ев, ов, ёв, ий, ин
! Prop-Derivable for fem: ева, ова, ёва, ая, ина


!This might need some working on 2012-01-23
LEXICON Prop-Mal-Derivable !Patronyms
ович+N+Prop+Patr+Mal+Sg:%>ович CASEPOSSLEX ; ! Владимирович
овна+N+Prop+Patr+Fem+Sg:%>овна CASEPOSSLEX ; ! Владимировна
 Prop-Mal ; ! Владимир


LEXICON Prop-Plc ! Inanimate proper nouns. Place names
+N+Prop+Plc+Sg: CASEPOSSLEX ;

LEXICON Prop-Plc-Rus ! Inanimate proper nouns. Place names
+N+Prop+Plc+Src/F+Sg: CASEPOSSLEX ;

LEXICON Prop-Org ! Inanimate proper nouns. Name of organization
+N+Prop+Org+Sg: CASEPOSSLEX ;

LEXICON Prop-Sur-Mal ! Basic nouns.
+N+Prop+Sur+Mal+Sg: CASEPOSSLEX ;

LEXICON Prop-Mal ! Male given names
+N+Prop+Mal+Sg: CASEPOSSLEX ;

LEXICON Prop-Sur-Fem ! Basic nouns.
+N+Prop+Sur+Fem+Sg: CASEPOSSLEX ;

LEXICON Prop-Fem ! Female given names
+N+Prop+Fem+Sg: CASEPOSSLEX ;

LEXICON Noun1-IS_PTC-OM ! This is to provide deverbal nouns with +Der/ӧм tags
+V+Der/ӧм: Noun1 ;

LEXICON Noun1-IS_PTC-AN !
+V+Der/ан: Noun1 ;

LEXICON Noun1-IS_PTC-YSJ !
+V+Der/ысь: Noun1 ;

LEXICON N-DOR ! мӧдладор The Russian is an adverbial phrase
+Der/дор: Noun1 ;

LEXICON N-VYV ! тулысвыв The Russian is an adverbial phrase
+Der/выв: Noun1 ;

LEXICON Noun1-F !
+Src/F: Noun1 ;

LEXICON Noun1-R-RY !
+Use/Sub:ь Noun1 ;  ! This deals with январь
           Noun1 ; ! This is the new proper spelling январ

LEXICON Noun1-SYT-STY !
+Use/Sub:ть Noun1 ;  ! This deals with гӧсть
:ьт         Noun1 ; ! This is the new proper spelling гӧсьт

LEXICON Noun1 ! Basic nouns.
+N+Cmp: R ;
+N+Sg: CASEPOSSLEX ;
+N+Pl:%>ъяс CASEPOSSLEX ;

LEXICON Noun1-JM ! понйыс, понмыс 
+N+Cmp:м R ;
+N+Sg:м CASEPOSSLEX ;
+N+Cmp:й R ;
+N+Sg:й CASEPOSSLEX ;
+N+Pl:%>ъяс CASEPOSSLEX ;

LEXICON NounLA ! ӧтила
Noun1 ; ! Difference betw Noun1 and NounLA still to be documented.

LEXICON NounMort
Noun1 ; ! Difference betw Noun1 and NounMort still to be documented.

LEXICON NounSG ! м орт
+N+Sg: CASEPOSSLEX ;

LEXICON NounPi ! пи These have a unique  pl marker ян.
+N+Sg: CASEPOSSLEX ;        
+N+Pl:%>ян CASEPOSSLEX ;    


LEXICON Noun1-IS_ACRON ! А*С*СР ! АТП Note the inconcistencies in the star use.
+N+ACR: ACRONDELIMITER ;

LEXICON NounPL ! бедьторъяс
+N+Pl: CASEPOSSLEX ; ! here, %>ъяс is put as part of the stem
! Very many plural nouns do not have this tag. We could change яс-final
! nouns from Noun1 to NounPL, I guess.

LEXICON NounPOSS ! чой niece, nephew
Noun1 ; ! Declension to be determined.

LEXICON FIRST
+Cmp: # ;

LEXICON ACRONDELIMITER
    CASEPOSSLEX ;
:%: CASEPOSSLEX ;
:%- CASEPOSSLEX ;
:%' CASEPOSSLEX ;


! =================== !
! Inflectional lexica !
! =================== !

!
!All nouns follow one contlex "Noun1"
!to begin with here is simply a list of all variant
!with no more variants beyond:
! Numbers
!Case + Poss or Poss+Case
!

LEXICON CASEPOSSLEX
 DERIVEDADJ ;    ! Deriving prenominal adjectives, республикаса
CASEPOSSLEX-afterDerivation ;

LEXICON CASEPOSSLEX-afterDerivation
+Nom: K ;
!+ZeroAcc: K ;
+Gen:%>лӧн K ;
+Abl:%>лысь K ;
+Dat:%>лы K ;
+Acc:%>ӧс K ;
+Instr:%>ӧн K ;
+Com:%>кӧд K ;
+Car:%>тӧг K ;
+Cns:%>ла K ;
+Comp:%>ся K ;
+Ine:%>ын SPAT-COMPARATIVE ;
+Ela:%>ысь SPAT-COMPARATIVE ;
+Ill:%>ӧ IllCompar ;
+Apr:%>лань SPAT-COMPARATIVE ;
+Egr:%>сянь SPAT-COMPARATIVE ;
+Prl:%>ӧд K ;
+Tra:%>ті K ;
+Ter:%>ӧдз K ;
+Apr+Ine:%>ланьын SPAT-COMPARATIVE ;  ! New cases in last grammar
+Apr+Ela:%>ланьысь SPAT-COMPARATIVE ;  !
+Apr+Ill:%>ланьӧ IllCompar ; !
+Apr+Egr:%>ланьсянь SPAT-COMPARATIVE ; !
+Apr+Prl:%>ланьӧд K ; !
+Apr+Tra:%>ланьті K ; !
+Apr+Ter:%>ланьӧдз K ; ! Tähän asti

!1SG
+PxSg1+Nom:%>ӧй K ;
+PxSg1+Gen:%>ӧйлӧн K ;
+PxSg1+Abl:%>ӧйлысь K ;
+PxSg1+Dat:%>ӧйлы K ;
+PxSg1+Acc:%>ӧс K ;

+Instr+PxSg1:%>нам K ;

+PxSg1+Com:%>ӧйкӧд K ;
+PxSg1+Car:%>ӧйтӧг K ;
+PxSg1+Cns:%>ӧйла K ;
+PxSg1+Comp:%>ӧйся K ;

+Ine+PxSg1:%>ам SPAT-COMPARATIVE ;
+Ela+PxSg1:%>сьым SPAT-COMPARATIVE ;
+Ill+PxSg1:%>ам IllCompar ; !

+PxSg1+Apr:%>ӧйлань SPAT-COMPARATIVE ;
+PxSg1+Egr:%>ӧйсянь SPAT-COMPARATIVE ;

+Prl+PxSg1:%>ӧдым K ;
+Tra+PxSg1:%>тіым K ;
+Ter+PxSg1:%>ӧдзым K ;
+Apr+Ine+PxSg1:%>ланям SPAT-COMPARATIVE ;
+Apr+Ela+PxSg1:%>ланьсьым SPAT-COMPARATIVE ;
+Apr+Ill+PxSg1:%>ланям IllCompar ; !

+PxSg1+Apr+Egr:%>ӧйланьсянь SPAT-COMPARATIVE ;

+Apr+Prl+PxSg1:%>ланьӧдым K ;
+Apr+Tra+PxSg1:%>ланьтіым K ;
+Apr+Ter+PxSg1:%>ланьӧдзым K ;

!2SG
+PxSg2+Nom:%>ыд K ;
+PxSg2+Gen:%>ыдлӧн K ;
+PxSg2+Abl:%>ыдлысь K ;
+PxSg2+Dat:%>ыдлы K ;
+PxSg2+Acc2:%>тӧ K ;

+Instr+PxSg2:%>над K ;

+PxSg2+Com:%>ыдкӧд K ;

+Car+PxSg2:%>тӧгыд K ;

+PxSg2+Cns:%>ыдла K ;

+Comp+PxSg2:%>сяыд K ;
+Ine+PxSg2:%>ад SPAT-COMPARATIVE ;
+Ela+PxSg2:%>сьыд SPAT-COMPARATIVE ;
+Ill+PxSg2:%>ад IllCompar ; !
+Apr+PxSg2:%>ланьыд SPAT-COMPARATIVE ;
+Egr+PxSg2:%>сяньыд SPAT-COMPARATIVE ;
+Prl+PxSg2:%>ӧдыд K ;
+Tra+PxSg2:%>тіыд K ;
+Ter+PxSg2:%>ӧдзыд K ;
+Apr+Ine+PxSg2:%>ланяд SPAT-COMPARATIVE ;
+Apr+Ela+PxSg2:%>ланьсьыд SPAT-COMPARATIVE ;
+Apr+Ill+PxSg2:%>ланяд IllCompar ; !
+Apr+Egr+PxSg2:%>ланьсяньыд SPAT-COMPARATIVE ;
+Apr+Prl+PxSg2:%>ланьӧдыд K ;
+Apr+Tra+PxSg2:%>ланьтіыд K ;
+Apr+Ter+PxSg2:%>ланьӧдзыд K ;


!3SG
+PxSg3+Nom:%>ыс K ;
+PxSg3+Gen:%>ыслӧн K ;
+PxSg3+Abl:%>ыслысь K ;
+PxSg3+Dat:%>ыслы K ;
+PxSg3+Acc3:%>сӧ K ;

+Instr+PxSg3:%>нас K ;

+PxSg3+Com:%>ыскӧд K ;

+Car+PxSg3:%>тӧгыс K ;

+PxSg3+Cns:%>ысла K ;

+Comp+PxSg3:%>сяыс K ;
+Ine+PxSg3:%>ас SPAT-COMPARATIVE ;
+Ela+PxSg3:%>сьыс SPAT-COMPARATIVE ;
+Ill+PxSg3:%>ас IllCompar ; !
+Apr+PxSg3:%>ланьыс SPAT-COMPARATIVE ;
+Egr+PxSg3:%>сяньыс SPAT-COMPARATIVE ;
+Prl+PxSg3:%>ӧдыс K ;
+Tra+PxSg3:%>тіыс K ;
+Ter+PxSg3:%>ӧдзыс K ;
+Apr+Ine+PxSg3:%>ланяс SPAT-COMPARATIVE ;
+Apr+Ela+PxSg3:%>ланьсьыс SPAT-COMPARATIVE ;
+Apr+Ill+PxSg3:%>ланяс IllCompar ; !
+Apr+Egr+PxSg3:%>ланьсяньыс SPAT-COMPARATIVE ;
+Apr+Prl+PxSg3:%>ланьӧдыс K ;
+Apr+Tra+PxSg3:%>ланьтіыс K ;
+Apr+Ter+PxSg3:%>ланьӧдзыc K ;



!1PL
+PxPl1+Nom:%>ным K ;
+PxPl1+Gen:%>нымлӧн K ;
+PxPl1+Abl:%>нымлысь K ;
+PxPl1+Dat:%>нымлы K ;
+PxPl1+Acc:%>нымӧс K ;

+Instr+PxPl1:%>наным K ;

+PxPl1+Com:%>нымкӧд K ;

+Car+PxPl1:%>тӧгным K ;
+PxPl1+Car:%>нымтӧг K ;

+PxPl1+Cns:%>нымла K ;

+Comp+PxPl1:%>сяным K ;
+Ine+PxPl1:%>аным SPAT-COMPARATIVE ;
+Ela+PxPl1:%>сьыным SPAT-COMPARATIVE ;
+Ill+PxPl1:%>аным IllCompar ; !
+Apr+PxPl1:%>ланьным SPAT-COMPARATIVE ;
+PxPl1+Apr:%>нымлань SPAT-COMPARATIVE ;
+Egr+PxPl1:%>сяньным SPAT-COMPARATIVE ; ! _ставсяньным_
+PxPl1+Egr:%>нымсянь SPAT-COMPARATIVE ; ! _ставнымсянь_
+Prl+PxPl1:%>ӧдным K ;
+Tra+PxPl1:%>тіным K ;
+Ter+PxPl1:%>ӧдзным K ;
+Apr+Ine+PxPl1:%>ланяным SPAT-COMPARATIVE ;
+Apr+Ela+PxPl1:%>ланьсьыным SPAT-COMPARATIVE ;
+Apr+Ill+PxPl1:%>ланяным IllCompar ; !
+Apr+Egr+PxPl1:%>ланьсяньным SPAT-COMPARATIVE ;
+Apr+Prl+PxPl1:%>ланьӧдным K ;
+Apr+Tra+PxPl1:%>ланьтіным K ;
+Apr+Ter+PxPl1:%>ланьӧдзным K ;

!2PL
+PxPl2+Nom:%>ныд K ;
+PxPl2+Gen:%>ныдлӧн K ;
+PxPl2+Abl:%>ныдлысь K ;
+PxPl2+Dat:%>ныдлы K ;
+PxPl2+Acc2:%>нытӧ K ;

+Instr+PxPl2:%>наныд K ;

+PxPl2+Com:%>ныдкӧд K ;

+Car+PxPl2:%>тӧгныд K ;

+PxPl2+Cns:%>ныдла K ;

+Comp+PxPl2:%>сяныд K ;
+Ine+PxPl2:%>аныд SPAT-COMPARATIVE ;
+Ela+PxPl2:%>сьыныд SPAT-COMPARATIVE ;
+Ill+PxPl2:%>аныд IllCompar ; !
+PxPl2+Apr:%>ныдлань SPAT-COMPARATIVE ;
+Egr+PxPl2:%>сяньныд SPAT-COMPARATIVE ;
+PxPl2+Egr:%>ныдсянь SPAT-COMPARATIVE ;
+Prl+PxPl2:%>ӧдныд K ;
+Tra+PxPl2:%>тіныд K ;
+Ter+PxPl2:%>ӧдзныд K ;
+Apr+Ine+PxPl2:%>ланяныд SPAT-COMPARATIVE ;
+Apr+Ela+PxPl2:%>ланьсьыныд SPAT-COMPARATIVE ;
+Apr+Ill+PxPl2:%>ланяныд IllCompar ; !
+Apr+Egr+PxPl2:%>ланьсяньныд SPAT-COMPARATIVE ;
+Apr+Prl+PxPl2:%>ланьӧдныд K ;
+Apr+Tra+PxPl2:%>ланьтіныд K ;
+Apr+Ter+PxPl2:%>ланьӧдзныд K ;

!3PL
+PxPl3+Nom:%>ныс K ;
+PxPl3+Gen:%>ныслӧн K ;
+PxPl3+Abl:%>ныслысь K ;
+PxPl3+Dat:%>ныслы K ;
+PxPl3+Acc3:%>нысӧ K ;

+Instr+PxPl3:%>наныс K ;

+PxPl3+Com:%>ныскӧд K ;

+Car+PxPl3:%>тӧгныс K ;

+PxPl3+Cns:%>нысла K ;

+Comp+PxPl3:%>сяныс K ;
+Ine+PxPl3:%>аныс SPAT-COMPARATIVE ;
+Ela+PxPl3:%>сьыныс SPAT-COMPARATIVE ;
+Ill+PxPl3:%>аныс IllCompar ; !
+PxPl3+Apr:%>ныслань SPAT-COMPARATIVE ;
+Apr+PxPl3:%>ланьныс SPAT-COMPARATIVE ;
+Egr+PxPl3:%>сяньныс SPAT-COMPARATIVE ;
+PxPl3+Egr:%>ныссянь SPAT-COMPARATIVE ;
+Prl+PxPl3:%>ӧдныс K ;
+Tra+PxPl3:%>тіныс K ;
+Ter+PxPl3:%>ӧдзныс K ;
+Apr+Ine+PxPl3:%>ланяныс SPAT-COMPARATIVE ;
+Apr+Ela+PxPl3:%>ланьсьыныс SPAT-COMPARATIVE ;
+Apr+Ill+PxPl3:%>ланяныс IllCompar ; !
+Apr+Egr+PxPl3:%>ланьсяньныс SPAT-COMPARATIVE ;
+Apr+Prl+PxPl3:%>ланьӧдныс K ;
+Apr+Tra+PxPl3:%>ланьтіныс K ;
+Apr+Ter+PxPl3:%>ланьӧдзныс K ;

!Case
LEXICON Noun1-IS_INE
+N+Ine: SPAT-COMPARATIVE ;

LEXICON DERIVEDADJ
+Der/са+IneMod:%>са Adj ;     
+Der/тӧм+AbeMod:%>тӧм Adj ;
+Der/а+HabObjMod:%>а Adj ;
+Der/ся+TempMod:%>ся Adj ; 


    

LEXICON AdjCompar
PredMonLex ;
+Der/джык+Compar:%>джык PredMonLex ;
+Der/джык+Compar+Der/а:%>джыка ADVTAG ;
+Der/джык+Compar+Der/MWN+N+Sg:%>джык CASEPOSSLEX-afterDerivation ;
+Der/джык+Compar+Der/MWN+N+Pl:%>джыкъяс CASEPOSSLEX-afterDerivation ;
+Der/MWN+N+Sg: CASEPOSSLEX-afterDerivation ; !This is for adjectives without nouns
+Der/MWN+N+Pl:%>ъяс CASEPOSSLEX-afterDerivation ; !This is for adjectives without nouns

LEXICON IllCompar
 K ;
+Der/джык+Compar:%>джык K ;

LEXICON PredMonLex-Spat
 K ;      ! Singular predicative
 PredMonLex-Spat-Add-Morphology ; !

LEXICON PredMonLex-Spat-Add-Morphology ! This allows consistency 
                                       ! with SPAT elsewhere
+Pred+Pl:ӧсь K ; ! Plural predicative.
+Der/джык+Compar:%>джык K ; !ывлаынджык
+Der/джык+Compar+Pred+Pl:%>джыкӧсь K ; !ывлаынджыкӧсь


LEXICON PredMonLex
 K ;      ! Singular predicative
+Pred+Pl:ӧсь K ; ! Plural predicative.
!! 2012-02-07 !!+Der/а+Adv:%>а K ;
 


