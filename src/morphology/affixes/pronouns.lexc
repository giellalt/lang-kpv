! ========================================================================== !
!!                        !!!Pronominal morphology                           
! ========================================================================== !

!! !!Closed class personal pronouns
! ------------------------------

LEXICON PERS                        !!≈ * __@CODE@__ 
ме+Pron+Pers+Sg1: perssg1decl ;    !!≈ * __@CODE@__ ...
тэ+Pron+Pers+Sg2: perssg2decl ; 
сійӧ+Pron+Pers+Sg3: perssg3decl ;
ми+Pron+Pers+Pl1: perspl1decl ;
ті+Pron+Pers+Pl2: perspl2decl ; 
найӧ+Pron+Pers+Pl3: perspl3decl ;


LEXICON perssg1decl   !!≈ * __@CODE@__ 
+Nom:ме WordEnd ;
+Cmpl:ме WordEnd ; !Postposition complement
+Gen:менам WordEnd ;
+Gen:мелӧн WordEnd ;
+Gen:менамлӧн WordEnd ;
+Ela:менсьым WordEnd ; !The elative case is used in Sg1 and Sg2 
                 !for the functions of the ablative in other persons
+Dat:меным WordEnd ; !What should be done with the 4 dative forms?
+Dat:мем WordEnd ;
+Dat:мен WordEnd ;
+Dat:менлы WordEnd ;
+Acc:менӧ WordEnd ;
+Ins:менамӧн WordEnd ;
+Com:менамкӧд WordEnd ;
+Car:менамтӧг WordEnd ;
+Inе:менамын WordEnd ;
+Ela:менамысь WordEnd ;
+Ill:менамӧ WordEnd ;
+Apr:менамлань WordEnd ;
+Egr:менамсянь WordEnd ;
+Ter:менамӧдз WordEnd ;
:ме perssg ; ! for the regular obl sg

LEXICON perspl1decl    !!≈ * __@CODE@__
+Nom:ми WordEnd ;
+Nom:мийӧ WordEnd ; !Der/йӧ
+Gen:миян WordEnd ;
+Acc:миянӧс WordEnd ;
:миян persplobl ;
!!Extras from 2000 Grammar: 163
+Gen:миянлӧн WordEnd ; !Synomymous with: миян
+Acc:мияннымӧс WordEnd ; !Synomymous with: миянӧс
+Ela:миянсьыным WordEnd ; !Synomymous with:миянлысь
+Com:микӧд WordEnd ; !Synomymous with: миянкӧд
+Com:мияннымкӧд WordEnd ; !Synomymous with: миянкӧд
+Car:митӧг WordEnd ; !Synomymous with: миянтӧг
+Car:миянтӧгным WordEnd ; !Synomymous with: миянтӧг
+Cns:мияннымла WordEnd ; !Synomymous with: миянла
+Ela:миысь WordEnd ; !Synomymous with: миянысь
+Ill:миӧ WordEnd ; !Synomymous with: миянӧ
+Apr:миянланьным WordEnd ; !Synomymous with: миянлань
+Apr:миланьным WordEnd ; !Synomymous with: миянлань
+Egr:миянсяньным WordEnd ; !Synomymous with: миянсянь
+Tra:миті WordEnd ; !Synomymous with: миянӧд
+Ter:миӧдз WordEnd ; !Synomymous with: миянӧдз

LEXICON perssg2decl     !!≈ * __@CODE@__
+Nom:тэ WordEnd ;
+Cmpl:тэ WordEnd ;
+Gen:тэнад WordEnd ;
+Gen:тэлӧн WordEnd ;
+Gen:тэнадлӧн WordEnd ;
+Ela:тэнсьыд WordEnd ;
+Dat:тэныд WordEnd ;
+Dat:тэн WordEnd ;
+Dat:тэд WordEnd ;
+Dat:тэдлы WordEnd ;
+Dat:тэнлы WordEnd ;
+Dat:тэныдлы WordEnd ;
+Acc:тэнӧ WordEnd ;
+Ins:тэнадӧн WordEnd ;
+Com:тэнадкӧд WordEnd ;
+Car:тэнадтӧг WordEnd ;
+Ine:тэнадын WordEnd ;
+Ela:тэнадысь WordEnd ;
+Ill:тэнадӧ WordEnd ;
+Egr:тэнадсянь WordEnd ;
+Ter:тэнадӧдз WordEnd ;
:тэ perssg ;  ! for the regular obl sg

LEXICON perspl2decl !!≈ * __@CODE@__ 
+Nom:ті WordEnd ;
+Cmpl:ті WordEnd ;
+Nom:тійӧ WordEnd ; !We need a special tag for discerning +Nom forms  Der/йӧ
+Gen:тіян WordEnd ;
+Gen:тіянлӧн WordEnd ;
+Acc:тіянӧс WordEnd ;
:тіян persplobl ;
!More forms from Ӧнія коми кыв 2000: 163
+Abl:тіянлысьныд WordEnd ;
+Dat:тіянлы WordEnd ;
+Acc2:тіянныдтӧ WordEnd ;
+Ins:тіӧн WordEnd ;
+Car:тіянныдтӧг WordEnd ;
+Car:тітӧг WordEnd ;
+Cns:тіянныдла WordEnd ;
+Ela:тіысь WordEnd ;
+Ill:тіӧ WordEnd ;
+Apr:тіянланьныд WordEnd ;
+Apr:тіянланьыд WordEnd ;
+Egr:тіянсяньныд WordEnd ;
+Ter:тіӧдз WordEnd ;

LEXICON perssg3decl  !!≈ * __@CODE@__ 
+Nom:сійӧ WordEnd ; !+Der/йӧ
+Gen:сылӧн WordEnd ;
+Abl:сылысь WordEnd ;
+Ela:сыысь WordEnd ;
+Dat:сылы WordEnd ;
+Acc:сійӧс WordEnd ; !Der/йӧ
+Ins:сійӧн WordEnd ; !Der/йӧ
:сы perssg ;  ! for the regular obl sg
+Cmpl:сы WordEnd ;
!More form ӧния коми кыв 163
+Gen:сійӧлӧн WordEnd ; !Der/йӧ
+Abl:сійӧлысь WordEnd ; !Der/йӧ
+Dat:сійӧлы WordEnd ; !Der/йӧ
+Com:сійӧкӧд WordEnd ; !Der/йӧ
+Car:сійӧтӧг WordEnd ; !Der/йӧ
+Apr:сійӧлань WordEnd ; !Der/йӧ
+Egr:сійӧсянь WordEnd ; !Der/йӧ
+Egr:сійӧсяньыс WordEnd ; !Der/йӧ
+Ter:сійӧӧдз WordEnd ; !Der/йӧ
! Special expressions
+Com+Ins:сыкӧдӧн WordEnd ; ! Ен сыкӧдӧн!

!! ми мийӧ The 1st and 2nd persons have Oblique case stem strategies
!! that differ from the 3rd person: 
!! ті тійӧ nämä ovat aivan eri asioita
!! сы сійӧ tosin joskus 

LEXICON perspl3decl !!≈ * __@CODE@__
+Nom:найӧ WordEnd ; !Der/йӧ
+Acc:найӧс WordEnd ; !Der/йӧ
+Gen:налӧн WordEnd ;
:на persplobl ; !This might be one solution for the different stem types
+Cmpl:на WordEnd ;
!
+Gen:найӧлӧн WordEnd ; !Der/йӧ
+Abl:найӧлысь WordEnd ; !Der/йӧ
+Dat:найӧлы WordEnd ; !Der/йӧ
+Ins:найӧӧн WordEnd ; !Der/йӧ
+Com:найӧкӧд WordEnd ; !Der/йӧ
+Com:накӧдыс WordEnd ;
+Car:найӧтӧг WordEnd ; !Der/йӧ
+Car:натӧгыс WordEnd ;
+Cns:наысла WordEnd ;
+Ela:насьыс WordEnd ;
+Apr:наыслань WordEnd ;
+Apr:наланьыс WordEnd ;
+Egr:наыссянь WordEnd ;
+Egr:насяньыс WordEnd ;
+Tra:натіыс WordEnd ;
+Ter:наӧдзыс WordEnd ;


LEXICON perssg !!≈ * __@CODE@__
+Ins:ӧн WordEnd ;
+Com:кӧд WordEnd ;
+Car:тӧг WordEnd ;
+Cns:ла WordEnd ;
+Ine:ын WordEnd ;
+Ill:ӧ WordEnd ; !
+Ela:ысь WordEnd ;
+Apr:лань WordEnd ;
+Egr:сянь WordEnd ;
+Prl:ӧд WordEnd ;
+Tra:ті WordEnd ;
+Ter:ӧдз WordEnd ;
+Abl:лысь WordEnd ; !This is not attested for Sg1 and Sg2 in the 2000 Grammar



LEXICON persplobl !!≈ * __@CODE@__
+Abl:лысь WordEnd ;
+Dat:лы WordEnd ;
+Acc:ӧс WordEnd ;

+Ins:ӧн WordEnd ;
+Com:кӧд WordEnd ;
+Car:тӧг WordEnd ;
+Cns:ла WordEnd ;
+Cmpr:ся WordEnd ;
+Ine:ын WordEnd ;
+Ela:ысь WordEnd ;
+Ill:ӧ WordEnd ;
+Apr:лань WordEnd ;
+Egr:сянь WordEnd ;
+Prl:ӧд WordEnd ;
+Tra:ті WordEnd ;
+Ter:ӧдз WordEnd ;
+AprIne:ланьын WordEnd ;  ! New cases in last grammar  
+AprEla:ланьысь WordEnd ;  ! 
+AprIll:ланьӧ WordEnd ; ! 
+AprEgr:ланьсянь WordEnd ; ! 
+AprPrl:ланьӧд WordEnd ; !
+AprTra:ланьті WordEnd ; !       
+AprTer:ланьӧдз WordEnd ; ! 


! These ones to be removed:
! LEXICON PRON-PERS-PL1-ACC
! +Pron+Pers+Pl1+Acc: WordEnd ;
! LEXICON PRON-PERS-PL1-COM
! +Pron+Pers+Pl1+Com: WordEnd ;
! LEXICON PRON-PERS-PL1-DAT
! +Pron+Pers+Pl1+Dat: WordEnd ;
! LEXICON PRON-PERS-PL1-GEN
! +Pron+Pers+Pl1+Gen: WordEnd ;
! LEXICON PRON-PERS-PL1-NOM
! +Pron+Pers+Pl1+Nom: WordEnd ;
! LEXICON PRON-PERS-PL2-ACC
! +Pron+Pers+Pl2+Acc: WordEnd ;
! LEXICON PRON-PERS-PL2-COM
! +Pron+Pers+Pl2+Com: WordEnd ;
! LEXICON PRON-PERS-PL2-DAT
! +Pron+Pers+Pl2+Dat: WordEnd ;
! LEXICON PRON-PERS-PL2-GEN
! +Pron+Pers+Pl2+Gen: WordEnd ;
! LEXICON PRON-PERS-PL2-NOM
! +Pron+Pers+Pl2+Nom: WordEnd ;
! LEXICON PRON-PERS-PL3-ACC
! +Pron+Pers+Pl3+Acc: WordEnd ;
! LEXICON PRON-PERS-PL3-COM
! +Pron+Pers+Pl3+Com: WordEnd ;
! LEXICON PRON-PERS-PL3-DAT
! +Pron+Pers+Pl3+Dat: WordEnd ;
! LEXICON PRON-PERS-PL3-ELA
! +Pron+Pers+Pl3+Ela: WordEnd ;
! LEXICON PRON-PERS-PL3-GEN
! +Pron+Pers+Pl3+Gen: WordEnd ;
! LEXICON PRON-PERS-PL3-NOM
! +Pron+Pers+Pl3+Nom: WordEnd ;
! LEXICON PRON-PERS-SG1-ACC
! +Pron+Pers+Sg1+Acc: WordEnd ;
! LEXICON PRON-PERS-SG1-DAT
! +Pron+Pers+Sg1+Dat: WordEnd ;
! LEXICON PRON-PERS-SG1-ELA
! +Pron+Pers+Sg1+Ela: WordEnd ;
! LEXICON PRON-PERS-SG1-GEN
! +Pron+Pers+Sg1+Gen: WordEnd ;
! LEXICON PRON-PERS-SG1-NOM ! tähän tulee ме
! +Pron+Pers+Sg1+Nom: WordEnd ;
! LEXICON PRON-PERS-SG2-ACC
! +Pron+Pers+Sg2+Acc: WordEnd ;
! LEXICON PRON-PERS-SG2-COM
! +Pron+Pers+Sg2+Com: WordEnd ;
! LEXICON PRON-PERS-SG2-DAT
! +Pron+Pers+Sg2+Dat: WordEnd ;
! LEXICON PRON-PERS-SG2-ELA
! +Pron+Pers+Sg2+Ela: WordEnd ;
! LEXICON PRON-PERS-SG2-GEN
! +Pron+Pers+Sg2+Gen: WordEnd ;
! LEXICON PRON-PERS-SG2-NOM
! +Pron+Pers+Sg2+Nom: WordEnd ;
! LEXICON PRON-PERS-SG2-ABL
! +Pron+Pers+Sg2+Abl: WordEnd ;
! LEXICON PRON-PERS-SG3-ACC
! +Pron+Pers+Sg3+Acc: WordEnd ;
! LEXICON PRON-PERS-SG3-COM
! +Pron+Pers+Sg3+Com: WordEnd ;
! LEXICON PRON-PERS-SG3-DAT
! +Pron+Pers+Sg3+Dat: WordEnd ;
! LEXICON PRON-PERS-SG3-ELA
! +Pron+Pers+Sg3+Ela: WordEnd ;
! LEXICON PRON-PERS-SG3-GEN
! +Pron+Pers+Sg3+Gen: WordEnd ;
! LEXICON PRON-PERS-SG3-NOM
! +Pron+Pers+Sg3+Nom: WordEnd ;
! LEXICON PRON-PERS-SG3-ABL
! +Pron+Pers+Sg3+Abl: WordEnd ;
! LEXICON PRON-PERS-SG3-OBL
! +Pron+Pers+Sg3+Dat:%>лы WordEnd ;
! +Pron+Pers+Sg3+Abl:%>лысь WordEnd ;


!Interrogative pronouns serve as the base of interrogative, relative, indefinite and distributional pronouns.

LEXICON INTERR
кин+Pron+Sem/Hum+Nom:кин INTERR-REL ; ! 2011-11-09 This will need cases
кодар+Det:кодар INTERR-REL-INDEF ;
!2017-09-19кодар+Det+Der/MWN:кодар PRON-HUM-OBL ;
!кодар+Det+Der/MWN:кодар PRON-HUM-OBL ;
кодар+Det+Der/ладор:кодарладор PRON-HUM-OBL ;

коді:код PRON-HUM ;

кодыр+Adv+Temp:кодыр INTERR-REL-INDEF ;
кор+Adv+Temp:кор INTERR-REL-INDEF ;
кор+Adv+Temp+Pl+Ill:коръясӧ INTERR-REL-INDEF ;
коръя+Adv+Temp:коръя INTERR-REL-INDEF ;
кудз+Adv+Manner+Err/Orth:кудз INTERR-REL-INDEF ;
кутшӧм+Pron:кутшӧм INTERR-REL-INDEF ; ! This needs to be 
кутшӧм+Pron+Rel+Cop+Pl:кутшӧмӧсь INTERR-REL-INDEF ; ! This needs to be 
кутшӧма+Adv+Manner:кутшӧма INTERR-REL-INDEF ; ! This needs to be 
кутшӧм+Pron+Sg:кутшӧм PRON-NONHUM ;
кутшӧм+Pron+Pl:кутшӧмъяс PRON-NONHUM ;
!2017-09-19кутшӧм+A+Der/MWN+N+Sg:кутшӧм PRON-NONHUM ;
!2017-09-19кутшӧм+A+Der/MWN+N+Pl:кутшӧмъяс PRON-NONHUM ;
кутшӧми+A:кутшӧми INTERR-REL-INDEF ; 
!2017-09-20кутшӧми+Pron+A:кутшӧми INTERR-REL-INDEF ; ! This needs to be 
кыдз+Pron+Adv+Manner:кыдз INTERR-REL-INDEF ;
кыдзи+Pron+Adv+Manner:кыдзи INTERR-REL-INDEF ;
кыдзик+Pron+Adv+Manner:кыдзик INTERR-REL-INDEF ;
кыдзик+Pron+Adv+Manner+Ins:кыдзикӧн INTERR-REL-INDEF ;
кымын+Det:кымын INTERR-REL-INDEF ; ! Quantifier info required
кымын+Det+Iter:кымынысь INTERR-REL-INDEF ; ! Quantifier info required
кымын+Det+Ord:кымынӧд INTERR-REL-INDEF ; ! Quantifier info required
кымын+Det+Ord+Iter:кымынӧдысь INTERR-REL-INDEF ; ! Quantifier info required
кысь+Adv+Spat+Ela:кысь INTERR-REL-INDEF ;
кытчӧ+Adv+Spat+Ill:кытчӧ INTERR-REL-INDEF ;
кытчӧдз+Adv+Spat+Ter:кытчӧдз INTERR-REL-INDEF ;
кытысь+Adv+Spat+Ela:кытысь INTERR-REL-INDEF ;
кыті+Adv+Spat+Tra:кыті INTERR-REL-INDEF ;
кыткӧ+Adv+Spat+Tra+Der/кӧ+Indef:кыткӧ K ;
кытсюрӧ+Adv+Spat+Tra+Der/сюрӧ+Indef:кытсюрӧ K ;
кытӧн+Adv+Spat+Ine:кытӧн INTERR-REL-INDEF ;
кытӧні+Adv+Spat+Ine:кытӧні INTERR-REL ;
 
кӧні+Adv+Spat+Ine+Pred+Sg:кӧні INTERR-REL-INDEF ;
кӧні+Adv+Spat+Ine+Pred+Pl:кӧнӧсь INTERR-REL ;
кӧні+Adv+Spat+Ine:кӧн PRON-INDEFTAG ;
мый+Pron+NonHum:мый PRON-NONHUM ;
мыйда+Pron+NonHum:мыйда INTERR-REL-INDEF ;
мыйта+Pron+NonHum:мыйта INTERR-REL-INDEF ;
мыйтӧм+Pron+NonHum:мыйтӧм INTERR-REL-INDEF ;
мыйӧс+Pron+NonHum+Err/Orth:мыйӧс INTERR-REL-INDEF ;
!Here are some negative pronouns out of place 2011-11-22
некысь+Adv+Neg+Spat+Ela:некысь K ;
некысянь+Adv+Neg+Spat+Egr:некысянь K ;
некыт+Adv+Neg+Spat+Tra:некыт K ;
некытчӧ+Adv+Neg+Spat+Ill:некытчӧ K ;
некытчӧдз+Adv+Neg+Spat+Ter:некытчӧдз K ;
некытысь+Adv+Neg+Spat+Ela:некытысь K ;
некыті+Adv+Neg+Spat+Tra:некыті K ;
некытӧн+Adv+Neg+Spat+Ine:некытӧн K ;
некӧн+Adv+Neg+Spat+Ine:некӧн K ;



LEXICON PRON-HUM
+Pron+Sg+Nom:і INTERR-REL ;
+Pron+Pl+Nom:ъяс INTERR-REL-INDEF ;
+Pron+Sg+Nom: INTERR-REL-INDEF ;
+Pron+Sg: PRON-HUM-OBL ;
+Pron+Pl:ъяс PRON-HUM-OBL ;
+Pron: PRON-HUM-COLL ;
!код, коді

LEXICON PRON-HUM-OBL
! 2000: 187
+Cmpl: INTERR-REL-INDEF ;
+Gen:лӧн INTERR-REL-INDEF ;
+Abl:лысь INTERR-REL-INDEF ; !
+Dat:лы INTERR-REL-INDEF ;
+Acc:ӧс INTERR-REL-INDEF ;
+Acc3:сӧ INTERR-REL-INDEF ;
+Ins:ӧн INTERR-REL-INDEF ;
+Com:кӧд INTERR-REL-INDEF ;
+Car:тӧг INTERR-REL-INDEF ;
+Cns:ла INTERR-REL-INDEF ;
+Ine:ын INTERR-REL-INDEF ;
+Ela:ысь INTERR-REL-INDEF ; 
+Ill:ӧ INTERR-REL-INDEF ; !
+Apr:лань INTERR-REL-INDEF ;
+Egr:сянь INTERR-REL-INDEF ;
+Tra:ті INTERR-REL-INDEF ;
+Ter:ӧдз INTERR-REL-INDEF ;

LEXICON PRON-HUM-COLL
+Coll:нан PRON-HUM-COLL-PERS ;
+ZeroColl: PRON-HUM-COLL-PERS ;
!
+ZeroColl+Acc2+PxPl2:ныдтӧ INTERR-REL-INDEF ;
+Coll+Acc2+PxPl2:наннытӧ INTERR-REL-INDEF ;

LEXICON PRON-HUM-COLL-PERS
+PxPl1:ным PRON-HUM-COLL-CASES ;
+PxPl2:ныд PRON-HUM-COLL-CASES ;
+PxPl3:ныс PRON-HUM-COLL-CASES ;
!
+Acc+PxPl1:нымӧс INTERR-REL-INDEF ;
+Acc2+PxPl2:нытӧ INTERR-REL-INDEF ;
+Acc3+PxPl3:ныссӧ INTERR-REL-INDEF ;

LEXICON PRON-HUM-COLL-CASES
+Nom: INTERR-REL-INDEF ;
 PRON-HUM-OBL ;

LEXICON PRON-NONHUM
+Nom: INTERR-REL-INDEF ;
+Gen:лӧн INTERR-REL-INDEF ; !
+Abl:лысь INTERR-REL-INDEF ; !
+Dat:лы INTERR-REL-INDEF ; !
+ZeroAcc: INTERR-REL-INDEF ;
+Acc3:сӧ INTERR-REL-INDEF ;
+Ins:ӧн INTERR-REL-INDEF ;
+Com:кӧд INTERR-REL-INDEF ;
+Car:тӧг INTERR-REL-INDEF ;
+Cmpr:ся INTERR-REL-INDEF ;
+Cns:ла INTERR-REL-INDEF ;
+Ine:ын INTERR-REL-INDEF ;
+Ela:ысь INTERR-REL-INDEF ; 
+Ill:ӧ INTERR-REL-INDEF ; !
+Apr:лань INTERR-REL-INDEF ;
+Egr:сянь INTERR-REL-INDEF ;
+Prl:ӧд INTERR-REL-INDEF ;
+Tra:ті INTERR-REL-INDEF ;
+Ter:ӧдз INTERR-REL-INDEF ;

! Removing 2012-02-08 The comparative can be taken care of once
! LEXICON INTERR-REL-INDEF_COMPARISON
! +Der/джык+Comp:%>джык INTERR-REL ;
!               INTERR-REL ;

LEXICON Ill4INTERR-REL-INDEF
+Ill: INTERR-REL-INDEF ;

LEXICON EgrIll4INTERR-REL-INDEF
+Egr+Ill: INTERR-REL-INDEF ;

LEXICON Egr4INTERR-REL-INDEF
+Egr: INTERR-REL-INDEF ;

LEXICON Tra4INTERR-REL-INDEF
+Pron+Tra: INTERR-REL-INDEF ;

LEXICON TraEgr4INTERR-REL-INDEF
+Tra+Egr: INTERR-REL-INDEF ;

LEXICON Ter4INTERR-REL-INDEF
+Ter: INTERR-REL-INDEF ;

LEXICON INTERR-REL-INDEF ! 3 types of pronouns are congregated here
 PRON-INDEFTAG ; ! This leads to a variety of indefinite pronouns
 INTERR-REL ; ! This leads to a split between relative and interrogative pronouns

LEXICON INTERR-REL
+Interr: COMPARATIVE ;
+Rel: K ;

LEXICON PRON-INDEFTAG
+Der/кӧ+Indef:%>кӧ K ;
+Der/сюрӧ+Indef:%>сюрӧ K ;


! Indefinite pronouns
 
LEXICON PRONINDEFTAG
+Pron+Indef: WordEnd ;

LEXICON PRONINDEFTAG-KO
+Pron: PRONINDEF-PERS-KO-CX ;

LEXICON PRONINDEF-PERS-KO-CX
+Nom: PRONINDEF-PERS-KO-END ;
+Acc:%>ӧс PRONINDEF-PERS-KO-END ;
+Gen:%>лӧн PRONINDEF-PERS-KO-END ;
+Abl:%>лысь PRONINDEF-PERS-KO-END ;
+Dat:%>лы PRONINDEF-PERS-KO-END ;
+Ins:%>ӧн PRONINDEF-PERS-KO-END ;
+Ela:%>ысь PRONINDEF-PERS-KO-END ; !2011-10-19 It might be that this only has to do with мый _what_ and мыйыськӧ _for some reason_
+Com:%>кӧд PRONINDEF-PERS-KO-END ;
+Egr:%>сянь PRONINDEF-PERS-KO-END ;

LEXICON PRONINDEF-PERS-KO-END
+Der/кӧ+Indef:%>кӧ K ;
+Der/сюрӧ+Indef:%>сюрӧ K ; !Basically both endings can be used for deriving 
 !indefinite pronouns from the interrogative/relative pronouns.


LEXICON IS_PRON-INDEF-KO
+Pron+Der/кӧ+Indef: K ;

LEXICON IS_PRON-INDEF-SJURO
+Pron+Der/сюрӧ+Indef: K ;

LEXICON IS_PRON-INTERR
+Pron+Interr: K ;

!LEXICON PRONINDEF-PERS-END
!+Der/сюрӧ:%>сюрӧ WordEnd ;

!LEXICON PRONINDEF-PERS-KO
!+Nom: PRONINDEF-PERS-KO-END ;
!+Acc:%>ӧс PRONINDEF-PERS-KO-END ;
!+Gen:%>лӧн PRONINDEF-PERS-KO-END ;
!+Dat:%>лы PRONINDEF-PERS-KO-END ;
!+Acc:%>ӧс PRONINDEF-PERS-KO-END ;


LEXICON PRON-SPAT
+Pron+Spat: COMPARATIVE ;

LEXICON PRON-RECIPR
+Pron+Recipr: PRON-RECIPR-CASE ;

LEXICON PRON-RECIPR-CASE
+Cmpl: WordEnd ;
+Cmpl+PxSg2:ыд K ;
+Cmpl+PxSg2:ыс K ;
+Cmpl+PxPl1:ным K ;
+Cmpl+PxPl2:ныд K ;
+Cmpl+PxPl3:ныс K ;
+Gen:лӧн K ;
+Abl+PxSg3:ыслысь K ;
+Abl+PxPl3:ныслысь K ;
+Dat:лы K ;
+Dat+PxSg3:ыслы K ;
+Dat+PxPl3:ныслы K ;
+Acc:ӧс K ;
+Acc+PxPl1:нымӧс K ;
+Acc2+PxPl2:нытӧ K ;
+Acc3+PxPl3:ныссӧ K ;
+Acc3:сӧ K ;
+Com+PxSg3:ыскӧд K ;
+Com+PxPl2:ныдкӧд K ;
+Com+PxPl3:ныскӧд K ;
+Car+PxSg3:тӧгыс K ;
+Ela:ысь K ;
+Ela+PxSg3:сьыс K ;
+Ela+PxPl3:сьыныс K ;
+Ill+PxSg3:ас K ; ! Here is a good problem: Should there be a special tag
                ! for the illative-inessive forms
+Ill+PxPl3:нысӧ K ;


LEXICON PRON-REL
+Pron+Rel: WordEnd ;

LEXICON PRON-REFL-IN_LADOR
+Pron+Refl+Der/ладор: Noun1 ; !2011-11-22

LEXICON REFL_AS !! * = @CODE@ ас:а 
+A+Refl:с ABS_ ; !ас is an absolute-form modifer
+Pron+Refl:  REFL_AS-01 ;

LEXICON REFL_AS-01
+Sg1:с PRON-REFL-SG1-4-OBL-CASES ;
+Sg2:с PRON-REFL-SG2-4-OBL-CASES ;
+Sg3:с PRON-REFL-SG3-4-OBL-CASES ;
+Pl1:с PRON-REFL-PL1-4-OBL-CASES ;
+Pl2:с PRON-REFL-PL2-4-OBL-CASES ;
+Pl3:с PRON-REFL-PL3-4-OBL-CASES ;
:с PRON-REFL-OBL-CASES ;

+Sg1:слам PRON-REFL-SG1-4-GEN  "minun oma -ni" ;
+Sg1:чым PRON-REFL-SG1-4-NOM  "minä itse" ;

+Sg2:сьтӧ PRON-REFL-SG2-4-ACC  "" ;
+Sg2:слад PRON-REFL-SG2-4-GEN  "sinun oma -si" ;
+Sg2:чыд PRON-REFL-SG2-4-CAR/ELA/NOM  "sinä itse" ;
+Sg3:сьсӧ PRON-REFL-SG3-4-ACC  "" ;
+Sg3:слас PRON-REFL-SG3-4-GEN  "hänen oma -nsa/ sen oma" ;
+Sg3:чыс PRON-REFL-SG3-4-NOM  "hän itse" ;

+Pl1:сланым PRON-REFL-PL1-4-GEN  "meidän oma -mme" ;
+Pl1:сьным PRON-REFL-PL1-4-NOM  "me itse" ;
+Pl2:сь PRON-REFL-PL2-4-ACC  "" ;
+Pl2:сланыд PRON-REFL-PL2-4-GEN  "teidän oma -nne" ;
+Pl2:сьныд PRON-REFL-PL2-4-NOM  "te itse" ;

+Pl3:сь PRON-REFL-PL3-4-ACC  "" ;
+Pl3:сланыс PRON-REFL-PL3-4-GEN  "heidän oma -nsa/ niiden oma" ;
+Pl3:сьныс PRON-REFL-PL3-4-NOM  "he/ ne itse" ;

:сладор PRON-REFL-IN_LADOR  "" ;

LEXICON PRON-REFL-SG1-4-OBL-CASES
+Car:тӧг K ; !There is no indication of person. = Sg1
+Com:кӧд K ; !There is no indication of person. = Sg1
+Cns:ла K ; !There is no indication of person. = Sg1
+Dat:лым K ;
+Ela:сьым K ;
+Ins:ӧн K ; !There is no indication of person. = Sg1, Pl3
+Ins:намӧн K ; !CHECKME

LEXICON PRON-REFL-SG2-4-OBL-CASES
+Car:тӧгыд K ; !
+Com:кӧдыд K ; !
+Cns:лаыд K ;
+Cns:ыдла K ;
+Dat:лыд K ;
+Ela:сьыд K ;
+Ine:ад K ;
+Ins:над K ;
LEXICON PRON-REFL-SG3-4-OBL-CASES
+Car:тӧгыс K ; !
+Com:кӧдыс K ; !
+Cns:ысла K ;
+Dat:лыс K ;
+Ela:сьыс K ;
+Ine:ас K ;
+Ins:нас K ;
LEXICON PRON-REFL-PL1-4-OBL-CASES
+Car:тӧгным K ; !
+Com:кӧдным K ; !
+Dat:лыным K ;
+Ela:сьыным K ;
+Ela:сьынымлысь K ; !CHECKME
+Ela:сьынымысь K ; !CHECKME
+Ins:наным K ;

LEXICON PRON-REFL-PL2-4-OBL-CASES
+Car:тӧгныд K ; !
+Car:ныдтӧг K ; !
+Cns:ныдла K ;
+Com:кӧдныд K ; !
+Com:ныдкӧд K ; !
+Dat:лыныд K ;
+Dat:лыныдлы K ;
+Ela:сьыныд K ;
+Ins:наныд K ;
LEXICON PRON-REFL-PL3-4-OBL-CASES
+Car:тӧгныс K ; !
+Car:ныстӧг K ; !
+Cns:нысла K ;
+Com:кӧдныс K ; !
+Com:ныскӧд K ; !
+Dat:лыныс K ;
+Dat:лыныслы K ;
+Dat:ныслы K ;
+Ela:сьыныс K ;
+Ins:ӧн K ; !There is no indication of person. = Sg1, Pl3
+Ins:наныс K ;


LEXICON PRON-REFL-OBL-CASES
+Ine+PxPl1:аным K ;
+Ine+PxPl2:аныд K ;
+Ine+PxPl3:аныс K ;
+Sg1+Ill:ӧ K ; !There is no indication of person. = Sg1
+Ill+PxSg1:ам K ; !Ӧнія коми кыв gives this for illative only
+Ill+PxSg2:ад K ; !
+Ill+PxSg3:ас K ; !
+Ill+PxPl1:аным K ; !
+Ill+PxPl2:аныд K ; !
+Ill+PxPl3:аныс K ; !
+Apr+PxSg1:лань K ; !There is no indication of person. = Sg1
+Apr+PxSg2:ланьыд K ;
+Apr+PxSg3:ланьыс K ;
+Apr+PxPl1:ланьным K ;
+Apr+PxPl2:ланьныд K ;
+Apr+PxPl3:ланьныс K ;
+Egr+PxSg1:сянь K ; !There is no indication of person. = Sg1
+Egr+PxSg2:сяньыд K ;
+Egr+PxSg3:сяньыс K ;
+Egr+PxPl1:сяньным K ;
+Egr+PxPl2:сяньныд K ;
+Egr+PxPl3:сяньныс K ;
+Tra+PxSg1:ті K ; !There is no indication of person. = Sg1
+Tra+PxSg2:тіыд K ;
+Tra+PxSg3:тіыс K ;
! Pl1 and Pl2 are not attested in Ӧнія коми кыв
+Tra+PxPl3:тіныс K ;
+Ter+PxSg1:ӧдз K ; !There is no indication of person. = Sg1
+Ter+PxSg2:ӧдзыд K ;
+Ter+PxSg3:ӧдзыс K ;
+Ter+PxPl1:нымӧдз K ;
+Ter+PxPl2:ныдӧдз K ;
+Ter+PxPl3:нысӧдз K ;
!Extras 2011-10-07


LEXICON PRON-REFL-SG1-4-NOM
 ACC-WORD-END ;
 CAR-WORD-END ;
 COM-WORD-END ;
 ELA-WORD-END ;
 ILL-WORD-END ;
 INE-WORD-END ;
 INS-WORD-END ;
 NOM-WORD-END ;


LEXICON PRON-REFL-SG1-4-GEN
+Gen: WordEnd ; !! This is bare
 COM-WORD-END ;
 EGR-WORD-END ;

LEXICON PRON-REFL-SG2-4-CAR/ELA/NOM
 CAR-WORD-END ;
 NOM-WORD-END ;
 ELA-WORD-END ;

LEXICON PRON-REFL-SG2-4-GEN
+Gen: WordEnd ;
 EGR-WORD-END ;

LEXICON PRON-REFL-SG2-4-ACC
+Acc2: WordEnd ;

LEXICON PRON-REFL-SG3-4-NOM
 CAR-WORD-END ;
 COM-WORD-END ;
 ELA-WORD-END ;
 NOM-WORD-END ;
 INS-WORD-END ;

LEXICON PRON-REFL-SG3-4-GEN
+Gen: WordEnd ; !! bare
 GEN-WORD-END ;
 ILL-WORD-END ;

LEXICON PRON-REFL-SG3-4-ACC
+Acc3: WordEnd ;

LEXICON PRON-REFL-PL1-4-NOM
+Acc:ӧс WordEnd ;
 CAR-WORD-END ;
 COM-WORD-END ;
 ELA-WORD-END ;
 NOM-WORD-END ;
 TER-WORD-END ;

LEXICON PRON-REFL-PL1-4-GEN
+Gen: WordEnd ;  !! bare
 ABL-WORD-END ;
+Der+Der/ладор:%>ладор Noun1 ;

LEXICON PRON-REFL-PL2-4-NOM
+Acc2:тӧ WordEnd ;
 CAR-WORD-END ;
 CNS-WORD-END ;
 ELA-WORD-END ;
 NOM-WORD-END ;

LEXICON PRON-REFL-PL2-4-GEN
+Gen: WordEnd ; !! bare
+Der+Der/ладор:%>ладор Noun1 ;


LEXICON PRON-REFL-PL2-4-ACC
+Acc2:нытӧ WordEnd ;

LEXICON PRON-REFL-PL3-4-NOM
 CNS-WORD-END ;
 NOM-WORD-END ;

LEXICON PRON-REFL-PL3-4-GEN
+Gen: WordEnd ; !! bare
 GEN-WORD-END ;
 PRL-WORD-END ;
+Der+Der/ладор:%>ладор Noun1 ;

LEXICON PRON-REFL-PL3-4-ACC
+Acc3:нысӧ WordEnd ;


LEXICON PRONDEMTAG
+Pron+Dem: WordEnd ; ! sg/pl?

LEXICON ADNDEMTAG
+Det+Dem: K ;
+Det+Dem+Pred+Pl:%>ӧсь K ;
+Det+Dem: Noun1 ;
!+Adn+Dem: K ;
!+Adn+Dem+Pred+Pl:%>ӧсь K ;
!+Adn+Dem+Der/MWN: Noun1 ;

LEXICON PRONDEMTAG-ES
+Pron+Dem+Sg+Nom:%>ійӧ K ;
+Pron+Dem+Sg+Acc:%>ійӧс K ;
+Pron+Dem+Sg+Ins:%>ійӧн K ;
+Pron+Dem+Sg:%>ы PRONDEMTAGSG ;
+Pron+Dem+Pl:%>ійӧяс WordEnd ;
+Pron+Dem+Pl:%>ійяс WordEnd ;
+Pron+Dem+Pl:%>ійӧян WordEnd ;
+Pron+Dem+Pl:%>ійян WordEnd ;

LEXICON PRONDEMTAGSG-TA
+Pron+Dem+Sg+Nom:%>йӧ K ;
+Pron+Dem+Sg+Acc:%>йӧс K ;
+Pron+Dem+Sg+Ins:%>йӧн K ;
+Pron+Dem+Sg: PRONDEMTAGSG ;
+Pron+Dem+Pl:%>йӧяс PRONDEMTAGPL ;
+Pron+Dem+Pl:%>йяс PRONDEMTAGPL ;
+Pron+Dem+Pl:%>йӧян PRONDEMTAGPL ;
+Pron+Dem+Pl:%>йян PRONDEMTAGPL ;

LEXICON PRONDEMTAGSG 
+Cmpl: WordEnd ;
+Ins:ӧн K ;
+Com:кӧд K ;
+Car:тӧг K ;
+Cns:ла K ;
+Dat:лы K ;
+Ine:ын K ;
+Ela:ысь K ; 
+Ill:ӧ K ; !This is questioned in the 2000 Grammar
+Abl:лысь K ; !
 PRONDEMTAG-APR ;
 PRONDEMTAG-EGR ;
 PRONDEMTAG-PRL ;
 PRONDEMTAG-TRA ;
 PRONDEMTAG-TER ;


LEXICON PRONDEMTAGPL
!+Pron+Dem+Pl: WordEnd ;
+Nom: WordEnd ;
+Gen:лӧн WordEnd ;
+Acc:ӧс WordEnd ;
+Ins:ӧн K ;
+Com:кӧд K ;
+Car:тӧг K ;
+Cns:ла K ;
+Ine:ын K ;
+Ela:ысь K ; 
+Ill:ӧ K ; !
+Abl:лысь K ; !
 PRONDEMTAG-APR ;
 PRONDEMTAG-APR-EGR ;
 PRONDEMTAG-APR-ELA ;
 PRONDEMTAG-APR-ILL ;
 PRONDEMTAG-APR-INE ;
 PRONDEMTAG-EGR ;
 PRONDEMTAG-PRL ;
 PRONDEMTAG-TRA ;
 PRONDEMTAG-TER ;

LEXICON PRONDEMTAG-ELA
+Ela:ысь K ;

LEXICON PRONDEMTAG-APR
+Apr:лань K ;

LEXICON PRONDEMTAG-APR-EGR
+AprEgr:ланьсянь K ;

LEXICON PRONDEMTAG-APR-ELA
+AprEla:ланьысь K ;

LEXICON PRONDEMTAG-APR-ILL
+AprIll:ланьӧ K ;

LEXICON PRONDEMTAG-APR-INE
+AprIne:ланьын K ;

LEXICON PRONDEMTAG-EGR
+Egr:сянь K ;

LEXICON PRONDEMTAG-PRL
+Prl:ӧд K ;

LEXICON PRONDEMTAG-TRA
+Tra:ті K ;

LEXICON PRONDEMTAG-TER
+Ter:ӧдз K ;

! Word forms that already have their morphology 
LEXICON PRON-DEM-IS_INE
+Pron+Dem+Ine: K ;

LEXICON PRON-DEM-IS_ILL
+Pron+Dem+Ill: K ;



LEXICON PRON-INTER-NONHUM
+Pron+Interr+Nom: WordEnd ;
+Pron+Interr+Gen:лӧн K ; !
+Pron+Interr+Abl:лысь K ; !
+Pron+Interr+Dat:лы K ; !
+Pron+Interr+ZeroAcc: WordEnd ;
+Pron+Interr+Acc3:сӧ WordEnd ;
+Pron+Interr+Ins:ӧн K ;
+Pron+Interr+Com:кӧд K ;
+Pron+Interr+Car:тӧг K ;
+Pron+Interr+Cns:ла K ;
+Pron+Interr+Ine:ын K ;
+Pron+Interr+Ela:ысь K ; 
+Pron+Interr+Ill:ӧ K ; !
+Pron+Interr+Apr:лань K ;
+Pron+Interr+Egr:сянь K ;
+Pron+Interr+Prl:ӧд K ;
+Pron+Interr+Tra:ті K ;
+Pron+Interr+Ter:ӧдз K ;

LEXICON PRON-NEG-HUM
+Pron+Neg+Sg+Nom: WordEnd ;
+Pron+Neg+Sg: PRON-NEG-OBL ;

LEXICON PRON-DUAL
+Pron+Du: PRON-NEG-OBL ;

! LEXICON PRON-INTER-REL-HUM-COLL
! +Coll:нан PRON-INTER-REL-HUM-COLL-PERS ;
! +ZeroColl: PRON-INTER-REL-HUM-COLL-PERS ;
! +ZeroColl+PxPl2+Acc2:ныдтӧ PRON-INTER-REL-HUM-COLL-INDEF ;
! +Coll+Acc2+PxPl2:наннытӧ PRON-INTER-REL-HUM-COLL-INDEF ;
! 
! LEXICON PRON-INTER-REL-HUM-COLL-PERS
! +PxPl1:ным PRON-INTER-REL-HUM-COLL-CASES ;
! +PxPl2:ныд PRON-INTER-REL-HUM-COLL-CASES ;
! +PxPl3:ныс PRON-INTER-REL-HUM-COLL-CASES ;
! +Acc+PxPl1:нымӧс PRON-INTER-REL-HUM-COLL-INDEF ;
! +Acc3+PxPl3:ныссӧ PRON-INTER-REL-HUM-COLL-INDEF ;
! 
! LEXICON PRON-INTER-REL-HUM-COLL-CASES
! +Nom: PRON-INTER-REL-HUM-COLL-INDEF ;
! +Gen:лӧн PRON-INTER-REL-HUM-COLL-INDEF ;
! +Abl:лысь PRON-INTER-REL-HUM-COLL-INDEF ;
! +Dat:лы PRON-INTER-REL-HUM-COLL-INDEF ;
! +Ins:ӧн PRON-INTER-REL-HUM-COLL-INDEF ;
! +Com:кӧд PRON-INTER-REL-HUM-COLL-INDEF ;
! +Car:тӧг PRON-INTER-REL-HUM-COLL-INDEF ;
! +Cns:ла PRON-INTER-REL-HUM-COLL-INDEF ;
! +Ine:ын PRON-INTER-REL-HUM-COLL-INDEF ;
! +Ela:ысь PRON-INTER-REL-HUM-COLL-INDEF ;
! +Ill:ӧ PRON-INTER-REL-HUM-COLL-INDEF ;
! +Apr:лань PRON-INTER-REL-HUM-COLL-INDEF ;
! +Egr:сянь PRON-INTER-REL-HUM-COLL-INDEF ;
! +Tra:ті PRON-INTER-REL-HUM-COLL-INDEF ;
! +Ter:ӧдз PRON-INTER-REL-HUM-COLL-INDEF ;
! 
! LEXICON PRON-INTER-REL-HUM-COLL-INDEF
! +Der/кӧ+Indef:кӧ K ;
!  K ;

!Actually there might be reason to consider +Der/MWN
LEXICON PRONQ-MUKOD
!2017-09-20+Pron+Abs: WordEnd ;
+Pron+Sg+Nom: K ;
+Pron+Sg: PRONQ-STAV-CASES ;
+Pron+Pl+Nom:ъяс K ;
+Pron+Pl:ъяс PRONQ-STAV-CASES ;

LEXICON PRONQ-STAV
!2017-09-20+Pron+Abs: WordEnd ;
+Pron+Sg+Nom: K ;
+Pron+Sg+ZeroAcc: K ;
+Pron+Sg: PRONQ-STAV-CASES ;

LEXICON PRONQ-STAV-CASES
+Nom+PxSg2:ыд K ;
+Nom+PxSg3:ыс K ;
+Nom+PxPl1:ным K ;
+Nom+PxPl2:ныд K ;
+Nom+PxPl3:ныс K ;

+Ins:ӧн K ;
+Ins+PxSg1:нам K ;
+Ins+PxSg2:над K ;
+Ins+PxSg3:нас K ;
+Ins+PxPl1:нымӧн K ; ! Should there be a Der/ӧн tag?
+Ins+PxPl2:ныдӧн K ; ! Should there be a Der/ӧн tag?
+Ins+PxPl3:нысӧн K ; ! Should there be a Der/ӧн tag?

+Ela:ысь K ;
+Ela+PxSg3:сьыс K ;
+Ela+PxPl1:сьыным K ;
+Ela+PxPl1:нымысь K ;
+Ela+PxPl2:сьыныд K ;
+Ela+PxPl2:ныдысь K ;
+Ela+PxPl3:сьыныс K ;
+Ela+PxPl3:нысысь K ;

+Gen:лӧн K ;
+Gen+PxSg3:ыслӧн K ;
+Gen+PxPl1:нымлӧн K ;
+Gen+PxPl2:ныдлӧн K ;
+Gen+PxPl3:ныслӧн K ;

+Acc:ӧс K ;
+Acc+PxSg1:ӧс K ;
+Acc2+PxSg2:тӧ K ;
+Acc3+PxSg3:сӧ K ;
+Acc+PxPl1:нымӧс K ;
+Acc2+PxPl2:нытӧ K ;
+Acc3+PxPl3:нысӧ K ;

+Dat+PxSg2:ыдлы K ; ! This far 2017-03-19
+Dat+PxSg3:ыслы K ;
+Dat+PxPl1:нымлы K ;
+Dat+PxPl2:ныдлы K ;
+Dat+PxPl3:ныслы K ;

+Abl:лысь K ;
+Abl+PxSg3:ыслысь K ;
+Abl+PxPl1:нымлысь K ;
+Abl+PxPl2:ныдлысь K ;
+Abl+PxPl3:ныслысь K ;

+Com:кӧд K ;
+Com+PxSg3:ыскӧд K ;
+Com+Com+PxPl1:нымкӧд K ;
+Com+PxPl2:ныдкӧд K ;
+Com+PxPl3:ныскӧд K ;

+Car:тӧг K ;
+Car+PxSg3:ыстӧг K ;
+Car+PxPl1:нымтӧг K ;
+Car+PxPl2:ныдтӧг K ;
+Car+PxPl3:ныстӧг K ;

+Cns:ла K ;
+Cns+PxSg3:ысла K ;
+Cns+PxPl1:нымла K ;
+Cns+PxPl2:ныдла K ;
+Cns+PxPl3:нысла K ;

+Cmpr+PxSg3:сяыс K ;

+Ine:ын K ;
+Ine+PxPl1:нымын K ;
+Ine+PxPl2:ныдын K ;
+Ine+PxPl3:нысын K ;

+Ill:ӧ K ;
+Ill+PxPl1:нымӧ K ;
+Ill+PxPl2:ныдӧ K ;
+Ill+PxPl3:нысӧ K ;

+Egr:сянь K ;
+Egr+PxSg3:сяньыс K ;
+Egr+PxSg3:ыссянь K ;
+Egr+PxPl1:сяньным K ;
+Egr+PxPl1:нымсянь K ;
+Egr+PxPl2:сяньныд K ;
+Egr+PxPl2:ныдсянь K ;
+Egr+PxPl3:ныссянь K ;

+Apr:лань K ;
+Apr+PxSg3:ланьыс K ;
+Apr+PxSg3:ыслань K ;
+Apr+PxPl1:нымлань K ;
+Apr+PxPl2:ныдлань K ;
+Apr+PxPl3:ныслань K ;

+Tra:ті K ;
+Tra+PxSg3:тіыс K ;
+Tra+PxSg3:ысті K ;
+Tra+PxPl1:нымті K ;
+Tra+PxPl2:ныдті K ;
+Tra+PxPl3:нысті K ;

+Ter:ӧдз K ;
+Ter+PxSg3:ысӧдз K ;
+Ter+PxPl1:нымӧдз K ;
+Ter+PxPl2:ныдӧдз K ;
+Ter+PxPl3:нысӧдз K ;



! COMPARATIVE ; ! sg/pl?

LEXICON PRONQ-COLL
+Pron+Coll+Sg: PRONQ-COLL-CASE ; ! sg/pl?

! быдӧн, ӧтнасӧн, ӧтнамӧн, ӧтнадӧн, ставӧн, кымынӧн, унаӧн, кыкӧн

LEXICON PRONQ-COLL-CASE
!ӧнія коми кыв 2000: 183
+Nom: K ;
+Gen:лӧн K ;
+Gen+PxSg2:ыдлӧн K ;
+Gen+PxSg3:ыслӧн K ;
+Abl:лысь K ;
+Abl+PxSg2:ыдлысь K ;
+Abl+PxSg3:ыслысь K ;
+Dat:лы K ;
+Dat+PxSg2:ыдлысь K ;
+Dat+PxSg3:ыслысь K ;
+Acc:ӧс K ;
+Acc2+PxSg2:тӧ K ;
+Acc3+PxSg3:сӧ K ;
+Ins:ӧн K ;
+Com:кӧд K ;
+Com+PxSg2:ыдкӧд K ;
+Com+PxSg3:ыскӧд K ;
+Car:тӧг K ;
+Car+PxSg2:тӧгыд K ;
+Car+PxSg3:тӧгыс K ;
+Cns:ла K ;
+Cns+PxSg2:ыдла K ;
+Cns+PxSg3:ысла K ;
+Ine:ын K ;
+Ela:ысь K ;
+Ela+PxSg2:сьыд K ;
+Ela+PxSg3:сьыс K ;
+Ill:ӧ K ;
+Apr:лань K ;
+Apr+PxSg2:ланьыд K ;
+Apr+PxSg3:ланьыс K ;
+Egr:сянь K ;
+Egr+PxSg2:сяньыд K ;
+Egr+PxSg3:сяньыс K ;
+Tra:ті K ;
+Ter:ӧдз K ;
+Ter+PxSg2:ӧдзыд K ;
+Ter+PxSg3:ӧдзыс K ;

LEXICON PRONQ-ATAG
+Pron+Qnt+A: K ;
+Pron+Qnt+A+Der/MWN: Noun1 ;

LEXICON PRON-QNT_
+Pron+Qnt: COMPARATIVE ; ! sg/pl?

LEXICON PRON-QNT_-PL
+Pron+Qnt+Pl: WordEnd ;

LEXICON PRON-QNT-IS_ILL
+Pron+Qnt+Ill: K ;


LEXICON PRON-NEG-End
+Pron+Neg: K ;

LEXICON PRON-NEG-A
+Pron+Der+Neg+A: WordEnd ;
+Pron+Der+Neg+A+Sg+Nom: WordEnd ;
+Pron+Der+Neg+A+Sg: PRON-NEG-OBL ;
!+Pron+Neg+A+Der/MWN+N+Sg+Nom: WordEnd ;
!+Pron+Neg+A+Der/MWN+N+Sg: PRON-NEG-OBL ;

LEXICON PRON-NEG-ADV
+Pron+Der+Neg+Adv: COMPARATIVE ;

LEXICON PRON-NEG-IN_LA
+Pron+Neg+Der/ла+Spat: PRON-NEG-SPAT ;

LEXICON PRON-NEG-ITER
+Pron+Der+Neg+Der/ысь+Adv: K ; !+Iter

LEXICON PRON-NEG
+Pron+Neg+Sg+Nom: WordEnd ;
+Pron+Neg+Sg: PRON-NEG-OBL ;


LEXICON PRON-NEG-COLL
+Coll:нан PRON-NEG-COLL-PERS ;
+Coll: PRON-NEG-COLL-PERS ; !+ZeroColl
!
+Coll+Acc2+PxPl2:ныдтӧ K ; !+ZeroColl
+Coll+Acc2+PxPl2:наннытӧ K ;

LEXICON PRON-NEG-COLL-PERS
+PxPl1:ным PRON-NEG-COLL-CASES ;
+PxPl2:ныд PRON-NEG-COLL-CASES ;
+PxPl3:ныс PRON-NEG-COLL-CASES ;
!
+Acc+PxPl1:нымӧс K ;
+Acc2+PxPl2:нытӧ K ;
+Acc3+PxPl3:ныссӧ K ;

LEXICON PRON-NEG-COLL-CASES
+Nom: K ;
 PRON-NEG-OBL ;

LEXICON PRON-NEG-OBL
! 2011-11-15 This is used by PRON-NEG
! also тэа-мэа inclusive 1Du, сыа-мэа exclusive 1Du
+Cmpl: WordEnd ;
+Gen:лӧн WordEnd ;
+Abl:лысь K ; !
+Dat:лы WordEnd ;
+Acc:ӧс WordEnd ;
+Acc3:сӧ WordEnd ;
+Ins:ӧн K ;
+Com:кӧд K ;
+Car:тӧг K ;
+Cns:ла K ;
 PRON-NEG-SPAT ;

LEXICON PRON-NEG-SPAT
+Ine:ын K ;
+Ela:ысь K ; 
+Ill:ӧ K ; !
+Apr:лань K ;
+Egr:сянь K ;
+Tra:ті K ;
+Ter:ӧдз K ;
 
LEXICON PRON-NEG-IS_ILL
+Pron+Neg+Ill: K ;

!! Tagged in the src/morphology/stems/pronouns.xml file
LEXICON PRON_
+Pron: K ;
LEXICON PronInterr
+Pron+Interr: WordEnd ;
LEXICON PronIndef
+Pron+Indef: WordEnd ;

!! Word-final cases

LEXICON ABL-WORD-END
+Abl:лысь WordEnd ;
LEXICON ACC-WORD-END
+Acc:ӧс WordEnd ;
LEXICON CAR-WORD-END
+Car:тӧг WordEnd ;
LEXICON CNS-WORD-END
+Cns:ла WordEnd ;
LEXICON COM-WORD-END
+Com:кӧд WordEnd ;
LEXICON EGR-WORD-END
+Egr:сянь WordEnd ;
LEXICON ELA-WORD-END
+Ela:ысь WordEnd ;
LEXICON GEN-WORD-END
+Gen:лӧн WordEnd ;
LEXICON ILL-WORD-END
+Ill:ӧ WordEnd ;
LEXICON INE-WORD-END
+Ine:ын WordEnd ;
LEXICON INS-WORD-END
+Ins:ӧн WordEnd ;
LEXICON NOM-WORD-END
+Nom: WordEnd ;
LEXICON PRL-WORD-END
+Prl:ӧд WordEnd ;
LEXICON TER-WORD-END
+Ter:ӧдз WordEnd ;

! vim: set ft=xfst-lexc:
