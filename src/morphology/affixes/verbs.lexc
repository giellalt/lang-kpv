

! ====================================== !
!! !!!Verbal morphology               
! ====================================== !

LEXICON V_
+WORK: # ;

!! !!Closed class verbs
! ------------------

LEXICON VERBNEGATIVE
!Present Neg
оз+V+Neg+Ind+Prs+Sg1:ог MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Sg2:он MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Sg3:оз MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl1:огӧ MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl2:онӧ MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl1:огӧй MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl2:онӧй MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl3:оз MatrixVerb-Comparison ;

оз+Err/Dial+V+Neg+Ind+Prs+Pl1:оге MatrixVerb-Comparison ;
оз+Err/Dial+V+Neg+Ind+Prs+Pl2:онэ MatrixVerb-Comparison ;
оз+Err/Dial+V+Neg+Ind+Prs+Pl1:огей MatrixVerb-Comparison ;
оз+Err/Dial+V+Neg+Ind+Prs+Pl2:онэй MatrixVerb-Comparison ;

!Past Neg
оз+V+Neg+Ind+Prt1+Sg1:эг MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt1+Sg2:эн MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt1+Sg3:эз MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt1+Pl1:эг MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt1+Pl2:эн MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt1+Pl1:эгӧ MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt1+Pl2:энӧ MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt1+Pl3:эз MatrixVerb-Comparison ;
оз+Err/Dial+V+Neg+Ind+Prt1+Pl1:эге MatrixVerb-Comparison ;
оз+Err/Dial+V+Neg+Ind+Prt1+Pl2:энэ MatrixVerb-Comparison ;

!Imperative
оз+V+Neg+Imprt+Sg2:эн K ;
оз+V+Neg+Imprt+Pl2:эн K ;
оз+V+Neg+Imprt+Pl2:энӧ K ;
оз+V+Neg+Imprt+Pl2:энӧй K ;
оз+Err/Dial+V+Neg+Imprt+Pl2:энэ K ;
!A tag is needed to indicate this clitic-like лы segment
оз+V+Neg+Imprt+Sg2:энлы K ;
оз+V+Neg+Imprt+Pl2:энлӧй K ;

!Final
моз+V+Neg+Final+1:мог K ; ! мед ог _so that I/we won't_
моз+V+Neg+Final+2:мон K ; ! мед он
моз+V+Neg+Final+3:моз K ; ! мед оз

! Negative copula supletive present forms
абу+V+Neg+Cop+Ind+Sg:абу K ; ! Aux these are supletive. Past is эз вӧв
абу+V+Neg+Cop+Ind+Pl:абуӧсь K ; ! Aux these are supletive. Past is эз вӧвны



!! !!Open class verbs
! ----------------

!! Some Flag diacritic lines are with regexes, other with aligned zeros. We want to migrate to
!! regexes < … > , for readability reasons (sic!)

LEXICON IV_ЛОКНЫ
+V+IV: V-0_ЛОКНЫ ;
!2018-10-21!LEXICON TV_ЛОКНЫ
!2018-10-21!+V+TV: V-0_ЛОКНЫ ;
!2018-10-21!LEXICON BV_ЛОКНЫ
!2018-10-21!+V+IV: V-0_ЛОКНЫ ;
!2018-10-21!+V+TV: V-0_ЛОКНЫ ;

!2018-10-21!LEXICON V_ЛОКНЫ !!≈ * __@CODE@__ One verb only, локны:лок
!2018-10-21!+V+IV:  V-0_ЛОКНЫ ;
LEXICON V-0_ЛОКНЫ !!≈ * __@CODE@__ One verb only, локны:лок
@U.CONJ-INF.YES@+Inf:@U.CONJ-INF.YES@%>ны VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.1@+Inf+Px1:@U.CONJ-INF.YES@@U.CONJ-POSS.1@%>ным VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.2@+Inf+Px2:@U.CONJ-INF.YES@@U.CONJ-POSS.2@%>ныд VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.3@+Inf+Px3:@U.CONJ-INF.YES@@U.CONJ-POSS.3@%>ныс VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.2ACC@+Inf+Px2+Acc:@U.CONJ-INF.YES@@U.CONJ-POSS.2ACC@%>нытӧ VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.3ACC@+Inf+Px3+Acc:@U.CONJ-INF.YES@@U.CONJ-POSS.3ACC@%>нысӧ VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.2ACC@+Err/Dial+Inf+Px2+Acc:@U.CONJ-INF.YES@@U.CONJ-POSS.2ACC@%>нытэ VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.3ACC@+Err/Dial+Inf+Px3+Acc:@U.CONJ-INF.YES@@U.CONJ-POSS.3ACC@%>нысэ VerbEnd-Comparison ;
@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@+ConNeg+Pl3:@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@%>ны VerbEnd ;
@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@+ConNeg:@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@ VerbEnd ;
@U.CONJ-INF.NO@@U.CONJ-MX.IMP@@U.CONJ-NX.SG@@U.CONJ-PX.2@+Imprt+Sg2:@U.CONJ-INF.NO@@U.CONJ-MX.IMP@@U.CONJ-NX.SG@@U.CONJ-PX.2@ VerbEnd ;
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@%>т VerbDer_КУТНЫ ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@%>т VerbDer-V ; !
:т  VerbConj_КУТНЫ ; !а
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@т VerbDer-ONSET_И/І ;


LEXICON VR_ШУНЫ !!≈ * __@CODE@__ 
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ Verb-nyDer ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer-V ; !
+V+Refl: VerbConj ; !а
+V+Refl: VerbConj-V ; !б
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@  VerbDer-ONSET_И/І ;
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@а Verb-nyDer-ONSET-А ;

LEXICON IV_ШУНЫ
+V+IV: V-0_ШУНЫ ;
LEXICON TV_ШУНЫ
+V+TV: V-0_ШУНЫ ;
LEXICON BV_ШУНЫ
+V+IV: V-0_ШУНЫ ;
+V+TV: V-0_ШУНЫ ;

LEXICON V_ШУНЫ !!≈ * __@CODE@__ шуны:шу янӧдны, контролируйтны. Ending in -дны, -тны, -Cны, -Vны.
+V+IV: V-0_ШУНЫ ;
LEXICON V-0_ШУНЫ !!≈ * __@CODE@__ шуны:шу янӧдны, контролируйтны. Ending in -дны, -тны, -Cны, -Vны.
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ Verb-nyDer ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer-V ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@  VerbDer-ONSET_И/І ;
@U.CONJ-INF.YES@+Refl:@U.CONJ-INF.YES@а Verb-nyDer-ONSET-А ;
: VerbConj ; !а
: VerbConj-V ; !б

LEXICON VR_АМНЫ !!≈ * __@CODE@__
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ Verb-nyDer ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer-V ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@  VerbDer-ONSET_И/І ;
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@а Verb-nyDer-ONSET-А ;
+V+Refl: VerbConj_АМНЫ ; !а
+V+Refl: VerbConj-V ; !б

LEXICON IV_АМНЫ
+V+IV:  V-0_АМНЫ ;
LEXICON TV_АМНЫ
+V+TV:  V-0_АМНЫ ;
LEXICON BV_АМНЫ
+V+IV:  V-0_АМНЫ ;
+V+TV:  V-0_АМНЫ ;

LEXICON V_АМНЫ !!≈ * __@CODE@__ амны:ам Ending in -мны, -жны, -дзны,
+V+IV:  V-0_АМНЫ ;
LEXICON V-0_АМНЫ !!≈ * __@CODE@__ амны:ам Ending in -мны, -жны, -дзны,
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ Verb-nyDer ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer-V ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer-ONSET_И/І ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@а Verb-nyDer-ONSET-А ;
: VerbConj_АМНЫ ; !а
: VerbConj-V ; !б

!2018-10-21!LEXICON VR_КЫЙНЫ !!≈ * __@CODE@__ 
!2018-10-21!+V+Refl@U.CONJ-INF.YES@:й0@U.CONJ-INF.YES@ Verb-nyDer-ONSET-CONS ; !
!2018-10-21!+V+Refl@U.CONJ-INF.YES@:й0@U.CONJ-INF.YES@ Verb-nyDer-ONSET-Ы ; !
!2018-10-21!+V+Refl@U.CONJ-INF.YES@:й0@U.CONJ-INF.YES@ Verb-nyDer-ONSET-Я ; !
!2018-10-21!+V+Refl@U.CONJ-INF.YES@:й0@U.CONJ-INF.YES@ VerbDer_КЫЙНЫ ; !
!2018-10-21!+V+Refl@U.CONJ-INF.YES@:й0@U.CONJ-INF.YES@ VerbDer-V ; !
!2018-10-21!+V+Refl:й VerbConj_КЫЙНЫ ; !а
!2018-10-21!+V+Refl:й VerbConj-V ; !б

LEXICON IV_КЫЙНЫ
+V+IV: V-0_КЫЙНЫ ;
LEXICON TV_КЫЙНЫ
+V+TV: V-0_КЫЙНЫ ;
LEXICON BV_КЫЙНЫ
+V+IV: V-0_КЫЙНЫ ;
+V+TV: V-0_КЫЙНЫ ;
LEXICON V_КЫЙНЫ !!≈ * __@CODE@__ кыйны:кы Ending in -йны, 
+V+IV:  V-0_КЫЙНЫ ;

LEXICON V-0_КЫЙНЫ !!≈ * __@CODE@__ кыйны:кы Ending in -йны, 
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@й Verb-nyDer-ONSET-CONS ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@й Verb-nyDer-ONSET-Ы ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@й Verb-nyDer-ONSET-А/Я ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@й VerbDer_КЫЙНЫ ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@й VerbDer-V ; !
:й VerbConj_КЫЙНЫ ; !а
:й VerbConj-V ; !б

LEXICON VR_КУТНЫ !!≈ * __@CODE@__
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ Verb-nyDer ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer_КУТНЫ ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer-V ; !
+V+Refl: VerbConj_КУТНЫ ; !а
+V+Refl: VerbConj-V ; !б

LEXICON IV_КУТНЫ
+V+IV: V-0_КУТНЫ ;
LEXICON TV_КУТНЫ
+V+TV: V-0_КУТНЫ ;
LEXICON BV_КУТНЫ
+V+IV: V-0_КУТНЫ ;
+V+TV: V-0_КУТНЫ ;
LEXICON V_КУТНЫ !!≈ * __@CODE@__ шуны:шу янӧдны, контролируйтны. Ending in -дны, -тны, -Cны, -Vны.
+V+IV:  V-0_КУТНЫ ;
LEXICON V-0_КУТНЫ !!≈ * __@CODE@__ шуны:шу янӧдны, контролируйтны. Ending in -дны, -тны, -Cны, -Vны.
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ Verb-nyDer ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer_КУТНЫ ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer-V ; !
: VerbConj_КУТНЫ ; !а
: VerbConj-V ; !б

LEXICON VR_НУАВНЫ !!≈ * __@CODE@__ нуавны:нуа
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@в Verb-nyDer-ONSET-CONS ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@л Verb-nyDer-ONSET-VOW ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@л VerbDer_КУТНЫ ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@в VerbDer-V ; !
+V+Refl:л VerbConj_КУТНЫ ; !а
+V+Refl:в VerbConj-V ; !б

LEXICON IV_НУАВНЫ
+V+IV: V-0_НУАВНЫ ;
LEXICON TV_НУАВНЫ
+V+TV: V-0_НУАВНЫ ;
LEXICON BV_НУАВНЫ
+V+IV: V-0_НУАВНЫ ;
+V+TV: V-0_НУАВНЫ ;
LEXICON V_НУАВНЫ !!≈ * __@CODE@__ нуавны:нуа
+V+IV:  V-0_НУАВНЫ ;
LEXICON V-0_НУАВНЫ !!≈ * __@CODE@__ нуавны:нуа
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@в Verb-nyDer-ONSET-CONS ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@л Verb-nyDer-ONSET-VOW ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@л VerbDer_КУТНЫ ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@в VerbDer-V ; !
:л VerbConj_КУТНЫ ; !а
:в VerbConj-V ; !! Consonant-onset

LEXICON VR_АРТАСЬНЫ !!≈ * __@CODE@__
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ Verb-nyDer-ONSET-CONS ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ Verb-nyDer-ONSET-Ы ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ Verb-nyDer-ONSET-А/Я ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer_АРТАСЬНЫ ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer-V ; !
+V+Refl: VerbConj ; !а
+V+Refl: VerbConj-V ; !б

LEXICON IV_АРТАСЬНЫ 
+V+IV: V-0_АРТАСЬНЫ ;
LEXICON TV_АРТАСЬНЫ 
+V+TV:  V-0_АРТАСЬНЫ ;
!2018-10-21!LEXICON BV_АРТАСЬНЫ 
!2018-10-21!+V+IV:  V-0_АРТАСЬНЫ ;
!2018-10-21!+V+TV:  V-0_АРТАСЬНЫ ;

LEXICON V_АРТАСЬНЫ !!≈ * __@CODE@__ артасьны:артась 
+V+IV:  V-0_АРТАСЬНЫ ;
LEXICON V-0_АРТАСЬНЫ !!≈ * __@CODE@__ артасьны:артась 
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ Verb-nyDer ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer_АРТАСЬНЫ ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@я Verb-nyDer-ONSET-А ;
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer-V ; !
: VerbConj ; !а
: VerbConj-V ; !б

LEXICON VR_ЛЫДДЬЫНЫ !!≈ * __@CODE@__ 
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ Verb-ynyDer ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ы VerbDer-V ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@  VerbDer-ONSET_И/І ;
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@я Verb-nyDer-ONSET-Я ;
+V+Refl: VerbConj ; ! check this!!!
+V+Refl:ы VerbConj-V ; ! check this!!!

LEXICON IV_ЛЫДДЬЫНЫ
+V+IV: V-0_ЛЫДДЬЫНЫ ;
LEXICON TV_ЛЫДДЬЫНЫ
+V+TV: V-0_ЛЫДДЬЫНЫ ;
LEXICON BV_ЛЫДДЬЫНЫ
+V+IV: V-0_ЛЫДДЬЫНЫ ;
+V+TV: V-0_ЛЫДДЬЫНЫ ;
LEXICON V_ЛЫДДЬЫНЫ !!≈ * __@CODE@__ лыддьыны:лыддь ! ярмыны. Multiple-syllable verb ending in -ыны.
+V+IV:  V-0_ЛЫДДЬЫНЫ ;
LEXICON V-0_ЛЫДДЬЫНЫ !!≈ * __@CODE@__ лыддьыны:лыддь ! ярмыны. Multiple-syllable verb ending in -ыны.
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ Verb-ynyDer ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ы VerbDer-V ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@  VerbDer-ONSET_И/І ;
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@я Verb-nyDer-ONSET-Я ;
: VerbConj ; ! check this!!!
:ы VerbConj-V ; ! check this!!!

LEXICON VR_ВОЛЫНЫ !!≈ * __@CODE@__ 
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ Verb-ynyDer ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ VerbDer_КУТНЫ ; !
@U.CONJ-INF.YES@+V+Refl:@U.CONJ-INF.YES@ы VerbDer-V ; !
+V+Refl: VerbConj_КУТНЫ ; ! check this!!!
+V+Refl:ы VerbConj-V ; ! check this!!!

LEXICON IV_ВОЛЫНЫ 
+V+IV: V-0_ВОЛЫНЫ ;
LEXICON TV_ВОЛЫНЫ 
+V+TV: V-0_ВОЛЫНЫ ;
LEXICON BV_ВОЛЫНЫ 
+V+IV: V-0_ВОЛЫНЫ ;
+V+TV: V-0_ВОЛЫНЫ ;

LEXICON V_ВОЛЫНЫ !!≈ * __@CODE@__ волыны:лыддь ! ярмыны. Multiple-syllable verb ending in -ыны.
+V+IV:  V-0_ВОЛЫНЫ ;
LEXICON V-0_ВОЛЫНЫ !!≈ * __@CODE@__ волыны:лыддь ! ярмыны. Multiple-syllable verb ending in -ыны.
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ Verb-ynyDer ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer_КУТНЫ ; !
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ы VerbDer-V ; !
: VerbConj_КУТНЫ ; ! check this!!!
:ы VerbConj-V ; ! check this!!!

! First model: ы added as part of the suffix in б
! суффикс а
! янӧд -----------\
!                   ышт, ан...
! ярм ------------/

! суффикс б
! янӧд -----------\
!                   ны, 
! ярм ----- ы ----/


! Second model: ы added earlier, and not as part of the suffix in б
! суффикс а
! янӧд -----------\
!                   ышт, ан...
! ярм ------------/

! суффикс б
! янӧд -----------\
!                   ыны, 
! ярм ------------/



! =================== !
!! !!Verb conjugation 
! =================== !

LEXICON VerbConj-V !!≈ * __@CODE@__  суф б -- ы 
@U.CONJ-INF.YES@+Inf:@U.CONJ-INF.YES@%>ны VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.1@+Inf+Px1:@U.CONJ-INF.YES@@U.CONJ-POSS.1@%>ным VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.2@+Inf+Px2:@U.CONJ-INF.YES@@U.CONJ-POSS.2@%>ныд VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.3@+Inf+Px3:@U.CONJ-INF.YES@@U.CONJ-POSS.3@%>ныс VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.2ACC@+Inf+Px2+Acc:@U.CONJ-INF.YES@@U.CONJ-POSS.2ACC@%>нытӧ VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.3ACC@+Inf+Px3+Acc:@U.CONJ-INF.YES@@U.CONJ-POSS.3ACC@%>нысӧ VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.2ACC@+Err/Dial+Inf+Px2+Acc:@U.CONJ-INF.YES@@U.CONJ-POSS.2ACC@%>нытэ VerbEnd-Comparison ;
@U.CONJ-INF.YES@@U.CONJ-POSS.3ACC@+Err/Dial+Inf+Px3+Acc:@U.CONJ-INF.YES@@U.CONJ-POSS.3ACC@%>нысэ VerbEnd-Comparison ;
@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@+ConNeg+Pl3:@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@%>ны VerbEnd ;
@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@+ConNeg:@U.CONJ-INF.NO@@U.CONJ-CONNEG.YES@ VerbEnd ;
@U.CONJ-INF.NO@@U.CONJ-MX.IMP@@U.CONJ-NX.SG@@U.CONJ-PX.2@+Imprt+Sg2:@U.CONJ-INF.NO@@U.CONJ-MX.IMP@@U.CONJ-NX.SG@@U.CONJ-PX.2@ VerbEnd ;

LEXICON VerbConj !!≈ * __@CODE@__ суф а
@U.CONJ-INF.NO@ Finiteforms ;
@U.CONJ-INF.YES@ Non-finiteforms ;

LEXICON VerbConj_КЫЙНЫ !!≈ * __@CODE@__ суф а
@U.CONJ-INF.NO@ Finiteforms_КЫЙНЫ ;
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer_КЫЙНЫ ;

LEXICON VerbConj_АМНЫ !!≈ * __@CODE@__ суф а
@U.CONJ-INF.NO@ Finiteforms_АМНЫ ;
@U.CONJ-INF.YES@ Non-finiteforms ;

LEXICON VerbConj_КУТНЫ !!≈ * __@CODE@__ суф а
@U.CONJ-INF.NO@ Finiteforms_КУТНЫ ;
@U.CONJ-INF.YES@:@U.CONJ-INF.YES@ VerbDer_КУТНЫ ; !


LEXICON Finiteforms  !!≈ * __@CODE@__ Gives linking vowels for 3 tenses
 present ;
 future ;
 preterite ;
 imperative ;

LEXICON Finiteforms_КЫЙНЫ  !!≈ * __@CODE@__ Gives linking vowels for 3 tenses
 present_КЫЙНЫ ;
 future_КЫЙНЫ ;
: preterite_КЫЙНЫ ;
: imperative ;

LEXICON Finiteforms_АМНЫ  !!≈ * __@CODE@__ Gives linking vowels for 3 tenses
 present_АМНЫ ;
 future_АМНЫ ;
 preterite_АМНЫ ;
 imperative ;

LEXICON Finiteforms_КУТНЫ  !!≈ * __@CODE@__ Gives linking vowels for 3 tenses
 present_КУТНЫ ;
 future_КУТНЫ ;
 preterite_КУТНЫ ;
 imperative ;

LEXICON present
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>%{ая%} PresPret1 ; ! 1,2  Present
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>ӧ PresPret1-SgPl3 ; ! 3
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs+Err/Dial:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>э PresPret1-SgPl3 ; ! 3
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>ӧ PresPret1-SgPl3 ; ! 3
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs+Pl3+Err/Dial:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>ӧныс K ; ! dialectal forms
@U.CONJ-MX.IND@@U.CONJ-TX.PRES@+Ind+Prs+Pl3+Err/Dial:@U.CONJ-MX.IND@@U.CONJ-TX.PRES@%>эныс K ; ! dialectal forms

LEXICON present_КЫЙНЫ
 present ;
!2018-01-29!+Ind+Prs@U.CONJ-MX.IND@@U.CONJ-TX.PRES@:%>%{ая%}@U.CONJ-MX.IND@@U.CONJ-TX.PRES@ PresPret1 ; ! 1,2  Present
!2018-01-29!+Ind+Prs@U.CONJ-MX.IND@@U.CONJ-TX.PRES@:%>ӧ@U.CONJ-MX.IND@@U.CONJ-TX.PRES@ PresPret1-SgPl3 ; ! 3

LEXICON present_АМНЫ
 present ;
!2018-01-29!+Ind+Prs@U.CONJ-MX.IND@@U.CONJ-TX.PRES@:%>%{ая%}@U.CONJ-MX.IND@@U.CONJ-TX.PRES@ PresPret1 ; ! 1,2  Present
!2018-01-29!+Ind+Prs@U.CONJ-MX.IND@@U.CONJ-TX.PRES@:%>ӧ@U.CONJ-MX.IND@@U.CONJ-TX.PRES@ PresPret1-SgPl3 ; ! 3

LEXICON present_КУТНЫ
 present ;
!2018-01-29!+Ind+Prs@U.CONJ-MX.IND@@U.CONJ-TX.PRES@:%>%{ая%}@U.CONJ-MX.IND@@U.CONJ-TX.PRES@ PresPret1 ; ! 1,2  Present
!2018-01-29!+Ind+Prs@U.CONJ-MX.IND@@U.CONJ-TX.PRES@:%>ӧ@U.CONJ-MX.IND@@U.CONJ-TX.PRES@ PresPret1-SgPl3 ; ! 3

LEXICON future
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@%>%{ая%} PresPret1 ; ! 1,2  Future
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@%>%{ая%}с PresPret1-SgPl3 ; ! 3 PresPret1-SgPl3   ХХХ check this

LEXICON future_КЫЙНЫ
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@%>%{ая%} PresPret1 ; ! 1,2  Future
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@%>%{ая%}с PresPret1-SgPl3 ; ! 3 PresPret1-SgPl3   ХХХ check this

LEXICON future_АМНЫ
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@%>%{ая%} PresPret1 ; ! 1,2  Future
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@%>%{ая%}с PresPret1-SgPl3 ; ! 3 PresPret1-SgPl3   ХХХ check this

LEXICON future_КУТНЫ
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@%>%{ая%} PresPret1 ; ! 1,2  Future
@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@+Ind+Fut:@U.CONJ-MX.IND@@U.CONJ-TX.FUT@@U.CONJ-PX.3@%>%{ая%}с PresPret1-SgPl3 ; ! 3 PresPret1-SgPl3   ХХХ check this

LEXICON preterite
@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@+Ind+Prt1:@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@%>%{иі%} PresPret1 ; 
@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@+Ind+Prt1:@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@%>%{иі%}  P3 ; ! 3 
	Pret2a-0 ;
LEXICON Pret2a-0
@U.CONJ-MX.IND@@U.CONJ-TX.PRET2@+Ind+Prt2:@U.CONJ-MX.IND@@U.CONJ-TX.PRET2@%>ӧм Pret2a ;
@U.CONJ-MX.IND@@U.CONJ-TX.PRET2@+Err/Dial+Ind+Prt2:@U.CONJ-MX.IND@@U.CONJ-TX.PRET2@%>эм Pret2a ;


LEXICON preterite_КЫЙНЫ
@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@+Ind+Prt1:@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@%>%{иі%} PresPret1 ; 
@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@+Ind+Prt1:@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@%>%{иі%}  P3 ; ! 3 
:й	Pret2a-0 ;

LEXICON preterite_АМНЫ
 preterite ;
!2018-01-29!+Ind+Prt1@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@:%>%{иі%}@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@ PresPret1 ; 
!2018-01-29!+Ind+Prt1@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@:%>%{иі%}@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@  P3 ; ! 3 
!2018-01-29!	Pret2a-0 ;

LEXICON preterite_КУТНЫ
 preterite ;
!2018-01-29!+Ind+Prt1@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@:%>%{иі%}@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@ PresPret1 ; 
!2018-01-29!+Ind+Prt1@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@:%>%{иі%}@U.CONJ-MX.IND@@U.CONJ-TX.PRET1@  P3 ; ! 3 
!2018-01-29!	Pret2a-0 ;

LEXICON imperative
@U.CONJ-MX.IMP@@U.CONJ-NX.PL@@U.CONJ-PX.2@+Imprt+Pl2:@U.CONJ-MX.IMP@@U.CONJ-NX.PL@@U.CONJ-PX.2@%>ӧй VerbEnd ;
@U.CONJ-MX.IMP@@U.CONJ-NX.PL@@U.CONJ-PX.2@+Err/Dial+Imprt+Pl2:@U.CONJ-MX.IMP@@U.CONJ-NX.PL@@U.CONJ-PX.2@%>эй VerbEnd ;

LEXICON PossessiveSuffixes
+PxSg1:%>ым K ;
            PossessiveSuffixes_NoSg1 ;


LEXICON PossessiveSuffixes_NoSg1
:     K ; !The ZERO Tag is used for +PxSg1 2017-03-23
+PxSg2:%>ыд  K ;
+PxSg3:%>ыс  K ;
+PxPl1:%>ным K ;
+PxPl2:%>ныд K ;
+PxPl3:%>ныс K ;


LEXICON PresPret1  ! First and second person
@U.CONJ-NX.SG@@U.CONJ-PX.1@+Sg1:@U.CONJ-NX.SG@@U.CONJ-PX.1@ VerbEnd-Comparison ; 
@U.CONJ-NX.SG@@U.CONJ-PX.2@+Sg2:@U.CONJ-NX.SG@@U.CONJ-PX.2@%>н VerbEnd-Comparison ;
@U.CONJ-NX.PL@@U.CONJ-PX.1@+Pl1:@U.CONJ-NX.PL@@U.CONJ-PX.1@%>м VerbEnd-Comparison ; 
@U.CONJ-NX.PL@@U.CONJ-PX.1@+Pl1+Err/Orth:@U.CONJ-NX.PL@@U.CONJ-PX.1@%>мӧ VerbEnd-Comparison ; 
@U.CONJ-NX.PL@@U.CONJ-PX.1@+Pl1+Err/Orth+Err/Dial:@U.CONJ-NX.PL@@U.CONJ-PX.1@%>ме VerbEnd-Comparison ; 
@U.CONJ-NX.PL@@U.CONJ-PX.1@+Pl1:@U.CONJ-NX.PL@@U.CONJ-PX.1@%>мӧй VerbEnd-Comparison ;  ! imperatiivi / optatiivi ??
@U.CONJ-NX.PL@@U.CONJ-PX.1@+Pl1+Err/Dial:@U.CONJ-NX.PL@@U.CONJ-PX.1@%>мей VerbEnd-Comparison ;  ! imperatiivi / optatiivi ??
@U.CONJ-NX.PL@@U.CONJ-PX.2@+Pl2:@U.CONJ-NX.PL@@U.CONJ-PX.2@%>нныд VerbEnd-Comparison ;
@U.CONJ-NX.PL@@U.CONJ-PX.2@+Pl2+Err/Orth:@U.CONJ-NX.PL@@U.CONJ-PX.2@%>д VerbEnd-Comparison ; 

LEXICON P3         
@U.CONJ-PX.3@:@U.CONJ-PX.3@%>с PresPret1-SgPl3 ; ! This is regular
@U.CONJ-PX.3@+Err/Dial:@U.CONJ-PX.3@  PresPret1-SgPl3 ; ! This is usually restricted to intransitives


LEXICON PresPret1-SgPl3
  SG3 ; 
  PL3 ;
  
LEXICON SG3
@U.CONJ-NX.SG@@U.CONJ-PX.3@+Sg3:@U.CONJ-NX.SG@@U.CONJ-PX.3@ VerbEnd-Comparison ;
  
LEXICON PL3
@U.CONJ-NX.PL@@U.CONJ-PX.3@+Pl3:@U.CONJ-NX.PL@@U.CONJ-PX.3@%>ны VerbEnd-Comparison ;
@U.CONJ-NX.PL@@U.CONJ-PX.3@+Pl3:@U.CONJ-NX.PL@@U.CONJ-PX.3@%>ны VerbEnd-Comparison ;

LEXICON Pret2a
:%{ая%} PRET2-linkki-a ; 
   PRET2PL2 ; 
   PL3 ;   ! for continuation in different persons. 
                  !Could also have used the homonymic participle
                  ! as point of departure

LEXICON PRET2PL2
@U.CONJ-NX.SG@@U.CONJ-PX.2@+Sg2:@U.CONJ-NX.SG@@U.CONJ-PX.2@%>ыд VerbEnd-Comparison ;
@U.CONJ-NX.PL@@U.CONJ-PX.2@+Pl2:@U.CONJ-NX.PL@@U.CONJ-PX.2@%>ныд VerbEnd-Comparison ;
@U.CONJ-NX.PL@@U.CONJ-PX.2@+Pl20:@U.CONJ-NX.PL@@U.CONJ-PX.2@%>ныдӧсь VerbEnd-Comparison ; !224
@U.CONJ-NX.PL@@U.CONJ-PX.2@+Pl2+Err/Dial:@U.CONJ-NX.PL@@U.CONJ-PX.2@%>ныдэсь VerbEnd-Comparison ; !224
@U.CONJ-NX.PL@@U.CONJ-PX.3@+Pl3:@U.CONJ-NX.PL@@U.CONJ-PX.3@%>ны VerbEnd-Comparison ; !227

LEXICON PRET2-linkki-a
!+Sg1+Err/Orth: VerbEnd-Comparison ; ! no Sg1 for Prt2 in the standard
@U.CONJ-NX.SG@@U.CONJ-PX.1@+Sg1:@U.CONJ-NX.SG@@U.CONJ-PX.1@ VerbEnd-Comparison ;  
@U.CONJ-NX.SG@@U.CONJ-PX.3@+Sg3:@U.CONJ-NX.SG@@U.CONJ-PX.3@ VerbEnd-Comparison ;
!+Pl1+Err/Orth:%>ӧсь VerbEnd-Comparison ; ! no Pl1 for Prt2 in the standard
!+Pl1+Err/Orth+Err/Dial:%>эсь VerbEnd-Comparison ; ! no Pl1 for Prt2 in the standard
@U.CONJ-NX.PL@@U.CONJ-PX.1@+Pl1:@U.CONJ-NX.PL@@U.CONJ-PX.1@%>ӧсь VerbEnd-Comparison ; 
@U.CONJ-NX.PL@@U.CONJ-PX.3@+Pl3:@U.CONJ-NX.PL@@U.CONJ-PX.3@%>ӧсь VerbEnd-Comparison ;
@U.CONJ-NX.PL@@U.CONJ-PX.1@+Pl1+Err/Dial:@U.CONJ-NX.PL@@U.CONJ-PX.1@%>эсь VerbEnd-Comparison ; 
@U.CONJ-NX.PL@@U.CONJ-PX.3@+Pl3+Err/Dial:@U.CONJ-NX.PL@@U.CONJ-PX.3@%>эсь VerbEnd-Comparison ;

LEXICON MatrixVerb-Comparison
               K ;
+Der+Der/моз+Ger:%>моз K ;
+Comp:%>джык K ;
+Der/кодь:%>кодь K ;

!This is where Serial verbs are made possible
LEXICON VerbEnd-Comparison
 Serial-Verbs ;
 MatrixVerb-Comparison ;

LEXICON VerbEnd
 Serial-Verbs ;
 K ;

!! !!Derivation
! ==========

LEXICON Non-finiteforms
VerbDer ; ! to be merged with next
          !2011-11-02 dealing with serial verbs there may be
          ! reason to distinguish between V2V derivation and non-finite forms

LEXICON Verb-nyDer !!≈ * __@CODE@__ 
Verb-nyDer-ONSET-CONS ;
Verb-nyDer-ONSET-VOW ;

LEXICON Verb-nyDer-ONSET-CONS !!≈ * __@CODE@__ Derivation onset in consonant
!! verb-to-verb

!! This is fed by LEXICON V_ШУНЫ, and therefore certain corrections
!! must be made 2012-01-18

@C.CONJ-INF@@C.CONJ-POSS@+Der/л:@C.CONJ-INF@@C.CONJ-POSS@%>л  V_ВОЛЫНЫ ; !!босьтлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@%>лывл V_ВОЛЫНЫ ; !!босьтлывлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы:@C.CONJ-INF@@C.CONJ-POSS@%>сь V_ЛЫДДЬЫНЫ ; !! овсьыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы+Der/ышт:@C.CONJ-INF@@C.CONJ-POSS@%>сьышт V_ШУНЫ ; !!вежсьыштны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы+Der/ышт+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@%>сьыштлывл V_ВОЛЫНЫ ; !! пусьыштлывлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы+Der/ышт+Der/лы:@C.CONJ-INF@@C.CONJ-POSS@%>сьыштл V_ВОЛЫНЫ ; !!?? пусьыштлыны
! зь:зь  !!югзьыны
! Why do the suffixes above have ">" marking
! while the ones below do not?
@C.CONJ-INF@@C.CONJ-POSS@+Der/чы:@C.CONJ-INF@@C.CONJ-POSS@ч V_ЛЫДДЬЫНЫ ; !!босьтчыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/чы+Der/л:@C.CONJ-INF@@C.CONJ-POSS@чыл V_ШУНЫ ; !!дӧзмӧдчывны
@C.CONJ-INF@@C.CONJ-POSS@+Der/чы+Der/ышт+Der/лы:@C.CONJ-INF@@C.CONJ-POSS@чыштл V_ВОЛЫНЫ ; !!босьтчыштлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/чы+Der/ышт+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@чыштлывл V_ВОЛЫНЫ ; !! босьтчыштлывлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/чы+Der/ышт:@C.CONJ-INF@@C.CONJ-POSS@чышт V_ШУНЫ ; !!босьтчыштны

LEXICON Verb-nyDer-ONSET-VOW !!≈ * __@CODE@__ Derivation onset	in vowel
 Verb-nyDer-ONSET-Ы ;
 Verb-nyDer-ONSET-А ;

LEXICON Verb-nyDer-ONSET-Ы !!≈ * __@CODE@__ Derivation onset	in vowel
@C.CONJ-INF@@C.CONJ-POSS@+Der/ышт+Der/лы:@C.CONJ-INF@@C.CONJ-POSS@ыштл V_ВОЛЫНЫ ;   !!сёйыштлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/ышт+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@ыштлывл V_ВОЛЫНЫ ;  !!сёйыштлывлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/ышт:@C.CONJ-INF@@C.CONJ-POSS@ышт V_ШУНЫ ; !!сёйыштны

LEXICON Verb-nyDer-ONSET-А !!≈ * __@CODE@__ Derivation onset	in vowel
	Verb-nyDer-ONSET-А/Я ;
LEXICON Verb-nyDer-ONSET-А/Я
@C.CONJ-INF@@C.CONJ-POSS@+Der/ал:@C.CONJ-INF@@C.CONJ-POSS@%>%{ая%}л V_ШУНЫ ; !!сетавны
@C.CONJ-INF@@C.CONJ-POSS@+Der/овт:@C.CONJ-INF@@C.CONJ-POSS@%>овт V_ШУНЫ ; !!пановтны
@C.CONJ-INF@@C.CONJ-POSS@+Der/ась:@C.CONJ-INF@@C.CONJ-POSS@%>%{ая%}сь V_ШУНЫ ; !! босьтасьны

LEXICON Verb-nyDer-ONSET-Я !!≈ * __@CODE@__ Derivation onset	in vowel
@C.CONJ-INF@@C.CONJ-POSS@+Der/ал:@C.CONJ-INF@@C.CONJ-POSS@%>%{ая%}л V_ШУНЫ ; !!сетавны
@C.CONJ-INF@@C.CONJ-POSS@+Der/овт:@C.CONJ-INF@@C.CONJ-POSS@%>ёвт V_ШУНЫ ; !!пановтны
@C.CONJ-INF@@C.CONJ-POSS@+Der/ась:@C.CONJ-INF@@C.CONJ-POSS@%>%{ая%}сь V_ШУНЫ ; !! босьтасьны


!! verb-to-noun
 
LEXICON VerbDer !!≈ * __@CODE@__ 
 VerbDer-ONSET_Ӧ ; !! FIX THIS 2015-09-06
 VerbDer-ONSET_А/Я ; !!≈ * __@CODE@__ 

LEXICON VerbDer_КЫЙНЫ !!≈ * __@CODE@__
: VerbDer-ONSET_Ӧ ; !!≈ * __@CODE@__
: VerbDer-ONSET_Я ; !!≈ * __@CODE@__ 
: VerbDer-ONSET_И ; !!≈ * __@CODE@__

LEXICON VerbDer_КУТНЫ !!≈ * __@CODE@__
 VerbDer-ONSET_Ӧ ; !!≈ * __@CODE@__
 VerbDer-ONSET_А ; !!≈ * __@CODE@__ 
 VerbDer-ONSET_І ; !!≈ * __@CODE@__

LEXICON VerbDer_АРТАСЬНЫ !!≈ * __@CODE@__
: VerbDer-ONSET_Ӧ ; !!≈ * __@CODE@__
 VerbDer-ONSET_Я ; !!≈ * __@CODE@__ 
 VerbDer-ONSET_И ; !!≈ * __@CODE@__

LEXICON VerbDer-ONSET_Ӧ !!≈ * __@CODE@__ 
+Der+Der/ӧм+Der/NomAct:%>ӧм N_ ;
+Der+Der/ӧм+PastPtc:%>ӧм K ;
+Der+Der/ӧм+Der+Der/NomAct+Ger:%>ӧмпырысь K ;
+Der+Der/ӧм+Der/NomAct+N+Sg+Ins:%>ӧмӧн K ;
+Der+Der/ӧм+Der/Ин:%>ӧм%>%{иі%}н N_ ;

+Err/Dial+Der+Der/ӧм+Der/NomAct:%>эм N_ ;
+Err/Dial+Der+Der/ӧм+PastPtc:%>эм K ;
+Err/Dial+Der+Der/ӧм+Der+Der/NomAct+Ger:%>эмпырысь K ;
+Err/Dial+Der+Der/ӧм+Der/NomAct+N+Sg+Ins:%>эмен K ;
+Err/Dial+Der+Der/ӧм+Der/Ин:%>эм%>%{иі%}н N_ ;

+Der+Der/Ан+Der/Ин:%>%{ая%}н%>%{иі%}н N_ ;
+Der+Der/Ан+PrsPrc:%>%{ая%}н N_ ;
!2017-10-27+Der+Der/Ан+ActPrsPtc:%>%{ая%}н K ;
+Der+Der/Ан+PrsPtc:%>%{ая%}н K ;
+Der+Der/ысь+ActPrsPtc:%>ысь K ;
+Der+Der/ысь+Der/NomAg:%>ысь N_ ;

LEXICON VerbDer-ONSET_І !!≈ * __@CODE@__ 
  ! nonfinites
 VerbDer-ONSET_И/І ;

LEXICON VerbDer-ONSET_И !!≈ * __@CODE@__ 
 VerbDer-ONSET_И/І ;
LEXICON VerbDer-ONSET_И/І !!≈ * __@CODE@__ 
@U.CONJ-GER.IG@+Der+Der/иг+Ger:@U.CONJ-GER.IG@%>%{иі%}г VerbEnd ; ! gerund (at the same time as the action)
!352 Pl
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Pl:@U.CONJ-GER.IG@%>%{иі%}гъяс K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Pl+Ill:@U.CONJ-GER.IG@%>%{иі%}гъясӧ K ; !345
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Pl+Ill+Err/Dial:@U.CONJ-GER.IG@%>%{иі%}гъясэ K ; !345
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Pl+Ill+PxSg1:@U.CONJ-GER.IG@%>%{иі%}гъясам K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Pl+Ill+PxSg2:@U.CONJ-GER.IG@%>%{иі%}гъясад K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Pl+Ill+PxSg3:@U.CONJ-GER.IG@%>%{иі%}гъясас K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Pl+Ill+PxPl1:@U.CONJ-GER.IG@%>%{иі%}гъясаным K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Pl+Ill+PxPl2:@U.CONJ-GER.IG@%>%{иі%}гъясаныд K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Pl+Ill+PxPl3:@U.CONJ-GER.IG@%>%{иі%}гъясаныс K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Pl+Ins:@U.CONJ-GER.IG@%>%{иі%}гъясӧн K ;! 2000:359 
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Pl+Ins+Err/Dial:@U.CONJ-GER.IG@%>%{иі%}гъясэн K ;! 2000:359 
                                           !And Marina 2011-10-05 no Px
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Sg+Ill:@U.CONJ-GER.IG@%>%{иі%}гӧ K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Sg+Ill+Err/Dial:@U.CONJ-GER.IG@%>%{иі%}ге K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Sg+Ill+PxSg1:@U.CONJ-GER.IG@%>%{иі%}гам K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Sg+Ill+PxSg2:@U.CONJ-GER.IG@%>%{иі%}гад K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Sg+Ill+PxSg3:@U.CONJ-GER.IG@%>%{иі%}гас K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Sg+Ill+PxPl1:@U.CONJ-GER.IG@%>%{иі%}ганым K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Sg+Ill+PxPl2:@U.CONJ-GER.IG@%>%{иі%}ганыд K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Sg+Ill+PxPl3:@U.CONJ-GER.IG@%>%{иі%}ганыс K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Sg+Ins:@U.CONJ-GER.IG@%>%{иі%}гӧн PossessiveSuffixes_NoSg1 ; !Ӧнія коми кыв, 2000: 360
!351 Pl
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Sg+Ins+Err/Dial:@U.CONJ-GER.IG@%>%{иі%}ген PossessiveSuffixes_NoSg1 ; !2018-0129
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Sg+Ins+Der/моз+Ger:@U.CONJ-GER.IG@%>%{иі%}гӧн%>моз K ; !
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Sg+Ins+Der/моз+Ger+Err/Dial:@U.CONJ-GER.IG@%>%{иі%}ген%>моз K ; !
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Der/моз+Ger:@U.CONJ-GER.IG@%>%{иі%}гмоз PossessiveSuffixes_NoSg1 ; !371
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Der/сор+Ger:@U.CONJ-GER.IG@%>%{иі%}гсор K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Der/сор+Ger+Ins:@U.CONJ-GER.IG@%>%{иі%}гсорӧн K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Der/сор+Ger+Ins+Err/Dial:@U.CONJ-GER.IG@%>%{иі%}гсорен K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Der/тыр+Ger:@U.CONJ-GER.IG@%>%{иі%}гтыр K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Der/тырйи+Ger:@U.CONJ-GER.IG@%>%{иі%}гтырйи PossessiveSuffixes_NoSg1 ; !374
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Der/тыръя+Ger:@U.CONJ-GER.IG@%>%{иі%}гтыръя K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Der/чӧж+Ger:@U.CONJ-GER.IG@%>%{иі%}гчӧж PossessiveSuffixes_NoSg1 ; !377
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Der/чӧж+Ger+Ins:@U.CONJ-GER.IG@%>%{иі%}гчӧжӧн K ; !

@U.CONJ-GER.IG@+Der+Der/иг+Ger+Der/чӧж+Ger+Err/Dial:@U.CONJ-GER.IG@%>%{иі%}гчеж PossessiveSuffixes_NoSg1 ; !377
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Der/чӧж+Ger+Ins+Err/Dial:@U.CONJ-GER.IG@%>%{иі%}гчежен K ; !
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Der/кості+Ger:@U.CONJ-GER.IG@%>%{иі%}гкості PossessiveSuffixes_NoSg1 ; !352, 379
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Der/коста+Ger:@U.CONJ-GER.IG@%>%{иі%}гкоста K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Der/кежлӧ+Ger:@U.CONJ-GER.IG@%>%{иі%}гкежлӧ K ; !393
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Der/кежлӧ+Ger+Err/Dial:@U.CONJ-GER.IG@%>%{иі%}гкежлэ K ; !393

@U.CONJ-GER.IG@+Der+Der/иг+Ger+Der/кежлӧ+Ger+PxSg2:@U.CONJ-GER.IG@%>%{иі%}гкежлад K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Der/кежлӧ+Ger+PxSg3:@U.CONJ-GER.IG@%>%{иі%}гкежлас K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Der/кежлӧ+Ger+PxPl1:@U.CONJ-GER.IG@%>%{иі%}гкежланым K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Der/кежлӧ+Ger+PxPl2:@U.CONJ-GER.IG@%>%{иі%}гкежланыд K ;
@U.CONJ-GER.IG@+Der+Der/иг+Ger+Der/кежлӧ+Ger+PxPl3:@U.CONJ-GER.IG@%>%{иі%}гкежланыс K ;


LEXICON VerbDer-ONSET_А !!≈ * __@CODE@__ 
 VerbDer-ONSET_А/Я ;
LEXICON VerbDer-ONSET_А/Я !!≈ * __@CODE@__ 
+Der+Der/Ана+Adv:%>%{ая%}на K ;
+Der+Der/Анаа+Adv:%>%{ая%}наа K ; !Adding after attestation in Komi Grammar 2000: 381
+Der+Der/Ан+Der/моз+Ger:%>%{ая%}нмоз PossessiveSuffixes_NoSg1 ; !383

LEXICON VerbDer-ONSET_Я !!≈ * __@CODE@__
 VerbDer-ONSET_А/Я ;


LEXICON Verb-ynyDer
! verb-yny-to-verb
! The source stems have no stem vowel "ы", and therefore
! dervational suffixes come with an onset vowel

@C.CONJ-INF@@C.CONJ-POSS@+Der/ышт+Dim:@C.CONJ-INF@@C.CONJ-POSS@ышт V_ШУНЫ ; !!кольмыштны
!+V+Der/ышт+Dim+V:%>ышт  VerbConj ;
!+V+Der/ышт+Dim+V+Inf:%>ыштны K ;
!+V+Der/ышт+Dim+V+ConNeg0@U.CONJ-CONNEG.YES@+Pl3@U.CONJ-NX.PL@@U.CONJ-PX.3@:%>ыштны@U.CONJ-CONNEG.YES@0@U.CONJ-NX.PL@@U.CONJ-PX.3@ WordEnd ;
!+V+Der/ышт+Dim+V+ConNeg@U.CONJ-CONNEG.YES@:%>ышт0@U.CONJ-CONNEG.YES@ WordEnd ;
!+V+Der/ышт+Dim+V+Imprt+Sg2:%>ышт WordEnd ;

@C.CONJ-INF@@C.CONJ-POSS@+Der/ышт+Dim+Der/лы:@C.CONJ-INF@@C.CONJ-POSS@ыштл V_ВОЛЫНЫ ;   !!??кольмыштлыны
!+V+Der/ышт+Dim+V+Der/лы+V:%>ыштл  VerbConj ;
!+V+Der/ышт+Dim+V+Der/лы+V+Inf:%>ыштлыны K ;
!+V+Der/ышт+Dim+V+Der/лы+V+ConNeg0@U.CONJ-CONNEG.YES@+Pl3@U.CONJ-NX.PL@@U.CONJ-PX.3@:%>ыштлыны@U.CONJ-CONNEG.YES@0@U.CONJ-NX.PL@@U.CONJ-PX.3@ WordEnd ;
!+V+Der/ышт+Dim+V+Der/лы+V+ConNeg@U.CONJ-CONNEG.YES@:%>ыштл@U.CONJ-CONNEG.YES@ WordEnd ;
!+V+Der/ышт+Dim+V+Der/лы+V+Imprt+Sg2:%>ыштл WordEnd ;

@C.CONJ-INF@@C.CONJ-POSS@+Der/ышт+Dim+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@ыштлывл V_ВОЛЫНЫ ;  !!??кольмыштлывлыны

@C.CONJ-INF@@C.CONJ-POSS@+Der/л:@C.CONJ-INF@@C.CONJ-POSS@ыл V_ШУНЫ ;  !!тшӧктывны

@C.CONJ-INF@@C.CONJ-POSS@+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@ывл V_ВОЛЫНЫ ; !!тшӧктывлыны
!+V+Der/ывлы+V:%>ывл VerbConj ; ! мынтывл - мынтывлан
!+V+Der/ывлы+V+Inf:%>ывлыны K ;
!+V+Der/ывлы+V+ConNeg@U.CONJ-CONNEG.YES@+Pl3@U.CONJ-NX.PL@@U.CONJ-PX.3@:%>ывлыны@U.CONJ-CONNEG.YES@0@U.CONJ-NX.PL@@U.CONJ-PX.3@ WordEnd ;
!+V+Der/ывлы+V+ConNeg0@U.CONJ-CONNEG.YES@:%>ывлы@U.CONJ-CONNEG.YES@ WordEnd ;
!+V+Der/ывлы+V+Imprt+Sg2:%>ывлы WordEnd ;

@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы:@C.CONJ-INF@@C.CONJ-POSS@ысь V_ЛЫДДЬЫНЫ ; !! вевттьысьыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы+Der/ышт+Dim:@C.CONJ-INF@@C.CONJ-POSS@ысьышт V_ШУНЫ ; !!лэптысьыштны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы+Der/ышт+Der/лывлы:@C.CONJ-INF@@C.CONJ-POSS@ысьыштлывл V_ВОЛЫНЫ ; !!?? вевттьысьыштлывлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы+Der/ышт+Der/лы:@C.CONJ-INF@@C.CONJ-POSS@ысьыштл V_ВОЛЫНЫ ; !!лэптысьыштлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/сьы+Der/л:@C.CONJ-INF@@C.CONJ-POSS@ысьл V_ВОЛЫНЫ ; !!кутчысьлыны

@C.CONJ-INF@@C.CONJ-POSS@+Der/ал:@C.CONJ-INF@@C.CONJ-POSS@ал V_ШУНЫ ; !!бергӧдчавны
@C.CONJ-INF@@C.CONJ-POSS@+Der/ал+Der/лы:@C.CONJ-INF@@C.CONJ-POSS@авл V_ВОЛЫНЫ ; !!бергӧдчавлыны
@C.CONJ-INF@@C.CONJ-POSS@+Der/ась:@C.CONJ-INF@@C.CONJ-POSS@ась V_ШУНЫ ; !! бертласьны


!<!-- This far 2012-01-19 -->

!+Der/овт@C.CONJ-INF@@C.CONJ-POSS@:овт@C.CONJ-INF@@C.CONJ-POSS@ V_ШУНЫ ; !!пановтны


! Stray verb forms :-о


LEXICON VerbDer-V
! Page 346 in the Komi grammar
+Der+Der/мысьт+Ger:%>мысьт K ; ! gerund (after the action) !353
+Der+Der/мысь+Ger:%>мысь K ; ! !353
+VTer:%>тӧдз PossessiveSuffixes_NoSg1 ; !396
+Der+Der/мӧн+Ger:%>мӧн PossessiveSuffixes_NoSg1 ; !399-403
+Der+Der/мӧнъя+Ger:%>мӧнъя WordEnd ; !399-403
+VAbess:%>тӧм PossessiveSuffixes ;
+VCar:%>тӧг PossessiveSuffixes_NoSg1 ; ! 390
+Der+Car+Err/Orth+Ger:%>тӧгъя K ; !387
+Der+Car+Err/Orth+Ger:%>тӧга K ; !387
+Der+Car+Der/моз+Ger:%>тӧгмоз PossessiveSuffixes_NoSg1 ; !
+Err/Dial+VTer:%>тэдз PossessiveSuffixes_NoSg1 ; !396
+Err/Dial+Der+Der/мӧн+Ger:%>мен PossessiveSuffixes_NoSg1 ; !399-403
+Err/Dial+Der+Der/мӧнъя+Ger:%>менъя WordEnd ; !399-403
+Err/Dial+VAbess:%>тэм PossessiveSuffixes ;
+Err/Dial+VCar:%>тэг PossessiveSuffixes_NoSg1 ; ! 390
+Err/Dial+Der+Car+Err/Orth+Ger:%>тэгъя K ; !387
+Err/Dial+Der+Car+Err/Orth+Ger:%>тэга K ; !387
+Err/Dial+Der+Car+Der/моз+Ger:%>тэгмоз PossessiveSuffixes_NoSg1 ; !



! 2012-02-11 This is a problem; олан-вӧлан, овны-вӧвны
!2018-10-21!LEXICON VerbPRT ! one verb, вӧвны 
!2018-10-21!+V: preterite ;
!2018-10-21!+V+ConNeg+Pl3@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@:%>ны@U.CONJ-CONNEG.YES@@U.CONJ-NX.PL@@U.CONJ-PX.3@ WordEnd ;
!2018-10-21!+V+ConNeg@U.CONJ-CONNEG.YES@:@U.CONJ-CONNEG.YES@ WordEnd ;

LEXICON EM !!≈ * __@CODE@__ эм:эм. Ending in -ӧсь.
@U.CONJ-MX.IND@+V+Ind+Prs+Sg3:@U.CONJ-MX.IND@ WordEnd ;
+V+Ind+Prs+Pl3:%>ӧсь K ; ! Plural predicative ;
+Err/Dial+V+Ind+Prs+Pl3:%>эсь K ; ! Plural predicative ;

LEXICON EMYSHT ! эмышт "is a little / are a few"
@U.CONJ-MX.IND@+V+Ind+Prs+Sg3:@U.CONJ-MX.IND@ WordEnd ;
@U.CONJ-MX.IND@@U.CONJ-NX.PL@@U.CONJ-PX.3@+V+Ind+Prs+Pl3:@U.CONJ-MX.IND@@U.CONJ-NX.PL@@U.CONJ-PX.3@ WordEnd ;
 
LEXICON GER ! 3 lexicalised +Ger forms.
+V+Ger: WordEnd ;

! vim: set ft=xfst-lexc:
