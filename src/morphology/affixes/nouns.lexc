
! ============================= !
!! !!!Noun morphological lexica !
! ============================= !

! ===== !
! Nouns !
! ===== !

LEXICON Vocative   !!≈ * __@CODE@__
+N+Voc: # ;

LEXICON Noun1-IS_PTC-OM !!≈ * __@CODE@__ is to provide deverbal nouns with +Der/ӧм tags
+V+Der+Der/ӧм: N_ ;

LEXICON Noun1-IS_PTC-AN !!≈ * __@CODE@__
+V+Der+Der/Ан: N_ ;

LEXICON Noun1-IS_PTC-YSJ !!≈ * __@CODE@__
+V+Der+Der/ысь: N_ ;

LEXICON N-DOR ! мӧдладор The Russian is an adverbial phrase
+Der+Der/дор: N_ ;

LEXICON N-VYV ! тулысвыв The Russian is an adverbial phrase
+Der+Der/выв: N_ ;

LEXICON Noun1-F !
+Src/F: N_ ;

LEXICON Noun1-R-RY !
+Err/Orth:ь N_ ;  ! This deals with январь
           N_ ; ! This is the new proper spelling январ

LEXICON Noun1-SYT-STY !
+Err/Orth:ть N_ ;  ! This deals with гӧсть
:ьт         N_ ; ! This is the new proper spelling гӧсьт


LEXICON N-KINDRED_
 N_ ;
+N+PxSg1+Assoc+Pl:%>ӧяс CASEPOSSLEX ;
+N+PxSg2+Assoc+Pl:%>ыдъяс CASEPOSSLEX ;
+N+PxSg3+Assoc+Pl:%>ысъяс CASEPOSSLEX ;

LEXICON N-PROFESSION_
 N_ ;
+N+Der+Der/ал@C.CONJ-INF@@C.CONJ-POSS@:%>%{ая%}л@C.CONJ-INF@@C.CONJ-POSS@ V_ШУНЫ ; !! таксисталӧ 
!! Basic nouns.

!! The lexicon for basic nouns is {{ N_ }}


LEXICON N-RELAT_
+N+Relat: NMN_ ;
LEXICON N_
+N: NMN_ ;

LEXICON Noun1 !! This should be phased out 2013-05-07
 N_ ;
LEXICON N_RU-ӦЙ
+Use/SpellNoSugg:  N_ ;

LEXICON NMN_
 R ;
 ABS_ ;
+Sg: CASEPOSSLEX ;
+Pl:%>ъяс CASEPOSSLEX ;


LEXICON Noun1-JM ! понйыс, понмыс
+N:м NMN_PARAGOGIC ;
!2017-09-19+N+Cmp:м R ;
!2017-09-19+N+Sg:м CASEPOSSLEX ;
!2017-09-19+N+Cmp:й R ;
+N+Sg:й CASEPOSSLEX-AFTERDERIVATION_VOW ;
!2017-09-19+N+Sg:й CASEPOSSLEX ;
!2017-09-19+N+Pl:%>ъяс CASEPOSSLEX ;

!! subsequent Cns vs Vow
LEXICON N_PARAGOGIC !!= * @CODE@ 
+N: NMN_PARAGOGIC ;
LEXICON NMN_PARAGOGIC !!= * @CODE@ 
+Cmp:%^Close R ;
:%^Close ABS_ ;
+Sg:%^Close CASEPOSSLEX-AFTERDERIVATION_CNS/END ;
+Sg: CASEPOSSLEX-AFTERDERIVATION_VOW ;
+Pl:%^Close%>ъяс CASEPOSSLEX ;


LEXICON N-RELAT_PARAGOGIC-V/L !!= * @CODE@ ныв:ны нылыс
+N+Relat: NMN_PARAGOGIC-V/L ;

LEXICON N_PARAGOGIC-V/L !!= * @CODE@ ныв:ны нылыс
+N: NMN_PARAGOGIC-V/L ;
LEXICON NMN_PARAGOGIC-V/L !!= * @CODE@ ныв:ны нылыс
:л NMN_PARAGOGIC ;

+Err/Orth+Dial/л+Cmp:л R ;
+Err/Orth+Dial/л+Cmp:л ABS_ ;
+Err/Orth+Dial/л+Sg:л CASEPOSSLEX-AFTERDERIVATION_CNS/END ;
+Err/Orth+Dial/в+Sg:л%^Close CASEPOSSLEX-AFTERDERIVATION_VOW ;
+Err/Orth+Dial/л+Pl:л%>ъяс CASEPOSSLEX ;


LEXICON N_PARAGOGIC-J !!= * @CODE@ ав:ав авйыс
+N: NMN_PARAGOGIC-J ;
LEXICON NMN_PARAGOGIC-J !!= * @CODE@ ав:ав авйыс
:й NMN_PARAGOGIC ;

LEXICON N_PARAGOGIC-K !!= * @CODE@ пос:пос поскыс
+N: NMN_PARAGOGIC-K ;
LEXICON NMN_PARAGOGIC-K !!= * @CODE@ пос:пос поскыс
:к NMN_PARAGOGIC ;

LEXICON N_PARAGOGIC-M !!= * @CODE@ зон:зон зонмыс
+N: NMN_PARAGOGIC-M ;
LEXICON NMN_PARAGOGIC-M !!= * @CODE@ зон:зон зонмыс
:м N_PARAGOGIC ;

LEXICON N_PARAGOGIC-T !!= * @CODE@ зеп:зеп зептыс
+N: NMN_PARAGOGIC-T ;
LEXICON NMN_PARAGOGIC-T !!= * @CODE@ зеп:зеп зептыс
:т N_PARAGOGIC ;

LEXICON N_END-IN-DD1 !!= * @CODE@ гадь:гад гаддьыс
+N: NMN_END-IN-DD1 ;
LEXICON NMN_END-IN-DD1 !!= * @CODE@ автомобиль:автомобил автомобильыс
:дь NMN_PARAGOGIC ;

LEXICON N_END-IN-DDZ !!= * @CODE@ кыдз:кыд
+N: NMN_END-IN-DDZ ;
LEXICON NMN_END-IN-DDZ !!= * @CODE@ кыдз:кыд
:дз NMN_PARAGOGIC ;

LEXICON N_END-IN-LL1 !!= * @CODE@ моль:мол молльӧ
+N: NMN_END-IN-LL1 ;
LEXICON NMN_END-IN-LL1 !!= * @CODE@ моль:мол молльӧ
:ль NMN_PARAGOGIC ;



LEXICON N-RELAT_END-IN-J !!= * @CODE@ абай:аба абайыс
+N+Relat: N-0_END-IN-J ;
LEXICON N_END-IN-J !!= * @CODE@ абай:аба абайыс
+N: N-0_END-IN-J ;
LEXICON N-0_END-IN-J !!= * @CODE@ абай:аба абайыс
+Cmp:й R ;
:й ABS_ ;
+Sg:й CASEPOSSLEX-AFTERDERIVATION_CNS/END ;
+Sg: CASEPOSSLEX-AFTERDERIVATION_Я ;
+Sg:й CASEPOSSLEX-AFTERDERIVATION_Ӧ ;
+Pl:й%>яс CASEPOSSLEX ;


LEXICON N-RELAT_END-IN-Ч/ДЗ !!= * @CODE@ водз:водз
+N+Relat: NMN_END-IN-Ч/ДЗ ;
LEXICON N_END-IN-Ч/ДЗ !!= * @CODE@ водз:водз
+N: NMN_END-IN-Ч/ДЗ ;
LEXICON NMN_END-IN-Ч/ДЗ !!= * @CODE@ водз:водз
+Cmp: R ;
: ABS_ ;
+Sg: CASEPOSSLEX-AFTERDERIVATION_CNS/END ;
+Sg: CASEPOSSLEX-AFTERDERIVATION_VOW ;
+Pl:%>ьяс CASEPOSSLEX ;

LEXICON N-RELAT_END-IN-Ь !!= * @CODE@ автомобиль:автомобил автомобильыс
+N+Relat:  N-0_END-IN-Ь ;
LEXICON N_END-IN-Ь !!= * @CODE@ автомобиль:автомобил автомобильыс
+N:  N-0_END-IN-Ь ;
LEXICON N-0_END-IN-Ь !!= * @CODE@ автомобиль:автомобил автомобильыс
+Cmp:ь R ;
:ь ABS_ ;
+Sg:ь CASEPOSSLEX-AFTERDERIVATION_CNS/END ;
+Sg: CASEPOSSLEX-AFTERDERIVATION_Я ;
+Sg:ь CASEPOSSLEX-AFTERDERIVATION_Ӧ ;
+Pl:%>ьяс CASEPOSSLEX ; !! 2015-09-04 change to ь%>яс after xfst rules are removed



LEXICON NounLA ! ӧтила
N_ ; ! Difference betw N_ and NounLA still to be documented.

LEXICON NounMort
! медводзаысь гӧгӧрвои, мый ӧтика лыдын морт, уна лыдын йӧз
! http://komicorpora.ru/index.php#poisk мортъяс => 503
! Сідзи кӧ позяс бырӧдны NounMort LEXICON-сӧ
N_ ; ! Difference betw N_ and NounMort still to be documented.

LEXICON NounSG ! м орт
+N+Sg: CASEPOSSLEX ;

LEXICON NounPi ! пи These have a unique  pl marker ян.
+N+Sg: CASEPOSSLEX ;
+N+Pl:%>ян CASEPOSSLEX ;


LEXICON NounPL ! бедьторъяс
+N+Pl: CASEPOSSLEX ; ! here, %>ъяс is put as part of the stem
! Very many plural nouns do not have this tag. We could change яс-final
! nouns from N_ to NounPL, I guess.

LEXICON NounPOSS ! чой niece, nephew
N_ ; ! Declension to be determined.

LEXICON FIRST
+Cmp: # ;


! =================== !
!! !!Inflectional lexica
! =================== !


!! All nouns follow one contlex "N_"
!! to begin with here is simply a list of all variant
!! with no more variants beyond:

! Numbers
! Case + Poss or Poss+Case


LEXICON CASEPOSSLEX !!≈ * __@CODE@__

 DERIVEDADJ ;    ! Deriving prenominal adjectives, республикаса
CASEPOSSLEX-afterDerivation ;

LEXICON CASEPOSSLEX-afterDerivation
 CASEPOSSLEX-AFTERDERIVATION_CNS/END ;
  CASEPOSSLEX-AFTERDERIVATION_VOW ;

LEXICON CASEPOSSLEX-AFTERDERIVATION_CNS/END
+Nom: K ;
+Acc: K ; !ZERO marker
	CASEPOSSLEX-AFTERDERIVATION_CNS/END-MINUS-ZERO-END ;
LEXICON CASEPOSSLEX-AFTERDERIVATION_CNS/END-MINUS-ZERO-END
+Gen:%>лӧн PredMonLex ;
+Err/Dial+Gen:%>лэн PredMonLex ;
+Abl:%>лысь K ;
+Dat:%>лы K ;
+Com:%>кӧд K ;
+Err/Dial+Com:%>кед K ;
+Car:%>тӧг K ;
+Err/Dial+Car:%>тэг K ;
+Cns:%>ла K ;
+Err/Dial+Cmpr:%>ся K ;
+Apr:%>лань SPAT-COMPARATIVE ;
+Egr:%>сянь SPAT-COMPARATIVE ;
+Tra:%>ті K ;
+AprIne:%>ланьын SPAT-COMPARATIVE ;  ! New cases in last grammar
+AprEla:%>ланьысь SPAT-COMPARATIVE ;  !
+AprIll:%>ланьӧ IllCompar ; !
+Err/Dial+AprIll:%>лане IllCompar ; !
+AprEgr:%>ланьсянь SPAT-COMPARATIVE ; !
+AprPrl:%>ланьӧд K ; !
+Err/Dial+AprPrl:%>ланед K ; !
+AprTra:%>ланьті K ; !
+AprTer:%>ланьӧдз K ; ! Tähän asti
+Err/Dial+AprTer:%>ланедз K ; ! Tähän asti
!! SG1
+Ins+PxSg1:%>нам K ;
+Ela+PxSg1:%>сьым SPAT-COMPARATIVE ;
+Tra+PxSg1:%>тіым K ;
+Apr+PxSg1:%>ланьым SPAT-COMPARATIVE ;
+Egr+PxSg1:%>саньым SPAT-COMPARATIVE ;
+AprIne+PxSg1:%>ланям SPAT-COMPARATIVE ;
+AprEla+PxSg1:%>ланьсьым SPAT-COMPARATIVE ;
+AprIll+PxSg1:%>ланям IllCompar ; !
+AprPrl+PxSg1:%>ланьӧдым K ;
+Err/Dial+AprPrl+PxSg1:%>ланедым K ;
+AprTra+PxSg1:%>ланьтіым K ;
+AprTer+PxSg1:%>ланьӧдзым K ;
+Err/Dial+AprTer+PxSg1:%>ланедзым K ;

!! SG2
+Ins+PxSg2:%>над K ;
+Car+PxSg2:%>тӧгыд K ;
+Err/Dial+Car+PxSg2:%>тэгыд K ;
+Car+PxSg2:%>ыдтӧг K ; ! Px + Case
+Err/Dial+Car+PxSg2:%>ыдтэг K ; ! Px + Case
+Cmpr+PxSg2:%>сяыд K ;
+Ela+PxSg2:%>сьыд SPAT-COMPARATIVE ;
+Apr+PxSg2:%>ланьыд SPAT-COMPARATIVE ;
+Egr+PxSg2:%>сяньыд SPAT-COMPARATIVE ;
+Egr+PxSg2:%>ыдсянь K ; ! Px + Case Ильяыдсянь
+Tra+PxSg2:%>тіыд K ;
+Acc+PxSg2:%>тӧ K ; ! Acc2
+Err/Dial+Acc+PxSg2:%>тэ K ; ! Acc2
+AprIne+PxSg2:%>ланяд SPAT-COMPARATIVE ;
+AprEla+PxSg2:%>ланьсьыд SPAT-COMPARATIVE ;
+AprIll+PxSg2:%>ланяд IllCompar ; !
+AprEgr+PxSg2:%>ланьсяньыд SPAT-COMPARATIVE ;
+AprPrl+PxSg2:%>ланьӧдыд K ;
+Err/Dial+AprPrl+PxSg2:%>ланедыд K ;
+AprTra+PxSg2:%>ланьтіыд K ;
+AprTer+PxSg2:%>ланьӧдзыд K ;
+Err/Dial+AprTer+PxSg2:%>ланедзыд K ;

!! SG3
+Acc+PxSg3:%>сӧ K ; !Acc3
+Err/Dial+Acc+PxSg3:%>сэ K ; !Acc3
+Ins+PxSg3:%>нас K ;
+Car+PxSg3:%>тӧгыс K ;
+Err/Dial+Car+PxSg3:%>тэгыс K ;
+Cmpr+PxSg3:%>сяыс K ;
+Ela+PxSg3:%>сьыс SPAT-COMPARATIVE ;
+Apr+PxSg3:%>ланьыс SPAT-COMPARATIVE ;
+Egr+PxSg3:%>сяньыс SPAT-COMPARATIVE ;
+Tra+PxSg3:%>тіыс K ;
+AprIne+PxSg3:%>ланяс SPAT-COMPARATIVE ;
+AprEla+PxSg3:%>ланьсьыс SPAT-COMPARATIVE ;
+AprIll+PxSg3:%>ланяс IllCompar ; !
+AprEgr+PxSg3:%>ланьсяньыс SPAT-COMPARATIVE ;
+AprPrl+PxSg3:%>ланьӧдыс K ;
+Err/Dial+AprPrl+PxSg3:%>ланедыс K ;
+AprTra+PxSg3:%>ланьтіыс K ;
+AprTer+PxSg3:%>ланьӧдзыc K ;
+Err/Dial+AprTer+PxSg3:%>ланедзыc K ;

!! PL1
+Nom+PxPl1:%>ным K ;
+Gen+PxPl1:%>нымлӧн K ;
+Err/Dial+Gen+PxPl1:%>нымлэн K ;
+Abl+PxPl1:%>нымлысь K ;
+Dat+PxPl1:%>нымлы K ;
+Acc+PxPl1:%>нымӧс K ;
+Err/Dial+Acc+PxPl1:%>нымес K ;
+Ins+PxPl1:%>наным K ;
+Com+PxPl1:%>нымкӧд K ;
+Err/Dial+Com+PxPl1:%>нымкед K ;
+Com+PxPl1:%>кӧдным K ;
+Err/Dial+Com+PxPl1:%>кедным K ;
+Car+PxPl1:%>тӧгным K ; ! Case+Px
+Err/Dial+Car+PxPl1:%>тэгным K ; ! Case+Px
+Car+PxPl1:%>нымтӧг K ; ! Px+Case
+Err/Dial+Car+PxPl1:%>нымтэг K ; ! Px+Case
+Cns+PxPl1:%>нымла K ;
+Cmpr+PxPl1:%>сяным K ;
+Ela+PxPl1:%>сьыным SPAT-COMPARATIVE ;

+Apr+PxPl1:%>ланьным SPAT-COMPARATIVE ; ! Case+Px
+Apr+PxPl1:%>нымлань SPAT-COMPARATIVE ; ! Px+Case
+Egr+PxPl1:%>сяньным SPAT-COMPARATIVE ; ! _ставсяньным_
+Egr+PxPl1:%>нымсянь SPAT-COMPARATIVE ; ! _ставнымсянь_  Px+Case
+Tra+PxPl1:%>тіным K ;
+Prl+PxPl1:%>нымӧд K ; ! Px + Case туйнымӧд
+Err/Dial+Prl+PxPl1:%>нымед K ; ! Px + Case туйнымӧд

+AprIne+PxPl1:%>ланяным SPAT-COMPARATIVE ;
+AprEla+PxPl1:%>ланьсьыным SPAT-COMPARATIVE ;
+AprIll+PxPl1:%>ланяным IllCompar ; !
+AprEgr+PxPl1:%>ланьсяньным SPAT-COMPARATIVE ;
+AprPrl+PxPl1:%>ланьӧдным K ;
+Err/Dial+AprPrl+PxPl1:%>ланедным K ;
+AprTra+PxPl1:%>ланьтіным K ;
+AprTer+PxPl1:%>ланьӧдзным K ;
+Err/Dial+AprTer+PxPl1:%>ланедзным K ;

!! PL2
+Nom+PxPl2:%>ныд K ;
+Gen+PxPl2:%>ныдлӧн K ;
+Err/Dial+Gen+PxPl2:%>ныдлэн K ;
+Abl+PxPl2:%>ныдлысь K ;
+Dat+PxPl2:%>ныдлы K ;
+Acc+PxPl2:%>нытӧ K ; ! Acc2
+Err/Dial+Acc+PxPl2:%>нытэ K ; ! Acc2
+Ins+PxPl2:%>наныд K ;
+Com+PxPl2:%>ныдкӧд K ;
+Err/Dial+Com+PxPl2:%>ныдкед K ;
+Com+PxPl2:%>кӧдныд K ;
+Err/Dial+Com+PxPl2:%>кедныд K ;
+Car+PxPl2:%>тӧгныд K ;
+Err/Dial+Car+PxPl2:%>тэгныд K ;
+Cns+PxPl2:%>ныдла K ;
+Cmpr+PxPl2:%>сяныд K ;
+Ela+PxPl2:%>сьыныд SPAT-COMPARATIVE ;
+Apr+PxPl2:%>ныдлань SPAT-COMPARATIVE ;
+Apr+PxPl2:%>ланьныд K ; ! Case + Px
+Egr+PxPl2:%>сяньныд SPAT-COMPARATIVE ;
+Egr+PxPl2:%>ныдсянь SPAT-COMPARATIVE ;
+Tra+PxPl2:%>тіныд K ;
+AprIne+PxPl2:%>ланяныд SPAT-COMPARATIVE ;
+AprEla+PxPl2:%>ланьсьыныд SPAT-COMPARATIVE ;
+AprIll+PxPl2:%>ланяныд IllCompar ; !
+AprEgr+PxPl2:%>ланьсяньныд SPAT-COMPARATIVE ;
+AprPrl+PxPl2:%>ланьӧдныд K ;
+Err/Dial+AprPrl+PxPl2:%>ланедныд K ;
+AprTra+PxPl2:%>ланьтіныд K ;
+AprTer+PxPl2:%>ланьӧдзныд K ;
+Err/Dial+AprTer+PxPl2:%>ланедзныд K ;

!! PL3
+Nom+PxPl3:%>ныс K ;
+Gen+PxPl3:%>ныслӧн K ; ! Px + Case
+Err/Dial+Gen+PxPl3:%>ныслэн K ; ! Px + Case
+Abl+PxPl3:%>ныслысь K ; ! Px + Case
+Dat+PxPl3:%>ныслы K ; ! Px + Case
+Acc+PxPl3:%>нысӧ K ; ! Px + Case Acc3
+Err/Dial+Acc+PxPl3:%>нысэ K ; ! Px + Case Acc3
+Ins+PxPl3:%>наныс K ;
+Com+PxPl3:%>ныскӧд K ; ! Px + Case
+Err/Dial+Com+PxPl3:%>ныскед K ; ! Px + Case
+Com+PxPl3:%>кӧдныс K ; ! Px + Case
+Err/Dial+Com+PxPl3:%>кедныс K ; ! Px + Case
+Car+PxPl3:%>тӧгныс K ;
+Err/Dial+Car+PxPl3:%>тэгныс K ;
+Cns+PxPl3:%>нысла K ; ! Px + Case
+Cmpr+PxPl3:%>сяныс K ;
+Ela+PxPl3:%>сьыныс SPAT-COMPARATIVE ;
+Apr+PxPl3:%>ныслань SPAT-COMPARATIVE ; ! Px + Case
+Apr+PxPl3:%>ланьныс SPAT-COMPARATIVE ;
+Egr+PxPl3:%>сяньныс SPAT-COMPARATIVE ;
+Egr+PxPl3:%>ныссянь SPAT-COMPARATIVE ;
+Tra+PxPl3:%>тіныс K ;
+AprIne+PxPl3:%>ланяныс SPAT-COMPARATIVE ;
+AprEla+PxPl3:%>ланьсьыныс SPAT-COMPARATIVE ;
+AprIll+PxPl3:%>ланяныс IllCompar ; !
+AprEgr+PxPl3:%>ланьсяньныс SPAT-COMPARATIVE ;
+AprPrl+PxPl3:%>ланьӧдныс K ;
+Err/Dial+AprPrl+PxPl3:%>ланедныс K ;
+AprTra+PxPl3:%>ланьтіныс K ;
+AprTer+PxPl3:%>ланьӧдзныс K ;
+Err/Dial+AprTer+PxPl3:%>ланедзныс K ;

LEXICON CASEPOSSLEX-AFTERDERIVATION_VOW
 CASEPOSSLEX-AFTERDERIVATION_А ;
 CASEPOSSLEX-AFTERDERIVATION_Ӧ ;

LEXICON CASEPOSSLEX-AFTERDERIVATION_А
!! SG1
+Ine+PxSg1:%>ам SPAT-COMPARATIVE ;
+Ill+PxSg1:%>ам IllCompar ; !
!! SG2
+Ine+PxSg2:%>ад SPAT-COMPARATIVE ;
+Ill+PxSg2:%>ад IllCompar ; !
!! SG3
+Ine+PxSg3:%>ас SPAT-COMPARATIVE ;
+Ill+PxSg3:%>ас IllCompar ; !
!! PL1
+Ine+PxPl1:%>аным SPAT-COMPARATIVE ;
+Ill+PxPl1:%>аным IllCompar ; !
!! PL2
+Ine+PxPl2:%>аныд SPAT-COMPARATIVE ;
+Ill+PxPl2:%>аныд IllCompar ; !
!! PL3
+Ine+PxPl3:%>аныс SPAT-COMPARATIVE ;
+Ill+PxPl3:%>аныс IllCompar ; !

LEXICON CASEPOSSLEX-AFTERDERIVATION_Я
!! SG1
+Ine+PxSg1:%>ям SPAT-COMPARATIVE ;
+Ill+PxSg1:%>ям IllCompar ; !
!! SG2
+Ine+PxSg2:%>яд SPAT-COMPARATIVE ;
+Ill+PxSg2:%>яд IllCompar ; !
!! SG3
+Ine+PxSg3:%>яс SPAT-COMPARATIVE ;
+Ill+PxSg3:%>яс IllCompar ; !
!! PL1
+Ine+PxPl1:%>яным SPAT-COMPARATIVE ;
+Ill+PxPl1:%>яным IllCompar ; !
!! PL2
+Ine+PxPl2:%>яныд SPAT-COMPARATIVE ;
+Ill+PxPl2:%>яныд IllCompar ; !
!! PL3
+Ine+PxPl3:%>яныс SPAT-COMPARATIVE ;
+Ill+PxPl3:%>яныс IllCompar ; !

! THIS FAR 2017-06-29 +Err/Dial
LEXICON CASEPOSSLEX-AFTERDERIVATION_Ӧ
+Acc:%>ӧс K ;
+Err/Dial+Acc:%>эс K ;
+Ins:%>ӧн PredMonLex ;
+Err/Dial+Ins:%>эн PredMonLex ;
+Ine:%>ын SPAT-COMPARATIVE ;
+Ela:%>ысь SPAT-COMPARATIVE ;
+Ill:%>ӧ IllCompar ;
+Err/Dial+Ill:%>э IllCompar ;
+Prl:%>ӧд K ;
+Ter:%>ӧдз K ;
+Err/Dial+Prl:%>эд K ;
+Err/Dial+Ter:%>эдз K ;

!! SG1
+Nom+PxSg1:%>ӧй K ;
+Gen+PxSg1:%>ӧйлӧн K ;
+Abl+PxSg1:%>ӧйлысь K ;
+Dat+PxSg1:%>ӧйлы K ;
+Acc+PxSg1:%>ӧс K ;
+Com+PxSg1:%>ӧйкӧд K ;
+Car+PxSg1:%>ӧйтӧг K ;
+Cns+PxSg1:%>ӧйла K ;
+Cmpr+PxSg1:%>ӧйся K ;
+Apr+PxSg1:%>ӧйлань SPAT-COMPARATIVE ;
+Egr+PxSg1:%>ӧйсянь SPAT-COMPARATIVE ;
+Prl+PxSg1:%>ӧдым K ;
+Ter+PxSg1:%>ӧдзым K ;
+AprEgr+PxSg1:%>ӧйланьсянь SPAT-COMPARATIVE ;

+Err/Dial+Nom+PxSg1:%>эй K ;
+Err/Dial+Gen+PxSg1:%>эйлэн K ;
+Err/Dial+Abl+PxSg1:%>эйлысь K ;
+Err/Dial+Dat+PxSg1:%>эйлы K ;
+Err/Dial+Acc+PxSg1:%>эс K ;
+Err/Dial+Com+PxSg1:%>эйкед K ;
+Err/Dial+Car+PxSg1:%>эйтэг K ;
+Err/Dial+Cns+PxSg1:%>эйла K ;
+Err/Dial+Cmpr+PxSg1:%>эйся K ;
+Err/Dial+Apr+PxSg1:%>эйлань SPAT-COMPARATIVE ;
+Err/Dial+Egr+PxSg1:%>эйсянь SPAT-COMPARATIVE ;
+Err/Dial+Prl+PxSg1:%>эдым K ;
+Err/Dial+Ter+PxSg1:%>эдзым K ;
+Err/Dial+AprEgr+PxSg1:%>эйланьсянь SPAT-COMPARATIVE ;

!! SG2
+Nom+PxSg2:%>ыд K ;
+Gen+PxSg2:%>ыдлӧн K ;
+Err/Dial+Gen+PxSg2:%>ыдлэн K ;
+Abl+PxSg2:%>ыдлысь K ;
+Dat+PxSg2:%>ыдлы K ;
+Com+PxSg2:%>ыдкӧд K ;
+Com+PxSg2:%>кӧдыд K ;
+Err/Dial+Com+PxSg2:%>кедыд K ;
+Err/Dial+Com+PxSg2:%>ыдкед K ;
+Cns+PxSg2:%>ыдла K ;
+Prl+PxSg2:%>ӧдыд K ;
+Ter+PxSg2:%>ӧдзыд K ;
+Err/Dial+Prl+PxSg2:%>эдыд K ;
+Err/Dial+Ter+PxSg2:%>эдзыд K ;

!! SG3
+Nom+PxSg3:%>ыс K ;
+Gen+PxSg3:%>ыслӧн K ;
+Err/Dial+Gen+PxSg3:%>ыслэн K ;
+Abl+PxSg3:%>ыслысь K ;
+Dat+PxSg3:%>ыслы K ;
+Com+PxSg3:%>ыскӧд K ;
+Com+PxSg3:%>кӧдыс K ;
+Err/Dial+Com+PxSg3:%>ыскед K ;
+Err/Dial+Com+PxSg3:%>кедыс K ;
+Cns+PxSg3:%>ысла K ;
+Prl+PxSg3:%>ӧдыс K ;
+Ter+PxSg3:%>ӧдзыс K ;
+Err/Dial+Prl+PxSg3:%>эдыс K ;
+Err/Dial+Ter+PxSg3:%>эдзыс K ;

!! PL1
+Prl+PxPl1:%>ӧдным K ;
+Ter+PxPl1:%>ӧдзным K ;
+Err/Dial+Prl+PxPl1:%>эдным K ;
+Err/Dial+Ter+PxPl1:%>эдзным K ;

!! PL2
+Prl+PxPl2:%>ӧдныд K ;
+Ter+PxPl2:%>ӧдзныд K ;
+Err/Dial+Prl+PxPl2:%>эдныд K ;
+Err/Dial+Ter+PxPl2:%>эдзныд K ;

!! PL3
+Prl+PxPl3:%>ӧдныс K ;
+Ter+PxPl3:%>ӧдзныс K ;
+Err/Dial+Prl+PxPl3:%>эдныс K ;
+Err/Dial+Ter+PxPl3:%>эдзныс K ;


!! !!Case

LEXICON Noun1-IS_INE
+N+Ine: SPAT-COMPARATIVE ;

LEXICON DERIVEDADJ
+Der+Der/са+IneMod:%>са A_ ;
+Der+Der/тӧм+AbeMod:%>тӧм A_ ;
+Err/Dial+Der+Der/тӧм+AbeMod:%>тэм A_ ;
+Der+Der/а+HabObjMod:%>а A_ ;
+Der+Der/ся+TempMod:%>ся A_ ;
+Der+Der/увса:%>увса A_ ;
+Der+Der/сайса:%>сайса A_ ;
+Der+Der/пӧвстса:%>пӧвстса A_ ;
+Err/Dial+Der+Der/пӧвстса:%>певстса A_ ;
+Der+Der/костса:%>костса A_ ;
+Der+Der/бердса:%>бердса A_ ;
+Der+Der/бӧрса:%>бӧрса A_ ;
+Err/Dial+Der+Der/бӧрса:%>берса A_ ;
+Der+Der/весьтса:%>весьтса A_ ;
+Der+Der/водзса:%>водзса A_ ;
+Der+Der/вывса:%>вывса A_ ;
+Der+Der/гӧгӧрса:%>гӧгӧрса A_ ;
+Err/Dial+Der+Der/гӧгӧрса:%>гегерса A_ ;
+Der+Der/дорса:%>дорса A_ ;




LEXICON AdjCompar
+Comp:%>джык PredMonLex ;
+Comp+Der+Der/а:%>джыка ADV_ ;
+Comp+Sg:%>джык CASEPOSSLEX-afterDerivation ;
+Comp+Pl:%>джыкъяс CASEPOSSLEX-afterDerivation ;
!2017-09-19+Comp+Der/MWN+N+Sg:%>джык CASEPOSSLEX-afterDerivation ;
!2017-09-19+Comp+Der/MWN+N+Pl:%>джыкъяс CASEPOSSLEX-afterDerivation ;
+Sg: CASEPOSSLEX-afterDerivation ; !This is for adjectives without nouns
+Pl:%>ъяс CASEPOSSLEX-afterDerivation ; !This is for adjectives without nouns
!2017-09-19+Der/MWN+N+Sg: CASEPOSSLEX-afterDerivation ; !This is for adjectives without nouns
!2017-09-19+Der/MWN+N+Pl:%>ъяс CASEPOSSLEX-afterDerivation ; !This is for adjectives without nouns

LEXICON IllCompar
 K ;
+Comp:%>джык K ;

LEXICON PredMonLex-Spat
 K ;      ! Singular predicative
 PredMonLex-Spat-Add-Morphology ; !

LEXICON PredMonLex-Spat-Add-Morphology ! This allows consistency
                                       ! with SPAT elsewhere
+Cop+Pl:%>ӧсь K ; ! Plural predicative.
+Err/Dial+Cop+Pl:%>эсь K ; ! Plural predicative.
+Comp:%>джык PredMonLex ; !ывлаынджык OR ывлаынджыкӧсь


LEXICON PredMonLex
 K ;      ! Singular predicative
+Cop+Pl:%>ӧсь K ; ! Plural predicative.
+Err/Dial+Cop+Pl:%>эсь K ; ! Plural predicative.
!! 2012-02-07 !!+Der/а+Adv:%>а K ;




! vim: set ft=xfst-lexc:
