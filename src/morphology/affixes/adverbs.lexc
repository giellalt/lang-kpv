!! !!!Adverb inflection
!! ----
!! Komi adverbs inflect for direction.


LEXICON ADV-ADA_
+Adv+AdA: WordEnd ;
LEXICON Ad-ATAG ! these are checked as advs.
ADV-ADA_ ;

LEXICON GER_
+V+Ger: ADV_ ;

LEXICON ADV-IS_GER-IGON ! these are checked as gerunds in +Der/иг
+V+Der/иг+Ins: ADV_ ;

LEXICON ADV-IS_GER-IGTYRJI ! these are checked as gerunds in +Der/иг
+V+Der/иг+Der/тырйи: ADV_ ;

LEXICON ADV-IS_GER-MON ! these are checked as gerunds in +Der/мӧн
+V+Der/мӧн: ADV_ ;

LEXICON ADV-IS_GER-TOG ! these are checked as gerunds in +Der/тӧг
+V+Der/тӧг: ADV_ ;

LEXICON ADV-IS_GER-TODZ ! these are checked as gerunds in +Der/тӧдз
+V+Der/тӧдз: ADV_ ;

LEXICON ADV-IS_N-TOG ! these are checked as state adverbials in +Der/тӧг
+N+Der/тӧг: ADV_ ;

LEXICON ADV-IS_ADV-TOG ! polar adverbials in +Der/тӧг
+Adv+Der/тӧг: ADV_ ;

LEXICON ADV-IS_Q-TOG ! polar adverbials in +Der/тӧг
+Qnt+Der/тӧг: ADV_ ;

LEXICON ADV-IS_SUPERL !
+Der/мед: ADV_ ;

LEXICON ADV-F !
+Src/F: ADV_ ;

LEXICON ADV-STATE !
+Adv+State: COMPARATIVE ; !

LEXICON AdvParenthetic
+Adv+Parenthetic: WordEnd ;

LEXICON ADV_SUB
+Err/Orth: ADV_ ;

LEXICON ADV_ ! these are checked as advs.
+Adv: COMPARATIVE ;

LEXICON ADV-INDEF_
+Adv+Indef: WordEnd ;

LEXICON ADV-INDEF_HAS-SJURO
+Adv+Indef+Der/сюрӧ: WordEnd ;
LEXICON ADV-INDEF_HAS-KO
+Adv+Indef+Der/кӧ: WordEnd ;
LEXICON ADV-4-INTERR-REL-INDEF
+Adv+Interr: WordEnd ;
+Adv+Rel: WordEnd ;
+Adv+Indef: 4-INDEF-TYPES_KO-SJURO ;
LEXICON ADV-EGR-4-INTERR-REL-INDEF !! кысянь
+Adv+Interr+Egr: WordEnd ;
+Adv+Rel+Egr: WordEnd ;
+Adv+Indef+Egr: 4-INDEF-TYPES_KO-SJURO ;
LEXICON ADV-EGR-ILL-4-INTERR-REL-INDEF !! кытчаньӧ
+Adv+Interr+Egr+Ill: WordEnd ;
+Adv+Rel+Egr+Ill: WordEnd ;
+Adv+Indef+Egr+Ill: 4-INDEF-TYPES_KO-SJURO ;
LEXICON ADV-ELA-4-INTERR-REL-INDEF !! кытысь
+Adv+Interr+Ela: WordEnd ;
+Adv+Rel+Ela: WordEnd ;
+Adv+Indef+Ela: 4-INDEF-TYPES_KO-SJURO ;
LEXICON ADV-ILL-4-INTERR-REL-INDEF !! кытчӧ
+Adv+Interr+Ill: WordEnd ;
+Adv+Rel+Ill: WordEnd ;
+Adv+Indef+Ill: 4-INDEF-TYPES_KO-SJURO ;
LEXICON ADV-TRA-4-INTERR-REL-INDEF !! кыті
+Adv+Interr+Tra: WordEnd ;
+Adv+Rel+Tra: WordEnd ;
+Adv+Indef+Tra: 4-INDEF-TYPES_KO-SJURO ;
LEXICON ADV-TRA-EGR-4-INTERR-REL-INDEF ! This might require something for double case
+Adv+Interr+Tra+Egr: WordEnd ;
+Adv+Rel+Tra+Egr: WordEnd ;
+Adv+Indef+Tra+Egr: 4-INDEF-TYPES_KO-SJURO ;
LEXICON ADV-TER-4-INTERR-REL-INDEF !! кытчӧдз
+Adv+Interr+Ter: WordEnd ;
+Adv+Rel+Ter: WordEnd ;
+Adv+Indef+Ter: 4-INDEF-TYPES_KO-SJURO ;
LEXICON 4-INDEF-TYPES_KO-SJURO
+Der/кӧ:кӧ WordEnd ;
+Der/сюрӧ:сюрӧ WordEnd ;

LEXICON ADV-INTERR_
+Adv+Interr: WordEnd ;


LEXICON ADV-REL-TEMP_
+Adv+Rel+Temp: COMPARATIVE ;

LEXICON AdvTemp
 ADV-TEMP_ ;

LEXICON ADV-TEMP_
+Adv+Temp: COMPARATIVE ;

LEXICON ADV-SPAT  !!= * __@CODE@__
      !! these are adverbs in spatial cases
      !! and they probably are equivalent in morphology to postpositions
 ADV-SPAT-APR ;
 ADV-SPAT-APRILL ;
 ADV-SPAT-APRINE ;
 ADV-SPAT-APRELA ;
 ADV-SPAT-EGR ;
 ADV-SPAT-ELA ;
 ADV-SPAT-ILL ;
 ADV-SPAT-INE ;
 ADV-SPAT-PRL ;
 ADV-SPAT-TER ;
 ADV-SPAT-TRA ;

LEXICON ADV-SPAT-LA ! These adverbs have a formative in ла
+Der/ла: ADV-SPAT ;

LEXICON ADV-SPAT-APR
+Adv+Spat+Apr:%>лань SPAT-COMPARATIVE ;

LEXICON ADV-SPAT-APRELA
+Adv+Spat+Apr:%>ланьысь SPAT-COMPARATIVE ;

LEXICON ADV-SPAT-APRILL
+Adv+Spat+Apr:%>ланьӧ SPAT-COMPARATIVE ;

LEXICON ADV-SPAT-APRINE
+Adv+Spat+Apr:%>ланьын SPAT-COMPARATIVE ;

LEXICON ADV-SPAT-EGR
+Adv+Spat+Egr:%>сянь SPAT-COMPARATIVE ;

LEXICON ADV-SPAT-ELA
+Adv+Spat+Ela:%>ысь SPAT-COMPARATIVE ;

LEXICON ADV-SPAT-ILL
+Adv+Spat+Ill:%>ӧ SPAT-COMPARATIVE ;

LEXICON ADV-SPAT-INE
+Adv+Spat+Ine:%>ын SPAT-COMPARATIVE ;

LEXICON ADV-SPAT-PRL
+Adv+Spat+Prl:%>ӧд K ;

LEXICON ADV-SPAT-TRA
+Adv+Spat+Tra:%>ті COMPARATIVE ;

LEXICON ADV-SPAT-TER
+Adv+Spat+Ter:%>ӧдз COMPARATIVE ;

LEXICON ADV-VTER
+V+Der/тӧдз+Adv+Temp+Ter:%>тӧдз K ;

! Adverbs with ready morphology
LEXICON ADV-IS_APRELA
+Adv+Apr+Ela: SPAT-COMPARATIVE ;

LEXICON ADV-IS_APRILL
+Adv+Apr+Ill: SPAT-COMPARATIVE ;

LEXICON ADV-IS_APRINE
+Adv+Apr+Ine: SPAT-COMPARATIVE ;

LEXICON ADV-IS_APR
+Adv+Apr: SPAT-COMPARATIVE ;

LEXICON ADV-IS_EGR
+Adv+Egr: SPAT-COMPARATIVE ;

LEXICON ADV-IS_ELA
+Adv+Ela: SPAT-COMPARATIVE ;

LEXICON ADV-IS_ILL
+Adv+Ill: IllCompar ;

LEXICON ADV-IS_INE
+Adv+Ine: SPAT-COMPARATIVE ;

LEXICON ADV-IS_PROL
+Adv+Prol: K ;

LEXICON ADV-IS_TER
+Adv+Ter: K ;

LEXICON ADV-IS_TRA
+Adv+Tra: K ;



! vim: set ft=xfst-lexc:

