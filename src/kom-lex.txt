Multichar_Symbols

+Err ! marking missing lexica

! POS
+N +V +A 
+Adv +CC +CS +Interj +Pron +Num +Pcle +Clt +Po
+Onom ! onomapoetic words


! Verbal MSP
+Prs +Fut +Fut1 +Prt1 +Prt2 +Prf
+PrfIpf +HstPrf +PluPrf +HstPluPrf
+Ind +Imp +Cond +Opt
+Sg1 +Sg2 +Sg3 +Pl1 +Pl2 +Pl3
+Inf
+Pos +Neg +ConNeg
+Ger !Gerund This is used with
!+Der/иг

! Used for predicate plural marking
+Pred

! Nominal MSP
+Sg +Pl
+Prop
+Nom +Gen +Abl +Dat +Com 
+Acc +Ins +Ine +Ill 
+Ela +Car +Egr +Tra  +Apr +Ter
+Abe 
+Prl !Prolative "ӧд"
+Compar ! comparative as opposed to superlative
!+Adc  ! not in use!
+HabObj
+Instr
+Superl
+ACR +ABBR
+Cmpl ! complement of postpostion

+Refl +Rel +Interr +Recipr

+PxSg1 +PxSg2 +PxSg3 +PxPl1 +PxPl2 +PxPl3

+Card
+Cns ! Consecutive -la
+Coll
+Comp ! Case form sya

+Det ! determiner like уна 'many'. Look at this.

+Ord
+Temp



! tags
+AN
+ANA
+ANAA
+CHOZH

+Instr

!+IG This will be replaced by the segments:
!!+Der/иг+Ger!! 
+Der/иг
!Here we need the two sequential characters
!+Der/ӧм+VN 
!This, of course, implicates a discussion 
!of the so-called gerund "ӧмӧн" +VN+Instr and
! the combinatory +VN preceding the NP-final noun
+Der/ӧм
! These should be added below in derivation declarations
!+Der/кежлӧ +Der/моз +Der/мысь +Der/мысьт 
! +Der/тӧдз +Der/тыр +Der/тырйи 
!+Der/ан +Der/ана +Der/анаа +Der/чӧж 
!!Then they should have indication of whether they are gerunds or participles!!
!!
+KEZHLO
+MOZ
+MYS
+MYST
+OM

+SOR
+TODZ
+TYR
+TYRJI

+CLB +PUNCT +LEFT +RIGHT

! Declaring Derivations
+Der/а +Der/са +Der/ся +Der/тӧм

!Modifier without noun
+Der/MWN

!Declaring Indefinite Pronoun derivations
+Der/сюрӧ +Der/кӧ

!Verb derivations
+Der/лыны +Der/ывлыны

! Declaring symbols for morphophonology

к2 м2 т2 К2 М2 Т2 %>
%^Inf

! Key lexicon

! These are the lexica that are found in the bin/ directory,
! generated by the Makefile from the dictionary

LEXICON Root
! contlexes    ! the file, where the contlex is located
! ------------ ! ---------------------------------------
A ;			   ! ../../tmp/out/A_kom-lex.xml
A-SUPERL ;	   ! ../../tmp/out/A-SUPERL_kom-lex.xml
ADV ;		   ! ../../tmp/out/ADV_kom-lex.xml
ADVROOT ;	   ! ../../tmp/out/ADVROOT_kom-lex.xml
Abbreviation ; ! abbr-kom-lex.txt
Acronym ;	   ! acro-kom-lex.txt
CONJ ;		   ! ../../tmp/out/CONJ_kom-lex.xml
IDIOM ;		   ! ../../tmp/out/IDIOM_kom-lex.xml
INTERJ ;	   ! ../../tmp/out/INTERJ_kom-lex.xml
N ;			   ! ../../tmp/out/N_kom-lex.xml
NUM ;		   ! ../../tmp/out/NUM_kom-lex.xml
NUM-PREFIXES ; ! num-kom-morph.txt
PART ;		   ! ../../tmp/out/PART_kom-lex.xml
PART-ONOM ;	   ! ../../tmp/out/PART-ONOM_kom-lex.xml
PERS ;         ! pron-kom-morph.txt
POSTP ;		   ! ../../tmp/out/POSTP_kom-lex.xml
PRON ;		   ! ../../tmp/out/PRON_kom-lex.xml
PRON-DEM ;	   ! ../../tmp/out/PRON-DEM_kom-lex.xml
PRON-INDEF ;   ! ../../tmp/out/PRON-INDEF_kom-lex.xml
PRON-INTER ;   ! ../../tmp/out/PRON-INTER_kom-lex.xml
PRON-NEG ;	   ! pron-kom-morph.txt
PRON-Q ;	   ! ../../tmp/out/PRON-Q_kom-lex.xml
PRON-REFL ;	   ! ../../tmp/out/PRON-REFL_kom-lex.xml
PRON-REL ;	   ! pron-kom-morph.txt
PROP ;		   ! ../../tmp/out/PROP_kom-lex.xml
Punctuation ;  ! punct-kom-lex.txt
V ;			   ! ../../tmp/out/V_kom-lex.xml
V-INST ;	   ! ../../tmp/out/V-INST_kom-lex.xml
V-REFL ;	   ! ../../tmp/out/V-REFL_kom-lex.xml
VERBNEGATIVE ; ! verb-kom-morph.txt




! ======================================================= !
! These lexica are for parts of speech without morphology !
! ======================================================= !



! ============ !
! Postp lexica !
! ============ !

! Jaska! mikä on näiden kahden +Po -ryhmien erot?
! Poistetaan se ero toistaiseksi

!LEXICON POSTP1a
!+Po: # ;
! these are:
! ногӧн, выйӧн

LEXICON POST1
POSTP1 ; ! error, correct in xml file

!This contlex allows for relational word which, otherwise, are open 
!to extensive declension
LEXICON POSTP1
PO-COMP ;
PO-EGR ;
PO-ELA ;
PO-ILL ;
PO-INE ;
PO-INSTR ;
PO-PROL ;
PO-TRA ;
! these are: 
! аддза, бӧрті, бокиті, боксянь, вöсна, дырйи, йитӧдын, 
! кындзи, кузя, мысьт, моз, ног, ньылыд, паныдӧн, пӧлӧн, пыдди, пыр, понда, 
! ради, уліті, выліті, вывті, вомас, вомӧн

LEXICON PO-COMP
+Po+Comp:%>ся PO-PX ;
! бӧрся, гырся

LEXICON PO-EGR
+Po+Egr:%>сянь PO-PX ;

LEXICON PO-ELA
+Po+Ela:%>ысь K ;
+Po+Ela+PxSg1:%>сьым K ;
+Po+Ela+PxPl1:%>сьыным K ;
+Po+Ela+PxSg2:%>сьыд K ;
+Po+Ela+PxPl2:%>сьыныд K ;
+Po+Ela+PxSg3:%>сьыс K ;
+Po+Ela+PxPl3:%>сьыныс K ;

LEXICON PO-ILL
+Po+Ill:%>ӧ PO-PX ;
+Po+Ill+PxSg1:%>ам K ;
+Po+Ill+PxPl1:%>аным K ;
+Po+Ill+PxSg2:%>ад K ;
+Po+Ill+PxPl2:%>аныд K ;
+Po+Ill+PxSg3:%>ас K ;
+Po+Ill+PxPl3:%>аныс K ;
! кежлӧ, 

LEXICON PO-INE
+Po+Ine:%>ын K ;
+Po+Ine+PxSg1:%>ам K ;
+Po+Ine+PxPl1:%>аным K ;
+Po+Ine+PxSg2:%>ад K ;
+Po+Ine+PxPl2:%>аныд K ;
+Po+Ine+PxSg3:%>ас K ;
+Po+Ine+PxPl3:%>аныс K ;

LEXICON PO-INSTR
+Po+Instr:%>ӧн K ;
+Po+Instr+PxSg1:%>нам K ;
+Po+Instr+PxPl1:%>наным K ;
+Po+Instr+PxSg2:%>над K ;
+Po+Instr+PxPl2:%>наныд K ;
+Po+Instr+PxSg3:%>нас K ;
+Po+Instr+PxPl3:%>наныс K ;

LEXICON PO-PROL
+Po+Prl:%>ӧд K ;
+Po+Prl+PxSg1:%>ӧдым K ;
+Po+Prl+PxPl1:%>ӧдным K ;
+Po+Prl+PxSg2:%>ӧдыд K ;
+Po+Prl+PxPl2:%>ӧдныд K ;
+Po+Prl+PxSg3:%>ӧдыс K ;
+Po+Prl+PxPl3:%>ӧдныс K ;

LEXICON PO-TRA
+Po+Tra:%>ті K ;
+Po+Tra+PxSg1:%>тіым K ;
+Po+Tra+PxPl1:%>тіным K ;
+Po+Tra+PxSg2:%>тіыд K ;
+Po+Tra+PxPl2:%>тіныд K ;
+Po+Tra+PxSg3:%>тіыс K ;
+Po+Tra+PxPl3:%>тіныс K ;

!LEXICON PO-TERM
!+Po+Term:%>ӧдз PO-PX ;

LEXICON PO-PX
+Po: # ;
+Po+PxSg1:ӧй # ;
+Po+PxSg2:ыд # ;
+Po+PxSg3:ыс # ;
+Po+PxPl1:ным # ;
+Po+PxPl2:ныд # ;
+Po+PxPl3:ныс # ;
! мында, серти, вӧсна, 


! ==================================== !
! Lexica called End, whatever they are !
! ==================================== !

LEXICON ADVTAG ! these are checked as advs.
+Adv: COMPARATIVE ;

LEXICON EndConj
+CC: # ;

LEXICON EndPart
+Pcle: # ;

LEXICON EndInterj
+Interj: # ;

LEXICON EndNeg 
+CC+Neg: # ; ! ни

LEXICON EndQST
+Qst: # ;

LEXICON ONOM
+Onom: # ;



! ======= !
! Clitics !
! ======= !

LEXICON K
          # ; 
+Clt:%-а  # ;
+Clt:%-ӧ  # ;
+Clt:%>тӧ # ;
+Clt:%>сӧ # ;


! List of spurious lexica
! These should be fixed.


LEXICON ADV-SUPERL
+Adv+Superl: # ;


LEXICON COMPARATIVE
# ;
+Compar:%>джык K ;

