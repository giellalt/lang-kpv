Multichar_Symbols

+Err ! marking missing lexica

! POS
+N +V +A 
+Adv
+AdA ! Ad-adjective
+Manner ! with reference to type of adverb
+CC +CS +Interj +Pcle +Clt +Po 
+Pron ! Pronoun
+Indef
+Refl +Rel +Interr +Recipr
+Hum +NonHum ! distinguishing коді and мый 2011-11-09

! FMT: was missing 9/april/2011
+Pers
+Imprt


+Qnt ! Quantifier
+Onom ! onomapoetic words
+Descr ! descriptive words
+Clt/а !
+Clt/ӧ !
+Clt/тӧ !
+Clt/сӧ !

! Quantifiers 
+Appr ! Appromative numeral кык-ӧ-куим, кык-куим 'two or three' 
    ! NB! do not confuse with case +Apr
    ! Let's use flags for this one
+Num ! numeral
+Card
+Coll !Collective
+ZeroColl ! In combinations with the plural possessive marker the +Coll
          ! morpheme нан is not always necessary
+Distr ! Distributive
+Iter ! Iterative form expressing number of times
+Ord


! Verbal MSP
+Prs +Fut +Fut1 +Prt1 +Prt2 +Prf
+PrfIpf +HstPrf +PluPrf +HstPluPrf
+Ind +Imp +Cond +Opt
+Sg1 +Sg2 +Sg3 +Pl1 +Pl2 +Pl3
+Inf
+Pos +Neg +ConNeg
+Final ! мог, мон, моз 'so that I won't'
+Ger !Gerund This is used with
!+Der/иг

! Used for predicate plural marking
+Pred

! Nominal MSP
+Sg +Pl 
+Prop
+Sur ! Surname
+Mal
+Fem
+Plc ! Place name
+Org ! name of organization
+Nom
+Der/йӧ ! This appears in certain personal pronouns (Sg3, Pl1, Pl2, Pl3)
        ! It is mainly confined to the nominative but apparently can be attested elsewhere
+Gen +Abl +Dat +Com 
! One problem with the accusative in Komi is that the different morphemes
! have not been labeled. The nominative or Zero morpheme is used
! but then there are also the accusative ӧс form and the 2(Sg) тӧ 3(Sg) сӧ
+ZeroAcc
+Acc ! ӧс used not only with the first person but elsewhere to boot.
+Acc2 ! тӧ also used with the plural in places нытӧ not ныдтӧ
+Acc3 !  сӧ also used with the plural in places
! What should be done with the presonal pronouns Sg1 and Sg2
! whose accusative forms are in ӧ: менӧ and тэнӧ?

 +Ine +Ill 
+Ela +Car +Egr +Tra  +Apr +Ter
+Abe 
+Prl !Prolative "ӧд"
+Der/джык ! this is the comparative formative
+Compar ! comparative as opposed to superlative

!+Adc  ! not in use!
!!In personal pronouns there is a tendancy to utilize two different stems
+GenStem !helps distinguish between +Sg+GenStem+Ill:менамӧ and +Sg1+Ill:меӧ
+HabObj
+Instr
+Superl
+ACR +ABBR
+Cmpl ! complement of postpostion
      ! in the phrase ӧта-мӧд вежмӧныс it will be noted that this complement
      ! is not necessarily limited to postpositions, non-finites are also
      ! possible
+Cmp ! first part of compound
+Abs ! Absolute given as a tag for numerals when they are not +MWN
!removing +Ins tag 2011-10-10

+PxSg1 !This will be used with +Acc, but there should be a resolution
+PxSg1-ym
+PxSg1-oj
+PxSg2 +PxSg3 +PxPl1 +PxPl2 +PxPl3
+NoPoss

+Cns ! Consecutive -la
+Comp ! Case form sya


+Temp

!!Pair Verbs, Serial Verbs compounding
+SerialVerb

! tags
+AN ! Process Participle
+ANA !Process Participle 
+ANAA ! adverb derived from participle (+ANA)
+Der/чӧж !+CHOZH


+Instr
+Event
+Duration
!+IG This will be replaced by the segments:
!!+Der/иг+Duration+Ger!! 
+Der/иг
!Here we need the two sequential characters
!!to replace +OM
!+Der/ӧм+Event 
!This, of course, implicates a discussion 
!of the so-called gerund "ӧмӧн" +Event+N+Instr and

! These should be added below in derivation declarations
!
!
!+Der/ан
! +Der/ана +Der/анаа +Der/чӧж 
!!Then they should have indication of whether they are gerunds or participles!!
!!
+Der/тӧдз !+VTer
!!+TODZ Verbal terminative
+Der/тӧм !+VAbe
+Der/ысь +ActPrsPtc

+Der/кості ! +KOSTI
+Der/коста !+KOSTA
+Der/кежлӧ !+KEZHLO

+Der/мысь !+MYS
+Der/мысьт !+MYST

+Der/сор !+SOR
+Der/тыр !+TYR
+Der/тырйи !+TYRJI
+Der/тыръя !+TYRJA
+Der/мӧн !+MON
+Der/мӧнъя !Ӧнія коми кыв. 2000: 399-403

+CLB +PUNCT +LEFT +RIGHT

! Declaring adjectival derivations
+Der/а +Der/са +Der/ся 

! Declaring spatial adverb derivations; see also spatial postpositions
+Der/ла
+Der/ладор
+Spat ! spatial

!Modifier without noun
+Der/MWN
+Der/мед ! Superlative

!Declaring Indefinite Pronoun derivations
+Der/сюрӧ +Der/кӧ

!Declaring Comparison particles
+Der/моз !+MOZ ! diminishing, kind of, sort of
+Der/кодь ! diminishing, kind of, sort of

!Declaring Noun derivations
+Der/лун ! adjective-to-noun
! the combinatory +Event preceding the NP-final noun
+Der/ӧм ! verb-to-noun

!Declaring Deverbal derivations of verbs
+Der/лы 
+Der/ывлы
+Der/ышт
+Der/лывлы
+Der/сь
+Der/ал
+Der/овт
+Der/ась


+Dim 
!! The +Dim is used with the V-V derivation +Der/ыштны
!! for expressing activity of 
!! (a) brief duration
!! (b) small/limited proportions
! Declaring symbols for morphophonology

к2 л2 м2 т2 К2 Л2 М2 Т2 %>
%^Inf

+Use/Sub ! Для не-литературного слова / форма / и.т.д.

! Flags used with serial verbs
! INF = Infinitive
@U.CONJ-INF.YES@
@U.CONJ-INF.NO@

! TX = tense
@U.CONJ-TX.FUT@
@U.CONJ-TX.PRES@
@U.CONJ-TX.PRET1@
@U.CONJ-TX.PRET2@

! MX = mood
@U.CONJ-MX.IND@
@U.CONJ-MX.IMP@

! CONNEG = negation
@U.CONJ-CONNEG.YES@
@U.CONJ-CONNEG.NO@

! NX = number
@U.CONJ-NX.PL@
@U.CONJ-NX.SG@

! PX = person
@U.CONJ-PX.1@
@U.CONJ-PX.2@
@U.CONJ-PX.3@

! Removal
@C.CONJ-INF@
@C.CONJ-TX@
@C.CONJ-MX@
@C.CONJ-CONNEG@
@C.CONJ-NX@
@C.CONJ-PX@


! Key lexicon

! These are the lexica that are found in the bin/ directory,
! generated by the Makefile from the dictionary

LEXICON Root
! contlexes    ! the file, where the contlex is located
! ------------ ! ---------------------------------------
A ;			   ! ../../tmp/out/A_kom-lex.xml
A-SUPERL ;	   ! ../../tmp/out/A-SUPERL_kom-lex.xml
ADV ;		   ! ../../tmp/out/ADV_kom-lex.xml
!!! ADVROOT ;	   ! ../../tmp/out/ADVROOT_kom-lex.xml ! no longer exists
Abbreviation ; ! abbr-kom-lex.txt
Acronym ;	   ! acro-kom-lex.txt
ACRON ;			   ! ../../tmp/out/ACRON_kom-lex.xml
CONJ ;		   ! ../../tmp/out/CONJ_kom-lex.xml
IDIOM ;		   ! ../../tmp/out/IDIOM_kom-lex.xml
INTERJ ;	   ! ../../tmp/out/INTERJ_kom-lex.xml
INTERR ;         ! pron-kom-morph.txt
N ;			   ! ../../tmp/out/N_kom-lex.xml
NUM ;		   ! ../../tmp/out/NUM_kom-lex.xml
NUM-PREFIXES ; ! num-kom-morph.txt
PART ;		   ! ../../tmp/out/PART_kom-lex.xml
PART-ONOM ;	   ! ../../tmp/out/PART-ONOM_kom-lex.xml
PERS ;         ! pron-kom-morph.txt
POSTP ;		   ! ../../tmp/out/POSTP_kom-lex.xml
PRON ;		   ! ../../tmp/out/PRON_kom-lex.xml
PRON-DEM ;	   ! ../../tmp/out/PRON-DEM_kom-lex.xml
PRON-INDEF ;   ! ../../tmp/out/PRON-INDEF_kom-lex.xml
PRON-INTER ;   ! ../../tmp/out/PRON-INTER_kom-lex.xml
PRON-NEG ;	   ! pron-kom-morph.txt
PRON-Q ;	   ! ../../tmp/out/PRON-Q_kom-lex.xml
PRON-REFL ;	   ! ../../tmp/out/PRON-REFL_kom-lex.xml
PRON-REL ;	   ! pron-kom-morph.txt
PROP ;		   ! ../../tmp/out/PROP_kom-lex.xml
PROP-RUSSIAN-MALE-SURNAMES-Derivable ;		   ! ../../tmp/out/PROP-RUSSIAN-MALE-SURNAMES-Derivable_kom-lex.xml
PROP-CYRILLIC-MALE-SURNAMES ;		   ! ../../tmp/out/PROP-CYRILLIC-MALE-SURNAMES_kom-lex.xml
Punctuation ;  ! punct-kom-lex.txt
V ;			   ! ../../tmp/out/V_kom-lex.xml
VERBNEGATIVE ; ! verb-kom-morph.txt




! ======================================================= !
! These lexica are for parts of speech without morphology !
! ======================================================= !

! Compounding

LEXICON R
%- N ;



! ============ !
! Postp lexica !
! ============ !

! Jaska! mikä on näiden kahden +Po -ryhmien erot?
! Poistetaan se ero toistaiseksi

!LEXICON POSTP1a
!+Po: # ;
! these are:
! ногӧн, выйӧн

LEXICON POST1
POSTP1 ; ! error, correct in xml file

!This contlex allows for relational word which, otherwise, are open 
!to extensive declension
LEXICON POSTP1
PO-APR ;
PO-APR-ILL ;
PO-APR-INE ;
PO-COMP ;
PO-EGR ;
PO-ELA ;
PO-ILL ;
PO-INE ;
PO-INSTR ;
PO-PROL ;
PO-TRA ;
PO-TER ;
! these are: 
! аддза, бӧрті, бокиті, боксянь, дырйи, йитӧдын, 
! кузя, ног, ньылыд, паныдӧн, пӧлӧн, пыдди, пыр, понда, 
! ради, уліті, выліті, вывті, вомас, вомӧн
! пӧвст

LEXICON PO-APR
+Po+Sg+Apr:%>лань PO-PX ;

LEXICON PO-APR-ILL
+Po+Sg+Apr+Ill:%>ланьӧ K ;
+Po+Sg+Apr+Ill+PxSg1-ym:%>ланям K ;
+Po+Sg+Apr+Ill+PxPl1:%>ланяным K ;
+Po+Sg+Apr+Ill+PxSg2:%>ланяд K ;
+Po+Sg+Apr+Ill+PxPl2:%>ланяныд K ;
+Po+Sg+Apr+Ill+PxSg3:%>ланяс K ;
+Po+Sg+Apr+Ill+PxPl3:%>ланяныс K ;

LEXICON PO-APR-INE
+Po+Sg+Apr+Ine:%>ланьын K ;
+Po+Sg+Apr+Ine+PxSg1-ym:%>ланям K ;
+Po+Sg+Apr+Ine+PxPl1:%>ланяным K ;
+Po+Sg+Apr+Ine+PxSg2:%>ланяд K ;
+Po+Sg+Apr+Ine+PxPl2:%>ланяныд K ;
+Po+Sg+Apr+Ine+PxSg3:%>ланяс K ;
+Po+Sg+Apr+Ine+PxPl3:%>ланяныс K ;


LEXICON PO-COMP
+Po+Sg+Comp:%>ся PO-PX ;
! бӧрся, гырся

LEXICON PO-EGR
+Po+Sg+Egr:%>сянь PO-PX ;

LEXICON PO-ELA
+Po+Sg+Ela:%>ысь K ;
+Po+Sg+Ela+PxSg1-ym:%>сьым K ;
+Po+Sg+Ela+PxPl1:%>сьыным K ;
+Po+Sg+Ela+PxSg2:%>сьыд K ;
+Po+Sg+Ela+PxPl2:%>сьыныд K ;
+Po+Sg+Ela+PxSg3:%>сьыс K ;
+Po+Sg+Ela+PxPl3:%>сьыныс K ;

LEXICON PO-ILL
+Po+Sg+Ill:%>ӧ PO-PX ;
+Po+Sg+Ill+PxSg1-ym:%>ам K ;
+Po+Sg+Ill+PxPl1:%>аным K ;
+Po+Sg+Ill+PxSg2:%>ад K ;
+Po+Sg+Ill+PxPl2:%>аныд K ;
+Po+Sg+Ill+PxSg3:%>ас K ;
+Po+Sg+Ill+PxPl3:%>аныс K ;
! кежлӧ, 

LEXICON PO-INE
+Po+Sg+Ine:%>ын K ;
+Po+Sg+Ine+PxSg1-ym:%>ам K ;
+Po+Sg+Ine+PxPl1:%>аным K ;
+Po+Sg+Ine+PxSg2:%>ад K ;
+Po+Sg+Ine+PxPl2:%>аныд K ;
+Po+Sg+Ine+PxSg3:%>ас K ;
+Po+Sg+Ine+PxPl3:%>аныс K ;

LEXICON PO-INSTR
+Po+Sg+Instr:%>ӧн K ;
+Po+Sg+Instr+PxSg1-ym:%>нам K ;
+Po+Sg+Instr+PxPl1:%>наным K ;
+Po+Sg+Instr+PxSg2:%>над K ;
+Po+Sg+Instr+PxPl2:%>наныд K ;
+Po+Sg+Instr+PxSg3:%>нас K ;
+Po+Sg+Instr+PxPl3:%>наныс K ;

LEXICON PO-PROL
+Po+Sg+Prl:%>ӧд K ;
+Po+Sg+Prl+PxSg1-ym:%>ӧдым K ;
+Po+Sg+Prl+PxPl1:%>ӧдным K ;
+Po+Sg+Prl+PxSg2:%>ӧдыд K ;
+Po+Sg+Prl+PxPl2:%>ӧдныд K ;
+Po+Sg+Prl+PxSg3:%>ӧдыс K ;
+Po+Sg+Prl+PxPl3:%>ӧдныс K ;

LEXICON PO-TRA
+Po+Sg+Tra:%>ті K ;
+Po+Sg+Tra+PxSg1-ym:%>тіым K ;
+Po+Sg+Tra+PxPl1:%>тіным K ;
+Po+Sg+Tra+PxSg2:%>тіыд K ;
+Po+Sg+Tra+PxPl2:%>тіныд K ;
+Po+Sg+Tra+PxSg3:%>тіыс K ;
+Po+Sg+Tra+PxPl3:%>тіныс K ;

LEXICON PO-TER
+Po+Sg+Term:%>ӧдз PO-PX ;


LEXICON POSTP-NOUN
+Po+Sg+Nom: # ;
+Po+Sg+Nom+PxSg1-oj:%>ӧй # ;
+Po+Sg+Nom+PxSg2:%>ыд # ;
+Po+Sg+Nom+PxSg3:%>ыс # ;
+Po+Sg+Nom+PxPl1:%>ным # ;
+Po+Sg+Nom+PxPl2:%>ныд # ;
+Po+Sg+Nom+PxPl3:%>ныс # ;
 POSTP1 ;
+Po+Pl+PRL:%>ъясӧд # ; 
! гӧгӧр, 

LEXICON PO-PX
+Po: # ;
+Po+PxSg1-oj:%>ӧй # ;
+Po+PxSg2:%>ыд # ;
+Po+PxSg3:%>ыс # ;
+Po+PxPl1:%>ным # ;
+Po+PxPl2:%>ныд # ;
+Po+PxPl3:%>ныс # ;
! вӧсна, кындзи, моз,  мында, мысьт, серти,

LEXICON PO-TEMP-IS_TRA-INSTR-SG3
+Po+Temp+Tra+Instr+PxSg3: K ;

! ==================================== !
! Lexica called End, whatever they are !
! ==================================== !

LEXICON Ad-ATAG ! these are checked as advs.
+AdA: # ;

LEXICON ADV-IS_GER-MON ! these are checked as gerunds in +Der/мӧн
+V+Der/мӧн: ADVTAG ;

LEXICON ADV-IS_SUPERL !
+Der/мед: ADVTAG ;

LEXICON ADVTAG ! these are checked as advs.
+Adv: COMPARATIVE ;

LEXICON ADV-SPAT ! these are adverbs in spatial cases
      !and they probably are equivalent in morphology to postpositions
 ADV-SPAT-APR ;
 ADV-SPAT-EGR ;
 ADV-SPAT-ELA ;
 ADV-SPAT-ILL ;
 ADV-SPAT-INE ;
 ADV-SPAT-PRL ;
 ADV-SPAT-TER ;
 ADV-SPAT-TRA ;

LEXICON ADV-SPAT-LA ! These adverbs have a formative in ла
+Der/ла: ADV-SPAT ;

LEXICON ADV-SPAT-APR
+Adv+Spat+Apr:%>лань COMPARATIVE ;

LEXICON ADV-SPAT-EGR
+Adv+Spat+Egr:%>сянь COMPARATIVE ;

LEXICON ADV-SPAT-ELA
+Adv+Spat+Ela:%>ысь COMPARATIVE ;

LEXICON ADV-SPAT-ILL
+Adv+Spat+Ill:%>ӧ COMPARATIVE ;

LEXICON ADV-SPAT-INE
+Adv+Spat+Ine:%>ын COMPARATIVE ;

LEXICON ADV-SPAT-PRL
+Adv+Spat+Prl:%>ӧд K ;

LEXICON ADV-SPAT-TRA
+Adv+Spat+Tra:%>ті COMPARATIVE ;

LEXICON ADV-SPAT-TER
+Adv+Spat+Ter:%>ӧдз COMPARATIVE ;

LEXICON ADV-VTER
+V+Der/тӧдз+Adv+Temp+Ter:%>тӧдз K ;

LEXICON EndConj
+CC: # ;

LEXICON EndPart
+Pcle: # ;

LEXICON EndInterj
+Interj: # ;

LEXICON EndNeg 
+CC+Neg: # ; ! ни

LEXICON EndQST
+Qst: # ;

LEXICON ONOM-End
+Onom: # ;

LEXICON DESCR-End
+Descr: # ;



! ======= !
! Clitics !
! ======= !

LEXICON K
                 # ; 
+Clt+Clt/а:%>%-а # ;
+Clt+Clt/ӧ:%>%-ӧ # ;
+Clt+Clt/тӧ:%>тӧ # ;
+Clt+Clt/сӧ:%>сӧ # ;


! List of spurious lexica
! These should be fixed.


LEXICON ADV-SUPERL
+Adv+Superl: # ;


LEXICON COMPARATIVE
                        K ;
+Der/джык+Compar:%>джык K ;

