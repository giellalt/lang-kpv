Multichar_Symbols

+Err ! marking missing lexica

! POS
+N +V +A 
+Adv +CC +CS +Interj +Pron +Num +Pcle +Clt +Po 
+Qnt ! Quantifier
+Onom ! onomapoetic words
+NoClt !
+Clt/а !
+Clt/ӧ !
+Clt/тӧ !
+Clt/сӧ !

! Verbal MSP
+Prs +Fut +Fut1 +Prt1 +Prt2 +Prf
+PrfIpf +HstPrf +PluPrf +HstPluPrf
+Ind +Imp +Cond +Opt
+Sg1 +Sg2 +Sg3 +Pl1 +Pl2 +Pl3
+Inf
+Pos +Neg +ConNeg
+Ger !Gerund This is used with
!+Der/иг

! FMT: was missing 9/april/2011
+Pers
+Imprt

! Used for predicate plural marking
+Pred

! Nominal MSP
+Sg +Pl
+Prop
+Nom +Gen +Abl +Dat +Com 
! One problem with the accusative in Komi is that the different morphemes
! have not been labeled. The nominative or Zero morpheme is used
! but then there are also the accusative ӧс form and the 2Sg тӧ 3Sg сӧ
+ZeroAcc
+Acc +Ine +Ill 
+Ela +Car +Egr +Tra  +Apr +Ter
+Abe 
+Prl !Prolative "ӧд"
+Compar ! comparative as opposed to superlative
+NoComparison ! counterpart to previous
!+Adc  ! not in use!
!!In personal pronouns there is a tendancy to utilize two different stems
+GenStem !helps distinguish between +Sg+GenStem+Ill:менамӧ and +Sg1+Ill:меӧ
+HabObj
+Instr
+Superl
+ACR +ABBR
+Cmpl ! complement of postpostion
+Cmp ! first part of compound
+Abs ! Absolute given as a tag for numerals when they are not +MWN
!removing +Ins tag 2011-10-10

+Refl +Rel +Interr +Recipr

+PxSg1 +PxSg2 +PxSg3 +PxPl1 +PxPl2 +PxPl3
+NoPoss

+Card
+Cns ! Consecutive -la
+Coll !Collective
+Comp ! Case form sya

+Det ! determiner like уна 'many'. Look at this.

+Ord
+Temp



! tags
+AN ! Process Participle
+ANA !Process Participle 
+ANAA ! adverb derived from participle (+ANA)
+Der/чӧж !+CHOZH

+Instr
+Event
+Duration
!+IG This will be replaced by the segments:
!!+Der/иг+Duration+Ger!! 
+Der/иг
!Here we need the two sequential characters
!!to replace +OM
!+Der/ӧм+Event 
!This, of course, implicates a discussion 
!of the so-called gerund "ӧмӧн" +Event+N+Instr and
! the combinatory +Event preceding the NP-final noun
+Der/ӧм

! These should be added below in derivation declarations
!
!
!+Der/ан +Der/ана +Der/анаа +Der/чӧж 
!!Then they should have indication of whether they are gerunds or participles!!
!!
+Der/тӧдз !+VTer
!!+TODZ Verbal terminative
+Der/тӧм !+VAbe
+Der/ысь +ActPrsPtc

+Der/кості ! +KOSTI
+Der/коста !+KOSTA
+Der/кежлӧ !+KEZHLO

+Der/мысь !+MYS
+Der/мысьт !+MYST

+Der/сор !+SOR
+Der/тыр !+TYR
+Der/тырйи !+TYRJI
+Der/тыръя !+TYRJA
+Der/мӧн !+MON
+Der/мӧнъя !Ӧнія коми кыв. 2000: 399-403

+CLB +PUNCT +LEFT +RIGHT

! Declaring Derivations
+Der/а +Der/са +Der/ся 

!Modifier without noun
+Der/MWN

!Declaring Indefinite Pronoun derivations
+Der/сюрӧ +Der/кӧ

!Declaring Comparison particles
+Der/моз !+MOZ ! diminishing, kind of, sort of
+Der/кодь ! diminishing, kind of, sort of

!Declaring Deverbal derivations of verbs
+Der/л 
+Der/ывл
+Der/ышт
+Der/лывл
+Der/сь
+Der/ал
+Der/овт
+Der/ась


+Dim 
!! The +Dim is used with the V-V derivation +Der/ыштны
!! for expressing activity of 
!! (a) brief duration
!! (b) small/limited proportions
! Declaring symbols for morphophonology

к2 л2 м2 т2 К2 Л2 М2 Т2 %>
%^Inf

+Use/Sub ! Для не-литературного слова / форма / и.т.д.


! Key lexicon

! These are the lexica that are found in the bin/ directory,
! generated by the Makefile from the dictionary

LEXICON Root
! contlexes    ! the file, where the contlex is located
! ------------ ! ---------------------------------------
A ;			   ! ../../tmp/out/A_kom-lex.xml
A-SUPERL ;	   ! ../../tmp/out/A-SUPERL_kom-lex.xml
ADV ;		   ! ../../tmp/out/ADV_kom-lex.xml
ADVROOT ;	   ! ../../tmp/out/ADVROOT_kom-lex.xml
Abbreviation ; ! abbr-kom-lex.txt
Acronym ;	   ! acro-kom-lex.txt
CONJ ;		   ! ../../tmp/out/CONJ_kom-lex.xml
IDIOM ;		   ! ../../tmp/out/IDIOM_kom-lex.xml
INTERJ ;	   ! ../../tmp/out/INTERJ_kom-lex.xml
N ;			   ! ../../tmp/out/N_kom-lex.xml
NUM ;		   ! ../../tmp/out/NUM_kom-lex.xml
NUM-PREFIXES ; ! num-kom-morph.txt
PART ;		   ! ../../tmp/out/PART_kom-lex.xml
PART-ONOM ;	   ! ../../tmp/out/PART-ONOM_kom-lex.xml
PERS ;         ! pron-kom-morph.txt
POSTP ;		   ! ../../tmp/out/POSTP_kom-lex.xml
PRON ;		   ! ../../tmp/out/PRON_kom-lex.xml
PRON-DEM ;	   ! ../../tmp/out/PRON-DEM_kom-lex.xml
PRON-INDEF ;   ! ../../tmp/out/PRON-INDEF_kom-lex.xml
PRON-INTER ;   ! ../../tmp/out/PRON-INTER_kom-lex.xml
PRON-NEG ;	   ! pron-kom-morph.txt
PRON-Q ;	   ! ../../tmp/out/PRON-Q_kom-lex.xml
PRON-REFL ;	   ! ../../tmp/out/PRON-REFL_kom-lex.xml
PRON-REL ;	   ! pron-kom-morph.txt
PROP ;		   ! ../../tmp/out/PROP_kom-lex.xml
Punctuation ;  ! punct-kom-lex.txt
V ;			   ! ../../tmp/out/V_kom-lex.xml
V-INST ;	   ! ../../tmp/out/V-INST_kom-lex.xml
V-REFL ;	   ! ../../tmp/out/V-REFL_kom-lex.xml
VERBNEGATIVE ; ! verb-kom-morph.txt




! ======================================================= !
! These lexica are for parts of speech without morphology !
! ======================================================= !

! Compounding

LEXICON R
%- N ;



! ============ !
! Postp lexica !
! ============ !

! Jaska! mikä on näiden kahden +Po -ryhmien erot?
! Poistetaan se ero toistaiseksi

!LEXICON POSTP1a
!+Po: # ;
! these are:
! ногӧн, выйӧн

LEXICON POST1
POSTP1 ; ! error, correct in xml file

!This contlex allows for relational word which, otherwise, are open 
!to extensive declension
LEXICON POSTP1
PO-COMP ;
PO-EGR ;
PO-ELA ;
PO-ILL ;
PO-INE ;
PO-INSTR ;
PO-PROL ;
PO-TRA ;
! these are: 
! аддза, бӧрті, бокиті, боксянь, дырйи, йитӧдын, 
! кузя, ног, ньылыд, паныдӧн, пӧлӧн, пыдди, пыр, понда, 
! ради, уліті, выліті, вывті, вомас, вомӧн

LEXICON PO-COMP
+Po+Sg+Comp:%>ся PO-PX ;
! бӧрся, гырся

LEXICON PO-EGR
+Po+Sg+Egr:%>сянь PO-PX ;

LEXICON PO-ELA
+Po+Sg+Ela:%>ысь K ;
+Po+Sg+Ela+PxSg1:%>сьым K ;
+Po+Sg+Ela+PxPl1:%>сьыным K ;
+Po+Sg+Ela+PxSg2:%>сьыд K ;
+Po+Sg+Ela+PxPl2:%>сьыныд K ;
+Po+Sg+Ela+PxSg3:%>сьыс K ;
+Po+Sg+Ela+PxPl3:%>сьыныс K ;

LEXICON PO-ILL
+Po+Sg+Ill:%>ӧ PO-PX ;
+Po+Sg+Ill+PxSg1:%>ам K ;
+Po+Sg+Ill+PxPl1:%>аным K ;
+Po+Sg+Ill+PxSg2:%>ад K ;
+Po+Sg+Ill+PxPl2:%>аныд K ;
+Po+Sg+Ill+PxSg3:%>ас K ;
+Po+Sg+Ill+PxPl3:%>аныс K ;
! кежлӧ, 

LEXICON PO-INE
+Po+Sg+Ine:%>ын K ;
+Po+Sg+Ine+PxSg1:%>ам K ;
+Po+Sg+Ine+PxPl1:%>аным K ;
+Po+Sg+Ine+PxSg2:%>ад K ;
+Po+Sg+Ine+PxPl2:%>аныд K ;
+Po+Sg+Ine+PxSg3:%>ас K ;
+Po+Sg+Ine+PxPl3:%>аныс K ;

LEXICON PO-INSTR
+Po+Sg+Instr:%>ӧн K ;
+Po+Sg+Instr+PxSg1:%>нам K ;
+Po+Sg+Instr+PxPl1:%>наным K ;
+Po+Sg+Instr+PxSg2:%>над K ;
+Po+Sg+Instr+PxPl2:%>наныд K ;
+Po+Sg+Instr+PxSg3:%>нас K ;
+Po+Sg+Instr+PxPl3:%>наныс K ;

LEXICON PO-PROL
+Po+Sg+Prl:%>ӧд K ;
+Po+Sg+Prl+PxSg1:%>ӧдым K ;
+Po+Sg+Prl+PxPl1:%>ӧдным K ;
+Po+Sg+Prl+PxSg2:%>ӧдыд K ;
+Po+Sg+Prl+PxPl2:%>ӧдныд K ;
+Po+Sg+Prl+PxSg3:%>ӧдыс K ;
+Po+Sg+Prl+PxPl3:%>ӧдныс K ;

LEXICON PO-TRA
+Po+Sg+Tra:%>ті K ;
+Po+Sg+Tra+PxSg1:%>тіым K ;
+Po+Sg+Tra+PxPl1:%>тіным K ;
+Po+Sg+Tra+PxSg2:%>тіыд K ;
+Po+Sg+Tra+PxPl2:%>тіныд K ;
+Po+Sg+Tra+PxSg3:%>тіыс K ;
+Po+Sg+Tra+PxPl3:%>тіныс K ;

!LEXICON PO-TERM
!+Po+Sg+Term:%>ӧдз PO-PX ;


LEXICON POSTP-NOUN
+Po+Sg+Nom: # ;
+Po+Sg+Nom+PxSg1:ӧй # ;
+Po+Sg+Nom+PxSg2:ыд # ;
+Po+Sg+Nom+PxSg3:ыс # ;
+Po+Sg+Nom+PxPl1:ным # ;
+Po+Sg+Nom+PxPl2:ныд # ;
+Po+Sg+Nom+PxPl3:ныс # ;
 POSTP1 ;
+Po+Pl+PRL:ъясӧд # ; 
! гӧгӧр, 

LEXICON PO-PX
+Po: # ;
+Po+PxSg1:ӧй # ;
+Po+PxSg2:ыд # ;
+Po+PxSg3:ыс # ;
+Po+PxPl1:ным # ;
+Po+PxPl2:ныд # ;
+Po+PxPl3:ныс # ;
! вӧсна, кындзи, моз,  мында, мысьт, серти,


! ==================================== !
! Lexica called End, whatever they are !
! ==================================== !

LEXICON ADVTAG ! these are checked as advs.
+Adv: COMPARATIVE ;

LEXICON ADV-SPAT ! these are adverbs in spatial cases
      !and they probably are equivalent in morphology to postpositions
+Adv+Ela:ысь COMPARATIVE ;
+Adv+Ill:ӧ COMPARATIVE ;
+Adv+Ine:ын COMPARATIVE ;
+Adv+Ter:ӧдз COMPARATIVE ;

LEXICON EndConj
+CC: # ;

LEXICON EndPart
+Pcle: # ;

LEXICON EndInterj
+Interj: # ;

LEXICON EndNeg 
+CC+Neg: # ; ! ни

LEXICON EndQST
+Qst: # ;

LEXICON ONOM
+Onom: # ;



! ======= !
! Clitics !
! ======= !

LEXICON K
+NoClt:      # ; 
+Clt+Clt/а:%-а  # ;
+Clt+Clt/ӧ:%-ӧ  # ;
+Clt+Clt/тӧ:%>тӧ # ;
+Clt+Clt/сӧ:%>сӧ # ;


! List of spurious lexica
! These should be fixed.


LEXICON ADV-SUPERL
+Adv+Superl: # ;


LEXICON COMPARATIVE
+NoComparison: K ;
+Compar:%>джык K ;

