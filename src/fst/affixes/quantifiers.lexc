

! ================================ !
!! !!!Numeral morphological lexica 
! ================================ !

!This has to be worked on 2012-01-19
LEXICON NUM-CARD_
 CARD ;
:%>ӧд ORD ;
+Err/Dial:%>эд ORD ;
!:%>нан COLL ;

LEXICON CARD
!+Card: ABS_ ; 2018-10-21
+Card: NumCASEPOSSLEX ;
+Card+Der/кӧ+Presentational:%>%-кӧ WordEnd ;
+Card+Der/кӧ+Presentational+Err/Dial:%>%-ке WordEnd ;
!+Ord:%>ӧд NumCASEPOSSLEX ;
+Card+Iter:%>ысь K ; ! нёльысь
+Card+Iter+Ins:%>ысьӧн K ; !нёльысьӧн
+Card+Iter+Ins+Err/Dial:%>ысен K ; !нёльысьӧн
+Distr:%>ӧн PredMonLex ; !нёльӧн
+Distr+Err/Dial:%>эн PredMonLex ; !нёльӧн




LEXICON ORD
+Ord: NumCASEPOSSLEX ; ! ӧд part of the stem
+Ord+Iter:%>ысь K ;
+Ord+Iter+Ins:%>ысьӧн K ;
+Ord+Iter+Ins+Err/Dial:%>ысен K ;
+Ord+Distr:%>ӧн PredMonLex ;
+Ord+Distr+Err/Dial:%>эн PredMonLex ;

LEXICON DET_
!+Det+Ord: ABS_ ; 2018-10-21
+Det+Ord: NumCASEPOSSLEX ; ! ӧд part of the stem
+Det+Ord+Iter:%>ысь K ;
+Det+Ord+Iter+Ins:%>ысьӧн K ;
+Det+Ord+Iter+Ins+Err/Dial:%>ысен K ;
+Det+Card+Distr:%>ӧн K ;
+Det+Card+Distr+Err/Dial:%>эн K ;

LEXICON DET_END
!кыкнан
+AssocColl: # ;


LEXICON NUM-IS_DISTR
+Distr: PredMonLex ;

LEXICON QNT_
+Qnt: NumCASEPOSSLEX ;
+Qnt+Card+Iter:%>ысь K ;
+Qnt+Card+Iter+Ins:%>ысьӧн K ;
+Qnt+Card+Iter+Ins+Err/Dial:%>ысен K ;
+Qnt+Distr:%>ӧн PredMonLex ;
+Qnt+Distr+Err/Dial:%>эн PredMonLex ;


!! LEXICON NUM-APPR ! 2011-11-03 This will need work
LEXICON CARD-APPR
+Card+Appr: K ;


! =================== !
!! !!Inflectional lexica 
! =================== !

!! All nouns follow one contlex "Noun1"
!! to begin with here is simply a list of all variant
!! with no more variants beyond:

! Numbers
! Case + Px or Px+Case


LEXICON NumCASEPOSSLEX
+Sg: NumMWN ;
+Pl:%>ъяс NumMWN ;

LEXICON NumMWN
+Nom: K ;
!+Acc: K ; !This needs something done to it. There should be a TAG distinction
          !between +Acc: and +Acc:%>ӧс
          ! Introducing +ZeroAcc
	  ! REMOVE +ACC if ZERO MORPH 2020-10-13

 ABL/MORPH-THEN-WORDEND ;
 ACC-PXSG1/MORPH-THEN-WORDEND ; !вошис эмакыв
 ACC-PXSG3/MORPH-THEN-WORDEND ;
 APR/MORPH-THEN-WORDEND ;
 CAR/MORPH-THEN-WORDEND ;
 CMPR/MORPH-THEN-WORDEND ;
 CNS/MORPH-THEN-WORDEND ;
 COM/MORPH-THEN-WORDEND ;
 DAT/MORPH-THEN-WORDEND ;
 EGR/MORPH-THEN-WORDEND ;
 ELA/MORPH-THEN-WORDEND ;
 GEN/MORPH-THEN-WORDEND ;
 ILL/MORPH-THEN-WORDEND ;
 INE/MORPH-THEN-WORDEND ;
 INS/MORPH-THEN-WORDEND ;
 PRL/MORPH-THEN-WORDEND ;
 TER/MORPH-THEN-WORDEND ; !149
!+Prl+Tra:%>ӧдті K ;


!1SG

ACC-PXSG1/MORPH-THEN-WORDEND ; !Zero marking on the possessive suffix, as it were.

+Ins+PxSg1:%>нам K ;
+Ins+PxSg1:%>намӧн K ; !Check me
!+Ins+PxSg1:%>намсӧ K ; !Check me

+Ine+PxSg1:%>ам K ;
+Ela+PxSg1:%>сьым K ;
+Ill+PxSg1:%>ам K ;

 ABL-PXSG1-SOPC/MORPH-THEN-K ;
 APR-PXSG1-SOPC/MORPH-THEN-K ;
 CAR-PXSG1-SOPC/MORPH-THEN-K ;
 DAT-PXSG1-SOPC/MORPH-THEN-K ;
 EGR-PXSG1-SOPC/MORPH-THEN-K ;
 GEN-PXSG1-SOPC/MORPH-THEN-K ;
 NOM-PXSG1/MORPH-THEN-K ;
 PRL-PXSG1-SOCP/MORPH-THEN-K ;
 TER-PXSG1-SOCP/MORPH-THEN-K ;


!2SG
+Nom+PxSg2:%>ыд K ;
+Gen+PxSg2:%>ыдлӧн K ;
+Abl+PxSg2:%>ыдлысь K ;
+Dat+PxSg2:%>ыдлы K ;
 ACC-PXSG2/MORPH-THEN-WORDEND ;

+Ins+PxSg2:%>над K ;
+Ins+PxSg2:%>надӧн K ; !Checkme

+Com+PxSg2:%>ыдкӧд K ;

 CAR-PXSG2-SOPC/MORPH-THEN-K ;

+Cns+PxSg2:%>ыдла K ;

+Ine+PxSg2:%>ад K ;
+Ela+PxSg2:%>сьыд K ;
+Ill+PxSg2:%>ад K ;
+Apr+PxSg2:%>ыдлань K ;
+Egr+PxSg2:%>ыдсянь K ;
+Prl+PxSg2:%>ӧдыд K ;
+Ter+PxSg2:%>ӧдзыд K ;


!3SG
+Nom+PxSg3:%>ыс K ;
+Gen+PxSg3:%>ыслӧн K ;
+Abl+PxSg3:%>ыслысь K ;
+Dat+PxSg3:%>ыслы K ;
 ACC-PXSG3/MORPH-THEN-WORDEND ;

+Ins+PxSg3:%>нас K ;

+Com+PxSg3:%>ыскӧд K ;

+Car+PxSg3:%>ыстӧг K ;
+Car+PxSg3:%>тӧгыс K ;

+Cns+PxSg3:%>ысла K ;

+Ine+PxSg3:%>ас K ;
+Ela+PxSg3:%>сьыс K ;
+Ill+PxSg3:%>ас K ;
+Apr+PxSg3:%>ыслань K ;
+Egr+PxSg3:%>ыссянь K ;
+Prl+PxSg3:%>ӧдыс K ;
+Ter+PxSg3:%>ӧдзыс K ;


!1PL
+Nom+PxPl1:%>ным K ;
+Gen+PxPl1:%>нымлӧн K ;
+Abl+PxPl1:%>нымлысь K ;
+Dat+PxPl1:%>нымлы K ;
 ACC-PXPL1/MORPH-THEN-WORDEND ;

+Ins+PxPl1:%>наным K ;

+Com+PxPl1:%>нымкӧд K ;

+Car+PxPl1:%>нымтӧг K ;

+Cns+PxPl1:%>нымла K ;

+Ine+PxPl1:%>аным K ;
+Ela+PxPl1:%>сьыным K ;
+Ill+PxPl1:%>аным K ;
+Apr+PxPl1:%>нымлань K ;
+Egr+PxPl1:%>нымсянь K ;
+Prl+PxPl1:%>ӧдным K ;
+Ter+PxPl1:%>ӧдзным K ;

!2PL
+Nom+PxPl2:%>ныд K ;
+Gen+PxPl2:%>ныдлӧн K ;
+Abl+PxPl2:%>ныдлысь K ;
+Dat+PxPl2:%>ныдлы K ;
 ACC-PXPL2/MORPH-THEN-WORDEND ;

+Ins+PxPl2:%>наныд K ;

+Com+PxPl2:%>ныдкӧд K ;

+Car+PxPl2:%>ныдтӧг K ;

+Cns+PxPl2:%>ныдла K ;

+Ine+PxPl2:%>аныд K ;
+Ela+PxPl2:%>сьыныд K ;
+Ill+PxPl2:%>аныд K ;
+Apr+PxPl2:%>ныдлань K ;
+Egr+PxPl2:%>ныдсянь K ;
+Prl+PxPl2:%>ӧдныд K ;
+Ter+PxPl2:%>ӧдзныд K ;


!3PL
+Nom+PxPl3:%>ныс K ;
+Gen+PxPl3:%>ныслӧн K ;
+Abl+PxPl3:%>ныслысь K ;
+Dat+PxPl3:%>ныслы K ;
 ACC-PXPL3/MORPH-THEN-WORDEND ;

!+Ins+PxPl3:%>наныс K ; ! ста
!+Nom+PxPl3:%>нанныс K ; ! 2012-02-16 this should be removed

+Com+PxPl3:%>ныскӧд K ;

+Car+PxPl3:%>ныстӧг K ;

+Cns+PxPl3:%>нысла K ;

+Ine+PxPl3:%>аныс K ;
+Ela+PxPl3:%>сьыныс K ;
+Ill+PxPl3:%>аныс K ;
+Apr+PxPl3:%>ныслань K ;
+Egr+PxPl3:%>ныссянь K ;
+Prl+PxPl3:%>ӧдныс K ;
+Ter+PxPl3:%>ӧдзныс K ;



                     
 
!! !!Arabic numerals
! ===============

LEXICON NUM-PREFIXES     !!= * __@CODE@__  
        ARABIC ;
§       ARABIC ;      ! §24                     !^C^                               
§§      ARABIC ;     ! §§24                     !^C^                               
§%      ARABIC ;    ! § 24                      !^C^                               
§§%     ARABIC ;   ! §§ 24                      !^C^                               
%-      ARABIC ;     ! -24                      !^C^                               
%‒      ARABIC ;     ! -24 U+2012 Figure line   !^C^                               
%–      ARABIC ;     ! -24 U+2013 en dash       !^C^                               
%—      ARABIC ;     ! -24 U+2014 em dash       !^C^                               
%―      ARABIC ;     ! -24 U+2015 horizontal l  !^C^                               
%+      ARABIC ;     ! +24                      !^C^                               
%-%     ARABIC ;        ! - 24                  !^C^                               
%‒%     ARABIC ;        ! - 24 2012             !^C^                               
%–%     ARABIC ;        ! - 24 2013             !^C^                               
%—%     ARABIC ;        ! - 24 2014             !^C^                               
%―%     ARABIC ;        ! - 24 2015             !^C^                               
%+%     ARABIC ;        ! + 24                  !^C^                               
%*      ARABIC ;     ! *24                      !^C^                               
%$      ARABIC ;     ! $24  ! And the Euro?     !^C^                               
%<      ARABIC ;     ! <24                      !^C^                               
!%>     ARABIC ;     ! >24                      !^C^                               
s%.     ARABIC ;     ! s.24                     !^C^                               




!2018-10-21!LEXICON NUMDELIMITER
!2018-10-21!    NumCASEPOSSLEX ;
!2018-10-21!:%: NumCASEPOSSLEX ;
!2018-10-21!:%- NumCASEPOSSLEX ;
!2018-10-21!:%' NumCASEPOSSLEX ;



! vim: set ft=xfst-lexc:

