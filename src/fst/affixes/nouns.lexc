
! ============================= !
!! !!!Noun morphological lexica !
! ============================= !

! ===== !
! Nouns !
! ===== !

LEXICON Vocative   !!≈ * __@CODE@__
+Voc: # ;

LEXICON Noun1-IS_PTC-OM !!≈ * __@CODE@__ is to provide deverbal nouns with +Der/ӧм tags
+V+Der+Der/ӧм: N_ ;

LEXICON Noun1-IS_PTC-AN !!≈ * __@CODE@__
+V+Der+Der/Ан: N_ ;

LEXICON Noun1-IS_PTC-YSJ !!≈ * __@CODE@__
+V+Der+Der/ысь: N_ ;

LEXICON N-DOR ! мӧдладор The Russian is an adverbial phrase
+Der+Der/дор: N_ ;

LEXICON N-VYV ! тулысвыв The Russian is an adverbial phrase
+Der+Der/выв: N_ ;

LEXICON Noun1-F !
+Src/F: N_ ;

LEXICON Noun1-R-RY !
+Use/NG+Err/Orth:ь N_ ;  ! This deals with январь
           N_ ; ! This is the new proper spelling январ

LEXICON Noun1-SYT-STY !
+Use/NG+Err/Orth:ть N_ ;  ! This deals with гӧсть
:ьт         N_ ; ! This is the new proper spelling гӧсьт


!2018-10-21!LEXICON N-KINDRED_
!2018-10-21! N_ ;
!2018-10-21!+PxSg1+Assoc+Pl:%>ӧяс CASEPOSSLEX ;
!2018-10-21!+PxSg2+Assoc+Pl:%>ыдъяс CASEPOSSLEX ;
!2018-10-21!+PxSg3+Assoc+Pl:%>ысъяс CASEPOSSLEX ;

!2018-10-21!LEXICON N-PROFESSION_
!2018-10-21! N_ ;
!2018-10-21!+Der+Der/ал@C.CONJ-INF@@C.CONJ-POSS@:%>%{ая%}л@C.CONJ-INF@@C.CONJ-POSS@ V_ШУНЫ ; !! таксисталӧ 
!! Basic nouns.

!! The lexicon for basic nouns is {{ N_ }}


LEXICON N-RELAT_
+Relat: NMN_ ;
LEXICON N_
: NMN_ ;

LEXICON Noun1 !! This should be phased out 2013-05-07
 N_ ;
LEXICON N_RU-ӦЙ
+Use/SpellNoSugg:  N_ ;

LEXICON NMN_
 R ;
! ABS_ ; 2018-10-21
+Sg: CASEPOSSLEX ;
+Pl:%>ъяс CASEPOSSLEX ;


LEXICON Noun1-JM ! понйыс, понмыс
+Use/NG:м CASEPOSSLEX-AFTERDERIVATION_VOW ;
!2017-09-19+Cmp:м R ;
!2017-09-19+Sg:м CASEPOSSLEX ;
!2017-09-19+Cmp:й R ;
:й NMN_PARAGOGIC ;
!2017-09-19+Sg:й CASEPOSSLEX ;
!2017-09-19+Pl:%>ъяс CASEPOSSLEX ;

!! subsequent Cns vs Vow
LEXICON N_PARAGOGIC !!= * @CODE@ 
: NMN_PARAGOGIC ;
LEXICON NMN_PARAGOGIC !!= * @CODE@ 
+Cmp:%^Close R ;
!:%^Close ABS_ ; 2018-10-21
+Sg:%^Close CASEPOSSLEX-AFTERDERIVATION_CNS/END ;
+Sg: CASEPOSSLEX-AFTERDERIVATION_VOW ;
+Sg:%^Close DERIVEDADJ-IN-CONS ;
+Sg: DERIVEDADJ-IN-VOW ;

+Pl:%^Close%>ъяс CASEPOSSLEX ;

!Izhma style
+Sg+Err/Dial:%^C2V CASEPOSSLEX-AFTERDERIVATION_CNS/END ;
+Sg+Err/Dial:%^C2V DERIVEDADJ-IN-CONS ;
+Pl+Err/Dial:%^C2V%>ъяс CASEPOSSLEX ;


LEXICON N-RELAT_PARAGOGIC-V/L !!= * @CODE@ ныв:ны нылыс
+Relat: NMN_PARAGOGIC-V/L ;

LEXICON N_PARAGOGIC-V/L !!= * @CODE@ ныв:ны нылыс
: NMN_PARAGOGIC-V/L ;
LEXICON NMN_PARAGOGIC-V/L !!= * @CODE@ ныв:ны нылыс
:л NMN_PARAGOGIC ;

+Use/NG+Err/Orth+Dial/л+Cmp:л R ;
!+Err/Orth+Dial/л+Cmp:л ABS_ ; 2018-10-21
+Use/NG+Err/Orth+Dial/л+Sg:л CASEPOSSLEX-AFTERDERIVATION_CNS/END ;
+Use/NG+Err/Orth+Dial/в+Sg:л%^Close CASEPOSSLEX-AFTERDERIVATION_VOW ;
+Use/NG+Err/Orth+Dial/л+Pl:л%>ъяс CASEPOSSLEX ;


LEXICON N_PARAGOGIC-J !!= * @CODE@ ав:ав авйыс
: NMN_PARAGOGIC-J ;
LEXICON NMN_PARAGOGIC-J !!= * @CODE@ ав:ав авйыс
:й NMN_PARAGOGIC ;
+Use/NG+Err/Orth-no-paragogic-j: CASEPOSSLEX-AFTERDERIVATION_VOW ;

LEXICON N_PARAGOGIC-K !!= * @CODE@ пос:пос поскыс
: NMN_PARAGOGIC-K ;
LEXICON NMN_PARAGOGIC-K !!= * @CODE@ пос:пос поскыс
:к NMN_PARAGOGIC ;
+Use/NG+Err/Orth-no-paragogic-k: CASEPOSSLEX-AFTERDERIVATION_VOW ;

LEXICON N_PARAGOGIC-M !!= * @CODE@ зон:зон зонмыс
: NMN_PARAGOGIC-M ;
LEXICON NMN_PARAGOGIC-M !!= * @CODE@ зон:зон зонмыс
:м N_PARAGOGIC ;
+Use/NG+Err/Orth-no-paragogic-m: CASEPOSSLEX-AFTERDERIVATION_VOW ;

LEXICON N_PARAGOGIC-T !!= * @CODE@ зеп:зеп зептыс
: NMN_PARAGOGIC-T ;
LEXICON NMN_PARAGOGIC-T !!= * @CODE@ зеп:зеп зептыс
:т N_PARAGOGIC ;
+Use/NG+Err/Orth-no-paragogic-t: CASEPOSSLEX-AFTERDERIVATION_VOW ;

LEXICON N_END-IN-DD1 !!= * @CODE@ гадь:гад гаддьыс
: NMN_END-IN-DD1 ;
LEXICON NMN_END-IN-DD1 !!= * @CODE@ автомобиль:автомобил автомобильыс
:дь NMN_PARAGOGIC ;

LEXICON N_END-IN-DDZ !!= * @CODE@ кыдз:кыд
: NMN_END-IN-DDZ ;
LEXICON NMN_END-IN-DDZ !!= * @CODE@ кыдз:кыд
:дз NMN_PARAGOGIC ;

LEXICON N_END-IN-LL1 !!= * @CODE@ моль:мол молльӧ
: NMN_END-IN-LL1 ;
LEXICON NMN_END-IN-LL1 !!= * @CODE@ моль:мол молльӧ
:ль NMN_PARAGOGIC ;



LEXICON N-RELAT_END-IN-J !!= * @CODE@ абай:аба абайыс
+Relat: N-0_END-IN-J ;
LEXICON N_END-IN-J !!= * @CODE@ абай:аба абайыс
: N-0_END-IN-J ;
LEXICON N-0_END-IN-J !!= * @CODE@ абай:аба абайыс
+Cmp:й R ;
!:й ABS_ ; 2018-10-21
+Sg:й CASEPOSSLEX-AFTERDERIVATION_CNS/END ;
+Sg: CASEPOSSLEX-AFTERDERIVATION_Я ;
+Sg:й CASEPOSSLEX-AFTERDERIVATION_Ӧ ;
+Pl:й%>яс CASEPOSSLEX ;


LEXICON N-RELAT_END-IN-Ч/ДЗ !!= * @CODE@ водз:водз
+Relat: NMN_END-IN-Ч/ДЗ ;
LEXICON N_END-IN-Ч/ДЗ !!= * @CODE@ водз:водз
: NMN_END-IN-Ч/ДЗ ;
LEXICON NMN_END-IN-Ч/ДЗ !!= * @CODE@ водз:водз
+Cmp: R ;
!: ABS_ ; 2018-10-21
+Sg: CASEPOSSLEX-AFTERDERIVATION_CNS/END ;
+Sg: CASEPOSSLEX-AFTERDERIVATION_VOW ;
+Pl:%>ьяс CASEPOSSLEX ;

LEXICON N-RELAT_END-IN-Ь !!= * @CODE@ автомобиль:автомобил автомобильыс
+Relat:  N-0_END-IN-Ь ;
LEXICON N_END-IN-Ь !!= * @CODE@ автомобиль:автомобил автомобильыс
:  N-0_END-IN-Ь ;
LEXICON N-0_END-IN-Ь !!= * @CODE@ автомобиль:автомобил автомобильыс
+Cmp:ь R ;
!:ь ABS_ ; 2018-10-21
+Sg:ь CASEPOSSLEX-AFTERDERIVATION_CNS/END ;
+Sg: CASEPOSSLEX-AFTERDERIVATION_Я ;
+Sg:ь CASEPOSSLEX-AFTERDERIVATION_Ӧ ;
+Pl:%>ьяс CASEPOSSLEX ; !! 2015-09-04 change to ь%>яс after xfst rules are removed
+Sg:ь DERIVEDADJ ;
+Pl:%>ьяс DERIVEDADJ ;

!2018-10-21!LEXICON NounLA ! ӧтила
!2018-10-21!N_ ; ! Difference betw N_ and NounLA still to be documented.

!LEXICON NounMort
! медводзаысь гӧгӧрвои, мый ӧтика лыдын морт, уна лыдын йӧз
! http://komicorpora.ru/index.php#poisk мортъяс => 503
! Сідзи кӧ позяс бырӧдны NounMort LEXICON-сӧ
!N_ ; ! Difference betw N_ and NounMort still to be documented.
! Бырӧдім 2019-06-14

LEXICON NounSG ! м орт
+Sg: CASEPOSSLEX ;

LEXICON NounPi ! пи These have a unique  pl marker ян.
+Sg: CASEPOSSLEX ;
+Pl:%>ян CASEPOSSLEX ;


LEXICON NounPL ! бедьторъяс
+Pl: CASEPOSSLEX ; ! here, %>ъяс is put as part of the stem
! Very many plural nouns do not have this tag. We could change яс-final
! nouns from N_ to NounPL, I guess.

!2018-10-21!LEXICON NounPOSS ! чой niece, nephew
!2018-10-21!N_ ; ! Declension to be determined.

LEXICON FIRST
+Cmp: # ;


! =================== !
!! !!Inflectional lexica
! =================== !


!! All nouns follow one contlex "N_"
!! to begin with here is simply a list of all variant
!! with no more variants beyond:

! Numbers
! Case + Poss or Poss+Case


LEXICON CASEPOSSLEX !!≈ * __@CODE@__
 DERIVEDADJ ;    ! Deriving prenominal adjectives, республикаса
CASEPOSSLEX-afterDerivation ;

LEXICON CASEPOSSLEX-afterDerivation
 CASEPOSSLEX-AFTERDERIVATION_CNS/END ;
  CASEPOSSLEX-AFTERDERIVATION_VOW ;

LEXICON CASEPOSSLEX-AFTERDERIVATION_CNS/END
 NOM/MORPH-THEN-WORDEND ;
!2020-10-23 remove ZERO +Acc: K ; !ZERO marker
	CASEPOSSLEX-AFTERDERIVATION_CNS/END-MINUS-ZERO-END ;
LEXICON CASEPOSSLEX-AFTERDERIVATION_CNS/END-MINUS-ZERO-END
+Gen:%>лӧн PredMonLex ;
+Use/NG+Err/Dial+Gen:%>лэн PredMonLex ;
+Abl:%>лысь K ;
+Dat:%>лы K ;
+Com:%>кӧд K ;
+Use/NG+Err/Dial+Com:%>кед K ;
+Car:%>тӧг K ;
+Use/NG+Err/Dial+Car:%>тэг K ;
+Cns:%>ла K ;
+Use/NG+Err/Dial+Cmpr:%>ся K ;
+Apr:%>лань SPAT-COMPARATIVE ;
+Egr:%>сянь SPAT-COMPARATIVE ;
+Tra:%>ті K ;
+AprIne:%>ланьын SPAT-COMPARATIVE ;  ! New cases in last grammar
+AprEla:%>ланьысь SPAT-COMPARATIVE ;  !
+AprIll:%>ланьӧ IllCompar ; !
+Use/NG+Err/Dial+AprIll:%>лане IllCompar ; !
+AprEgr:%>ланьсянь SPAT-COMPARATIVE ; !
+AprPrl:%>ланьӧд K ; !
+Use/NG+Err/Dial+AprPrl:%>ланед K ; !
+AprTra:%>ланьті K ; !
+AprTer:%>ланьӧдз K ; ! Tähän asti
+Use/NG+Err/Dial+AprTer:%>ланедз K ; ! Tähän asti
!! SG1
 ACC-PXSG1/MORPH-THEN-WORDEND ;
+Ins+PxSg1+So/CP:%>нам K ;
+Ela+PxSg1+So/CP:%>сьым SPAT-COMPARATIVE ;
+Tra+PxSg1+So/CP:%>тіым K ;
+Apr+PxSg1+So/CP:%>ланьым SPAT-COMPARATIVE ;
+Egr+PxSg1+So/CP:%>саньым SPAT-COMPARATIVE ;
+AprIne+PxSg1+So/CP:%>ланям SPAT-COMPARATIVE ;
+AprEla+PxSg1+So/CP:%>ланьсьым SPAT-COMPARATIVE ;
+AprIll+PxSg1+So/CP:%>ланям IllCompar ; !
+AprPrl+PxSg1+So/CP:%>ланьӧдым K ;
+Use/NG+Err/Dial+AprPrl+PxSg1+So/CP:%>ланедым K ;
+AprTra+PxSg1+So/CP:%>ланьтіым K ;
+AprTer+PxSg1+So/CP:%>ланьӧдзым K ;
+Use/NG+Err/Dial+AprTer+PxSg1+So/CP:%>ланедзым K ;

!! SG2
+Ins+PxSg2+So/CP:%>над K ;
+Car+PxSg2+So/CP:%>тӧгыд K ;
+Use/NG+Err/Dial+Car+PxSg2+So/CP:%>тэгыд K ;
+Com+PxSg2+So/CP:%>кӧдыд K ;
+Use/NG+Err/Dial+Com+PxSg2+So/CP:%>кедыд K ;
+Cmpr+PxSg2+So/CP:%>сяыд K ;
+Ela+PxSg2+So/CP:%>сьыд SPAT-COMPARATIVE ;
+Apr+PxSg2+So/CP:%>ланьыд SPAT-COMPARATIVE ;
+Egr+PxSg2+So/CP:%>сяньыд SPAT-COMPARATIVE ;
+Tra+PxSg2+So/CP:%>тіыд K ;
 ACC-PXSG2/MORPH-THEN-WORDEND ;
+AprIne+PxSg2+So/CP:%>ланяд SPAT-COMPARATIVE ;
+AprEla+PxSg2+So/CP:%>ланьсьыд SPAT-COMPARATIVE ;
+AprIll+PxSg2+So/CP:%>ланяд IllCompar ; !
+AprEgr+PxSg2+So/CP:%>ланьсяньыд SPAT-COMPARATIVE ;
+AprPrl+PxSg2+So/CP:%>ланьӧдыд K ;
+Use/NG+Err/Dial+AprPrl+PxSg2+So/CP:%>ланедыд K ;
+AprTra+PxSg2+So/CP:%>ланьтіыд K ;
+AprTer+PxSg2+So/CP:%>ланьӧдзыд K ;
+Use/NG+Err/Dial+AprTer+PxSg2+So/CP:%>ланедзыд K ;

!! SG3
 ACC-PXSG3/MORPH-THEN-WORDEND ;
+Ins+PxSg3+So/CP:%>нас K ;
+Car+PxSg3+So/CP:%>тӧгыс K ;
+Use/NG+Err/Dial+Car+PxSg3+So/CP:%>тэгыс K ;
+Com+PxSg3+So/CP:%>кӧдыс K ;
+Use/NG+Err/Dial+Com+PxSg3+So/CP:%>кедыс K ;
+Cmpr+PxSg3+So/CP:%>сяыс K ;
+Ela+PxSg3+So/CP:%>сьыс SPAT-COMPARATIVE ;
+Apr+PxSg3+So/CP:%>ланьыс SPAT-COMPARATIVE ;
+Egr+PxSg3+So/CP:%>сяньыс SPAT-COMPARATIVE ;
+Tra+PxSg3+So/CP:%>тіыс K ;
+AprIne+PxSg3+So/CP:%>ланяс SPAT-COMPARATIVE ;
+AprEla+PxSg3+So/CP:%>ланьсьыс SPAT-COMPARATIVE ;
+AprIll+PxSg3+So/CP:%>ланяс IllCompar ; !
+AprEgr+PxSg3+So/CP:%>ланьсяньыс SPAT-COMPARATIVE ;
+AprPrl+PxSg3+So/CP:%>ланьӧдыс K ;
+Use/NG+Err/Dial+AprPrl+PxSg3+So/CP:%>ланедыс K ;
+AprTra+PxSg3+So/CP:%>ланьтіыс K ;
+AprTer+PxSg3+So/CP:%>ланьӧдзыc K ;
+Use/NG+Err/Dial+AprTer+PxSg3+So/CP:%>ланедзыc K ;
+Cns+PxSg3+So/CP:%>лаыс K ;

!! PL1
+Nom+PxPl1:%>ным K ;
+Nom+PxPl1+Use/NG+Err/Dial:%>нум K ;
+Gen+PxPl1+So/PC+So/PC:%>нымлӧн K ;
+Use/NG+Err/Dial+Gen+PxPl1+So/PC:%>нымлэн K ;
+Abl+PxPl1+So/PC:%>нымлысь K ;
+Dat+PxPl1+So/PC:%>нымлы K ;
 ACC-PXPL1/MORPH-THEN-WORDEND ;
+Ins+PxPl1+So/CP:%>наным K ;
+Com+PxPl1+So/PC:%>нымкӧд K ;
+Use/NG+Err/Dial+Com+PxPl1+So/PC:%>нымкед K ;
+Com+PxPl1+So/CP:%>кӧдным K ;
+Use/NG+Err/Dial+Com+PxPl1+So/CP:%>кедным K ;
+Car+PxPl1+So/CP:%>тӧгным K ; ! Case+Px
+Use/NG+Err/Dial+Car+PxPl1+So/CP:%>тэгным K ; ! Case+Px
+Car+PxPl1+So/PC:%>нымтӧг K ; ! Px+Case
+Use/NG+Err/Dial+Car+PxPl1+So/PC:%>нымтэг K ; ! Px+Case
+Cns+PxPl1+So/PC:%>нымла K ;
+Cmpr+PxPl1+So/CP:%>сяным K ;
+Ela+PxPl1+So/CP:%>сьыным SPAT-COMPARATIVE ;

+Apr+PxPl1+So/CP:%>ланьным SPAT-COMPARATIVE ; ! Case+Px
+Apr+PxPl1+So/PC:%>нымлань SPAT-COMPARATIVE ; ! Px+Case
+Egr+PxPl1+So/CP:%>сяньным SPAT-COMPARATIVE ; ! _ставсяньным_
+Egr+PxPl1+So/PC:%>нымсянь SPAT-COMPARATIVE ; ! _ставнымсянь_  Px+Case
+Tra+PxPl1+So/CP:%>тіным K ;
+Prl+PxPl1+So/PC:%>нымӧд K ; ! Px + Case туйнымӧд
+Use/NG+Err/Dial+Prl+PxPl1+So/PC:%>нымед K ; ! Px + Case туйнымӧд

+AprIne+PxPl1+So/CP:%>ланяным SPAT-COMPARATIVE ;
+AprEla+PxPl1+So/CP:%>ланьсьыным SPAT-COMPARATIVE ;
+AprIll+PxPl1+So/CP:%>ланяным IllCompar ; !
+AprEgr+PxPl1+So/CP:%>ланьсяньным SPAT-COMPARATIVE ;
+AprPrl+PxPl1+So/CP:%>ланьӧдным K ;
+Use/NG+Err/Dial+AprPrl+PxPl1+So/CP:%>ланедным K ;
+AprTra+PxPl1+So/CP:%>ланьтіным K ;
+AprTer+PxPl1+So/CP:%>ланьӧдзным K ;
+Use/NG+Err/Dial+AprTer+PxPl1+So/CP:%>ланедзным K ;

!! PL2
+Nom+PxPl2:%>ныд K ;
+Gen+PxPl2+So/PC:%>ныдлӧн K ;
+Use/NG+Err/Dial+Gen+PxPl2+So/PC:%>ныдлэн K ;
+Abl+PxPl2+So/PC:%>ныдлысь K ;
+Dat+PxPl2+So/PC:%>ныдлы K ;
 ACC-PXPL2/MORPH-THEN-WORDEND ;
+Ins+PxPl2+So/CP:%>наныд K ;
+Com+PxPl2+So/PC:%>ныдкӧд K ;
+Use/NG+Err/Dial+Com+PxPl2+So/PC:%>ныдкед K ;
+Com+PxPl2+So/CP:%>кӧдныд K ;
+Use/NG+Err/Dial+Com+PxPl2+So/CP:%>кедныд K ;
+Car+PxPl2+So/CP:%>тӧгныд K ;
+Use/NG+Err/Dial+Car+PxPl2+So/CP:%>тэгныд K ;
+Cns+PxPl2+So/PC:%>ныдла K ;
+Cmpr+PxPl2+So/CP:%>сяныд K ;
+Ela+PxPl2+So/CP:%>сьыныд SPAT-COMPARATIVE ;
+Apr+PxPl2+So/PC:%>ныдлань SPAT-COMPARATIVE ;
+Apr+PxPl2+So/CP:%>ланьныд K ; ! Case + Px
+Egr+PxPl2+So/CP:%>сяньныд SPAT-COMPARATIVE ;
+Egr+PxPl2+So/PC:%>ныдсянь SPAT-COMPARATIVE ;
+Tra+PxPl2+So/CP:%>тіныд K ;
+AprIne+PxPl2+So/CP:%>ланяныд SPAT-COMPARATIVE ;
+AprEla+PxPl2+So/CP:%>ланьсьыныд SPAT-COMPARATIVE ;
+AprIll+PxPl2+So/CP:%>ланяныд IllCompar ; !
+AprEgr+PxPl2+So/CP:%>ланьсяньныд SPAT-COMPARATIVE ;
+AprPrl+PxPl2+So/CP:%>ланьӧдныд K ;
+Use/NG+Err/Dial+AprPrl+PxPl2+So/CP:%>ланедныд K ;
+AprTra+PxPl2+So/CP:%>ланьтіныд K ;
+AprTer+PxPl2+So/CP:%>ланьӧдзныд K ;
+Use/NG+Err/Dial+AprTer+PxPl2+So/CP:%>ланедзныд K ;

!! PL3
+Nom+PxPl3:%>ныс K ;
+Gen+PxPl3+So/PC:%>ныслӧн K ; ! Px + Case
+Use/NG+Err/Dial+Gen+PxPl3+So/PC:%>ныслэн K ; ! Px + Case
+Abl+PxPl3+So/PC:%>ныслысь K ; ! Px + Case
+Dat+PxPl3+So/PC:%>ныслы K ; ! Px + Case
 ACC-PXPL3/MORPH-THEN-WORDEND ;
+Ins+PxPl3+So/CP:%>наныс K ;
+Com+PxPl3+So/PC:%>ныскӧд K ; ! Px + Case
+Use/NG+Err/Dial+Com+PxPl3+So/PC:%>ныскед K ; ! Px + Case
+Com+PxPl3+So/CP:%>кӧдныс K ; ! Px + Case
+Use/NG+Err/Dial+Com+PxPl3+So/CP:%>кедныс K ; ! Px + Case
+Car+PxPl3+So/CP:%>тӧгныс K ;
+Use/NG+Err/Dial+Car+PxPl3+So/CP:%>тэгныс K ;
+Cns+PxPl3+So/PC:%>нысла K ; ! Px + Case
+Cmpr+PxPl3+So/CP:%>сяныс K ;
+Ela+PxPl3+So/CP:%>сьыныс SPAT-COMPARATIVE ;
+Apr+PxPl3+So/PC:%>ныслань SPAT-COMPARATIVE ; ! Px + Case
+Apr+PxPl3+So/CP:%>ланьныс SPAT-COMPARATIVE ;
+Egr+PxPl3+So/CP:%>сяньныс SPAT-COMPARATIVE ;
+Egr+PxPl3+So/PC:%>ныссянь SPAT-COMPARATIVE ;
+Tra+PxPl3+So/CP:%>тіныс K ;
+AprIne+PxPl3+So/CP:%>ланяныс SPAT-COMPARATIVE ;
+AprEla+PxPl3+So/CP:%>ланьсьыныс SPAT-COMPARATIVE ;
+AprIll+PxPl3+So/CP:%>ланяныс IllCompar ; !
+AprEgr+PxPl3+So/CP:%>ланьсяньныс SPAT-COMPARATIVE ;
+AprPrl+PxPl3+So/CP:%>ланьӧдныс K ;
+Use/NG+Err/Dial+AprPrl+PxPl3+So/CP:%>ланедныс K ;
+AprTra+PxPl3+So/CP:%>ланьтіныс K ;
+AprTer+PxPl3+So/CP:%>ланьӧдзныс K ;
+Use/NG+Err/Dial+AprTer+PxPl3+So/CP:%>ланедзныс K ;

LEXICON CASEPOSSLEX-AFTERDERIVATION_VOW
 CASEPOSSLEX-AFTERDERIVATION_А ;
 CASEPOSSLEX-AFTERDERIVATION_Ӧ ;

LEXICON CASEPOSSLEX-AFTERDERIVATION_А
!! SG1
+Ine+PxSg1+So/CP:%>%{ая%}м SPAT-COMPARATIVE ;
+Ill+PxSg1+So/CP:%>%{ая%}м IllCompar ; !
!! SG2
+Ine+PxSg2+So/CP:%>%{ая%}д SPAT-COMPARATIVE ;
+Ill+PxSg2+So/CP:%>%{ая%}д IllCompar ; !
!! SG3
+Ine+PxSg3+So/CP:%>%{ая%}с SPAT-COMPARATIVE ;
+Ill+PxSg3+So/CP:%>%{ая%}с IllCompar ; !
!! PL1
+Ine+PxPl1+So/CP:%>%{ая%}ным SPAT-COMPARATIVE ;
+Ill+PxPl1+So/CP:%>%{ая%}ным IllCompar ; !
!! PL2
+Ine+PxPl2+So/CP:%>%{ая%}ныд SPAT-COMPARATIVE ;
+Ill+PxPl2+So/CP:%>%{ая%}ныд IllCompar ; !
!! PL3
+Ine+PxPl3+So/CP:%>%{ая%}ныс SPAT-COMPARATIVE ;
+Ill+PxPl3+So/CP:%>%{ая%}ныс IllCompar ; !

LEXICON CASEPOSSLEX-AFTERDERIVATION_Я
!! SG1
+Ine+PxSg1+So/CP:%>ям SPAT-COMPARATIVE ;
+Ill+PxSg1+So/CP:%>ям IllCompar ; !
!! SG2
+Ine+PxSg2+So/CP:%>яд SPAT-COMPARATIVE ;
+Ill+PxSg2+So/CP:%>яд IllCompar ; !
!! SG3
+Ine+PxSg3+So/CP:%>яс SPAT-COMPARATIVE ;
+Ill+PxSg3+So/CP:%>яс IllCompar ; !
!! PL1
+Ine+PxPl1+So/CP:%>яным SPAT-COMPARATIVE ;
+Ill+PxPl1+So/CP:%>яным IllCompar ; !
!! PL2
+Ine+PxPl2+So/CP:%>яныд SPAT-COMPARATIVE ;
+Ill+PxPl2+So/CP:%>яныд IllCompar ; !
!! PL3
+Ine+PxPl3+So/CP:%>яныс SPAT-COMPARATIVE ;
+Ill+PxPl3+So/CP:%>яныс IllCompar ; !

! THIS FAR 2017-06-29 +Err/Dial
LEXICON CASEPOSSLEX-AFTERDERIVATION_Ӧ
+Acc:%>ӧс K ;
+Use/NG+Err/Dial+Acc:%>эс K ;
+Ins:%>ӧн PredMonLex ;
+Use/NG+Err/Dial+Ins:%>эн PredMonLex ;
+Ine:%>ын SPAT-COMPARATIVE ;
+Ela:%>ысь SPAT-COMPARATIVE ;
+Ill:%>ӧ IllCompar ;
+Use/NG+Err/Dial+Ill:%>э IllCompar ;
+Prl:%>ӧд K ;
+Ter:%>ӧдз K ;
+Use/NG+Err/Dial+Prl:%>эд K ;
+Use/NG+Err/Dial+Ter:%>эдз K ;

!! SG1
 ABL-PXSG1-SOPC/MORPH-THEN-K ;
 CAR-PXSG1-SOPC/MORPH-THEN-K ;
 CMPR-PXSG1-SOPC/MORPH-THEN-K ;
 CNS-PXSG1-SOPC/MORPH-THEN-K ;
 COM-PXSG1-SOPC/MORPH-THEN-K ;
 DAT-PXSG1-SOPC/MORPH-THEN-K ;
 GEN-PXSG1-SOPC/MORPH-THEN-K ;
 NOM-PXSG1/MORPH-THEN-K ;
 PRL-PXSG1-SOCP/MORPH-THEN-K ;
 TER-PXSG1-SOCP/MORPH-THEN-K ;

+Acc+PxSg1:%>ӧс K ;
+Apr+PxSg1+So/PC:%>ӧйлань SPAT-COMPARATIVE ;
+Egr+PxSg1+So/PC:%>ӧйсянь SPAT-COMPARATIVE ;
+AprEgr+PxSg1+So/PC:%>ӧйланьсянь SPAT-COMPARATIVE ;

+Use/NG+Err/Dial+Acc+PxSg1:%>эс K ;
+Use/NG+Err/Dial+Apr+PxSg1+So/PC:%>эйлань SPAT-COMPARATIVE ;
+Use/NG+Err/Dial+Egr+PxSg1+So/PC:%>эйсянь SPAT-COMPARATIVE ;
+Use/NG+Err/Dial+AprEgr+PxSg1+So/PC:%>эйланьсянь SPAT-COMPARATIVE ;

!! SG2
+Nom+PxSg2:%>ыд K ;
+Gen+PxSg2+So/PC:%>ыдлӧн K ;
+Use/NG+Err/Dial+Gen+PxSg2+So/PC:%>ыдлэн K ;
+Abl+PxSg2+So/PC:%>ыдлысь K ;
+Dat+PxSg2+So/PC:%>ыдлы K ;
+Car+PxSg2+So/PC:%>ыдтӧг K ; ! Px + Case
+Use/NG+Err/Dial+Car+PxSg2+So/PC:%>ыдтэг K ; ! Px + Case
+Egr+PxSg2+So/PC:%>ыдсянь K ; ! Px + Case Ильяыдсянь
+Com+PxSg2+So/PC:%>ыдкӧд K ;
+Use/NG+Err/Dial+Com+PxSg2+So/PC:%>ыдкед K ;
+Cns+PxSg2+So/PC:%>ыдла K ;
+Prl+PxSg2+So/CP:%>ӧдыд K ;
+Ter+PxSg2+So/CP:%>ӧдзыд K ;
+Use/NG+Err/Dial+Prl+PxSg2+So/CP:%>эдыд K ;
+Use/NG+Err/Dial+Ter+PxSg2+So/CP:%>эдзыд K ;

!! SG3
+Nom+PxSg3:%>ыс K ;
+Gen+PxSg3+So/PC:%>ыслӧн K ;
+Use/NG+Err/Dial+Gen+PxSg3+So/PC:%>ыслэн K ;
+Abl+PxSg3+So/PC:%>ыслысь K ;
+Dat+PxSg3+So/PC:%>ыслы K ;
+Com+PxSg3+So/PC:%>ыскӧд K ;
+Use/NG+Err/Dial+Com+PxSg3+So/PC:%>ыскед K ;
+Cns+PxSg3+So/PC:%>ысла K ;
+Prl+PxSg3+So/CP:%>ӧдыс K ;
+Ter+PxSg3+So/CP:%>ӧдзыс K ;
+Use/NG+Err/Dial+Prl+PxSg3+So/CP:%>эдыс K ;
+Use/NG+Err/Dial+Ter+PxSg3+So/CP:%>эдзыс K ;

!! PL1
+Prl+PxPl1+So/CP:%>ӧдным K ;
+Ter+PxPl1+So/CP:%>ӧдзным K ;
+Use/NG+Err/Dial+Prl+PxPl1+So/CP:%>эдным K ;
+Use/NG+Err/Dial+Ter+PxPl1+So/CP:%>эдзным K ;

!! PL2
+Prl+PxPl2+So/CP:%>ӧдныд K ;
+Ter+PxPl2+So/CP:%>ӧдзныд K ;
+Use/NG+Err/Dial+Prl+PxPl2+So/CP:%>эдныд K ;
+Use/NG+Err/Dial+Ter+PxPl2+So/CP:%>эдзныд K ;

!! PL3
+Prl+PxPl3+So/CP:%>ӧдныс K ;
+Ter+PxPl3+So/CP:%>ӧдзныс K ;
+Use/NG+Err/Dial+Prl+PxPl3+So/CP:%>эдныс K ;
+Use/NG+Err/Dial+Ter+PxPl3+So/CP:%>эдзныс K ;


!! !!Case

LEXICON Noun1-IS_INE
+Ine: SPAT-COMPARATIVE ;




LEXICON AdjCompar
+Comp:%>джык PredMonLex ;
+Comp+Der+Der/а:%>джыка ADV_ ;
+Comp+Sg:%>джык CASEPOSSLEX-afterDerivation ;
+Comp+Pl:%>джыкъяс CASEPOSSLEX-afterDerivation ;
!2017-09-19+Comp+Der/MWN+Sg:%>джык CASEPOSSLEX-afterDerivation ;
!2017-09-19+Comp+Der/MWN+Pl:%>джыкъяс CASEPOSSLEX-afterDerivation ;
+Sg: CASEPOSSLEX-afterDerivation ; !This is for adjectives without nouns
+Pl:%>ъяс CASEPOSSLEX-afterDerivation ; !This is for adjectives without nouns
!2017-09-19+Der/MWN+Sg: CASEPOSSLEX-afterDerivation ; !This is for adjectives without nouns
!2017-09-19+Der/MWN+N+Pl:%>ъяс CASEPOSSLEX-afterDerivation ; !This is for adjectives without nouns

LEXICON IllCompar
 K ;
+Comp:%>джык K ;

!2018-10-21!LEXICON PredMonLex-Spat
!2018-10-21! K ;      ! Singular predicative
!2018-10-21! PredMonLex-Spat-Add-Morphology ; !

LEXICON PredMonLex-Spat-Add-Morphology ! This allows consistency
                                       ! with SPAT elsewhere
+Cop+Pl:%>ӧсь K ; ! Plural predicative.
+Use/NG+Err/Dial+Cop+Pl:%>эсь K ; ! Plural predicative.
+Comp:%>джык PredMonLex ; !ывлаынджык OR ывлаынджыкӧсь


LEXICON PredMonLex
 K ;      ! Singular predicative
+Cop+Pl:%>ӧсь K ; ! Plural predicative.
+Use/NG+Err/Dial+Cop+Pl:%>эсь K ; ! Plural predicative.
!! 2012-02-07 !!+Der/а+Adv:%>а K ;




! vim: set ft=xfst-lexc:
