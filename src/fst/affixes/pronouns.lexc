! ========================================================================== !
!!                        !!!Pronominal morphology                           
! ========================================================================== !

!! !!Closed class personal pronouns
! ------------------------------
LEXICON PRONOUN-TYPES
 PERS ;
 INTERR ;
 INDEFINITE-PRON ;
 NEG-PRON ;
 COLL-PRON ;

LEXICON PERS                        !!≈ * __@CODE@__ 
ме+Pron+Pers+Sg1: PERS-SG1 ;    !!≈ * __@CODE@__ ...
тэ+Pron+Pers+Sg2: PERS-SG2 ; 
сійӧ+Pron+Pers+Sg3: PERS-SG3 ;
ме+Pron+Pers+Pl1: PERS-PL1 ;
тэ+Pron+Pers+Pl2: PERS-PL2 ; 
сійӧ+Pron+Pers+Pl3: PERS-PL3 ;


LEXICON PERS-SG1   !!≈ * __@CODE@__ 
+Nom:ме K ;
!+Cmpl:ме WordEnd ; !Postposition complement removed 2019-10-27 same as Nom
+Gen:менам K ;
+Gen+Cop+Pl:менамӧсь K ;
+Gen:мелӧн K ;
+Gen:менамлӧн K ;
+Ela:менсьым K ; !The elative case is used in Sg1 and Sg2 
+Ela+Err/Dial:менчум K ; !The elative case is used in Sg1 and Sg2 
                 !for the functions of the ablative in other persons
+Dat:меным K ; !What should be done with the 4 dative forms?
+Dat+Err/Dial:менум K ;
+Dat:мем K ;
+Dat:мен K ;
+Dat:менлы K ;
+Acc:менӧ K ;
+Use/NG+Err/Dial+Acc:менэ K ;
+Ins:менамӧн K ;
+Com:менамкӧд K ;
+Car:менамтӧг K ;
+Inе:менамын K ;
+Ela:менамысь K ;
+Ill:менамӧ K ;
+Apr:менамлань K ;
+Egr:менамсянь K ;
+Ter:менамӧдз K ;
:ме perssg ; ! for the regular obl sg

LEXICON PERS-PL1    !!≈ * __@CODE@__
+Nom:ми K ;
+Nom:мийӧ K ; !Der/йӧ
+Gen:миян K ;
+Acc:миянӧс K ;
:миян persplobl ;
!!Extras from 2000 Grammar: 163
+Gen:миянлӧн K ; !Synomymous with: миян
+Acc:мияннымӧс K ; !Synomymous with: миянӧс
+Acc+Err/Dial:мияннымес K ; !Synomymous with: миянӧс
+Ela:миянсьыным K ; !Synomymous with:миянлысь
+Ela+Err/Dial:миянсьынум K ; !Synomymous with:миянлысь
+Com:микӧд K ; !Synomymous with: миянкӧд
+Com:мияннымкӧд K ; !Synomymous with: миянкӧд
+Com+Err/Dial:мияннумкед K ; !Synomymous with: миянкӧд
+Car:митӧг K ; !Synomymous with: миянтӧг
+Car:миянтӧгным K ; !Synomymous with: миянтӧг
+Car+Err/Dial:миянтегнум K ; !Synomymous with: миянтӧг
+Cns:мияннымла K ; !Synomymous with: миянла
+Cns+Err/Dial:мияннумла K ; !Synomymous with: миянла
+Ela:миысь K ; !Synomymous with: миянысь
+Ill:миӧ K ; !Synomymous with: миянӧ
+Apr:миянланьным K ; !Synomymous with: миянлань
+Apr:миланьным K ; !Synomymous with: миянлань
+Egr:миянсяньным K ; !Synomymous with: миянсянь
+Apr+Err/Dial:миянланьнум K ; !Synomymous with: миянлань
+Apr+Err/Dial:миланьнум K ; !Synomymous with: миянлань
+Egr+Err/Dial:миянсяньнум K ; !Synomymous with: миянсянь
+Tra:миті K ; !Synomymous with: миянӧд
+Ter:миӧдз K ; !Synomymous with: миянӧдз

LEXICON PERS-SG2     !!≈ * __@CODE@__
+Nom:тэ K ;
!+Cmpl:тэ WordEnd ;  removed 2019-10-27 same as Nom
+Gen:тэнад K ;
+Gen+Cop+Pl:тэнадӧсь K ;
+Gen:тэлӧн K ;
+Gen:тэнадлӧн K ;
+Ela:тэнсьыд K ;
+Dat:тэныд K ;
+Dat:тэн K ;
+Dat:тэд K ;
+Dat:тэдлы K ;
+Dat:тэнлы K ;
+Dat:тэныдлы K ;
+Acc:тэнӧ K ;
+Use/NG+Err/Dial+Acc:тэнэ K ;
+Ins:тэнадӧн K ;
+Com:тэнадкӧд K ;
+Car:тэнадтӧг K ;
+Ine:тэнадын K ;
+Ela:тэнадысь K ;
+Ill:тэнадӧ K ;
+Egr:тэнадсянь K ;
+Ter:тэнадӧдз K ;
:тэ perssg ;  ! for the regular obl sg

LEXICON PERS-PL2 !!≈ * __@CODE@__ 
+Nom:ті K ;
!+Cmpl:ті WordEnd ;  removed 2019-10-27 same as Nom
+Nom:тійӧ K ; !We need a special tag for discerning +Nom forms  Der/йӧ
+Gen:тіян K ;
+Gen:тіянлӧн K ;
+Acc:тіянӧс K ;
:тіян persplobl ;
!More forms from Ӧнія коми кыв 2000: 163
+Abl:тіянлысьныд K ;
+Dat:тіянлы K ;
+Acc:тіянныдтӧ K ;
+Ins:тіӧн K ;
+Car:тіянныдтӧг K ;
+Car:тітӧг K ;
+Cns:тіянныдла K ;
+Ela:тіысь K ;
+Ill:тіӧ K ;
+Apr:тіянланьныд K ;
+Apr:тіянланьыд K ;
+Egr:тіянсяньныд K ;
+Ter:тіӧдз K ;

LEXICON PERS-SG3  !!≈ * __@CODE@__ 
+Nom:сійӧ K ; !+Der/йӧ
+Nom+Err/Dial:сіе K ; !+Der/йӧ
+Gen:сылӧн K ;
+Gen+Err/Dial:сылэн K ;
+Abl:сылысь K ;
+Ela:сыысь K ;
+Dat:сылы K ;
+Acc:сійӧс K ; !Der/йӧ
+Ins:сійӧн K ; !Der/йӧ
+Acc+Err/Dial:сіес K ; !Der/йӧ
+Ins+Err/Dial:сіен K ; !Der/йӧ
:сы perssg ;  ! for the regular obl sg
+Cmpl:сы WordEnd ; ! distinct from Nom
!More form ӧния коми кыв 163
+Gen:сійӧлӧн K ; !Der/йӧ
+Abl:сійӧлысь K ; !Der/йӧ
+Dat:сійӧлы K ; !Der/йӧ
+Com:сійӧкӧд K ; !Der/йӧ
+Car:сійӧтӧг K ; !Der/йӧ
+Apr:сійӧлань K ; !Der/йӧ
+Egr:сійӧсянь K ; !Der/йӧ
+Egr:сійӧсяньыс K ; !Der/йӧ
+Ter:сійӧӧдз K ; !Der/йӧ
+Gen+Err/Dial:сіелӧн K ; !Der/йӧ
+Abl+Err/Dial:сіелысь K ; !Der/йӧ
+Dat+Err/Dial:сіелы K ; !Der/йӧ
+Com+Err/Dial:сіекед K ; !Der/йӧ
+Car+Err/Dial:сіетэг K ; !Der/йӧ
+Apr+Err/Dial:сіелань K ; !Der/йӧ
+Egr+Err/Dial:сіесянь K ; !Der/йӧ
+Egr+Err/Dial:сіесяньыс K ; !Der/йӧ
+Ter+Err/Dial:сіеедз K ; !Der/йӧ
! Special expressions
+Com+Ins:сыкӧдӧн K ; ! Ен сыкӧдӧн!
+Com+Ins+Err/Dial:сыкедэн K ; ! Ен сыкӧдӧн!

!! ми мийӧ The 1st and 2nd persons have Oblique case stem strategies
!! that differ from the 3rd person: 
!! ті тійӧ nämä ovat aivan eri asioita
!! сы сійӧ tosin joskus 

LEXICON PERS-PL3 !!≈ * __@CODE@__
+Nom:найӧ K ; !Der/йӧ
+Acc:найӧс K ; !Der/йӧ
+Acc:найӧӧс K ; !
+Gen:налӧн K ;
+Nom+Err/Dial:нае K ; !Der/йӧ
+Acc+Err/Dial:наес K ; !Der/йӧ
+Acc+Err/Dial:наеэс K ; !
+Gen+Err/Dial:налэн K ;
:на persplobl ; !This might be one solution for the different stem types
+Cmpl:на WordEnd ; ! Distinct from Nom
!
+Gen:найӧлӧн K ; !Der/йӧ
+Abl:найӧлысь K ; !Der/йӧ
+Dat:найӧлы K ; !Der/йӧ
+Ins:найӧӧн K ; !Der/йӧ
+Com:найӧкӧд K ; !Der/йӧ
+Com:накӧдыс K ;
+Car:найӧтӧг K ; !Der/йӧ
+Car:натӧгыс K ;

+Gen+Err/Dial:наелэн K ; !Der/йӧ
+Abl+Err/Dial:наелысь K ; !Der/йӧ
+Dat+Err/Dial:наелы K ; !Der/йӧ
+Ins+Err/Dial:наеэн K ; !Der/йӧ
+Com+Err/Dial:наекед K ; !Der/йӧ
+Com+Err/Dial:накедыс K ;
+Car+Err/Dial:наетэг K ; !Der/йӧ
+Car+Err/Dial:натэгыс K ;
+Cns:наысла K ;
+Ela:насьыс K ;
+Apr:наыслань K ;
+Apr:наланьыс K ;
+Egr:наыссянь K ;
+Egr:насяньыс K ;
+Tra:натіыс K ;
+Ter:наӧдзыс K ;
+Ter+Err/Dial:наэдзыс K ;

!2020-10-12 THIS FAR
LEXICON perssg !!≈ * __@CODE@__
+Ins:ӧн K ;
+Com:кӧд K ;
+Car:тӧг K ;
+Cns:ла K ;
+Ine:ын K ;
+Ill:ӧ K ; !
+Ela:ысь K ;
+Apr:лань K ;
+Egr:сянь K ;
+Prl:ӧд K ;
+Tra:ті K ;
+Ter:ӧдз K ;
+Abl:лысь K ; !This is not attested for Sg1 and Sg2 in the 2000 Grammar



LEXICON persplobl !!≈ * __@CODE@__
+Abl:лысь K ;
+Dat:лы K ;
+Acc:ӧс K ;

+Ins:ӧн K ;
+Com:кӧд K ;
+Car:тӧг K ;
+Cns:ла K ;
+Cmpr:ся K ;
+Ine:ын K ;
+Ela:ысь K ;
+Ill:ӧ K ;
+Apr:лань K ;
+Egr:сянь K ;
+Prl:ӧд K ;
+Tra:ті K ;
+Ter:ӧдз K ;
+AprIne:ланьын K ;  ! New cases in last grammar  
+AprEla:ланьысь K ;  ! 
+AprIll:ланьӧ K ; ! 
+AprEgr:ланьсянь K ; ! 
+AprPrl:ланьӧд K ; !
+AprTra:ланьті K ; !       
+AprTer:ланьӧдз K ; ! 


! These ones to be removed:
! LEXICON PRON-PERS-PL1-ACC
! +Pron+Pers+Pl1+Acc: WordEnd ;
! LEXICON PRON-PERS-PL1-COM
! +Pron+Pers+Pl1+Com: WordEnd ;
! LEXICON PRON-PERS-PL1-DAT
! +Pron+Pers+Pl1+Dat: WordEnd ;
! LEXICON PRON-PERS-PL1-GEN
! +Pron+Pers+Pl1+Gen: WordEnd ;
! LEXICON PRON-PERS-PL1-NOM
! +Pron+Pers+Pl1+Nom: WordEnd ;
! LEXICON PRON-PERS-PL2-ACC
! +Pron+Pers+Pl2+Acc: WordEnd ;
! LEXICON PRON-PERS-PL2-COM
! +Pron+Pers+Pl2+Com: WordEnd ;
! LEXICON PRON-PERS-PL2-DAT
! +Pron+Pers+Pl2+Dat: WordEnd ;
! LEXICON PRON-PERS-PL2-GEN
! +Pron+Pers+Pl2+Gen: WordEnd ;
! LEXICON PRON-PERS-PL2-NOM
! +Pron+Pers+Pl2+Nom: WordEnd ;
! LEXICON PRON-PERS-PL3-ACC
! +Pron+Pers+Pl3+Acc: WordEnd ;
! LEXICON PRON-PERS-PL3-COM
! +Pron+Pers+Pl3+Com: WordEnd ;
! LEXICON PRON-PERS-PL3-DAT
! +Pron+Pers+Pl3+Dat: WordEnd ;
! LEXICON PRON-PERS-PL3-ELA
! +Pron+Pers+Pl3+Ela: WordEnd ;
! LEXICON PRON-PERS-PL3-GEN
! +Pron+Pers+Pl3+Gen: WordEnd ;
! LEXICON PRON-PERS-PL3-NOM
! +Pron+Pers+Pl3+Nom: WordEnd ;
! LEXICON PRON-PERS-SG1-ACC
! +Pron+Pers+Sg1+Acc: WordEnd ;
! LEXICON PRON-PERS-SG1-DAT
! +Pron+Pers+Sg1+Dat: WordEnd ;
! LEXICON PRON-PERS-SG1-ELA
! +Pron+Pers+Sg1+Ela: WordEnd ;
! LEXICON PRON-PERS-SG1-GEN
! +Pron+Pers+Sg1+Gen: WordEnd ;
! LEXICON PRON-PERS-SG1-NOM ! tähän tulee ме
! +Pron+Pers+Sg1+Nom: WordEnd ;
! LEXICON PRON-PERS-SG2-ACC
! +Pron+Pers+Sg2+Acc: WordEnd ;
! LEXICON PRON-PERS-SG2-COM
! +Pron+Pers+Sg2+Com: WordEnd ;
! LEXICON PRON-PERS-SG2-DAT
! +Pron+Pers+Sg2+Dat: WordEnd ;
! LEXICON PRON-PERS-SG2-ELA
! +Pron+Pers+Sg2+Ela: WordEnd ;
! LEXICON PRON-PERS-SG2-GEN
! +Pron+Pers+Sg2+Gen: WordEnd ;
! LEXICON PRON-PERS-SG2-NOM
! +Pron+Pers+Sg2+Nom: WordEnd ;
! LEXICON PRON-PERS-SG2-ABL
! +Pron+Pers+Sg2+Abl: WordEnd ;
! LEXICON PRON-PERS-SG3-ACC
! +Pron+Pers+Sg3+Acc: WordEnd ;
! LEXICON PRON-PERS-SG3-COM
! +Pron+Pers+Sg3+Com: WordEnd ;
! LEXICON PRON-PERS-SG3-DAT
! +Pron+Pers+Sg3+Dat: WordEnd ;
! LEXICON PRON-PERS-SG3-ELA
! +Pron+Pers+Sg3+Ela: WordEnd ;
! LEXICON PRON-PERS-SG3-GEN
! +Pron+Pers+Sg3+Gen: WordEnd ;
! LEXICON PRON-PERS-SG3-NOM
! +Pron+Pers+Sg3+Nom: WordEnd ;
! LEXICON PRON-PERS-SG3-ABL
! +Pron+Pers+Sg3+Abl: WordEnd ;
! LEXICON PRON-PERS-SG3-OBL
! +Pron+Pers+Sg3+Dat:%>лы WordEnd ;
! +Pron+Pers+Sg3+Abl:%>лысь WordEnd ;


!Interrogative pronouns serve as the base of interrogative, relative, indefinite and distributional pronouns.

LEXICON INTERR
кин+Pron+Sem/Hum+Nom:кин INTERR-REL ; ! 2011-11-09 This will need cases
кодар+Det:кодар INTERR-REL ;
!2017-09-19кодар+Det+Der/MWN:кодар PRON-HUM-OBL ;
!кодар+Det+Der/MWN:кодар PRON-HUM-OBL ;
кодар+Det+Der/ладор:кодарладор PRON-HUM-OBL ;

коді+Pron+Interr:код PRON-HUM ;
коді+Pron+Rel:код PRON-HUM ;

кодыр+Adv+Temp:кодыр INTERR-REL ;
кор+Adv+Temp:кор INTERR-REL ;
кор+Adv+Temp+Pl+Ill:коръясӧ INTERR-REL ;
коръя+Adv+Temp:коръя INTERR-REL ;
кудз+Adv+Manner+Use/NG+Err/Orth:кудз INTERR-REL ;
кутшӧм+Pron:кутшӧм INTERR-REL ; ! This needs to be 
кутшӧм+Pron+Rel+Cop+Pl:кутшӧмӧсь INTERR-REL ; ! This needs to be 
кутшӧма+Adv+Manner:кутшӧма INTERR-REL ; ! This needs to be 
кутшӧм+Pron+Interr+Sg:кутшӧм PRON-NONHUM ;
кутшӧм+Pron+Interr+Pl:кутшӧмъяс PRON-NONHUM ;
кутшӧм+Pron+Rel+Sg:кутшӧм PRON-NONHUM ;
кутшӧм+Pron+Rel+Pl:кутшӧмъяс PRON-NONHUM ;
!2017-09-19кутшӧм+A+Der/MWN+N+Sg:кутшӧм PRON-NONHUM ;
!2017-09-19кутшӧм+A+Der/MWN+N+Pl:кутшӧмъяс PRON-NONHUM ;
кутшӧми+A:кутшӧми INTERR-REL ; 
!2017-09-20кутшӧми+Pron+A:кутшӧми INTERR-REL ; ! This needs to be 
кыдз+Pron+Adv+Manner:кыдз INTERR-REL ;
кыдзи+Pron+Adv+Manner:кыдзи INTERR-REL ;
кыдзик+Pron+Adv+Manner:кыдзик INTERR-REL ;
кыдзик+Pron+Adv+Manner+Ins:кыдзикӧн INTERR-REL ;
кымын+Det:кымын INTERR-REL ; ! Quantifier info required
кымын+Det+Iter:кымынысь INTERR-REL ; ! Quantifier info required
кымын+Det+Ord:кымынӧд INTERR-REL ; ! Quantifier info required
кымын+Det+Ord+Iter:кымынӧдысь INTERR-REL ; ! Quantifier info required
кысь+Adv+Spat+Ela:кысь INTERR-REL ;
кытчӧ+Adv+Spat+Ill:кытчӧ INTERR-REL ;
кытчӧдз+Adv+Spat+Ter:кытчӧдз INTERR-REL ;
кытысь+Adv+Spat+Ela:кытысь INTERR-REL ;
кыті+Adv+Spat+Tra:кыті INTERR-REL ;
кыткӧ+Adv+Spat+Tra+Der/кӧ+Indef:кыткӧ K ;
кытсюрӧ+Adv+Spat+Tra+Der/сюрӧ+Indef:кытсюрӧ K ;
кытӧн+Adv+Spat+Ine:кытӧн INTERR-REL ;
кытӧні+Adv+Spat+Ine:кытӧні INTERR-REL ;
 
кӧні+Adv+Spat+Ine+Cop+Sg:кӧні INTERR-REL ;
кӧні+Adv+Spat+Ine+Cop+Pl:кӧнӧсь INTERR-REL ;
мый+Pron+Interr+Sg:мый PRON-NONHUM ;
мый+Pron+Interr+Pl:мыйяс PRON-NONHUM ;
мыйда+Pron+NonHum:мыйда INTERR-REL ;
мыйта+Pron+NonHum:мыйта INTERR-REL ;
мыйтӧм+Pron+NonHum:мыйтӧм INTERR-REL ;
мыйӧс+Pron+NonHum+Use/NG+Err/Orth:мыйӧс INTERR-REL ;

!Here are some negative pronouns out of place 2011-11-22
LEXICON NEG-PRON
некысь+Adv+Neg+Spat+Ela:некысь K ;
некысянь+Adv+Neg+Spat+Egr:некысянь K ;
некыт+Adv+Neg+Spat+Tra:некыт K ;
некытчӧ+Adv+Neg+Spat+Ill:некытчӧ K ;
некытчӧдз+Adv+Neg+Spat+Ter:некытчӧдз K ;
некытысь+Adv+Neg+Spat+Ela:некытысь K ;
некыті+Adv+Neg+Spat+Tra:некыті K ;
некытӧн+Adv+Neg+Spat+Ine:некытӧн K ;
некӧн+Adv+Neg+Spat+Ine:некӧн K ;

LEXICON COLL-PRON
некоднан+Pron:некоднан COLL-PRON_CASE ;
кыкнан+Pron:кыкнан COLL-PRON_CASE ;
куимнан+Pron:куимнан COLL-PRON_CASE ;
нёльнан+Pron:нёльнан COLL-PRON_CASE ;
витнан+Pron:витнан COLL-PRON_CASE ;
квайтнан+Pron:квайтнан COLL-PRON_CASE ;
сизимнан+Pron:сизимнан COLL-PRON_CASE ;
кӧкъямыснан+Pron:кӧкъямыснан COLL-PRON_CASE ;
ӧкмыснан+Pron:ӧкмыснан COLL-PRON_CASE ;
даснан+Pron:даснан COLL-PRON_CASE ;
кызьнан+Pron:кызьнан COLL-PRON_CASE ;
комыннан+Pron:комыннан COLL-PRON_CASE ;
нелямыннан+Pron:нелямыннан COLL-PRON_CASE ;
ӧтинан+Pron:ӧтинан COLL-PRON_CASE ;

LEXICON COLL-PRON_CASE
+AssocColl: NumCASEPOSSLEX ; ! _нан_ is already part of the stem


LEXICON INDEFINITE-PRON
мыйсюрӧ+Pron+Indef+Sg+Nom:мыйсюрӧ K ;
кодсюрӧ+Pron+Indef+Sg+Acc:кодсюрӧӧс K ;
кодсюрӧ+Pron+Indef+Sg+Dat:кодсюрӧлы K ;
кодсюрӧ+Pron+Indef+Sg+Abl:кодсюрӧлысь K ;
кодсюрӧ+Pron+Indef+Pl+Nom:кодсюрӧяс K ;

корсюрӧ+Adv+Indef+Temp:корсюрӧ K ;
кудзсюрӧ+Adv+Indef+Manner+Use/NG+Err/Orth:кудзсюрӧ K ;!what is the lemma2020-04-16
кутшӧмсюрӧ+Pron+Indef:кутшӧмсюрӧ K ;
кыдзсюрӧ+v1+Adv+Indef+Manner:кыдзсюрӧ K ;
кыдзсюрӧ+v2+Adv+Indef+Manner:кыдзисюрӧ K ;
кымынысьсюрӧ+Adv+Indef+Iter:кымынысьсюрӧ K ;
кымынӧдысьсюрӧ+Adv+Indef+Ord+Iter:кымынӧдысьсюрӧ K ;
кысьсюрӧ+Adv+Indef+Ela:кысьсюрӧ K ;
кытчӧсюрӧ+Adv+Indef+Ill:кытчӧсюрӧ K ;
кытчӧдзсюрӧ+Adv+Indef+Ter:кытчӧдзсюрӧ K ;
кытысьсюрӧ+Adv+Indef+Ela:кытысьсюрӧ K ;
кытісюрӧ+Adv+Indef+Tra:кытісюрӧ K ;
кытсюрӧ+Adv+Indef+Tra+Der/сюрӧ+Indef:кытсюрӧ K ; 
кытӧнсюрӧ+Adv+Indef+Ine:кытӧнсюрӧ K ;
кӧнісюрӧ+Adv+Indef+Ine+Cop+Sg:кӧнісюрӧ K ;
кӧнісюрӧ+Adv+Indef+Ine:кӧнсюрӧ K ;

кодаркӧ+Det+Indef:кодаркӧ K ;
кодаркӧ+Det+Indef+Ins+PxSg3:кодаркӧнас K ;
кодыркӧ+Adv+Indef+Temp+Indef:кодыркӧ K ;
коркӧ+Adv+Indef+Temp:коркӧ K ;
коркӧ+Adv+Indef+Temp+Pl+Ill:коръясӧкӧ K ;
коръякӧ+Adv+Indef+Temp:коръякӧ K ;

кутшӧмкӧ+Pron+Indef:кутшӧмкӧ K ;
кутшӧмкӧ+Pron+Indef+Cop+Pl:кутшӧмӧськӧ K ;
кутшӧмакӧ+Adv+Indef+Manner:кутшӧмакӧ K ;
кутшӧми+A+Indef:кутшӧмикӧ K ;

кыдзкӧ+Adv+Indef+Manner:кыдзкӧ K ;
кыдзикӧ+Adv+Indef+Manner:кыдзикӧ K ;

кыдзикӧнкӧ+Adv+Indef+Manner+Ins:кыдзикӧнкӧ K ;

кымынкӧ+Det+Indef:кымынкӧ K ;
кымынӧдкӧ+Det+Indef+Ord:кымынӧдкӧ K ;
кымыныськӧ+Adv+Iter:кымыныськӧ K ;
кымынӧдыськӧ+Adv+Ord+Iter:кымынӧдыськӧ K ;
кытчӧкӧ+Adv+Indef+Ill:кытчӧкӧ K ;
кытчӧдзкӧ+Adv+Indef+Ter:кытчӧдзкӧ K ;
кытыськӧ+Adv+Indef+Ela:кытыськӧ K ;
кыськӧ+Adv+Indef+Ela:кыськӧ K ;
кытӧнкӧ+Adv+Indef+Ine:кытӧнкӧ K ;
кӧнікӧ+v1+Adv+Indef+Ine:кӧнікӧ K ;
кӧнікӧ+v2+Adv+Indef+Ine:кӧнкӧ K ;
кытікӧ+v1+Adv+Indef+Tra:кытікӧ K ;
кыткӧ+v2+Adv+Indef+Tra:кыткӧ K ; 


LEXICON PRON-HUM
+Sg+Nom:і WordEnd ;
+Pl+Nom:ъяс WordEnd ;
+Sg+Nom: WordEnd ;
+Sg: PRON-HUM-OBL ;
+Pl:ъяс PRON-HUM-OBL ;
: PRON-HUM-COLL ;
!код, коді

LEXICON PRON-HUM-OBL
! 2000: 187
!+Cmpl: WordEnd ;  removed 2019-10-27 same as Nom

 ABL/MORPH-THEN-WORDEND ;
 APR/MORPH-THEN-WORDEND ;
 CAR/MORPH-THEN-WORDEND ;
 CNS/MORPH-THEN-WORDEND ;
 COM/MORPH-THEN-WORDEND ;
 DAT/MORPH-THEN-WORDEND ;
 EGR/MORPH-THEN-WORDEND ;
 ELA/MORPH-THEN-WORDEND ;
 GEN/MORPH-THEN-WORDEND ;
 ILL/MORPH-THEN-WORDEND ;
 INE/MORPH-THEN-WORDEND ;
 INS/MORPH-THEN-WORDEND ;
 NOM/MORPH-THEN-WORDEND ;
 TER/MORPH-THEN-WORDEND ;
 TRA/MORPH-THEN-WORDEND ;
+Acc:ӧс WordEnd ;
+Acc+PxSg3:сӧ WordEnd ;

LEXICON PRON-HUM-COLL
+Coll:нан PRON-HUM-COLL-PERS ;
+ZeroColl: PRON-HUM-COLL-PERS ;
!
+ZeroColl+Acc+PxPl2:ныдтӧ WordEnd ;
+Coll+Acc+PxPl2:наннытӧ WordEnd ;
+ZeroColl+Acc+PxPl2+Err/Dial:ныдтэ WordEnd ;
+Coll+Acc+PxPl2+Err/Dial:наннытэ WordEnd ;
!+ZeroColl+Acc2+PxPl2:ныдтӧ WordEnd ;
!+Coll+Acc2+PxPl2:наннытӧ WordEnd ;

LEXICON PRON-HUM-COLL-PERS
+PxPl1:ным PRON-HUM-COLL-CASES ;
+PxPl1+Err/Dial:нум PRON-HUM-COLL-CASES ;
+PxPl2:ныд PRON-HUM-COLL-CASES ;
+PxPl3:ныс PRON-HUM-COLL-CASES ;
!
+Acc+PxPl1:нымӧс WordEnd ;
+Acc+PxPl1+Err/Dial:нумес WordEnd ;
+Acc+PxPl2:нытӧ WordEnd ;
+Acc+PxPl3:ныссӧ WordEnd ;
+Acc+PxPl2+Err/Dial:нытэ WordEnd ;
+Acc+PxPl3+Err/Dial:ныссэ WordEnd ;
!+Acc2+PxPl2:нытӧ WordEnd ;
!+Acc3+PxPl3:ныссӧ WordEnd ;

LEXICON PRON-HUM-COLL-CASES
 NOM/MORPH-THEN-WORDEND ;
 PRON-HUM-OBL ;

LEXICON PRON-NONHUM
CASE/MORPH-THEN-WORDEND ;

LEXICON CASE/MORPH-THEN-WORDEND
 ABL/MORPH-THEN-WORDEND ;
 APR/MORPH-THEN-WORDEND ;
 CAR/MORPH-THEN-WORDEND ;
 CMPR/MORPH-THEN-WORDEND ;
 CNS/MORPH-THEN-WORDEND ;
 COM/MORPH-THEN-WORDEND ;
 DAT/MORPH-THEN-WORDEND ;
 EGR/MORPH-THEN-WORDEND ;
 ELA/MORPH-THEN-WORDEND ;
 GEN/MORPH-THEN-WORDEND ;
 ILL/MORPH-THEN-WORDEND ;
 INE/MORPH-THEN-WORDEND ;
 INS/MORPH-THEN-WORDEND ;
 NOM/MORPH-THEN-WORDEND ;
 PRL/MORPH-THEN-WORDEND ;
 TER/MORPH-THEN-WORDEND ;
 TRA/MORPH-THEN-WORDEND ;
+Acc+PxSg3:%>сӧ WordEnd ;
+Acc+PxSg1:%>ӧс WordEnd ;
+Acc+PxSg3+Err/Dial:%>сэ WordEnd ;
+Acc+PxSg1+Err/Dial:%>эс WordEnd ; 

LEXICON ABL/MORPH-THEN-WORDEND
+Abl:лысь WordEnd ; !

LEXICON APL/MORPH-THEN-WORDEND
+Apr:лань WordEnd ;

LEXICON CAR/MORPH-THEN-WORDEND
+Car:тӧг WordEnd ;
+Car+Err/Dial:тэг WordEnd ;

LEXICON CMPR/MORPH-THEN-WORDEND
+Cmpr:ся WordEnd ;

LEXICON CNS/MORPH-THEN-WORDEND
+Cns:ла WordEnd ;

LEXICON COM/MORPH-THEN-WORDEND
+Com:%>кӧд WordEnd ;
+Com+Err/Dial:%>кед WordEnd ;

LEXICON DAT/MORPH-THEN-WORDEND
+Dat:лы WordEnd ; !

LEXICON EGR/MORPH-THEN-WORDEND
+Egr:сянь WordEnd ;

LEXICON ELA/MORPH-THEN-WORDEND
+Ela:ысь WordEnd ; 

LEXICON GEN/MORPH-THEN-WORDEND
+Gen:%>лӧн WordEnd ; !
+Gen+Err/Dial:%>лэн WordEnd ; !

LEXICON ILL/MORPH-THEN-WORDEND
+Ill:%>ӧ WordEnd ; !
+Ill+Err/Dial:%>э WordEnd ; !

LEXICON INE/MORPH-THEN-WORDEND
+Ine:ын WordEnd ;

LEXICON INS/MORPH-THEN-WORDEND
+Ins+Err/Dial:%>эн WordEnd ;
+Ins:%>ӧн WordEnd ;

LEXICON NOM/MORPH-THEN-WORDEND
+Nom: WordEnd ;

LEXICON PRL/MORPH-THEN-WORDEND
+Prl:%>ӧд WordEnd ;
+Prl+Err/Dial:%>эд WordEnd ;

LEXICON TER/MORPH-THEN-WORDEND
+Ter:%>ӧдз WordEnd ;
+Ter+Err/Dial:%>эдз WordEnd ;

LEXICON TRA/MORPH-THEN-WORDEND
+Tra:ті WordEnd ;



! Removing 2012-02-08 The comparative can be taken care of once
! LEXICON INTERR-REL-INDEF_COMPARISON
! +Comp:%>джык INTERR-REL ;
!               INTERR-REL ;

LEXICON Ill4INTERR-REL-INDEF
+Ill: INTERR-REL-INDEF ;

LEXICON EgrIll4INTERR-REL-INDEF
+Egr+Ill: INTERR-REL-INDEF ;

LEXICON Egr4INTERR-REL-INDEF
+Egr: INTERR-REL-INDEF ;

LEXICON Tra4INTERR-REL-INDEF
+Tra: INTERR-REL-INDEF ;

LEXICON TraEgr4INTERR-REL-INDEF
+Tra+Egr: INTERR-REL-INDEF ;

LEXICON Ter4INTERR-REL-INDEF
+Ter: INTERR-REL-INDEF ;

LEXICON INTERR-REL-INDEF ! 3 types of pronouns are congregated here
 PRON-INDEFTAG ; ! This leads to a variety of indefinite pronouns
 INTERR-REL ; ! This leads to a split between relative and interrogative pronouns

LEXICON INTERR-REL
+Interr: COMPARATIVE ;
+Rel: K ;

LEXICON PRON-INDEFTAG
+Der/кӧ+Indef:%>кӧ K ;
+Der/сюрӧ+Indef:%>сюрӧ K ;
+Der/кӧ+Indef+Err/Dial:%>ке K ;
+Der/сюрӧ+Indef+Err/Dial:%>сюре K ;


! Indefinite pronouns
 
LEXICON PRONINDEFTAG
+Indef: WordEnd ;

LEXICON PRONINDEFTAG-KO
+Indef+Sg: PRONINDEF-PERS-KO-CX ;
+Indef+Pl:%>ъяс PRONINDEF-PERS-KO-CX ;

LEXICON PRONINDEF-PERS-KO-CX
+Nom: PRONINDEF-PERS-KO-END ;
+Acc:%>ӧс PRONINDEF-PERS-KO-END ;
+Gen:%>лӧн PRONINDEF-PERS-KO-END ;
+Acc+Err/Dial:%>эс PRONINDEF-PERS-KO-END ;
+Gen+Err/Dial:%>лэн PRONINDEF-PERS-KO-END ;
+Abl:%>лысь PRONINDEF-PERS-KO-END ;
+Dat:%>лы PRONINDEF-PERS-KO-END ;
+Ins:%>ӧн PRONINDEF-PERS-KO-END ;
+Ins+Err/Dial:%>эн PRONINDEF-PERS-KO-END ;
+Ela:%>ысь PRONINDEF-PERS-KO-END ; !2011-10-19 It might be that this only has to do with мый _what_ and мыйыськӧ _for some reason_
+Com:%>кӧд PRONINDEF-PERS-KO-END ;
+Com+Err/Dial:%>кед PRONINDEF-PERS-KO-END ;
+Egr:%>сянь PRONINDEF-PERS-KO-END ;

LEXICON PRONINDEF-PERS-KO-END
:%>кӧ K ;
+Err/Dial:%>ке K ;

LEXICON PRONINDEFTAG-SJURO
+Indef: PRONINDEF-PERS-SJURO-CX ;

LEXICON PRONINDEF-PERS-SJURO-CX
+Nom: PRONINDEF-PERS-SJURO-END ;
+Acc:%>ӧс PRONINDEF-PERS-SJURO-END ;
+Gen:%>лӧн PRONINDEF-PERS-SJURO-END ;
+Acc+Err/Dial:%>эс PRONINDEF-PERS-SJURO-END ;
+Gen+Err/Dial:%>лэн PRONINDEF-PERS-SJURO-END ;
+Abl:%>лысь PRONINDEF-PERS-SJURO-END ;
+Dat:%>лы PRONINDEF-PERS-SJURO-END ;
+Ins:%>ӧн PRONINDEF-PERS-SJURO-END ;
+Ins+Err/Dial:%>эн PRONINDEF-PERS-SJURO-END ;
+Ela:%>ысь PRONINDEF-PERS-SJURO-END ; !2011-10-19 It might be that this only has to do with мый _what_ and мыйыськӧ _for some reason_
+Com:%>кӧд PRONINDEF-PERS-SJURO-END ;
+Com+Err/Dial:%>кед PRONINDEF-PERS-SJURO-END ;
+Egr:%>сянь PRONINDEF-PERS-SJURO-END ;



LEXICON PRONINDEF-PERS-SJURO-END
:%>сюрӧ K ;
+Err/Dial:%>сюре K ;
 !indefinite pronouns from the interrogative/relative pronouns.


LEXICON IS_PRON-INDEF-KO
+Der/кӧ+Indef: K ;

LEXICON IS_PRON-INDEF-SJURO
+Der/сюрӧ+Indef: K ;

LEXICON IS_PRON-INTERR
+Pron+Interr: K ;

!LEXICON PRONINDEF-PERS-END
!+Der/сюрӧ:%>сюрӧ WordEnd ;

!LEXICON PRONINDEF-PERS-KO
!+Nom: PRONINDEF-PERS-KO-END ;
!+Acc:%>ӧс PRONINDEF-PERS-KO-END ;
!+Gen:%>лӧн PRONINDEF-PERS-KO-END ;
!+Dat:%>лы PRONINDEF-PERS-KO-END ;
!+Acc:%>ӧс PRONINDEF-PERS-KO-END ;


!2018-10-21!LEXICON PRON-SPAT
!2018-10-21!+Pron+Spat: COMPARATIVE ;

LEXICON PRON-RECIPR
+Recipr: PRON-RECIPR-CASE ;

LEXICON PRON-RECIPR-CASE
+Cmpl: WordEnd ; !CHECKME 2019-10-27 is this same as Sg Nom
+Cmpl+PxSg2:ыд K ;
+Cmpl+PxSg2:ыс K ;
+Cmpl+PxPl1:ным K ;
+Cmpl+PxPl1+Err/Dial:нум K ;
+Cmpl+PxPl2:ныд K ;
+Cmpl+PxPl3:ныс K ;
+Gen:лӧн K ;
+Gen+Err/Dial:лэн K ;
+Abl+PxSg3:ыслысь K ;
+Abl+PxPl3:ныслысь K ;
+Dat:лы K ;
+Dat+PxSg3:ыслы K ;
+Dat+PxPl3:ныслы K ;
+Acc:%>ӧс K ;
+Acc+PxSg2:тӧ K ;
+Acc+Err/Dial:%>эс K ;
+Acc+PxSg2+Err/Dial:тэ K ;
+Acc+PxPl1:нымӧс K ;
+Acc+PxPl1+Err/Dial:нумес K ;
+Acc+PxPl2:нытӧ K ;
+Acc+PxPl3:ныссӧ K ;
+Acc+PxSg3:сӧ K ;
+Acc+PxPl2+Err/Dial:нытэ K ;
+Acc+PxPl3+Err/Dial:ныссэ K ;
+Acc+PxSg3+Err/Dial:сэ K ;

!+Acc2+PxPl2:нытӧ K ;
!+Acc3+PxPl3:ныссӧ K ;
!+Acc3:сӧ K ;
+Com+PxSg3:ыскӧд K ;
+Com+PxPl2:ныдкӧд K ;
+Com+PxPl3:ныскӧд K ;
+Com+PxSg3+Err/Dial:ыскед K ;
+Com+PxPl2+Err/Dial:ныдкед K ;
+Com+PxPl3+Err/Dial:ныскед K ;
+Car+PxSg3:тӧгыс K ;
+Car+PxSg3+Err/Dial:тэгыс K ;
+Ela:ысь K ;
+Ela+PxSg3:сьыс K ;
+Ela+PxPl2:сьыныд K ;
+Ela+PxPl3:сьыныс K ;
+Ill+PxSg3:ас K ; ! Here is a good problem: Should there be a special tag
                ! for the illative-inessive forms
+Ill+PxPl3:нысӧ K ;
+Ill+PxPl3+Err/Dial:нысэ K ;

+Abl+PxPl2:ныдлысь K ;
+Dat+PxPl2:ныдлы K ;

+Com:кӧд K ;
+Com+Err/Dial:кед K ;
+Ins:%>ӧн K ;
+Ins+Err/Dial:%>эн K ;
+Egr+PxPl2:сяньныд K ;


LEXICON PRON-REL
+Rel: WordEnd ;

LEXICON PRON-REFL-IN_LADOR
+Pron+Refl+Der/ладор: Noun1 ; !2011-11-22

LEXICON REFL_AS !! * = @CODE@ ас:а 
+Det+Refl:с ABS_ ; !ас is an absolute-form modifer
+Pron+Refl:  REFL_AS-01 ;
+Pron+Refl+Ins:сӧн K ; !There is no indication of person. = Sg1, Sg3, Pl3
+Pron+Refl+Ins+Err/Dial:сэн K ; !There is no indication of person. = Sg1, Sg3, Pl3

+Pron+Poss+Sg1:слам PRON-POSS  "minun oma -ni" ;
+Pron+Poss+Sg2:слад PRON-POSS  "sinun oma -si" ;
+Pron+Poss+Sg3:слас PRON-POSS  "hänen oma -nsa/ sen oma" ;
+Pron+Poss+Pl1:сланым PRON-POSS  "meidän oma -mme" ;
+Pron+Poss+Pl1+Err/Dial:сланум PRON-POSS  "meidän oma -mme" ;
+Pron+Poss+Pl2:сланыд PRON-POSS  "teidän oma -nne" ;
+Pron+Poss+Pl3:сланыс PRON-POSS  "heidän oma -nsa/ niiden oma" ;

+Det+Poss+Sg1:слам WordEnd-PredMonAdj  "minun oma -ni" ;
+Det+Poss+Sg2:слад WordEnd-PredMonAdj  "sinun oma -si" ;
+Det+Poss+Sg3:слас WordEnd-PredMonAdj  "hänen oma -nsa/ sen oma" ;
+Det+Poss+Pl1:сланым WordEnd-PredMonAdj  "meidän oma -mme" ;
+Det+Poss+Pl1+Err/Dial:сланум WordEnd-PredMonAdj  "meidän oma -mme" ;
+Det+Poss+Pl2:сланыд WordEnd-PredMonAdj  "teidän oma -nne" ;
+Det+Poss+Pl3:сланыс WordEnd-PredMonAdj  "heidän oma -nsa/ niiden oma" ;



LEXICON REFL_AS-01
+Sg1:с PRON-REFL-SG1-4-OBL-CASES ;
+Sg2:с PRON-REFL-SG2-4-OBL-CASES ;
+Sg3:с PRON-REFL-SG3-4-OBL-CASES ;
+Pl1:с PRON-REFL-PL1-4-OBL-CASES ;
+Pl2:с PRON-REFL-PL2-4-OBL-CASES ;
+Pl3:с PRON-REFL-PL3-4-OBL-CASES ;
:с PRON-REFL-OBL-CASES ;

+Sg1:чым PRON-REFL-SG1-4-NOM  "minä itse" ;
+Sg1+Err/Dial:чум PRON-REFL-SG1-4-NOM  "minä itse" ;

+Sg2:сьтӧ PRON-REFL-SG2-4-ACC  "" ;
+Sg2+Err/Dial:сьтэ PRON-REFL-SG2-4-ACC  "" ;
+Sg2:чыд PRON-REFL-SG2-4-CAR/ELA/NOM  "sinä itse" ;
+Sg3:сьсӧ PRON-REFL-SG3-4-ACC  "" ;
+Sg3+Err/Dial:сьсэ PRON-REFL-SG3-4-ACC  "" ;
+Sg3:чыс PRON-REFL-SG3-4-NOM  "hän itse" ;

+Pl1:сьным PRON-REFL-PL1-4-NOM  "me itse" ;
+Pl1+Err/Dial:сьнум PRON-REFL-PL1-4-NOM  "me itse" ;
+Pl2:сь PRON-REFL-PL2-4-ACC  "" ;
+Pl2:сьныд PRON-REFL-PL2-4-NOM  "te itse" ;

+Pl3:сь PRON-REFL-PL3-4-ACC  "" ;
+Pl3:сьныс PRON-REFL-PL3-4-NOM  "he/ ne itse" ;

:сладор PRON-REFL-IN_LADOR  "" ;

LEXICON PRON-REFL-SG1-4-OBL-CASES
+Car:тӧг K ; !There is no indication of person. = Sg1
+Com:кӧд K ; !There is no indication of person. = Sg1
+Cns:ла K ; !There is no indication of person. = Sg1
+Dat:лым K ;
+Dat+Err/Dial:лум K ;
+Ela:сьым K ;
+Ela+Err/Dial:сьум K ;
!+Ins:ӧн K ; !There is no indication of person. = Sg1, Sg3, Pl3
+Ins:намӧн K ; !CHECKME

LEXICON PRON-REFL-SG2-4-OBL-CASES
+Car:тӧгыд K ; !
+Com:кӧдыд K ; !
+Car+Err/Dial:тэгыд K ; !
+Com+Err/Dial:кедыд K ; !
+Cns:лаыд K ;
+Cns:ыдла K ;
+Dat:лыд K ;
+Ela:сьыд K ;
+Ine:ад K ;
+Ins:над K ;
LEXICON PRON-REFL-SG3-4-OBL-CASES
+Car:тӧгыс K ; !
+Com:кӧдыс K ; !
+Car+Err/Dial:тэгыс K ; !
+Com+Err/Dial:кедыс K ; !
+Cns:ысла K ;
+Dat:лыс K ;
+Ela:сьыс K ;
+Ine:ас K ;
+Ins:нас K ;
!+Ins:ӧн K ; !There is no indication of person. = Sg1, Sg3, Pl3
LEXICON PRON-REFL-PL1-4-OBL-CASES
+Car:тӧгным K ; !
+Com:кӧдным K ; !
+Dat:лыным K ;
+Ela:сьыным K ;
+Ela:сьынымлысь K ; !CHECKME
+Ela:сьынымысь K ; !CHECKME
+Ins:наным K ;
+Car+Err/Dial:тегнум K ; !
+Com+Err/Dial:кеднум K ; !
+Dat+Err/Dial:лынум K ;
+Ela+Err/Dial:сьынум K ;
+Ela+Err/Dial:сьынумлысь K ; !CHECKME
+Ela+Err/Dial:сьынумысь K ; !CHECKME
+Ins+Err/Dial:нанум K ;

LEXICON PRON-REFL-PL2-4-OBL-CASES
+Car+So/CP:тӧгныд K ; !
+Car+So/PC:ныдтӧг K ; !
+Car+So/CP+Err/Dial:тэгныд K ; !
+Car+So/PC+Err/Dial:ныдтэг K ; !
+Cns:ныдла K ;
+Com+So/CP:кӧдныд K ; !
+Com+So/PC:ныдкӧд K ; !
+Com+So/CP+Err/Dial:кедныд K ; !
+Com+So/PC+Err/Dial:ныдкед K ; !
+Dat:лыныд K ;
+Dat:лыныдлы K ;
+Ela:сьыныд K ;
+Ins:наныд K ;
LEXICON PRON-REFL-PL3-4-OBL-CASES
+Car+So/PC:тӧгныс K ; !
+Car+So/PC:ныстӧг K ; !
+Car+So/PC+Err/Dial:тэгныс K ; !
+Car+So/PC+Err/Dial:ныстэг K ; !
+Cns:нысла K ;
+Com+So/CP:кӧдныс K ; !
+Com+So/PC:ныскӧд K ; !
+Com+So/CP+Err/Dial:кедныс K ; !
+Com+So/PC+Err/Dial:ныскед K ; !
+Dat:лыныс K ;
+Dat:лыныслы K ;
+Dat:ныслы K ;
+Ela:сьыныс K ;
+Ins:наныс K ;
!+Ins:ӧн K ; !There is no indication of person. = Sg1, Sg3, Pl3


LEXICON PRON-REFL-OBL-CASES
+Ine+PxPl1:аным K ;
+Ine+PxPl1+Err/Dial:анум K ;
+Ine+PxPl2:аныд K ;
+Ine+PxPl3:аныс K ;
+Sg1+Ill:ӧ K ; !There is no indication of person. = Sg1
+Ill+PxSg1:ам K ; !Ӧнія коми кыв gives this for illative only
+Ill+PxSg2:ад K ; !
+Ill+PxSg3:ас K ; !
+Ill+PxPl1:аным K ; !
+Ill+PxPl1+Err/Dial:анум K ; !
+Ill+PxPl2:аныд K ; !
+Ill+PxPl3:аныс K ; !
+Apr+PxSg1:лань K ; !There is no indication of person. = Sg1
+Apr+PxSg2:ланьыд K ;
+Apr+PxSg3:ланьыс K ;
+Apr+PxPl1:ланьным K ;
+Apr+PxPl1+Err/Dial:ланьнум K ;
+Apr+PxPl2:ланьныд K ;
+Apr+PxPl3:ланьныс K ;
+Egr+PxSg1:сянь K ; !There is no indication of person. = Sg1
+Egr+PxSg2:сяньыд K ;
+Egr+PxSg3:сяньыс K ;
+Egr+PxPl1:сяньным K ;
+Egr+PxPl1+Err/Dial:сяньнум K ;
+Egr+PxPl2:сяньныд K ;
+Egr+PxPl3:сяньныс K ;
+Tra+PxSg1:ті K ; !There is no indication of person. = Sg1
+Tra+PxSg2:тіыд K ;
+Tra+PxSg3:тіыс K ;
! Pl1 and Pl2 are not attested in Ӧнія коми кыв
+Tra+PxPl3:тіныс K ;
+Ter+PxSg1:%>ӧдз K ; !There is no indication of person. = Sg1
+Ter+PxSg2:%>ӧдзыд K ;
+Ter+PxSg3:%>ӧдзыс K ;
+Ter+PxSg2:%>эдзыд K ;
+Ter+PxSg3:%>эдзыс K ;
+Ter+PxPl1:нымӧдз K ;
+Ter+PxPl1+Err/Dial:нумедз K ;
+Ter+PxPl2:ныдӧдз K ;
+Ter+PxPl3:нысӧдз K ;
+Ter+PxPl2+Err/Dial:ныдэдз K ;
+Ter+PxPl3+Err/Dial:нысэдз K ;
!Extras 2011-10-07


LEXICON PRON-REFL-SG1-4-NOM
 ACC-WORD-END ;
 CAR-WORD-END ;
 COM-WORD-END ;
 ELA-WORD-END ;
 ILL-WORD-END ;
 INE-WORD-END ;
 INS-WORD-END ;
 NOM-WORD-END ;

LEXICON PRON-POSS
+Sg: PRON-POSS_CASES ;
+Pl:%>ъяс PRON-POSS_CASES ;

LEXICON PRON-POSS_CASES
 ABL-WORD-END ;
 ACC-WORD-END ;
 CAR-WORD-END ;
 COM-WORD-END ;
 EGR-WORD-END ;
 ELA-WORD-END ;
 GEN-WORD-END ;
 ILL-WORD-END ;
 INE-WORD-END ;
 INS-WORD-END ;
 NOM-WORD-END ;
 PRL-WORD-END ;
 PRONDEMTAG-APR ;
PRONDEMTAG-APR-EGR ;
CNS-WORD-END ;
+Dat:лы WordEnd ;
+AprIll:%>ланьӧ IllCompar ; !
+AprIll+Err/Dial:%>лане IllCompar ; !

+Nom+PxSg2:%>ыд K ;
+Nom+PxSg3:%>ыс K ;

!LEXICON PRON-REFL-SG1-4-GEN
!+Gen: WordEnd ; !! This is bare
! COM-WORD-END ;
! EGR-WORD-END ;

LEXICON PRON-REFL-SG2-4-CAR/ELA/NOM
 CAR-WORD-END ;
 NOM-WORD-END ;
 ELA-WORD-END ;

!LEXICON PRON-REFL-SG2-4-GEN
!+Gen: WordEnd ;
! EGR-WORD-END ;

LEXICON PRON-REFL-SG2-4-ACC
+Acc: WordEnd ;

LEXICON PRON-REFL-SG3-4-NOM
 CAR-WORD-END ;
 COM-WORD-END ;
 ELA-WORD-END ;
 NOM-WORD-END ;
 INS-WORD-END ;

!LEXICON PRON-REFL-SG3-4-GEN
!+Gen: WordEnd ; !! bare
! GEN-WORD-END ;
! ILL-WORD-END ;

LEXICON PRON-REFL-SG3-4-ACC
+Acc: WordEnd ;

LEXICON PRON-REFL-PL1-4-NOM
+Acc:%>ӧс WordEnd ;
+Acc+Err/Dial:%>эс WordEnd ;
 CAR-WORD-END ;
 COM-WORD-END ;
 ELA-WORD-END ;
 NOM-WORD-END ;
 TER-WORD-END ;

!LEXICON PRON-REFL-PL1-4-GEN
!+Gen: WordEnd ;  !! bare
! ABL-WORD-END ;
!+Der+Der/ладор:%>ладор Noun1 ;

LEXICON PRON-REFL-PL2-4-NOM
+Acc+PxSg2:тӧ WordEnd ;
+Acc+PxSg2+Err/Dial:тэ WordEnd ;
!+Acc2:тӧ WordEnd ;
 CAR-WORD-END ;
 CNS-WORD-END ;
 ELA-WORD-END ;
 NOM-WORD-END ;

!LEXICON PRON-REFL-PL2-4-GEN
!+Gen: WordEnd ; !! bare
!+Der+Der/ладор:%>ладор Noun1 ;


LEXICON PRON-REFL-PL2-4-ACC
+Acc+PxPl2:нытӧ WordEnd ;
+Acc+PxPl2+Err/Dial:нытэ WordEnd ;
!+Acc2:нытӧ WordEnd ;

LEXICON PRON-REFL-PL3-4-NOM
 CNS-WORD-END ;
 NOM-WORD-END ;

!LEXICON PRON-REFL-PL3-4-GEN
!+Gen: WordEnd ; !! bare
! GEN-WORD-END ;
! PRL-WORD-END ;
!

LEXICON PRON-REFL-PL3-4-ACC
+Acc+PxPl3:нысӧ WordEnd ;
!+Acc3:нысӧ WordEnd ;


LEXICON PRONDEMTAG
+Pron+Dem: WordEnd ; ! sg/pl?

LEXICON ADNDEMTAG
+Det+Dem: K ;
+Det+Dem+Cop+Pl:%>ӧсь K ;
+Det+Dem: Noun1 ;
!+Adn+Dem: K ;
!+Adn+Dem+Cop+Pl:%>ӧсь K ;
!+Adn+Dem+Der/MWN: Noun1 ;

LEXICON PRONDEMTAG-ES
+Pron+Dem+Sg+Nom:%>ійӧ K ;
+Pron+Dem+Sg+Acc:%>ійӧс K ;
+Pron+Dem+Sg+Ins:%>ійӧн K ;
+Pron+Dem+Sg+Nom+Err/Dial:%>іе K ;
+Pron+Dem+Sg+Acc+Err/Dial:%>іес K ;
+Pron+Dem+Sg+Ins+Err/Dial:%>іен K ;
+Pron+Dem+Sg:%>ы PRONDEMTAGSG ;
+Pron+Dem+Pl:%>ійӧяс WordEnd ;
+Pron+Dem+Pl+Err/Dial:%>іеяс WordEnd ;
+Pron+Dem+Pl:%>ійяс WordEnd ;
+Pron+Dem+Pl:%>ійӧян WordEnd ;
+Pron+Dem+Pl+Err/Dial:%>іеян WordEnd ;
+Pron+Dem+Pl:%>ійян WordEnd ;

LEXICON PRONDEMTAGSG-TA
+Pron+Dem+Sg+Nom:%>йӧ K ;
+Pron+Dem+Sg+Acc:%>йӧс K ;
+Pron+Dem+Sg+Ins:%>йӧн K ;
+Pron+Dem+Sg+Nom+Err/Dial:%>е K ;
+Pron+Dem+Sg+Acc+Err/Dial:%>ес K ;
+Pron+Dem+Sg+Ins+Err/Dial:%>ен K ;
+Pron+Dem+Sg: PRONDEMTAGSG ;
+Pron+Dem+Pl:%>йӧяс PRONDEMTAGPL ;
+Pron+Dem+Pl+Err/Dial:%>еяс PRONDEMTAGPL ;
+Pron+Dem+Pl:%>йяс PRONDEMTAGPL ;
+Pron+Dem+Pl:%>йӧян PRONDEMTAGPL ;
+Pron+Dem+Pl+Err/Dial:%>еян PRONDEMTAGPL ;
+Pron+Dem+Pl:%>йян PRONDEMTAGPL ;

LEXICON PRONDEMTAGSG 
+Cmpl: WordEnd ; !distinct from Nom
+Ins:%>ӧн K ;
+Ins+Err/Dial:%>эн K ;
+Com:кӧд K ;
+Car:тӧг K ;
+Com+Err/Dial:кед K ;
+Car+Err/Dial:тэг K ;
+Cns:ла K ;
+Dat:лы K ;
+Ine:ын K ;
+Ela:ысь K ; 
+Ill:ӧ K ; !This is questioned in the 2000 Grammar
+Abl:лысь K ; !
 PRONDEMTAG-APR ;
 PRONDEMTAG-EGR ;
 PRONDEMTAG-PRL ;
 PRONDEMTAG-TRA ;
 PRONDEMTAG-TER ;


LEXICON PRONDEMTAGPL
!+Pron+Dem+Pl: WordEnd ;
+Nom: WordEnd ;
+Gen:лӧн WordEnd ;
+Gen+Err/Dial:лэн WordEnd ;
+Acc:ӧс WordEnd ;
+Ins:ӧн K ;
+Acc+Err/Dial:%>эс WordEnd ;
+Ins+Err/Dial:%>эн K ;
+Com:кӧд K ;
+Car:тӧг K ;
+Com+Err/Dial:кед K ;
+Car+Err/Dial:тэг K ;
+Cns:ла K ;
+Ine:ын K ;
+Ela:ысь K ; 
+Ill:%>ӧ K ; !
+Ill+Err/Dial:%>э K ; !
+Abl:лысь K ; !
 PRONDEMTAG-APR ;
 PRONDEMTAG-APR-EGR ;
 PRONDEMTAG-APR-ELA ;
 PRONDEMTAG-APR-ILL ;
 PRONDEMTAG-APR-INE ;
 PRONDEMTAG-EGR ;
 PRONDEMTAG-PRL ;
 PRONDEMTAG-TRA ;
 PRONDEMTAG-TER ;

LEXICON PRONDEMTAG-ELA
+Ela:ысь K ;

LEXICON PRONDEMTAG-APR
+Apr:лань K ;

LEXICON PRONDEMTAG-APR-EGR
+AprEgr:ланьсянь K ;

LEXICON PRONDEMTAG-APR-ELA
+AprEla:ланьысь K ;

LEXICON PRONDEMTAG-APR-ILL
+AprIll:ланьӧ K ;
+AprIll+Err/Dial:лане K ;

LEXICON PRONDEMTAG-APR-INE
+AprIne:ланьын K ;

LEXICON PRONDEMTAG-EGR
+Egr:сянь K ;

LEXICON PRONDEMTAG-PRL
+Prl:ӧд K ;
+Prl+Err/Dial:%>эд K ;

LEXICON PRONDEMTAG-TRA
+Tra:ті K ;

LEXICON PRONDEMTAG-TER
+Ter:ӧдз K ;
+Ter+Err/Dial:%>эдз K ;

! Word forms that already have their morphology 
LEXICON PRON-DEM-IS_INE
+Pron+Dem+Ine: K ;

LEXICON PRON-DEM-IS_ILL
+Pron+Dem+Ill: K ;



!2018-10-21!LEXICON PRON-INTER-NONHUM
!2018-10-21!+Pron+Interr+Nom: WordEnd ;
!2018-10-21!+Pron+Interr+Gen:лӧн K ; !
!2018-10-21!+Pron+Interr+Abl:лысь K ; !
!2018-10-21!+Pron+Interr+Dat:лы K ; !
!2018-10-21!+Pron+Interr+ZeroAcc: WordEnd ;
!2018-10-21!+Pron+Interr+Acc3:сӧ WordEnd ;
!2018-10-21!+Pron+Interr+Ins:ӧн K ;
!2018-10-21!+Pron+Interr+Com:кӧд K ;
!2018-10-21!+Pron+Interr+Car:тӧг K ;
!2018-10-21!+Pron+Interr+Cns:ла K ;
!2018-10-21!+Pron+Interr+Ine:ын K ;
!2018-10-21!+Pron+Interr+Ela:ысь K ; 
!2018-10-21!+Pron+Interr+Ill:ӧ K ; !
!2018-10-21!+Pron+Interr+Apr:лань K ;
!2018-10-21!+Pron+Interr+Egr:сянь K ;
!2018-10-21!+Pron+Interr+Prl:ӧд K ;
!2018-10-21!+Pron+Interr+Tra:ті K ;
!2018-10-21!+Pron+Interr+Ter:ӧдз K ;

LEXICON PRON-NEG-HUM
+Pron+Neg+Sg+Nom: WordEnd ;
+Pron+Neg+Sg+Nom:і WordEnd ;
+Pron+Neg+Sg: PRON-NEG-OBL ;
+Pron+Neg+Pl+Nom:%>ъяс WordEnd ;
+Pron+Neg+Pl:%>ъяс PRON-NEG-OBL ;

LEXICON PRON-DUAL
+Pron+Du: PRON-NEG-OBL ;

! LEXICON PRON-INTER-REL-HUM-COLL
! +Coll:нан PRON-INTER-REL-HUM-COLL-PERS ;
! +ZeroColl: PRON-INTER-REL-HUM-COLL-PERS ;
! +ZeroColl+PxPl2+Acc2:ныдтӧ PRON-INTER-REL-HUM-COLL-INDEF ;
! +Coll+Acc2+PxPl2:наннытӧ PRON-INTER-REL-HUM-COLL-INDEF ;
! 
! LEXICON PRON-INTER-REL-HUM-COLL-PERS
! +PxPl1:ным PRON-INTER-REL-HUM-COLL-CASES ;
! +PxPl2:ныд PRON-INTER-REL-HUM-COLL-CASES ;
! +PxPl3:ныс PRON-INTER-REL-HUM-COLL-CASES ;
! +Acc+PxPl1:нымӧс PRON-INTER-REL-HUM-COLL-INDEF ;
! +Acc3+PxPl3:ныссӧ PRON-INTER-REL-HUM-COLL-INDEF ;
! 
! LEXICON PRON-INTER-REL-HUM-COLL-CASES
! +Nom: PRON-INTER-REL-HUM-COLL-INDEF ;
! +Gen:лӧн PRON-INTER-REL-HUM-COLL-INDEF ;
! +Abl:лысь PRON-INTER-REL-HUM-COLL-INDEF ;
! +Dat:лы PRON-INTER-REL-HUM-COLL-INDEF ;
! +Ins:ӧн PRON-INTER-REL-HUM-COLL-INDEF ;
! +Com:кӧд PRON-INTER-REL-HUM-COLL-INDEF ;
! +Car:тӧг PRON-INTER-REL-HUM-COLL-INDEF ;
! +Cns:ла PRON-INTER-REL-HUM-COLL-INDEF ;
! +Ine:ын PRON-INTER-REL-HUM-COLL-INDEF ;
! +Ela:ысь PRON-INTER-REL-HUM-COLL-INDEF ;
! +Ill:ӧ PRON-INTER-REL-HUM-COLL-INDEF ;
! +Apr:лань PRON-INTER-REL-HUM-COLL-INDEF ;
! +Egr:сянь PRON-INTER-REL-HUM-COLL-INDEF ;
! +Tra:ті PRON-INTER-REL-HUM-COLL-INDEF ;
! +Ter:ӧдз PRON-INTER-REL-HUM-COLL-INDEF ;
! 
! LEXICON PRON-INTER-REL-HUM-COLL-INDEF
! +Der/кӧ+Indef:кӧ K ;
!  K ;

!Actually there might be reason to consider +Der/MWN
LEXICON PRONQ-MUKOD
!2017-09-20+Pron+Abs: WordEnd ;
+Pron+Sg+Nom: K ;
+Pron+Sg: PRONQ-STAV-CASES ;
+Pron+Pl+Nom:ъяс K ;
+Pron+Pl:ъяс PRONQ-STAV-CASES ;

LEXICON PRONQ-STAV
!2017-09-20+Pron+Abs: WordEnd ;
+Pron+Sg+Nom: K ;
!+Pron+Sg+ZeroAcc: K ;
+Pron+Sg: PRONQ-STAV-CASES ;

LEXICON PRONQ-STAV-CASES
+Nom+PxSg2:ыд K ;
+Nom+PxSg3:ыс K ;
+Nom+PxPl1:ным K ;
+Nom+PxPl1+Err/Dial:нум K ;
+Nom+PxPl2:ныд K ;
+Nom+PxPl3:ныс K ;

+Ins:ӧн K ;
+Ins+PxSg1:нам K ;
+Ins+PxSg2:над K ;
+Ins+PxSg3:нас K ;
+Ins+PxPl1:нымӧн K ; ! Should there be a Der/ӧн tag?
+Ins+PxPl1+Err/Dial:нумен K ; ! Should there be a Der/ӧн tag?
+Ins+PxPl2:ныдӧн K ; ! Should there be a Der/ӧн tag?
+Ins+PxPl3:нысӧн K ; ! Should there be a Der/ӧн tag?

+Ela:ысь K ;
+Ela+PxSg3:сьыс K ;
+Ela+PxPl1+So/CP:сьыным K ;
+Ela+PxPl1+So/PC:нымысь K ;
+Ela+PxPl1+So/CP+Err/Dial:сьынум K ;
+Ela+PxPl1+So/PC+Err/Dial:нумысь K ;
+Ela+PxPl2+So/CP:сьыныд K ;
+Ela+PxPl2+So/PC:ныдысь K ;
+Ela+PxPl3+So/CP:сьыныс K ;
+Ela+PxPl3+So/PC:нысысь K ;

+Gen:лӧн K ;
+Gen+PxSg3:ыслӧн K ;
+Gen+PxPl1:нымлӧн K ;
+Gen+PxPl1+Err/Dial:нумлен K ;
+Gen+PxPl2:ныдлӧн K ;
+Gen+PxPl3:ныслӧн K ;

+Acc:ӧс K ;
+Acc+PxSg1:ӧс K ;
+Acc+PxSg2:тӧ K ;
+Acc+PxSg3:сӧ K ;
+Acc+PxPl1:нымӧс K ;
+Acc+PxPl1+Err/Dial:нумес K ;
+Acc+PxPl2:нытӧ K ;
+Acc+PxPl3:нысӧ K ;

+Dat+PxSg2:ыдлы K ; ! This far 2017-03-19
+Dat+PxSg3:ыслы K ;
+Dat+PxPl1:нымлы K ;
+Dat+PxPl1+Err/Dial:нумлы K ;
+Dat+PxPl2:ныдлы K ;
+Dat+PxPl3:ныслы K ;

+Abl:лысь K ;
+Abl+PxSg3:ыслысь K ;
+Abl+PxPl1:нымлысь K ;
+Abl+PxPl1+Err/Dial:нумлысь K ;
+Abl+PxPl2:ныдлысь K ;
+Abl+PxPl3:ныслысь K ;

+Com:кӧд K ;
+Com+PxSg3:ыскӧд K ;
+Com+Com+PxPl1:нымкӧд K ;
+Com+Com+PxPl1+Err/Dial:нумкед K ;
+Com+PxPl2:ныдкӧд K ;
+Com+PxPl3:ныскӧд K ;

+Car:тӧг K ;
+Car+PxSg3:ыстӧг K ;
+Car+PxPl1:нымтӧг K ;
+Car+PxPl1+Err/Dial:нумтег K ;
+Car+PxPl2:ныдтӧг K ;
+Car+PxPl3:ныстӧг K ;

+Cns:ла K ;
+Cns+PxSg3:ысла K ;
+Cns+PxPl1:нымла K ;
+Cns+PxPl1+Err/Dial:нумла K ;
+Cns+PxPl2:ныдла K ;
+Cns+PxPl3:нысла K ;

+Cmpr+PxSg3:сяыс K ;

+Ine:ын K ;
+Ine+PxPl1:нымын K ;
+Ine+PxPl1+Err/Dial:нумын K ;
+Ine+PxPl2:ныдын K ;
+Ine+PxPl3:нысын K ;

+Ill:ӧ K ;
+Ill+PxPl1:нымӧ K ;
+Ill+PxPl1+Err/Dial:нуме K ;
+Ill+PxPl2:ныдӧ K ;
+Ill+PxPl3:нысӧ K ;

+Egr:сянь K ;
+Egr+PxSg3+So/CP:сяньыс K ;
+Egr+PxSg3+So/PC:ыссянь K ;
+Egr+PxPl1+So/CP:сяньным K ;
+Egr+PxPl1+So/PC:нымсянь K ;
+Egr+PxPl1+So/CP+Err/Dial:сяньнум K ;
+Egr+PxPl1+So/PC+Err/Dial:нумсянь K ;
+Egr+PxPl2+SoCP:сяньныд K ;
+Egr+PxPl2+SoPC:ныдсянь K ;
+Egr+PxPl3:ныссянь K ;

+Apr:лань K ;
+Apr+PxSg3:ланьыс K ;
+Apr+PxSg3:ыслань K ;
+Apr+PxPl1:нымлань K ;
+Apr+PxPl1+Err/Dial:нумлань K ;
+Apr+PxPl2:ныдлань K ;
+Apr+PxPl3:ныслань K ;

+Tra:ті K ;
+Tra+PxSg3:тіыс K ;
+Tra+PxSg3:ысті K ;
+Tra+PxPl1:нымті K ;
+Tra+PxPl1+Err/Dial:нумті K ;
+Tra+PxPl2:ныдті K ;
+Tra+PxPl3:нысті K ;

+Ter:ӧдз K ;
+Ter+PxSg3:ысӧдз K ;
+Ter+PxPl1:нымӧдз K ;
+Ter+PxPl1+Err/Dial:нумедз K ;
+Ter+PxPl2:ныдӧдз K ;
+Ter+PxPl3:нысӧдз K ;



! COMPARATIVE ; ! sg/pl?

LEXICON PRONQ-COLL
+Coll+Sg: PRONQ-COLL-CASE ; ! sg/pl?
+Coll+Cop+Pl:ӧсь ENDLEX ; ! sg/pl?

! быдӧн, ӧтнасӧн, ӧтнамӧн, ӧтнадӧн, ставӧн, кымынӧн, унаӧн, кыкӧн

LEXICON PRONQ-COLL-CASE
!ӧнія коми кыв 2000: 183
+Nom: K ;
+Gen:лӧн K ;
+Gen+PxSg2:ыдлӧн K ;
+Gen+PxSg3:ыслӧн K ;
+Abl:лысь K ;
+Abl+PxSg2:ыдлысь K ;
+Abl+PxSg3:ыслысь K ;
+Dat:лы K ;
+Dat+PxSg2:ыдлысь K ;
+Dat+PxSg3:ыслысь K ;
+Acc:ӧс K ;
+Acc+PxSg2:тӧ K ;
+Acc+PxSg3:сӧ K ;
+Ins:ӧн K ;
+Com:кӧд K ;
+Com+PxSg2:ыдкӧд K ;
+Com+PxSg3:ыскӧд K ;
+Car:тӧг K ;
+Car+PxSg2:тӧгыд K ;
+Car+PxSg3:тӧгыс K ;
+Cns:ла K ;
+Cns+PxSg2:ыдла K ;
+Cns+PxSg3:ысла K ;
+Ine:ын K ;
+Ela:ысь K ;
+Ela+PxSg2:сьыд K ;
+Ela+PxSg3:сьыс K ;
+Ill:ӧ K ;
+Apr:лань K ;
+Apr+PxSg2:ланьыд K ;
+Apr+PxSg3:ланьыс K ;
+Egr:сянь K ;
+Egr+PxSg2:сяньыд K ;
+Egr+PxSg3:сяньыс K ;
+Tra:ті K ;
+Ter:ӧдз K ;
+Ter+PxSg2:ӧдзыд K ;
+Ter+PxSg3:ӧдзыс K ;

LEXICON PRONQ-ATAG
+Pron+Qnt+A: K ;
+Pron+Qnt+A+Der/MWN: Noun1 ;

LEXICON PRON-QNT_
+Qnt: COMPARATIVE ; ! sg/pl?

!2018-10-21!LEXICON PRON-QNT_-PL
!2018-10-21!+Pron+Qnt+Pl: WordEnd ;

LEXICON PRON-QNT-IS_ILL
+Pron+Qnt+Ill: K ;


LEXICON PRON-NEG-End
+Pron+Neg: K ;

LEXICON PRON-NEG-A
+Pron+Der+Neg+A: WordEnd ;
+Pron+Der+Neg+A+Sg+Nom: WordEnd ;
+Pron+Der+Neg+A+Sg: PRON-NEG-OBL ;
!+Pron+Neg+A+Der/MWN+N+Sg+Nom: WordEnd ;
!+Pron+Neg+A+Der/MWN+N+Sg: PRON-NEG-OBL ;

LEXICON PRON-NEG-ADV
+Pron+Der+Neg+Adv: COMPARATIVE ;

LEXICON PRON-NEG-IN_LA
+Pron+Neg+Der/ла+Spat: PRON-NEG-SPAT ;

LEXICON PRON-NEG-ITER
+Pron+Der+Neg+Der/ысь+Adv: K ; !+Iter

LEXICON PRON-NEG
+Pron+Neg+Sg: PRON-NEG_0 ;
+Pron+Neg+Sg: PRON-NEG_J ;
+Pron+Neg+Pl:ъяс PRON-NEG_0 ;
+Pron+Neg+Pl:ъяс PRON-NEG_J ;

LEXICON PRON-NEG_0/J
+Pron+Neg+Sg: PRON-NEG_0 ;
+Pron+Neg+Sg:й PRON-NEG_J ;
+Pron+Neg+Pl:ъяс PRON-NEG_0 ;
+Pron+Neg+Pl:ъяс PRON-NEG_J ;

LEXICON PRON-NEG_0
+Nom: WordEnd ;
+Gen:лӧн WordEnd ;
+Abl:лысь K ; !
+Dat:лы WordEnd ;
+Acc+PxSg2:тӧ WordEnd ;
+Acc+PxSg3:сӧ WordEnd ;
+Com:кӧд K ;
+Car:тӧг K ;
+Cns:ла K ;
+Apr:лань K ;
+Egr:сянь K ;
+Tra:ті K ;

LEXICON PRON-NEG_J
+Acc+PxSg1:ӧс WordEnd ;
+Ins:ӧн K ;
+Ine:ын K ;
+Ela:ысь K ; 
+Ill:ӧ K ; !
+Ter:ӧдз K ;




LEXICON PRON-NEG-COLL
+Coll:нан PRON-NEG-COLL-PERS ;
+Coll: PRON-NEG-COLL-PERS ; !+ZeroColl
!
+Coll+Acc+PxPl2:ныдтӧ K ; !+ZeroColl
+Coll+Acc+PxPl2:наннытӧ K ;

LEXICON PRON-NEG-COLL-PERS
+PxPl1:ным PRON-NEG-COLL-CASES ;
+PxPl1+Err/Dial:нум PRON-NEG-COLL-CASES ;
+PxPl2:ныд PRON-NEG-COLL-CASES ;
+PxPl3:ныс PRON-NEG-COLL-CASES ;
!
+Acc+PxPl1:нымӧс K ;
+Acc+PxPl1+Err/Dial:нумес K ;
+Acc+PxPl2:нытӧ K ;
+Acc+PxPl3:ныссӧ K ;

LEXICON PRON-NEG-COLL-CASES
+Nom: K ;
 PRON-NEG-OBL ;

LEXICON PRON-NEG-OBL
! 2011-11-15 This is used by PRON-NEG
! also тэа-мэа inclusive 1Du, сыа-мэа exclusive 1Du
+Cmpl: WordEnd ; !CHECKME 2019-10-27 is this distinct from Sg Nom
+Gen:лӧн WordEnd ;
+Abl:лысь K ; !
+Dat:лы K ;
+Acc:ӧс WordEnd ;
+Acc+PxSg3:сӧ WordEnd ;
+Ins:ӧн K ;
+Com:кӧд K ;
+Car:тӧг K ;
+Cns:ла K ;
 PRON-NEG-SPAT ;

LEXICON PRON-NEG-SPAT
+Ine:ын K ;
+Ela:ысь K ; 
+Ill:ӧ K ; !
+Apr:лань K ;
+Egr:сянь K ;
+Tra:ті K ;
+Ter:ӧдз K ;
 
LEXICON PRON-NEG-IS_ILL
+Pron+Neg+Ill: K ;

!! Tagged in the src/morphology/stems/pronouns.xml file
LEXICON PRON_
: K ;
LEXICON PronInterr
+Pron+Interr: WordEnd ;
!2018-10-21!LEXICON PronIndef
!2018-10-21!+Pron+Indef: WordEnd ;

!! Word-final cases

LEXICON ABL-WORD-END
+Abl:лысь WordEnd ;
LEXICON ACC-WORD-END
+Acc:ӧс WordEnd ;
LEXICON CAR-WORD-END
+Car:тӧг WordEnd ;
LEXICON CNS-WORD-END
+Cns:ла WordEnd ;
LEXICON COM-WORD-END
+Com:кӧд WordEnd ;
LEXICON EGR-WORD-END
+Egr:сянь WordEnd ;
LEXICON ELA-WORD-END
+Ela:ысь WordEnd ;
LEXICON GEN-WORD-END
+Gen:лӧн WordEnd ;
LEXICON ILL-WORD-END
+Ill:ӧ WordEnd ;
LEXICON INE-WORD-END
+Ine:ын WordEnd ;
LEXICON INS-WORD-END
+Ins:ӧн WordEnd ;
LEXICON NOM-WORD-END
+Nom: K ;
LEXICON PRL-WORD-END
+Prl:ӧд WordEnd ;
LEXICON TER-WORD-END
+Ter:ӧдз WordEnd ;

! vim: set ft=xfst-lexc:
