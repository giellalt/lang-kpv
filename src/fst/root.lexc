! Divvun & Giellatekno - open source grammars for Komi-Zyrian language
! Copyright © 2015 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no

!! !!!Tags and root lexicon for Komi

!! Morphology


Multichar_Symbols

! Check these:

+Acron
+Aux
+Cmp/SplitR
+Der/La
+Du
+Err/lex
+Hom1
+Hom2
+Hom3
+Hom4
+Intens

+Sem/Ant_Fem
+Sem/Ant_Mal
+Sem/Patr-Маl
+Sem/Dummy ! change to +Sem/Dummytag

!! !!Analysis symbols
+WORK
!  ----------------
!! The morphological analyses of wordforms for the Komi-Zyrian
!! language are presented in this system in terms of the following symbols.
!! (It is highly suggeste	d to follow existing standards when adding new tags).

!! The parts-of-speech tags are:
 +A      !!≈ ;@CODE@: adjective  кывберд	прилагательное
 +Adp 	 !!≈ ;@CODE@: adposition (prepositio, postposition) 	 
 +Adv    !!≈ ;@CODE@: adverb	 урчитан	      	наречие
 +CS     !!≈ ;@CODE@: subordinating conjunction		XX	подчинительный союз
 +CC     !!≈ ;@CODE@: coordinating conjunction		XX	сочинительный союз
 +CC   !!≈ ;@CODE@: conjunction word			XX	союзное слово (здесь надо узнать который из 2 выш.)
 +Det    !!≈ ;@CODE@: determiner  XX   XX
 +Interj !!≈ ;@CODE@: interjection	междометтьӧ	междометие
 +N      !!≈ ;@CODE@: noun		эмакыв	 - существительное
 +Num	 !!≈ ;@CODE@: numeral		лыдакыв	числительное
 +Pcle   !!≈ ;@CODE@: particle		кывтор	частица
 +Po     !!≈ ;@CODE@: postposition	кывбӧр	послелог
 +Pr     !!≈ ;@CODE@: preposition	XX	предлог
 +Pron   !!≈ ;@CODE@: pronoun	нимвежтас	местоимение
 +Qnt    !!≈ ;@CODE@: Quantifier	ХХ	XX
 +V      !!≈ ;@CODE@: verb		кадакыв	глагол
 
!! The parts of speech are further split up into:
!! Adjectives

!! Adverbs
 +Ideoph  !!≈ * __@CODE@__ These are ideophonic descriptors used to modify the verb
              !!               вырк ливтясь "__flit__ and it flew off"
 +Deg         !!≈ * __@CODE@__ Degree depricate AdA
 +Manner      !!≈ * __@CODE@__ with reference to type of adverb
 +Mult       !! multiplicative, i.e. iterations
 +Spat        !!≈ * __@CODE@__ spatial
 +Temp        !!≈ * __@CODE@__ temporal
 +Parenthetic !!≈ * __@CODE@__ parenthetical phrase
 +Presentational
 
!! Interjections
 +Formulaic !!≈ @CODE@ = ???
 +Conative  !!≈ @CODE@ Used for calling animals, for example

!! Nouns
 +Prop  !!≈ * __@CODE@__ proper
 +CollN !!≈ * __@CODE@__ used with paired nouns __collective nouns__
 +Relat !!≈ * __@CODE@__ relational noun: выв, ув

!! Postpositions
! _+ Spat_, _+ Temp_

!! !!Pronouns
 +Dem    !!≈ ;@CODE@: demonstrative
 +Indef  !!≈ ;@CODE@: indefinite
 +Interr !!≈ ;@CODE@: interrogative
 +Pers   !!≈ ;@CODE@: personal
 +Recipr !!≈ ;@CODE@: reciprocal
 +Refl   !!≈ ;@CODE@: reflexive
 +Rel    !!≈ ;@CODE@: relative
 +Poss	 !!≈ ;@CODE@: possessive

!! !!Quantifiers (numerals)
!! Quantifiers and Numerals are classified under:
 +Num       !!≈ ;@CODE@: numeral лыдакыв
 +Appr      !!≈ ;@CODE@: Approximative numeral кавто-колмо, колмошка ''two or three''
            !!           NB! do not confuse with Komi case +Apr
 +AssocColl !!≈ ;@CODE@: -ne- ; avide-
 +Assoc     !!≈ ;@CODE@: +мезть
 +Card      !!≈ ;@CODE@: cardinal + NCard
 +ZeroColl      !!≈ ;@CODE@: Zero collective кодныс
 +Coll      !!≈ ;@CODE@: collective
 +Distr     !!≈ ;@CODE@: Distributive
 +Iter      !!≈ ;@CODE@: Iterative form expressing number of consecutive times; kpv: {{кыкысь}}
 +Mult      !!≈ ;@CODE@: Multiplicative adverbs number of times; kpv: {{кык пӧв}}
 +Ord       !!≈ ;@CODE@: ordinal + NOrd
 +Coord	    !!≈ ;@CODE@: Coordinates, i.e. 65˚36′8,30″ in numerals.lexc
 +Cop	    !!≈ ;@CODE@: this is for copula complement predicate position with pl in -ӧсь depricated Pred

!! !!Nominals are inflected for Number and Case

!! !!Number
 +Sg !!≈ * __@CODE@__  singular
 +Pl !!≈ * __@CODE@__  plural


!! !!Case

!! A category of case in Komi can be identified as:
! (1) regular noun morphology
! (2) affixed to the head of a complex noun phrase,
! i.e. the noun should be able to take at least ONE modifier, (cf. Rueter 2010: 74--75)
! (3) regular noun morphology includes the category of number
! (4) Cases are used for (clause) argument and adjunct marking

 +Acc    !!≈ *  __@CODE@__ accusative ZERO керан
 +Acc1   !!≈ *  __@CODE@__ accusative -ӧс керан
 +Acc3   !!≈ *  __@CODE@__ accusative -сӧ керан
 +Abl    !!≈ *  __@CODE@__ ablative case -лысь босьтан
 +Apr    !!≈ *  __@CODE@__ approximative -лань матыстчан
 +AprEgr !!≈ ** __@CODE@__ approximative egressive -ланьсянь матысь ылыстчан
 +AprEla !!≈ ** __@CODE@__ approximative elative -ланьысь матысь петан
 +AprIll !!≈ ** __@CODE@__ approximative illative -ланьӧ матӧ матыстчан
 +AprIne !!≈ ** __@CODE@__ approximative inessive -ланьын матыс ина
 +AprPrl !!≈ ** __@CODE@__ approximative prolative -ланьӧд маті вуджан
 +AprTer !!≈ ** __@CODE@__ approximative terminative -ланьӧдз матіӧдз воан
 +AprTra !!≈ ** __@CODE@__ approximative translative -ланьті маті вуджан
 +Car    !!≈ *  __@CODE@__ caritive -тӧг торйӧдан
 +Cns    !!≈ *  __@CODE@__ consecultative -ла могман
 +Com    !!≈ *  __@CODE@__ Comitative -кӧд ӧтвывтан
 +Cmpr   !!≈ *  __@CODE@__ Comparative case form -ся ӧткодялан
 +Cmpl   !!≈ *  __@CODE@__  Postposition complement
 +Dat    !!≈ *  __@CODE@__ dative case -лы сетан
 +Egr    !!≈ *  __@CODE@__ egressive -сянь ылыстчан
 +Ela    !!≈ *  __@CODE@__ elative -ысь петан
 +Gen    !!≈ *  __@CODE@__ genitive case -лӧн асалан
 +Ill    !!≈ *  __@CODE@__ illative -ӧ пыран
 +Ine    !!≈ *  __@CODE@__ inessive -ын ина
 +Ins    !!≈ *  __@CODE@__ instrumental -ӧн керанторъя
 +Nom    !!≈ *  __@CODE@__ nominative case нимтан
 +Prl    !!≈ *  __@CODE@__ prolative -ӧд вуджан
 +Tra    !!≈ *  __@CODE@__ translative -ті вуджан
 +Ter    !!≈ *  __@CODE@__ Terminative -ӧдз матыстчан
 +Voc    !!≈ *  __@CODE@__ Vocative ??
 +Abs    !!≈ *  __@CODE@__ Absolute = +Sg+Nom

! Compounding regular derivation case
! +Der/Apr+Ine, +Der/Apr+Ela
! +Der/Apr+Ill, +Der/Apr+Egr
! +Der/Apr+Prl, +Der/Apr+Tra
! +Der/Apr+Ter




!! The possession is marked as such:
 +PxSg1 +PxSg2 +PxSg3 +PxPl1 +PxPl2 +PxPl3 !!≈ * __@CODE@__ 
!! ODD
 +Px1 +Px2 +Px3 !!≈ * __@CODE@__

!! The possession and case markers may have switched ordering
+So/CP  !!≈ * __@CODE@__ segment ordering: case, person
+So/PC  !!≈ * __@CODE@__ segment ordering: person, case

!! The comparative forms are:
 +Comp !!≈ * __@CODE@__ джык
 +Superl !!≈ * __@CODE@__ 
!! Numerals are classified under:
 +Attr +Card !!≈ * __@CODE@__ 
 +Ord  !!≈ * __@CODE@__ 
 +Coll  !!≈ * __@CODE@__ Collective
 +Distr !!≈ * __@CODE@__  Distributive
 +Iter !!≈ * __@CODE@__  Iterative form expressing number of times
 +Tot
 +Arab +Rom 

!! Verb moods are:
 +Ind +Prs +Prt1 +Prt2 +Fut +Imprt
!!!!! +Pot +Cond
!! Verb personal forms are:
 +Sg1 +Sg2 +Sg3 +Pl1 +Pl2 +Pl3
+1 +2 +3 !!Final мед ог _so that I/we won't_ 2019-04-06

!! Other verb forms are
 +Inf
 +Ger !! Gerund This is used with derivations

+ConNeg +Neg
! +ConNegII +ImprtII +PrsPrc +PrfPrc +Sup +VGen 
+VAbess !!≈ * __@CODE@__ тӧм Participle
+VCar !!≈ * __@CODE@__ тӧг Gerund
+VTer !!≈ * __@CODE@__ тӧдз Gerund

+Final ! мог, мон, моз 'so that I won't'

! Abbreviated words are classified with:
 +ABBR +ACR
+Symbol !!≈ * @CODE@ = independent symbols in the text stream, like £, €, ©
!! Special symbols are classified with:
+CLB +PUNCT +LEFT +RIGHT
!! The verbs are syntactically split according to transitivity:
 +TV !!≈ * __@CODE@__ 
 +IV !!≈ * __@CODE@__
 
!! Special multiword units are analysed with:
 +Multi !!≈ * __@CODE@__ 
!! Non-dictionary words can be recognised with:
 +Guess !!≈ * __@CODE@__ 

!! Question and Focus particles:
 +Qst  !!≈ * __@CODE@__
 +Foc  !!≈ * __@CODE@__ 
+Clt/И  !!≈ * __@CODE@__  This comes at the end of a word -и or after vowels (some authors use -й)
+Clt  !!≈ * __@CODE@__ 
+Qst/A  !!≈ * __@CODE@__ 
+Qst/Oe  !!≈ * __@CODE@__ 
+Clt/тӧ  !!≈ * __@CODE@__ 
+Clt/сӧ  !!≈ * __@CODE@__
+Clt/Kodj  !!≈ * __@CODE@__ adjectival phrase
+Clt/Moz  !!≈ * __@CODE@__ adverbial clause

!!! !Tags usually not visible outside the code:


!! !Tags distinguishing different versions of the same lemma (before POS)
+v1   !!≈ * @CODE@
+v2   !!≈ * @CODE@
+v3   !!≈ * @CODE@
+v4   !!≈ * @CODE@
+v5   !!≈ * @CODE@
+v6   !!≈ * @CODE@
+v7   !!≈ * @CODE@
+v8   !!≈ * @CODE@
+v9   !!≈ * @CODE@
+v10  !!≈ * @CODE@
+v11  !!≈ * @CODE@
+v12  !!≈ * @CODE@
+v13  !!≈ * @CODE@
+v14  !!≈ * @CODE@
+v15  !!≈ * @CODE@
+v16  !!≈ * @CODE@
+v17  !!≈ * @CODE@
+v18  !!≈ * @CODE@
+v19  !!≈ * @CODE@
+v20  !!≈ * @CODE@
+v21  !!≈ * @CODE@
+v22  !!≈ * @CODE@
+v23  !!≈ * @CODE@
+v24  !!≈ * @CODE@

!! The Usage extents are marked using following tags:
 +Err/Orth !!≈ * __@CODE@__ 
 +Err/Orth-colloq !!≈ * __@CODE@__ colloquial form for patronymic
 +Err/Orth-no-paragogic-j !!≈ * __@CODE@__ 
 +Err/Orth-no-paragogic-k !!≈ * __@CODE@__ 
 +Err/Orth-no-paragogic-m !!≈ * __@CODE@__ 
 +Err/Orth-no-paragogic-t !!≈ * __@CODE@__ 
 +Err/Dial !!≈ * __@CODE@__ e.g. тэг instead of тӧг
 +Err/Lex  !!≈ * __@CODE@__ substandard, not in normative fst, no normative lemma помсьыны
 +Use/-Spell !!≈ * __@CODE@__ 
 +Use/SpellNoSugg !!≈ * __@CODE@__ 

 +Use/PMatch       !!≈ * __@CODE@__ means that the following is only used in the analyser feeding the disambiguator
 +Use/-PMatch      !!≈ * __@CODE@__ Do not include in fst's made for hfst-pmatch
 +Use/NG	   !!≈ * __@CODE@__ This is used for minimizing excess generation in apertium kpv-koi

!! Dialect features
+Dial/л
+Dial/в


!! Where do these come from source
+Src/F         !!≈ * __@CODE@__ foreign source apparently 2015-09-08
+Dim         !!≈ * __@CODE@__ diminutive for verbs -ышт- (there might be a better term)
+Dimin         !!≈ * __@CODE@__ diminutive for nouns -тор-

 +NonHum  !!≈ * __@CODE@__ look at this and place somewhere

! Semantic tags to help disambiguation & synt. analysis: (before POS)
! Borrowed from main/langs/sme/src/morphology/root.lexc

 +Sem/Act         !!≈ * __@CODE@__ Activity
 +Sem/Amount      !!≈ * __@CODE@__ Amount
 +Sem/Ani         !!≈ * __@CODE@__ Animate
 +Sem/Aniprod     !!≈ * __@CODE@__ Animal Product
 +Sem/Body        !!≈ * __@CODE@__ Bodypart
 +Sem/Body-abstr  !!≈ * __@CODE@__ siellu, vuoig?a, jierbmi
 +Sem/Build       !!≈ * __@CODE@__ Building
 +Sem/Build-part  !!≈ * __@CODE@__ Part of Bulding, like the closet
 +Sem/Cat         !!≈ * __@CODE@__ Category
 +Sem/Clth        !!≈ * __@CODE@__ Clothes
 +Sem/Clth-jewl   !!≈ * __@CODE@__ Jewelery
 +Sem/Clth-part   !!≈ * __@CODE@__ part of clothes, boallu, sávdnji...
 +Sem/Ctain       !!≈ * __@CODE@__ Container
 +Sem/Ctain-abstr !!≈ * __@CODE@__ Abstract container like bank account
 +Sem/Ctain-clth  !!≈ * __@CODE@__
 +Sem/Curr        !!≈ * __@CODE@__ Currency like dollár, Not Money
 +Sem/Dance       !!≈ * __@CODE@__ Dance
 +Sem/Dir         !!≈ * __@CODE@__ Direction like GPS-kursa
 +Sem/Domain      !!≈ * __@CODE@__ Domain like politics, reindeerherding (a system of actions)
 +Sem/Drink       !!≈ * __@CODE@__ Drink
 +Sem/Dummytag    !!≈ * __@CODE@__ Dummytag
 +Sem/Edu         !!≈ * __@CODE@__ Educational event
 +Sem/Event       !!≈ * __@CODE@__ Event
 +Sem/Feat        !!≈ * __@CODE@__ Feature, like Árvu
 +Sem/Feat-phys   !!≈ * __@CODE@__ Physiological feature, ivdni, fárda
 +Sem/Feat-psych  !!≈ * __@CODE@__ Psychological feauture
 +Sem/Feat-measr  !!≈ * __@CODE@__ Psychological feauture
 +Sem/Fem         !!≈ * __@CODE@__ Female name
 +Sem/Food        !!≈ * __@CODE@__ Food
 +Sem/Food-med    !!≈ * __@CODE@__ Medicine
 +Sem/Furn        !!≈ * __@CODE@__ Furniture
 +Sem/Game        !!≈ * __@CODE@__ Game
 +Sem/Geom        !!≈ * __@CODE@__ Geometrical object
 +Sem/Group       !!≈ * __@CODE@__ Animal or Human Group
 +Sem/Hum         !!≈ * __@CODE@__ Human
 +Sem/Hum-abstr   !!≈ * __@CODE@__ Human abstract
 +Sem/Ideol       !!≈ * __@CODE@__ Ideology
 +Sem/Lang        !!≈ * __@CODE@__ Language
 +Sem/Mal         !!≈ * __@CODE@__ Male name
 +Sem/Mat         !!≈ * __@CODE@__ Material for producing things
 +Sem/Measr       !!≈ * __@CODE@__ Measure
 +Sem/Money       !!≈ * __@CODE@__ Has to do with money, like wages, not Curr(ency)
 +Sem/Obj         !!≈ * __@CODE@__ Object
 +Sem/Obj-clo         !!≈ * __@CODE@__ Cloth
 +Sem/Obj-cogn        !!≈ * __@CODE@__ Cloth
 +Sem/Obj-el          !!≈ * __@CODE@__ (Electrical) machine or apparatus
 +Sem/Obj-ling        !!≈ * __@CODE@__ Object with something written on it
 +Sem/Obj-rope        !!≈ * __@CODE@__ flexible ropelike object
 +Sem/Obj-surfc       !!≈ * __@CODE@__ Surface object
 +Sem/Org         !!≈ * __@CODE@__ Organisation
 +Sem/Part        !!≈ * __@CODE@__ Feature, oassi, bealli
 +Sem/Perc-cogn       !!≈ * __@CODE@__ Cognative perception
 +Sem/Perc-emo        !!≈ * __@CODE@__ Emotional perception
 +Sem/Perc-phys       !!≈ * __@CODE@__ Physical perception
 +Sem/Perc-psych      !!≈ * __@CODE@__ Physical perception
 +Sem/Plant       !!≈ * __@CODE@__ Plant
 +Sem/Plant-part      !!≈ * __@CODE@__ Plant part
 +Sem/Plc         !!≈ * __@CODE@__ Place
 +Sem/Plc-abstr       !!≈ * __@CODE@__ Abstract place
 +Sem/Plc-elevate     !!≈ * __@CODE@__ Place
 +Sem/Plc-line        !!≈ * __@CODE@__ Place
 +Sem/Plc-water       !!≈ * __@CODE@__ Place
 +Sem/Pos         !!≈ * __@CODE@__ Position (as in social position job)
 +Sem/Process     !!≈ * __@CODE@__ Process
 +Sem/Prod        !!≈ * __@CODE@__ Product
 +Sem/Prod-audio      !!≈ * __@CODE@__ Audio product
 +Sem/Prod-cogn       !!≈ * __@CODE@__ Cognition product
 +Sem/Prod-ling       !!≈ * __@CODE@__ Linguistic product
 +Sem/Prod-vis        !!≈ * __@CODE@__ Visual product
 +Sem/Rel         !!≈ * __@CODE@__ Relation
 +Sem/Route       !!≈ * __@CODE@__ Name of a Route
 +Sem/Rule        !!≈ * __@CODE@__ Rule or convention
 +Sem/Semcon      !!≈ * __@CODE@__ Semantic concept
 +Sem/Sign        !!≈ * __@CODE@__ Sign (e.g. numbers, punctuation)
 +Sem/Sport       !!≈ * __@CODE@__ Sport
 +Sem/State       !!≈ * __@CODE@__
 +Sem/State-sick      !!≈ * __@CODE@__ Illness
 +Sem/Substnc     !!≈ * __@CODE@__ Substance, like Air and Water
 +Sem/Sur         !!≈ * __@CODE@__ Surname
 +Sem/Symbol      !!≈ * __@CODE@__ Symbol
 +Sem/Time        !!≈ * __@CODE@__ Time
 +Sem/Tool        !!≈ * __@CODE@__ Prototypical tool for repairing things
 +Sem/Tool-catch      !!≈ * __@CODE@__ Tool used for catching (e.g. fish)
 +Sem/Tool-clean      !!≈ * __@CODE@__ Tool used for cleaning
 +Sem/Tool-it         !!≈ * __@CODE@__ Tool used in IT
 +Sem/Tool-measr      !!≈ * __@CODE@__ Tool used for measuring
 +Sem/Tool-music      !!≈ * __@CODE@__ Music instrument
 +Sem/Tool-write      !!≈ * __@CODE@__ Writing tool
 +Sem/Txt         !!≈ * __@CODE@__ Text (girji, lávlla...)
 +Sem/Veh         !!≈ * __@CODE@__ Vehicle
 +Sem/Wpn         !!≈ * __@CODE@__ Weapon
 +Sem/Wthr        !!≈ * __@CODE@__ The Weather or the state of ground
 +Sem/Year        !!≈ * __@CODE@__

 +Sem/Sur-Fem         !!≈ * __@CODE@__ Surname female
 +Sem/Sur-Mal         !!≈ * __@CODE@__ Surname male
 +Sem/Ant             !!≈ * __@CODE@__  Anthroponym
 +Sem/Ant-Fem         !!≈ * __@CODE@__  Anthroponym female
 +Sem/Ant-Mal         !!≈ * __@CODE@__  Anthroponym male
 +Sem/Patr            !!≈ * __@CODE@__  Patronym
 +Sem/Patr-Fem        !!≈ * __@CODE@__  Patronym female
 +Sem/Patr-Mal        !!≈ * __@CODE@__  Patronym male


! Multiple Semantic tags:
+Sem/Act_Group
+Sem/Act_Plc
+Sem/Act_Route
+Sem/Amount_Build
+Sem/Amount_Semcon
+Sem/Ani_Body-abstr_Hum
+Sem/Ani_Build
+Sem/Ani_Build-part
+Sem/Ani_Build_Hum_Txt
+Sem/Ani_Group
+Sem/Ani_Group_Hum
+Sem/Ani_Hum
+Sem/Ani_Hum_Plc
+Sem/Ani_Hum_Time
+Sem/Ani_Plc
+Sem/Ani_Plc_Txt
+Sem/Ani_Time
+Sem/Ani_Veh
+Sem/Aniprod_Hum
+Sem/Aniprod_Obj-clo
+Sem/Aniprod_Perc-phys
+Sem/Aniprod_Plc
+Sem/Body-abstr_Prod-audio_Semcon
+Sem/Body_Body-abstr
+Sem/Body_Clth
+Sem/Body_Food
+Sem/Body_Group_Hum
+Sem/Body_Hum
+Sem/Body_Mat
+Sem/Body_Measr
+Sem/Body_Obj_Tool-catch
+Sem/Body_Plc
+Sem/Body_Time
+Sem/Build-part_Plc
+Sem/Build_Build-part
+Sem/Build_Clth-part
+Sem/Build_Edu_Org
+Sem/Build_Event_Org
+Sem/Build_Org
+Sem/Build_Route
+Sem/Clth-jewl_Curr
+Sem/Clth-jewl_Money
+Sem/Clth-jewl_Plant
+Sem/Clth_Hum
+Sem/Ctain-abstr_Org
+Sem/Ctain-clth_Plant
+Sem/Ctain-clth_Veh
+Sem/Ctain_Feat-phys
+Sem/Ctain_Furn
+Sem/Ctain_Tool
+Sem/Ctain_Tool-measr
+Sem/Curr_Org
+Sem/Dance_Org
+Sem/Dance_Prod-audio
+Sem/Domain_Food-med
+Sem/Domain_Prod-audio
+Sem/Edu_Event
+Sem/Edu_Group_Hum
+Sem/Edu_Mat
+Sem/Edu_Org
+Sem/Event_Food
+Sem/Event_Hum
+Sem/Event_Plc	        !!≈ * __@CODE@__  сёянін
+Sem/Event_Time
+Sem/Feat-phys_Tool-write
+Sem/Feat-phys_Veh
+Sem/Feat-phys_Wthr
+Sem/Feat-psych_Hum
+Sem/Feat_Plant
+Sem/Food_Perc-phys
+Sem/Food_Plant
+Sem/Game_Obj-play
+Sem/Geom_Obj
+Sem/Group_Hum
+Sem/Group_Hum_Org
+Sem/Group_Hum_Plc
+Sem/Group_Hum_Prod-vis
+Sem/Group_Org
+Sem/Group_Sign
+Sem/Group_Txt
+Sem/Hum_Lang
+Sem/Hum_Lang_Plc
+Sem/Hum_Lang_Time
+Sem/Hum_Obj
+Sem/Hum_Org
+Sem/Hum_Plant
+Sem/Hum_Plc
+Sem/Hum_Prof		!!≈ * __@CODE@__  profession, capacity doctor, tractor driver 
+Sem/Hum_Tool
+Sem/Hum_Veh
+Sem/Hum_Wthr
+Sem/Lang_Tool
+Sem/Mat_Plant
+Sem/Mat_Txt
+Sem/Measr_Time
+Sem/Money_Obj
+Sem/Money_Txt
+Sem/Obj-play
+Sem/Obj-play_Sport
+Sem/Obj_Semcon
+Sem/Clth-jewl_Org
+Sem/Org_Rule
+Sem/Org_Txt
+Sem/Org_Veh
+Sem/Part_Prod-cogn
+Sem/Perc-emo_Wthr
+Sem/Plant_Plant-part
+Sem/Plant_Tool
+Sem/Plant_Tool-measr
+Sem/Plc-abstr_Rel_State
+Sem/Plc-abstr_Route
+Sem/Plc_Pos
+Sem/Plc_Route
+Sem/Plc_Substnc
+Sem/Plc_Substnc_Wthr
+Sem/Plc_Time
+Sem/Plc_Tool-catch
+Sem/Plc_Wthr
+Sem/Prod-audio_Txt
+Sem/Prod-cogn_Txt
+Sem/Semcon_Txt
+Sem/Obj_State
+Sem/Substnc_Wthr
+Sem/Time_Wthr


!! Semantics are classified with


!! Derivations are classified under the morphophonetic form of the suffix, the
!! source and target part-of-speech.
! +V→N  !!≈ * __@CODE@__
! +V→V  !!≈ * __@CODE@__
! +V→A  !!≈ * __@CODE@__
 
 +Der/xxx

! tags
+Der	!!≈ * __@CODE@__  In front of every derivation to make it
	!!  possible to target derivations as a class e.g. in regular expressions etc 
+Der/Ан  !!≈ * __@CODE@__  Process Participle +AN
+Der/Ана  !!≈ * __@CODE@__ Process Participle +ANA
+Der/Анаа  !!≈ * __@CODE@__  adverb derived from participle (+ANA) +ANAA
+Der/чӧж  !!≈ * __@CODE@__  +CHOZH
+Der/тӧг

+Der/Patr	!!≈ * __@CODE@__ patronymics in Russian
+Instr  !!≈ * __@CODE@__ 
+NomAct  !!≈ * __@CODE@__
+Der/NomAct  !!≈ * __@CODE@__  +Event
+Der/NomAg  !!≈ * __@CODE@__ 
+Duration  !!≈ * __@CODE@__

!+IG This will be replaced by the segments:
!+Der/иг+Duration+Ger!!
+Der/иг  !!≈ * __@CODE@__
+Der/Ig  !!≈ * __@CODE@__
+Der/IgKezhlo  !!≈ * __@CODE@__
+Der/IgKosta  !!≈ * __@CODE@__
+Der/IgKosti  !!≈ * __@CODE@__
+Der/IgMoz  !!≈ * __@CODE@__ %{иі%}гмоз
+Der/IgonMoz  !!≈ * __@CODE@__ %{иі%}гӧнмоз
+Der/IgSor  !!≈ * __@CODE@__ %{иі%}гсор
+Der/IgTyr  !!≈ * __@CODE@__ %{иі%}гтыр
+Der/IgTyrji  !!≈ * __@CODE@__ %{иі%}гтырйи
+Der/IgTyrja  !!≈ * __@CODE@__ %{иі%}гтыръя
+Der/IgChozh  !!≈ * __@CODE@__%{иі%}гчӧж


!Here we need the two sequential characters
!to replace +OM
!+Der/ӧм+Event
!This, of course, implicates a discussion
!of the so-called gerund "ӧмӧн" +Event+N+Ins and

! These should be added below in derivation declarations
!
!
!+Der/Ан
! +Der/Ана +Der/Анаа +Der/чӧж
!Then they should have indication of whether they are gerunds or participles!
!+Der/тӧдз    !!≈ * __@CODE@__  +VTer
!+TODZ Verbal terminative
+Der/ысь  !!≈ * __@CODE@__
+ActPrsPtc  !!≈ * __@CODE@__ 
+Der/Ан  !!≈ * __@CODE@__ Participle
+Der/Ана  !!≈ * __@CODE@__ Gerund or participle according to context (with...)
+PrsPrc  !!≈ * __@CODE@__
+PrsPtc  !!≈ * __@CODE@__ 
+Der/ӧм  !!≈ * __@CODE@__
+PastPtc  !!≈ * __@CODE@__ 

+Der/кості  !!≈ * __@CODE@__  +KOSTI
+Der/коста  !!≈ * __@CODE@__  +KOSTA
+Der/кежлӧ  !!≈ * __@CODE@__  +KEZHLO

+Der/мысь  !!≈ * __@CODE@__  +MYS
+Der/мысьт  !!≈ * __@CODE@__  +MYST

+Der/сор !+SOR
+Der/тыр !+TYR
+Der/тырйи !+TYRJI
+Der/тыръя !+TYRJA
+Der/мӧн !+MON
+Der/мӧнъя !Ӧнія коми кыв. 2000: 399-403
+Der/ӧмӧн !+OMON !Ӧнія коми кыв. 2000: 425


! Declaring adjectival derivations
! Noun phrase modifiers are generally considered derivational 
 +MAbe    !!≈ *  __@CODE@__ abessive modifier -тӧм 
 +MLoc    !!≈ *  __@CODE@__ locative modifier са -
 +MHab    !!≈ *  __@CODE@__ habeo modifier а -
 +MTmp    !!≈ *  __@CODE@__ temporal modifier ся -

+Der/LocMod ! +Der/ALoc  Denominal locative adjective Der/са
+Der/ProprietiveMod ! +Der/APrior  Denominal prioritive adjective Der/а
+Der/PrivMod ! тӧм
+Der/TempMod ! Der/ся

+Der/а +Der/са +Der/ся +Der/кодь +Der/Иник
+Der/Ин
+Der/увса
+Der/сайса
+Der/пӧвстса
+Der/костса
+Der/бердса
+Der/бӧрса
+Der/весьтса
+Der/водзса
+Der/вывса
+Der/гӧгӧрса
+Der/дорса


+LocMod !!≈ * __@CODE@__ IneMod Быд во шедӧдӧны бур успеваемость Воркута да Инта каръясса, Прилузскӧй да Княжпогостскӧй районъясса школаяс.
+CompMod !!≈ * __@CODE@__ 
+Der/тӧм  !!≈ * __@CODE@__  used with nouns and followed by +AbeMod
+PrivMod !!≈ * __@CODE@__ AbeMod джуджыд анализъястӧм да обобщениеястӧм статьяяс.
+ProprietiveMod !!≈ * __@CODE@__ HabObjMod Весиг киясыс тӧдсаӧсь, найӧ мугов рӧмаӧсь, кузь чорыд чуньясаӧсь.
 +Der/TempMod !!≈ * __@CODE@__ TempMod Der/ся но и Ф. В. Плесовскийлысь квайтымынӧд вояссяяссӧ * позьӧ аддзыны сӧмын библиотекаясысь.
! Declaring spatial adverb derivations; see also spatial postpositions
+Der/ла
+Der/ладор
+Der/дор
+Der/выв
+Der/тор

! Modifier without noun
!! 2012-09-11 Perhaps this is only syntactic
+MWN ! check! used once, should it be +Der/MWN?
+Der/MWN
+Der/мед ! Superlative

!Declaring Indefinite Pronoun derivations
+Der/сюрӧ +Der/кӧ

!Declaring Comparison particles
+Der/моз !+MOZ ! diminishing, kind of, sort of
+Der/кодь ! diminishing, kind of, sort of

!Declaring Noun derivations
+Der/лун ! adjective-to-noun
! the combinatory +Event preceding the NP-final noun
+Der/ӧм ! verb-to-noun

!Declaring Deverbal derivations of verbs
+Der/л
+Der/лы
!2017-09-19+Der/ывл
+Der/ывлы
+Der/ышт
!2017-09-19+Der/лывл
+Der/лывлы
+Der/сь ! This only occurs following a vowel in an yny-stem
!2017-09-19+Der/сь
+Der/сьы
!2017-09-19+Der/ч ! This appears to be a variant of +Der/сьы; it follows plosives
+Der/чы ! This appears to be a variant of +Der/сьы; it follows plosives
+Der/ал
+Der/овт
+Der/ась

+Der/N	      !!≈ * @CODE@ Noun derived with conversion from noun, conversion but not ZERO
+Der/A        !!≈ * @CODE@ Adjective derivated from Noun or Verb
+Der/Adv      !!≈ * @CODE@ Adverb derivated from Adjective


!! Tags for Ethymological Origin marking. This has initially used used with proper nouns
+EOLang/BXR
+EOLang/CHM
+EOLang/KOI
+EOLang/KOM
+EOLang/KPV
+EOLang/MHR
+EOLang/MRJ
+EOLang/MDF
+EOLang/MYV
+EOLang/RUS
+EOLang/YRK


!! Sentence markers

+Cop  !!≈ * __@CODE@__ Copula	кадакыв, коді шуӧ: вӧлі либӧ ӧнія кадся	Связка


!! Morphophonology
!  ---------------

!! To represent phonologic variations in word forms we use the following
!! symbols in the lexicon files:

{aä}  !!≈ ; @CODE@ : Vowel alternating symbol
{oö}  !!≈ ; @CODE@ : Vowel alternating symbol 
{uü}  !!≈ ; @CODE@ : Vowel alternating symbol

%^к2 %^л2 %^м2 %^т2 %^ь2 %^К2 %^Л2 %^М2 %^Т2 %^Ь2 %^И2  !!≈ * __@CODE@__
%^V1       !!= * @CODE@ for reduplicated vowel унаӧн > унаан
%>   !!≈ * __@CODE@__  suffix border

%{иі%}	!!≈ ; @CODE@ : for soft and hard
%{ая%}	!!≈ ; @CODE@ : for soft and hard

!! And following triggers to control variation
 {front} !!≈ ; @CODE@ : Vowel change triggers
 {back}  !!≈ ; @CODE@ : Vowel change triggers
 
%^Close	!!≈ * @CODE@ Close syllable, this triggers final consonant drop, seen in
        !!    word-final position and before consonant
%^C2V       !!= * @CODE@ Consonant v to vowel, Izhva ныы, ооны

!! Valency tags, i.e. tags assigned to verbs for denoting their arbuments

+%<acc%>                  !!≈ * @CODE@ accusative
+%<ela%>                  !!≈ * @CODE@ elative -ысь
+%<ins%>                  !!≈ * @CODE@ instrumental -ӧн
+%<inf_ны%>               !!≈ * @CODE@ infinitive in -ны
+%<po_вылӧ%>              !!≈ * @CODE@ postposition вылӧ
+%<sub_мый%>              !!≈ * @CODE@ subordinate clause in мый/that


!! !Symbols that need to be escaped on the lower side (towards twolc):

 »7     !! * »
 «7     !! * «
 %[%>%] !! * > (written with square brackets, see the root.lexc file)
 %[%<%] !! * < (written with square brackets, see the root.lexc file)


!! !!Flag diacritics


!! We have manually optimised the structure of our lexicon using following
!! flag diacritics to restrict morhpological combinatorics - only allow compounds
!! with verbs if the verb is further derived into a noun again:
 @P.NeedNoun.ON@    !!≈ | @CODE@ | (Dis)allow compounds with verbs unless nominalised
 @D.NeedNoun.ON@    !!≈ | @CODE@ | (Dis)allow compounds with verbs unless nominalised
 @C.NeedNoun@       !!≈ | @CODE@ | (Dis)allow compounds with verbs unless nominalised



!! Two flags copied from sme

 @P.Pmatch.Loc@       !!≈ | @CODE@ | Used on multi-token analyses; tell hfst-tokenise/pmatch where in the form/analysis the token should be split.
 @P.Pmatch.Backtrack@ !!≈ | @CODE@ | Used on single-token analyses; tell hfst-tokenise/pmatch to backtrack by reanalysing the substrings before and after this point in the form (to find combinations of shorter analyses that would otherwise be missed)



!! For languages that allow compounding, the following flag diacritics are needed
!! to control position-based compounding restrictions for nominals. Their use is

!! handled automatically if combined with +CmpN/xxx tags. If not used, they will
!! do no harm.
 @P.CmpFrst.FALSE@ !!≈ | @CODE@ | Require that words tagged as such only appear first
 @D.CmpPref.TRUE@  !!≈ | @CODE@ | Block such words from entering ENDLEX
 @P.CmpPref.FALSE@ !!≈ | @CODE@ | Block these words from making further compounds
 @D.CmpLast.TRUE@  !!≈ | @CODE@ | Block such words from entering R
 @D.CmpNone.TRUE@  !!≈ | @CODE@ | Combines with the next tag to prohibit compounding
 @U.CmpNone.FALSE@ !!≈ | @CODE@ | Combines with the prev tag to prohibit compounding
 @P.CmpOnly.TRUE@  !!≈ | @CODE@ | Sets a flag to indicate that the word has passed R
 @D.CmpOnly.FALSE@ !!≈ | @CODE@ | Disallow words coming directly from root.
!!
!! Use the following flag diacritics to control downcasing of derived proper
!! nouns (e.g. Finnish Pariisi -> pariisilainen). See e.g. North Sámi for how to use
!! these flags. There exists a ready-made regex that will do the actual down-casing
!! given the proper use of these flags.
 @U.Cap.Obl@        !!≈ | @CODE@ | Always capital letter for names: Deatnu.
 @U.Cap.Opt@        !!≈ | @CODE@ | Allowing downcasing of derived names: deatnulasj.

+Cmp    !!≈ * __@CODE@__

+Cmp/Serial   !!≈ | @CODE@ used with serial verbs

! Flags used with serial verbs
! VAL = Valence
@U.CONJ-VAL.TV@
@U.CONJ-VAL.IV@

! INF = Infinitive
@U.CONJ-INF.YES@
@U.CONJ-INF.NO@

! TX = tense
@U.CONJ-TX.FUT@
@U.CONJ-TX.PRES@
@U.CONJ-TX.PRET1@
@U.CONJ-TX.PRET2@

! GER = gerund
@U.CONJ-GER.IG@

! MX = mood
@U.CONJ-MX.IND@
@U.CONJ-MX.IMP@

! CONNEG = negation
@U.CONJ-CONNEG.YES@
@U.CONJ-CONNEG.NO@

! NX = number
@U.CONJ-NX.PL@
@U.CONJ-NX.SG@

! POSS = possessive person
@U.CONJ-POSS.1@
@U.CONJ-POSS.2@
@U.CONJ-POSS.3@
@U.CONJ-POSS.2ACC@
@U.CONJ-POSS.3ACC@

! PX = person
@U.CONJ-PX.1@
@U.CONJ-PX.2@
@U.CONJ-PX.3@

! Removal
@C.CONJ-VAL@
@C.CONJ-INF@
@C.CONJ-TX@
@C.CONJ-MX@
@C.CONJ-GER@
@C.CONJ-CONNEG@
@C.CONJ-NX@
@C.CONJ-PX@
@C.CONJ-POSS@

!! FLAGS USED WITH COLLECTIVE NOUNS
@P.PossPx.Sg1@
@P.PossPx.Sg2@
@P.PossPx.Sg3@
@P.PossPx.Pl1@
@P.PossPx.Pl2@
@P.PossPx.Pl3@


@U.PossPx.Sg1@
@U.PossPx.Sg2@
@U.PossPx.Sg3@
@U.PossPx.Pl1@
@U.PossPx.Pl2@
@U.PossPx.Pl3@

@D.PossPx@
@C.PossPx@


! number
@U.DECL-NX.SG@
@U.DECL-NX.PL@
@R.DECL-NX.PL@
! case
@U.DECL-CX.ABE@
 @U.DECL-CX.ABL@
 @U.DECL-CX.ACC@
 @U.DECL-CX.APR@
 @U.DECL-CX.APRINE@
 @U.DECL-CX.APRILL@
 @U.DECL-CX.APRELA@
 @U.DECL-CX.APREGR@
 @U.DECL-CX.APRPRL@
 @U.DECL-CX.APRTRA@
 @U.DECL-CX.APRTER@
 @U.DECL-CX.CAR@
 @U.DECL-CX.CMP@
 @U.DECL-CX.CNS@
 @U.DECL-CX.COM@
 @U.DECL-CX.DAT@
 @U.DECL-CX.EGR@
 @U.DECL-CX.ELA@
 @U.DECL-CX.GEN@
 @U.DECL-CX.ILL@
 @U.DECL-CX.INE@
 @U.DECL-CX.INS@
 @U.DECL-CX.NOM@
 @U.DECL-CX.PRL@
 @U.DECL-CX.TRA@
 @U.DECL-CX.TER@

! declension type
@U.DECL-DX.INDEF@
@U.DECL-DX.PX@

! predicate
!@U.DECL-PR.1@
!@C.DECL-PR@

!! Removal
@C.DECL-NX@
@C.DECL-DX@
@C.DECL-CX@

 @U.Cap.Obl@      !!≈ | @CODE@ | Allowing downcasing of derived names: deatnulasj
 @U.Cap.Opt@      !!≈ | @CODE@ | Allowing downcasing of derived names: deatnulasj

!! !!!Lexicon Root
LEXICON Root

  SUBSTANDARDS ; ! temporary solution

!! The word forms in Komi (Zyrian) language start from the lexeme roots of basic
!! word classes, or optionally from prefixes:
              adjectives                                 ;
              adjectives-russian-like                    ;
              kom-adjectives-russian-like                ;
              adpositions                                ;
              adverbs                                    ;
              conjunctors                                ;
              descriptives                               ;
              determiners                                ;
              gerunds                                    ;
              interjections                              ;
              nouns-deverbal                             ;
              nouns                                      ;
              nouns-russian-homographs                   ;
              numerals                                   ;
              particles                                  ;
              pronouns                                   ;
              propernouns-malenames-cyrillic             ;
!             propernouns-malenames-russian-derivable    ; ! 2015-09-07
              propernouns-malesurnames-cyrillic          ;
!             propernouns-malesurnames-russian-derivable ; ! 2015-09-07
! ! 2015-09-07
              propernouns-toponyms-Russian               ; !2019-10-30 Cyrillic
  @U.Cap.Obl@ propernouns-toponyms-Komi                  ; ! toponyms - always uppercase
  @U.Cap.Opt@ propernouns-toponyms-Komi                  ; ! toponyms - allow downcasing for adj derivation
              propernouns                                ;
              quantifiers                                ;
              subjunctors                                ;
              verbs-A2M                                  ;
              verbs-N2END                                ;
              VERBNEGATIVE                               ; ! affixes/verbs.lexc
              PRONOUN-TYPES                              ; ! in affixes/pronouns.lexc 2019-04-06

              Abbreviation                               ;
              Acronym                                    ;
              kpv-Acronym                                ;
              Punctuation                                ;
              Symbols                                    ;
              EXCEPTIONS                                 ;
              dialect_lexicon                            ;
              !RussianSurnamesDerive                     ; ! Only testing 2012-11-15
              urj-Cyrl-ProperNouns                       ; !! Testing 2015-09-06
!! Feeding new words
             A_NEWWORDS              ;
             A-Russian-like_NEWWORDS ;
             ADV_NEWWORDS            ;
             N_NEWWORDS              ;
 @U.Cap.Obl@ PROP_NEWWORDS           ;
 @U.Cap.Opt@ PROP_NEWWORDS           ;
             V_NEWWORDS              ;

! ======================================================= !
! These lexica are for parts of speech without morphology !
! ======================================================= !

! Absolute forms
LEXICON ABS_
 +Abs: # ;
!! пу керка
!! выль керка

! Compounding

LEXICON R
+Cmp#:%-# nouns ;
+Cmp#:%-# nouns-russian-homographs ;
+Cmp#:%-# nouns-deverbal ;

LEXICON Serial-Verbs
+Cmp/Serial+Cmp#:%-# verbs-A2M ;
+Cmp/Serial+Cmp#:%-# verbs-N2END ;
+Cmp/Serial+Cmp#:%-# V_NEWWORDS ;



! ==================================== !
! Lexica called End, whatever they are !
! ==================================== !

LEXICON ABBR-IS_ADV
+ABBR: ADV_ ;

LEXICON ABBR-IS_N
+ABBR+N: WordEnd ; ! This needs a Noun1 continuation but the hyphen must
             ! be dealt with for non-nominative-singular marking 2012-01-27

!2018-10-21!LEXICON EndNeg
!2018-10-21!+CC+Neg: WordEnd ; ! ни

!LEXICON EndQST
!+Qst: WordEnd ;


! ======= !
! Clitics !
! ======= !

LEXICON K
 WordEnd ;
+Clt/тӧ:%>тӧ WordEnd ;
+Clt/сӧ:%>сӧ WordEnd ;
+Clt/тӧ+Err/Dial:%>тэ WordEnd ;
+Clt/тӧ+Err/Dial:%>то WordEnd ;
+Clt/сӧ+Err/Dial:%>сэ WordEnd ;

! For this all instances of _#_
! were converted to _WordEnd_
LEXICON WordEnd
            # ;
+Qst/A:%>%-а WordEnd-2 ;
+Qst/Oe:%>%-ӧ WordEnd-2 ;
+Qst/Oe:%>%-э WordEnd-2 ;

LEXICON WordEnd-2
            # ;
+Clt/И:%-И2 # ;

! List of spurious lexica
! These should be fixed.

!2018-10-21!LEXICON ADV-SUPERL
!2018-10-21!+Adv+Superl: WordEnd ;

! 2012-02-11
! This should have the following case forms pointing toward it:
! ine, ill, ela, apr, aprela, aprill, aprine
LEXICON SPAT-COMPARATIVE
 COMPARATIVE ;
 PredMonLex-Spat-Add-Morphology ; !

LEXICON COMPARATIVE
               K ;
+Comp:%>джык K ; !more
+Der+Der/моз:%>моз K ; ! like -- in verbal constructions (Jaska thinks 2018-10-21)
+Der+Der/кодь:%>кодь K ; ! like -- in copula constructions

! TODO: Integrate this in xml-files in working…


LEXICON SUBSTANDARDS
коръя+Err/Orth:корья Noun1 ;

LEXICON ENDLEX
!! !!!Lexicon @LEXNAME@
!! And this is the @LEXNAME@ of everything:
!! {{{
   @D.CmpOnly.FALSE@@D.CmpPref.TRUE@@D.NeedNoun.ON@ # ; !!≈ @CODE@
! @D.Need3Part.ON@ # ; !3part
!! }}}
!! The {{@D.CmpOnly.FALSE@}} flag diacritic is ued to disallow words tagged
!! with +CmpNP/Only to end here.
!! The {{@D.NeedNoun.ON@}} flag diacritic is used to block illegal compounds.


! vim: set ft=xfst-lexc:
