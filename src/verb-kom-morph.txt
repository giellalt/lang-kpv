
! ====================================== !
!        Verbal morphology               !
! ====================================== !


! Closed class verbs
! ------------------

LEXICON VERBNEGATIVE
!Present Neg
оз+V+Neg+Ind+Prs+Sg1:ог K ;
оз+V+Neg+Ind+Prs+Sg2:он K ;
оз+V+Neg+Ind+Prs+Sg3:оз K ;
оз+V+Neg+Ind+Prs+Pl1:огӧ K ;
оз+V+Neg+Ind+Prs+Pl2:онӧ K ;
оз+V+Neg+Ind+Prs+Pl1:огӧй K ;
оз+V+Neg+Ind+Prs+Pl2:онӧй K ;
оз+V+Neg+Ind+Prs+Pl3:оз K ;

!Past Neg
оз+V+Neg+Ind+Prt+Sg1:эг K ;
оз+V+Neg+Ind+Prt+Sg2:эн K ;
оз+V+Neg+Ind+Prt+Sg3:эз K ;
оз+V+Neg+Ind+Prt+Pl1:эг K ;
оз+V+Neg+Ind+Prt+Pl2:эн K ;
оз+V+Neg+Ind+Prt+Pl1:эгӧ K ;
оз+V+Neg+Ind+Prt+Pl2:энӧ K ;
оз+V+Neg+Ind+Prt+Pl3:эз K ;

!Imperative
оз+V+Neg+Imprt+Sg2:эн K ;
оз+V+Neg+Imprt+Pl2:эн K ;
оз+V+Neg+Imprt+Pl2:энӧ K ;
!A tag is needed to indicate this clitic-like лы segment
оз+V+Neg+Imprt+Sg2:энлы K ;



! Open class verbs
! ----------------


LEXICON Verb1 ! яйсявны,  в - л change кывны кылан
Verb1-Verb2 ;


LEXICON Verb2 ! янӧдны, контролируйтны. Ending in -дны, -тны.
Verb1-Verb2 ;

LEXICON Verb1-Verb2
+V:  VerbConj ; ! кывны - кыван
+V+Inf:%>ны K ;
+V+ConNeg: # ;
+V+Imp+Sg2: #;
+V+Der/лыны+V:%>л VerbConj ; ! босьтлыны - босьтлан
+V+Der/лыны+V+Inf:%>лыны # ;
+V+ConNeg:%>лы # ;
+V+Imp+Sg2:%>лы #;
+V+Der/ыштны+V:%>ышт  VerbConj ;
+V+Der/ыштны+V+Inf:%>ыштны K ;
+V+Der/ыштны+V+ConNeg:%>ышт # ;
+V+Der/ыштны+V+Imp+Sg2:%>ышт #;
!
+V+Der/ыштны+V+Der/лыны+V:%>ыштл  VerbConj ;
+V+Der/ыштны+V+Der/лыны+V+Inf:%>ыштлыны K ;
+V+Der/ыштны+V+Der/лыны+V+ConNeg:%>ыштл # ;
+V+Der/ыштны+V+Der/лыны+V+Imp+Sg2:%>ыштл #;

LEXICON Verb3 ! ярмыны. Ending in -ыны.
+V:  VerbConj ; ! check this!!!
+V+Inf:%>ыны K ;
+V+ConNeg:%>ы # ;
+Imp+Sg2:%>ы #;
+V+Der/лыны+V:%>ыл VerbConj ; ! мынтылыны - мынтылан
+V+Der/лыны+V+Inf:%>ылыны # ;
+V+ConNeg:%>ылы # ;
+Imp+Sg2:%>ылы #;
+V+Der/ывлыны+V:%>ывл VerbConj ; ! мынтывлыны - мынтывлан
+V+Der/ывлыны+V+Inf:%>ывлыны # ;
+V+ConNeg:%>ывлы # ;
+Imp+Sg2:%>ывлы #;
!
+V+Der/ыштны+V:%>ышт  VerbConj ;
+V+Der/ыштны+V+Inf:%>ыштны K ;
+V+Der/ыштны+V+ConNeg:%>ышт # ;
+V+Der/ыштны+V+Imp+Sg2:%>ышт #;
!
+V+Der/ыштны+V+Der/лыны+V:%>ыштл  VerbConj ;
+V+Der/ыштны+V+Der/лыны+V+Inf:%>ыштлыны K ;
+V+Der/ыштны+V+Der/лыны+V+ConNeg:%>ыштл # ;
+V+Der/ыштны+V+Der/лыны+V+Imp+Sg2:%>ыштл #;



! Stray verb forms

LEXICON VerbPRT ! one verb, вӧвны 
 +V: preterite ;

LEXICON EMYSHT ! эмышт "is a little / are a few"
 +V+Ind+Prs+Sg3: # ;
 +V+Ind+Prs+Pl3: # ;
 
LEXICON GER ! 3 lexicalised +Ger forms.
+V+Ger: # ;




! ================ !
! Verb conjugation !
! ================ !

LEXICON VerbConj
 Finiteforms ;
 Non-finiteforms ;

LEXICON Finiteforms  ! Gives linking vowels for 3 tenses
present ;
future ;
preterite ;
imperative ;

LEXICON present
+Ind+Prs:%>а PresPret1 ; ! 1,2  Present
+Ind+Prs:%>ӧ PresPret1-SgPl3 ; ! 3

LEXICON future
+Ind+Fut:%>а PresPret1 ; ! 1,2  Future
+Ind+Fut:%>ас PresPret1-SgPl3 ; ! 3 PresPret1-SgPl3   

LEXICON preterite
+Ind+Prt1:%>і PresPret1 ; ! 1,2  Preterite1. і и variation?
+Ind+Prt1:%>і P3 ; ! 3 
+Ind+Prt2:%>ӧм Pret2a ;

LEXICON imperative
+Imp+Sg2: #;
+Imp+Pl2:%>ӧй #;

LEXICON Non-finiteforms
+Der/ӧм+VN:%>ӧм Noun1 ;
! Page 346 in the Komi grammar
+MYST:%>мысьт K ; ! gerund (after the action)
+MYS:%>мысь K ; !
+Der/иг+Ger:%>іг K ; ! gerund (at the same time as the action)
!352 Pl
+Der/иг+Ger+Pl:%>ігъяс K ;
+Der/иг+Ger+Pl+Ill:%>ігъясӧ K ;
+Der/иг+Ger+Pl+Instr:%>ігъясӧн PossessiveSuffixes_NoSg1 ;
+Der/иг+Ger+Ill:%>ігӧ K ;
+Der/иг+Ger+Ill+PxSg1:%>ігам K ;
+Der/иг+Ger+Ill+PxSg2:%>ігад K ;
+Der/иг+Ger+Ill+PxSg3:%>ігас K ;
+Der/иг+Ger+Ill+PxPl1:%>іганым K ;
+Der/иг+Ger+Ill+PxPl2:%>іганыд K ;
+Der/иг+Ger+Ill+PxPl3:%>іганыс K ;
+Der/иг+Ger+Instr:%>ігӧн PossessiveSuffixes_NoSg1 ;
!351 Pl
+Der/иг+Ger+MOZ:%>ігмоз PossessiveSuffixes ;
+Der/иг+Ger+SOR:%>ігсор K ;
+Der/иг+Ger+TYR:%>ігтыр K ;
+Der/иг+Ger+TYRJI:%>ігтырйи K ;
+Der/иг+Ger+CHOZH:%>ігчӧж K ;
+Der/иг+Ger+KOSTI:%>ігкості PossessiveSuffixes ;
+ANA:%>ана K ;
+ANAA:%>анаа K ;
+AN+MOZ:%>анмоз K ;
+Car:%>тӧг PossessiveSuffixes ;
+OM+Instr:%>ӧмӧн K ;
+Der/иг+Ger+KEZHLO:%>ігкежлӧ K ;
+TODZ:%>тӧдз PossessiveSuffixes ;

LEXICON PossessiveSuffixes
+PxSg1:%>ым K ;
            PossessiveSuffixes_NoSg1 ;


LEXICON PossessiveSuffixes_NoSg1
+NoPoss:     K ;
+PxSg2:%>ыд  K ;
+PxSg3:%>ыс  K ;
+PxPl1:%>ным K ;
+PxPl2:%>ныд K ;
+PxPl3:%>ныс K ;


LEXICON PresPret1  ! First and second person
+Sg1:%> K ; 
+Sg2:%>н K ;
+Pl1:%>м K ; 
+Pl1+Use/Sub:%>мӧ K ; 
+Pl1:%>мӧй K ;  ! imperatiivi / optatiivi ??
+Pl2:%>нныд K ;
+Pl2+Use/Sub:%>д K ; 

LEXICON P3         
:%>с PresPret1-SgPl3 ; ! This is regular
  PresPret1-SgPl3 ; ! This is usually restricted to intransitives
!:%>с PL3 ; ! what?

LEXICON PresPret1-SgPl3
  SG3 ; 
  PL3 ;
  
LEXICON SG3
+Sg3:%> K ;
  
LEXICON PL3
+Pl3:%>ны K;

LEXICON Pret2a
:а PRET2-linkki-a ; 
   PRET2PL2 ; 
   PL3 ;   ! for continuation in different persons. 
                  !Could also have used the homonymic participle
                  ! as point of departure

LEXICON PRET2PL2
+Sg2:%>ыд K ;
+Pl2:%>ныд K ;
+Pl2:%>ныдӧсь K ; !224

LEXICON PRET2-linkki-a
+Sg1:%> K ;
+Sg3:%> K ;
+Pl1:%>ӧсь K ;
+Pl3:%>ӧсь K ;
