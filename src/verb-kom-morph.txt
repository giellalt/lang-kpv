
! ====================================== !
!        Verbal morphology               !
! ====================================== !


! Closed class verbs
! ------------------

LEXICON VERBNEGATIVE
!Present Neg
оз+V+Neg+Ind+Prs+Sg1:ог MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Sg2:он MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Sg3:оз MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl1:огӧ MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl2:онӧ MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl1:огӧй MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl2:онӧй MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prs+Pl3:оз MatrixVerb-Comparison ;

!Past Neg
оз+V+Neg+Ind+Prt+Sg1:эг MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt+Sg2:эн MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt+Sg3:эз MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt+Pl1:эг MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt+Pl2:эн MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt+Pl1:эгӧ MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt+Pl2:энӧ MatrixVerb-Comparison ;
оз+V+Neg+Ind+Prt+Pl3:эз MatrixVerb-Comparison ;

!Imperative
оз+V+Neg+Imprt+Sg2:эн K ;
оз+V+Neg+Imprt+Pl2:эн K ;
оз+V+Neg+Imprt+Pl2:энӧ K ;
!A tag is needed to indicate this clitic-like лы segment
оз+V+Neg+Imprt+Sg2:энлы K ;

!Final
моз+V+Neg+Final+1:мог K ; ! мед ог _so that I/we won't_
моз+V+Neg+Final+2:мон K ; ! мед он
моз+V+Neg+Final+3:моз K ; ! мед оз


! Open class verbs
! ----------------

! Some Flag diacritic lines are with regexes, other with aligned zeros. We want to migrate to
! regexes < … > , for readability reasons (sic!)

LEXICON Verb-lokny ! One verb only, локны
< "+V":0 "+Inf":0 "@U.CONJ-INF.YES@" > VerbEnd-Comparison ;
< "+V":0 "+ConNeg":0 "@U.CONJ-INF.NO@" "@U.CONJ-CONNEG.YES@" "+Pl3":0 "@U.CONJ-NX.PL@" "@U.CONJ-PX.3@" > VerbEnd ;
< "+V":0 "+ConNeg":0 "@U.CONJ-INF.NO@" "@U.CONJ-CONNEG.YES@" > VerbEnd ;
< "+V":0 "+Imprt":0 "@U.CONJ-INF.NO@" "@U.CONJ-MX.IMP@" "+Sg2":0 "@U.CONJ-NX.SG@" "@U.CONJ-PX.2@" > VerbEnd ;
< 0:">" 0:т "+V":0 "@U.CONJ-INF.YES@" > VerbDer ; !
< 0:">" 0:т "+V":0 "@U.CONJ-INF.YES@" > VerbDer-V ; !
+V:т  VerbConj ; !а



LEXICON Verb-ny ! янӧдны, контролируйтны. Ending in -дны, -тны, -Cны, -Vны.
< "+V":0 "@U.CONJ-INF.YES@" > Verb-nyDer ; !
< "+V":0 "@U.CONJ-INF.YES@" > VerbDer ; !
< "+V":0 "@U.CONJ-INF.YES@" > VerbDer-V ; !
< "+V":0 > VerbConj ; !а
< "+V":0 > VerbConj-V ; !б

LEXICON Verb-yny ! ярмыны. Multiple-syllable verb ending in -ыны.
< "+V":0 "@U.CONJ-INF.YES@" > Verb-ynyDer ; !
< "+V":0 "@U.CONJ-INF.YES@" > VerbDer ; !
< 0:ы "+V":0 "@U.CONJ-INF.YES@" > VerbDer-V ; !
< "+V":0 > VerbConj ; ! check this!!!
< 0:ы "+V":0 > VerbConj-V ; ! check this!!!

! First model: ы added as part of the suffix in б
! суффикс а
! янӧд -----------\
!                   ышт, ан...
! ярм ------------/

! суффикс б
! янӧд -----------\
!                   ны, 
! ярм ----- ы ----/


! Second model: ы added earlier, and not as part of the suffix in б
! суффикс а
! янӧд -----------\
!                   ышт, ан...
! ярм ------------/

! суффикс б
! янӧд -----------\
!                   ыны, 
! ярм ------------/



! ================ !
! Verb conjugation !
! ================ !

LEXICON VerbConj-V ! суф б -- ы --
< 0:">" 0:н 0:ы "+Inf":0 "@U.CONJ-INF.YES@" > VerbEnd-Comparison ;
< 0:">" 0:н 0:ы "+ConNeg":0 "@U.CONJ-INF.NO@" "@U.CONJ-CONNEG.YES@" +Pl3:0 "@U.CONJ-NX.PL@" "@U.CONJ-PX.3@" > VerbEnd ;
< "+ConNeg":0 "@U.CONJ-INF.NO@" "@U.CONJ-CONNEG.YES@" > VerbEnd ;
< "+Imprt":0 "+Sg2":0 "@U.CONJ-INF.NO@" "@U.CONJ-MX.IMP@" "@U.CONJ-NX.SG@" "@U.CONJ-PX.2@" > VerbEnd ;

LEXICON VerbConj ! суф а -------
@U.CONJ-INF.NO@ Finiteforms ;
@U.CONJ-INF.YES@ Non-finiteforms ;

LEXICON Finiteforms  ! Gives linking vowels for 3 tenses
 present ;
 future ;
 preterite ;
 imperative ;

LEXICON present
< 0:">" 0:а "+Ind":0 "@U.CONJ-MX.IND@" "+Prs":0 "@U.CONJ-TX.PRES@" > PresPret1 ; ! 1,2  Present
< 0:">" 0:ӧ "+Ind":0 "@U.CONJ-MX.IND@" "+Prs":0 "@U.CONJ-TX.PRES@" > PresPret1-SgPl3 ; ! 3

LEXICON future
< 0:">" 0:а "+Ind":0 "@U.CONJ-MX.IND@" "+Fut":0 "@U.CONJ-TX.FUT@" > PresPret1 ; ! 1,2  Future
< 0:">" 0:а 0:с "+Ind":0 "@U.CONJ-MX.IND@" "+Fut":0 "@U.CONJ-TX.FUT@" "@U.CONJ-PX.3@" > PresPret1-SgPl3 ; ! 3 PresPret1-SgPl3   ХХХ check this

LEXICON preterite
< 0:">" 0:і "+Ind":0 "@U.CONJ-MX.IND@" "+Prt1":0 "@U.CONJ-TX.PRET1@" > PresPret1 ; 
< 0:">" 0:і "+Ind":0 "@U.CONJ-MX.IND@" "+Prt1":0 "@U.CONJ-TX.PRET1@" >  P3 ; ! 3 
< 0:">" 0:ӧ 0:м "+Ind":0 "@U.CONJ-MX.IND@" "+Prt2":0 "@U.CONJ-TX.PRET2@" > Pret2a ;

LEXICON imperative
< 0:">" 0:ӧ 0:й "+Imprt":0 "@U.CONJ-MX.IMP@" "+Pl2":0 "@U.CONJ-NX.PL@" "@U.CONJ-PX.2@" > VerbEnd ;

LEXICON PossessiveSuffixes
+PxSg1-ym:%>ым K ;
            PossessiveSuffixes_NoSg1 ;


LEXICON PossessiveSuffixes_NoSg1
+NoPoss:     K ;
+PxSg2:%>ыд  K ;
+PxSg3:%>ыс  K ;
+PxPl1:%>ным K ;
+PxPl2:%>ныд K ;
+PxPl3:%>ныс K ;


LEXICON PresPret1  ! First and second person
< "+Sg1":0 "@U.CONJ-NX.SG@" "@U.CONJ-PX.1@" > VerbEnd-Comparison ; 
< 0:">" 0:н  "+Sg2":0 "@U.CONJ-NX.SG@" "@U.CONJ-PX.2@" > VerbEnd-Comparison ;
< 0:">" 0:м "+Pl1":0 "@U.CONJ-NX.PL@" "@U.CONJ-PX.1@" > VerbEnd-Comparison ; 
< 0:">" 0:м 0:ӧ "+Pl1":0 "+Use/Sub":0 "@U.CONJ-NX.PL@" "@U.CONJ-PX.1@" > VerbEnd-Comparison ; 
< 0:">" 0:м 0:ӧ 0:й "+Pl1":0 "@U.CONJ-NX.PL@" "@U.CONJ-PX.1@" > VerbEnd-Comparison ;  ! imperatiivi / optatiivi ??
< 0:">" 0:н 0:н 0:ы 0:д "+Pl2":0 "@U.CONJ-NX.PL@" "@U.CONJ-PX.2@" > VerbEnd-Comparison ;
< 0:">" 0:д "+Pl2":0 "+Use/Sub":0 "@U.CONJ-NX.PL@" "@U.CONJ-PX.2@" > VerbEnd-Comparison ; 

LEXICON P3         
< 0:">" 0:с "@U.CONJ-PX.3@" > PresPret1-SgPl3 ; ! This is regular
< "@U.CONJ-PX.3@" >  PresPret1-SgPl3 ; ! This is usually restricted to intransitives


LEXICON PresPret1-SgPl3
  SG3 ; 
  PL3 ;
  
LEXICON SG3
< "+Sg3":0 "@U.CONJ-NX.SG@" "@U.CONJ-PX.3@" > VerbEnd-Comparison ;
  
LEXICON PL3
< 0:">" 0:н 0:ы "+Pl3":0 "@U.CONJ-NX.PL@" "@U.CONJ-PX.3@" > VerbEnd-Comparison ;
< 0:">" 0:н 0:ы "+Pl3":0 "@U.CONJ-NX.PL@" "@U.CONJ-PX.3@" > VerbEnd-Comparison ;

LEXICON Pret2a
:а PRET2-linkki-a ; 
   PRET2PL2 ; 
   PL3 ;   ! for continuation in different persons. 
                  !Could also have used the homonymic participle
                  ! as point of departure

LEXICON PRET2PL2
< 0:">" 0:ы 0:д "+Sg2":0 "@U.CONJ-NX.SG@" "@U.CONJ-PX.2@" > VerbEnd-Comparison ;
< 0:">" 0:н 0:ы 0:д "+Pl2":0 "@U.CONJ-NX.PL@" "@U.CONJ-PX.2@" > VerbEnd-Comparison ;
< 0:">" 0:н 0:ы 0:д 0:ӧ 0:с 0:ь "+Pl2":0 "@U.CONJ-NX.PL@" "@U.CONJ-PX.2@" > VerbEnd-Comparison ; !224
< 0:">" 0:н 0:ы "+Pl3":0 "@U.CONJ-NX.PL@" "@U.CONJ-PX.3@" > VerbEnd-Comparison ; !227

LEXICON PRET2-linkki-a
!+Sg1+Use/Sub: VerbEnd-Comparison ; ! no Sg1 for Prt2 in the standard
< "+Sg1":0 "@U.CONJ-NX.SG@" "@U.CONJ-PX.1@" > VerbEnd-Comparison ;  
< "+Sg3":0 "@U.CONJ-NX.SG@" "@U.CONJ-PX.3@" > VerbEnd-Comparison ;
!+Pl1+Use/Sub:%>ӧсь VerbEnd-Comparison ; ! no Pl1 for Prt2 in the standard
< 0:">" "+Pl1":0 0:ӧ 0:с 0:ь "@U.CONJ-NX.PL@" "@U.CONJ-PX.1@" > VerbEnd-Comparison ; 
< 0:">" "+Pl3":0 0:ӧ 0:с 0:ь "@U.CONJ-NX.PL@" "@U.CONJ-PX.3@" > VerbEnd-Comparison ;

LEXICON MatrixVerb-Comparison
               K ;
+Der/моз:%>моз K ;
+Compar:%>джык K ;
+Der/кодь:%>кодь K ;

!This is where Serial verbs are made possible
LEXICON VerbEnd-Comparison
 Serial-Verbs ;
 MatrixVerb-Comparison ;

LEXICON VerbEnd
 Serial-Verbs ;
 # ;

LEXICON Serial-Verbs
+SerialVerb:- V-A2M ;
+SerialVerb:- V-N2END ;

! Derivation
! ==========

LEXICON Non-finiteforms
VerbDer ; ! to be merged with next
          !2011-11-02 dealing with serial verbs there may be
          ! reason to distinguish between V2V derivation and non-finite forms

LEXICON Verb-nyDer
  ! verb-to-verb
  ! This is fed by LEXICON Verb-ny, and therefore certain corrections
  ! must be made 2012-01-18
< 0:">" 0:л "+Der/лы":0 "@C.CONJ-INF@" >  Verb-yny ; !!босьтлыны
< 0:">" 0:л 0:ы 0:в 0:л "+Der/лывлы":0 "@C.CONJ-INF@" > Verb-yny ; !!босьтлывлыны
< 0:">" 0:с 0:ь "+Der/сьы":0 "@C.CONJ-INF@" > Verb-yny ; !! овсьыны
< 0:">" 0:с 0:ь 0:ы 0:ш 0:т "+Der/сьы":0 "+Der/ышт":0 "@C.CONJ-INF@" > Verb-ny ; !!вежсьыштны
< 0:">" 0:с 0:ь 0:ы 0:ш 0:т 0:л 0:ы 0:в 0:л "+Der/сьы":0 "+Der/ышт":0 "+Der/лывлы":0 "@C.CONJ-INF@" > Verb-yny ; !! пусьыштлывлыны
< 0:">" 0:с 0:ь 0:ы 0:ш 0:т 0:л "+Der/сьы":0 "+Der/ышт":0 "+Der/лы":0 "@C.CONJ-INF@" > Verb-yny ; !!?? пусьыштлыны
!!!!зь:зь  !!югзьыны
< 0:ч "+Der/чы":0 "@C.CONJ-INF@" > Verb-yny ; !!босьтчыны
< 0:ч 0:ы 0:л  "+Der/чы":0 "+Der/л":0 "@C.CONJ-INF@" > Verb-ny ; !!дӧзмӧдчывны
< 0:ч 0:ы 0:ш 0:т 0:л "+Der/чы":0 "+Der/ышт":0 "+Der/лы":0 "@C.CONJ-INF@" > Verb-yny ; !!босьтчыштлыны
< 0:ч 0:ы 0:ш 0:т 0:л 0:ы 0:в 0:л "+Der/чы":0 "+Der/ышт":0 "+Der/лывлы":0 "@C.CONJ-INF@" > Verb-yny ; !! босьтчыштлывлыны
< 0:ы 0:ш 0:т 0:л "+Der/ышт":0 "+Der/лы":0 "@C.CONJ-INF@" > Verb-yny ;   !!сёйыштлыны
< 0:ы 0:ш 0:т 0:л 0:ы 0:в 0:л "+Der/ышт":0  "+Der/лывлы":0 "@C.CONJ-INF@" > Verb-yny ;  !!сёйыштлывлыны
< 0:ы 0:ш 0:т "+Der/ышт":0 "@C.CONJ-INF@" > Verb-ny ; !!сёйыштны
< 0:ч 0:ы 0:ш 0:т "+Der/чы":0 "+Der/ышт":0 "@C.CONJ-INF@" > Verb-ny ; !!босьтчыштны
< 0:а 0:л "+Der/ал":0 "@C.CONJ-INF@" > Verb-ny ; !!сетавны
< 0:о 0:в 0:т "+Der/овт":0 "@C.CONJ-INF@" > Verb-ny ; !!пановтны
< 0:а 0:с 0:ь "+Der/ась":0 "@C.CONJ-INF@" > Verb-ny ; !! босьтасьны

 
LEXICON VerbDer 



  ! verb-to-noun
+Der/ӧм+Event:%>ӧм Noun1 ;

  ! nonfinites
+Der/иг+Duration+Ger:%>іг K ; ! gerund (at the same time as the action)
!352 Pl
+Der/иг+Duration+Ger+Pl:%>ігъяс K ;
+Der/иг+Duration+Ger+Pl+Ill:%>ігъясӧ K ; !345
+Der/иг+Duration+Ger+Pl+Ill+PxSg1-ym:%>ігъясам K ;
+Der/иг+Duration+Ger+Pl+Ill+PxSg2:%>ігъясад K ;
+Der/иг+Duration+Ger+Pl+Ill+PxSg3:%>ігъясас K ;
+Der/иг+Duration+Ger+Pl+Ill+PxPl1:%>ігъясаным K ;
+Der/иг+Duration+Ger+Pl+Ill+PxPl2:%>ігъясаныд K ;
+Der/иг+Duration+Ger+Pl+Ill+PxPl3:%>ігъясаныс K ;
+Der/иг+Duration+Ger+Pl+Instr+NoPoss:%>ігъясӧн K ;! 2000:359 
                                           !And Marina 2011-10-05 no Px
+Der/иг+Duration+Ger+Sg+Ill:%>ігӧ K ;
+Der/иг+Duration+Ger+Sg+Ill+PxSg1-ym:%>ігам K ;
+Der/иг+Duration+Ger+Sg+Ill+PxSg2:%>ігад K ;
+Der/иг+Duration+Ger+Sg+Ill+PxSg3:%>ігас K ;
+Der/иг+Duration+Ger+Sg+Ill+PxPl1:%>іганым K ;
+Der/иг+Duration+Ger+Sg+Ill+PxPl2:%>іганыд K ;
+Der/иг+Duration+Ger+Sg+Ill+PxPl3:%>іганыс K ;
+Der/иг+Duration+Ger+Sg+Instr:%>ігӧн PossessiveSuffixes_NoSg1 ; !Ӧнія коми кыв, 2000: 360
!351 Pl
+Der/иг+Duration+Ger+Der/моз:%>ігмоз PossessiveSuffixes_NoSg1 ; !371
+Der/иг+Duration+Ger+Der/сор:%>ігсор K ;
+Der/иг+Duration+Ger+Der/сор+Instr:%>ігсорӧн K ;
+Der/иг+Duration+Ger+Der/тыр:%>ігтыр K ;
+Der/иг+Duration+Ger+Der/тырйи:%>ігтырйи PossessiveSuffixes_NoSg1 ; !374
+Der/иг+Duration+Ger+Der/тыръя:%>ігтыръя K ;
+Der/иг+Duration+Ger+Der/чӧж:%>ігчӧж PossessiveSuffixes_NoSg1 ; !377
+Der/иг+Duration+Ger+Der/чӧж+Instr:%>ігчӧжӧн K ; !
+Der/иг+Duration+Ger+Der/кості:%>ігкості PossessiveSuffixes_NoSg1 ; !352, 379
+Der/иг+Duration+Ger+Der/коста:%>ігкоста K ;
+Der/иг+Duration+Ger+Der/кежлӧ:%>ігкежлӧ K ; !393

+Der/иг+Duration+Ger+Der/кежлӧ+PxSg2:%>ігкежлад K ;
+Der/иг+Duration+Ger+Der/кежлӧ+PxSg3:%>ігкежлас K ;
+Der/иг+Duration+Ger+Der/кежлӧ+PxPl1:%>ігкежланым K ;
+Der/иг+Duration+Ger+Der/кежлӧ+PxPl2:%>ігкежланыд K ;
+Der/иг+Duration+Ger+Der/кежлӧ+PxPl3:%>ігкежланыс K ;


+ANA+Adv:%>ана K ;
+ANAA+Adv:%>анаа K ; !Adding after attestation in Komi Grammar 2000: 381
+AN+Der/моз:%>анмоз PossessiveSuffixes_NoSg1 ; !383

+Der/ӧм+Event+N+Instr:%>ӧмӧн K ;
+Der/ысь+ActPrsPtc:%>ысь Noun1 ;



LEXICON Verb-ynyDer
! verb-yny-to-verb
! The source stems have no stem vowel "ы", and therefore
! dervational suffixes come with an onset vowel

< 0:ы 0:ш 0:т "+Der/ышт":0 "+Dim":0 "@C.CONJ-INF@" > Verb-ny ; !!кольмыштны
!+V+Der/ышт+Dim+V:%>ышт  VerbConj ;
!+V+Der/ышт+Dim+V+Inf:%>ыштны K ;
!+V+Der/ышт+Dim+V+ConNeg0@U.CONJ-CONNEG.YES@+Pl3@U.CONJ-NX.PL@" "@U.CONJ-PX.3@:%>ыштны@U.CONJ-CONNEG.YES@0@U.CONJ-NX.PL@" "@U.CONJ-PX.3@ # ;
!+V+Der/ышт+Dim+V+ConNeg@U.CONJ-CONNEG.YES@:%>ышт0@U.CONJ-CONNEG.YES@ # ;
!+V+Der/ышт+Dim+V+Imprt+Sg2:%>ышт # ;

< 0:ы 0:ш 0:т 0:л "+Der/ышт":0 "+Dim":0 "+Der/лы":0 "@C.CONJ-INF@" > Verb-yny ;   !!??кольмыштлыны
!+V+Der/ышт+Dim+V+Der/лы+V:%>ыштл  VerbConj ;
!+V+Der/ышт+Dim+V+Der/лы+V+Inf:%>ыштлыны K ;
!+V+Der/ышт+Dim+V+Der/лы+V+ConNeg0@U.CONJ-CONNEG.YES@+Pl3@U.CONJ-NX.PL@" "@U.CONJ-PX.3@:%>ыштлыны@U.CONJ-CONNEG.YES@0@U.CONJ-NX.PL@" "@U.CONJ-PX.3@ # ;
!+V+Der/ышт+Dim+V+Der/лы+V+ConNeg@U.CONJ-CONNEG.YES@:%>ыштл00@U.CONJ-CONNEG.YES@ # ;
!+V+Der/ышт+Dim+V+Der/лы+V+Imprt+Sg2:%>ыштл # ;

< 0:ы 0:ш 0:т 0:л 0:ы 0:в 0:л "+Der/ышт":0 "+Dim":0 "+Der/лывлы":0 "@C.CONJ-INF@" > Verb-yny ;  !!??кольмыштлывлыны

< 0:ы 0:л "+Der/л":0 "@C.CONJ-INF@" > Verb-ny ;  !!тшӧктывны

< 0:ы 0:в 0:л "+Der/лывлы":0 "@C.CONJ-INF@" > Verb-yny ; !!тшӧктывлыны
!+V+Der/ывлы+V:%>ывл VerbConj ; ! мынтывл - мынтывлан
!+V+Der/ывлы+V+Inf:%>ывлыны K ;
!+V+Der/ывлы+V+ConNeg000@U.CONJ-CONNEG.YES@+Pl3@U.CONJ-NX.PL@" "@U.CONJ-PX.3@:%>ывлыны@U.CONJ-CONNEG.YES@0@U.CONJ-NX.PL@" "@U.CONJ-PX.3@ # ;
!+V+Der/ывлы+V+ConNeg0@U.CONJ-CONNEG.YES@:%>ывлы@U.CONJ-CONNEG.YES@ # ;
!+V+Der/ывлы+V+Imprt+Sg2:%>ывлы # ;

< 0:ы 0:с 0:ь "+Der/сьы":0 "@C.CONJ-INF@" > Verb-yny ; !! вевттьысьыны
< 0:ы 0:с 0:ь 0:ы 0:ш 0:т "+Der/сь":0 "+Der/ышт":0 "+Dim":0 "@C.CONJ-INF@" > Verb-ny ; !!лэптысьыштны
< 0:ы 0:с 0:ь 0:ы 0:ш 0:т 0:л 0:ы 0:в 0:л "+Der/сьы":0 "+Der/ышт":0 "+Der/лывлы":0 "@C.CONJ-INF@" > Verb-yny ; !!?? вевттьысьыштлывлыны
< 0:ы 0:с 0:ь 0:ы 0:ш 0:т 0:л "+Der/сьы":0 "+Der/ышт":0 "+Der/лы":0 "@C.CONJ-INF@" > Verb-yny ; !!лэптысьыштлыны
< 0:ы 0:с 0:ь 0:л  "+Der/сь":0 "+Der/л":0 "@C.CONJ-INF@" > Verb-yny ; !!кутчысьлыны

< 0:а 0:л "+Der/ал":0 "@C.CONJ-INF@" > Verb-ny ; !!бергӧдчавны
< 0:а 0:в 0:л "+Der/ал":0 "+Der/лы":0 "@C.CONJ-INF@" > Verb-yny ; !!бергӧдчавлыны
< 0:а 0:с 0:ь "+Der/ась":0 "@C.CONJ-INF@" > Verb-ny ; !! бертласьны


!<!-- This far 2012-01-19 -->

!< 0:о 0:в 0:т "+Der/овт":0 "@C.CONJ-INF@" > Verb-ny ; !!пановтны


! Stray verb forms :-о


LEXICON VerbDer-V
! Page 346 in the Komi grammar
+Der/мысьт:%>мысьт K ; ! gerund (after the action) !353
+Der/мысь:%>мысь K ; ! !353
+Der/тӧдз:%>тӧдз PossessiveSuffixes_NoSg1 ; !396
+Der/мӧн:%>мӧн PossessiveSuffixes_NoSg1 ; !399-403
+Der/мӧнъя:%>мӧнъя # ; !399-403
+Der/тӧм+VAbe:%>тӧм PossessiveSuffixes ;
+Car:%>тӧг PossessiveSuffixes_NoSg1 ; ! 390
+Car+Use/Sub:%>тӧгъя K ; !387
+Car+Use/Sub:%>тӧга K ; !387
+Car+Der/моз:%>тӧгмоз PossessiveSuffixes_NoSg1 ; !




LEXICON VerbPRT ! one verb, вӧвны 
+V: preterite ;
< ">":0 0:н 0:ы "+V":0 "+ConNeg":0  "@U.CONJ-CONNEG.YES@" "+Pl3":0 "@U.CONJ-NX.PL@" "@U.CONJ-PX.3@" > # ;
< "+V":0 "+ConNeg":0 "@U.CONJ-CONNEG.YES@" > # ;

LEXICON EMYSHT ! эмышт "is a little / are a few"
< "+V":0 "+Ind":0 "@U.CONJ-MX.IND@" "+Prs":0 "+Sg3":0 > # ;
< "+V":0 "+Ind":0 "@U.CONJ-MX.IND@" "+Prs":0 "+Pl3":0 "@U.CONJ-NX.PL@" "@U.CONJ-PX.3@" > # ;
 
LEXICON GER ! 3 lexicalised +Ger forms.
+V+Ger: # ;

