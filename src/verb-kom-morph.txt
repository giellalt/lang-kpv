
! ========================================================================== !
!                               Verbal morphology                            !
! ========================================================================== !

!   Here are the verbal continuation lexica, ordered according to frequency
!   in the V-kom-lex.txt file.

!   1714 Verb1
!   1495 Verb3
!   1495 Verb2
!     15 NounOBJ
!     13 NounSUBJ
!      3 Verb
!      2 VerbPRT
!      2 DoubleVerb1
!      1 Verbve3c4
!      1 VConjVow
!      1 VConjCons
!      1 V2   

!      8 End1
!      1 End2
!      1 End1a


! ============================= !
! Verbal continuation sublexica !
! ============================= !

! Closed class verbs
LEXICON VNEG
VERBNEGATIVE ;
 
LEXICON VDER
Verb1 ;
 
LEXICON V-AUX
Verb1 ;


LEXICON VERBNEGATIVE
!Verbs of Negation
ог+V+Prs+Neg+Sg1:ог K ;
он+V+Prs+Neg+Sg2:он K ;
оз+V+Prs+Neg+Sg3:оз K ;
огӧ+V+Prs+Neg+Pl1:огӧ K ;
онӧ+V+Prs+Neg+Pl2:онӧ K ;
огӧй+V+Prs+Neg+Pl1:огӧй K ;
онӧй+V+Prs+Neg+Pl2:онӧй K ;
оз+V+Prs+Neg+Pl3:оз K ;

!Past Neg
эг+V+Prt+Neg+Sg1:эг K ;
эн+V+Prt+Neg+Sg2:эн K ;
эз+V+Prt+Neg+Sg3:эз K ;
эг+V+Prt+Neg+Pl1:эг K ;
эн+V+Prt+Neg+Pl2:эн K ;
эгӧ+V+Prt+Neg+Pl1:эгӧ K ;
энӧ+V+Prt+Neg+Pl2:энӧ K ;
эз+V+Prt+Neg+Pl3:эз K ;



LEXICON Verb1 ! яйсявны
+V:%^Inf  Finiteforms ;


! Awaiting real analyses, I direct the other verb lexica to Verb1.
+V+Inf:%>ны K ;

LEXICON Verb3 ! ярмыны. Ending in -ыны.
Verb1 ;
+V+Inf:%>ыны K ;

LEXICON Verb2 ! янӧдны, контролируйтны. Ending in -дны, -тны.
Verb1 ;
+V+Inf:%>ны K ;


LEXICON Vdz4i3k
 Verb1 ;

LEXICON VerbPRT
 Verb1 ;

LEXICON DoubleVerb1 !This is very dubious in this system.
 Verb1 ;

LEXICON VConjVow
 Verb1 ;

LEXICON VConjCons
 Verb1 ;

LEXICON Verbve3c4
 Verb1 ;

LEXICON V
 Verb1 ;

LEXICON V-DUR
 Verb1 ;
 
LEXICON V-FREQ
 Verb1 ;

LEXICON V-INST
 Verb1 ;




 



LEXICON NounOBJ ! This is probably a formatting error
+V: # ;
! All the 17 NounOBJ are verbs of different kinds.


LEXICON NounSUBJ ! This is probably a formatting error
+V: # ;
! All the 15 NounOBJ are verbs of different kinds.


! ================ !
! Verb conjugation !
! ================ !

LEXICON Finiteforms        !Gives linking vowels for 3 tenses
+Ind+Fut:%>а PresPret1 ;   ! 1,2  Future
+Ind+Prs:%>а PresPret1 ;   ! 1,2  Present
+Ind+Prt1:%>і PresPret1 ;  ! 1,2  Preterite1. і и variation?
+Ind+Fut:%>а P3 ;          ! 3    
+Ind+Prs:%>ӧ P3 ;          ! 3
+Ind+Prt1:%>і P3 ;         ! 3
+Ind+Prt2:%>ӧм Pret2a ;

Non-finiteforms ;

LEXICON Non-finiteforms
! Page 346 in the Komi grammar X.
+MYST:%>мысьт K ;
+MYS:%>мысь K ;
+IG:%>іг K ;
!352 Pl
+IG+Pl:%>ігъяс K ;
+IG+Pl+Ill:%>ігъясӧ K ;
+IG+Pl+Instr:%>ігъясӧн PossessiveSuffixes_NoSg1 ;
+IG+Ill:%>ігӧ K ;
+IG+Ill+PxSg1:%>ігам K ;
+IG+Ill+PxSg2:%>ігад K ;
+IG+Ill+PxSg3:%>ігас K ;
+IG+Ill+PxPl1:%>іганым K ;
+IG+Ill+PxPl2:%>іганыд K ;
+IG+Ill+PxPl3:%>іганыс K ;
+IG+Instr:%>ігӧн PossessiveSuffixes_NoSg1 ;
!351 Pl
+IG+MOZ:%>ігмоз PossessiveSuffixes ;
+IG+SOR:%>ігсор K ;
+IG+TYR:%>ігтыр K ;
+IG+TYRJI:%>ігтырйи K ;
+IG+CHOZH:%>ігчӧж K ;
+IG+KOSTI:%>ігкості PossessiveSuffixes ;
+ANA:%>ана K ;
+ANAA:%>анаа K ;
+AN+MOZ:%>анмоз K ;
+Car:%>тӧг PossessiveSuffixes ;
+OM+Instr:%>ӧмӧн K ;
+IG+KEZHLO:%>ігкежлӧ K ;
+TODZ:%>тӧдз PossessiveSuffixes ;

LEXICON PossessiveSuffixes
+PxSg1:%>ым K ;
            PossessiveSuffixes_NoSg1 ;


LEXICON PossessiveSuffixes_NoSg1
+NoPoss:     K ;
+PxSg2:%>ыд  K ;
+PxSg3:%>ыс  K ;
+PxPl1:%>ным K ;
+PxPl2:%>ныд K ;
+PxPl3:%>ныс K ;


LEXICON PresPret1  ! First and second person
+Sg1:%> K ; 
+Sg2:%>н K ;
+Pl1:%>м K ; 
+Pl1:%>мӧ K ;   ! Evidently free variation?
+Pl1:%>мӧй K ;  ! Evidently free variation?
+Pl2:%>нныд K ;
+Pl2:%>д K ;    ! Evidently free variation?

LEXICON P3         
!:%>с PresPret1-SgPl3 ; ! what?
  PresPret1-SgPl3 ; 
!:%>с PL3 ; ! what?

LEXICON PresPret1-SgPl3
  SG3 ; 
  PL3 ;
  
LEXICON SG3
+Sg3:%> K ;
  
LEXICON PL3
+Pl3:%>ны K;

LEXICON Pret2a
:а PRET2-linkki-a ; 
   PRET2PL2 ; 
   PL3 ;   ! for continuation in different persons. 
                  !Could also have used the homonymic participle
                  ! as point of departure

LEXICON PRET2PL2
+Sg2:%>ыд K ;
+Pl2:%>ныд K ;
+Pl2:%>ныдӧсь K ; !224

LEXICON PRET2-linkki-a
+Sg1:%> K ;
+Sg3:%> K ;
+Pl1:%>ӧсь K ;
+Pl3:%>ӧсь K ;
