! ========================================================================== !
!                           Pronominal morphology                            !
! ========================================================================== !

! Closed class personal pronouns
! ------------------------------

LEXICON PERS
ме+Pron+Pers+Sg1: perssg1decl ;
тэ+Pron+Pers+Sg2: perssg2decl ; 
сійӧ+Pron+Pers+Sg3: perssg3decl ;
ми+Pron+Pers+Pl1: perspl1decl ;
ті+Pron+Pers+Pl2: perspl2decl ; 
найӧ+Pron+Pers+Pl3: perspl3decl ;


LEXICON perssg1decl
+Nom:ме # ;
+Cmpl:ме # ; !Postposition complement
+GenStem:менам # ;
+Cmpl+Gen:мелӧн # ;
+GenStem+Gen:менамлӧн # ;
+Ela+PxSg1:менсьым # ; !The elative case is used in Sg1 and Sg2 
                 !for the functions of the ablative in other persons
+Dat:меным # ; !What should be done with the 4 dative forms?
+Dat:мем # ;
+Dat:мен # ;
+Dat:менлы # ;
+Acc:менӧ # ;
+GenStem+Instr:менамӧн # ;
+GenStem+Com:менамкӧд # ;
+GenStem+Car:менамтӧг # ;
+GenStem+Inе:менамын # ;
+GenStem+Ela:менамысь # ;
+GenStem+Ill:менамӧ # ;
+GenStem+Apr:менамсянь # ;
+GenStem+Ter:менамӧдз # ;
+Cmpl:ме perssg ; ! for the regular obl sg

LEXICON perspl1decl
+Nom:ми # ;
+Der/йӧ+Nom:мийӧ # ;
+GenStem:миян # ;
+Acc:миянӧс # ;
+GenStem:миян persplobl ;
!!Extras form 2000 Grammar: 163
+GenStem+Gen:миянлӧн # ; !Synomymous with: миян
+GenStem+PxPl1+Acc:мияннымӧс # ; !Synomymous with: миянӧс
+GenStem+Ela+PxPl1:миянсьыным # ; !Synomymous with:миянлысь
+Com:микӧд # ; !Synomymous with: миянкӧд
+GenStem+PxPl1+Com:мияннымкӧд # ; !Synomymous with: миянкӧд
+Car:митӧг # ; !Synomymous with: миянтӧг
+GenStem+Car+PxPl1:миянтӧгным # ; !Synomymous with: миянтӧг
+GenStem+PxPl1+Cns:мияннымла # ; !Synomymous with: миянла
+Ela:миысь # ; !Synomymous with: миянысь
+Ill:миӧ # ; !Synomymous with: миянӧ
+GenStem+Apr+PxPl1:миянланьным # ; !Synomymous with: миянлань
+Apr+PxPl1:миланьным # ; !Synomymous with: миянлань
+GenStem+Egr+PxPl1:миянсяньным # ; !Synomymous with: миянсянь
+Tra:миті # ; !Synomymous with: миянӧд
+Ter:миӧдз # ; !Synomymous with: миянӧдз

LEXICON perssg2decl
+Nom:тэ # ;
+Cmpl:тэ # ;
+Cmpl+Gen:тэлӧн # ;
+GenStem:тэнад # ;
+GenStem+Gen:тэнадлӧн # ;
+Ela+PxSg2:тэнсьыд # ;
+Dat:тэныд # ;
+Dat:тэн # ;
+Dat:тэд # ;
+Dat:тэдлы # ;
+Dat:тэнлы # ;
+Dat:тэныдлы # ;
+Acc:тэнӧ # ;
+GenStem+Instr:тэнадӧн # ;
+GenStem+Com:тэнакӧд # ;
+GenStem+Car:тэнатӧг # ;
+GenStem+Ine:тэнадын # ;
+GenStem+Ela:тэнадысь # ;
+GenStem+Ill:тэнадӧ # ;
+GenStem+Egr:тэнадсянь # ;
+GenStem+Ter:тэнадӧдз # ;
+Cmpl:тэ perssg ;  ! for the regular obl sg

LEXICON perspl2decl
+Nom:ті # ;
+Cmpl:ті # ;
+Der/йӧ+Nom:тійӧ # ; !We need a special tag for discerning +Nom forms
+GenStem:тіян # ;
+GenStem+Gen:тіянлӧн # ;
+Acc:тіянӧс # ;
+GenStem:тіян persplobl ;
!More forms from Ӧнія коми кыв 2000: 163
+GenStem+Abl+PxPl2:тіянлысьныд # ;
+GenStem+Dat:тіянлы # ;
+GenStem+PxPl2+PxSg2+Acc2:тіянныдтӧ # ;
+Instr:тіӧн # ;
+GenStem+PxPl2+Car:тіянныдтӧг # ;
+Car:тітӧг # ;
+GenStemPxPl2+Cns:тіянныдла # ;
+Ela:тіысь # ;
+Ill:тіӧ # ;
+GenStem+Apr+PxPl2:тіянланьныд # ;
+GenStem+Apr+PxSg2:тіянланьыд # ;
+GenStem+Egr+PxPl2:тіянсяньныд # ;
+Ter:тіӧдз # ;

LEXICON perssg3decl
+Der/йӧ+Nom:сійӧ # ;
+Cmpl+Gen:сылӧн # ;
+Cmpl+Abl:сылысь # ;
+Cmpl+Ela:сыысь # ;
+Cmpl+Dat:сылы # ;
+Der/йӧ+Acc:сійӧс # ;
+Der/йӧ+Instr:сійӧн # ;
+Cmpl:сы perssg ;  ! for the regular obl sg
+Cmpl:сы # ;
!More form ӧния коми кыв 163
+Der/йӧ+Gen:сійӧлӧн # ;
+Der/йӧ+Abl:сійӧлысь # ;
+Der/йӧ+Dat:сійӧлы # ;
+Der/йӧ+Com:сійӧкӧд # ;
+Der/йӧ+Car:сійӧтӧг # ;
+Der/йӧ+Apr:сійӧлань # ;
+Der/йӧ+Egr:сійӧсянь # ;
+Der/йӧ+Egr+PxSg3:сійӧсяньыс # ;
+Der/йӧ+Ter:сійӧӧдз # ;

!!ми мийӧ The 1st and 2nd persons have Oblique case stem strategies
!! that differ from the 3rd person: 
!!ті тійӧ nämä ovat aivan eri asioita
!!сы сійӧ tosin joskus 

LEXICON perspl3decl
+Der/йӧ+Nom:найӧ # ;
+Der/йӧ+Acc:найӧс # ;
+Cmpl+Gen:налӧн # ;
+Cmpl:на persplobl ; !This might be one solution for the different stem types
+Cmpl:на # ;
!
+Der/йӧ+Gen:найӧлӧн # ;
+Der/йӧ+Abl:найӧлысь # ;
+Der/йӧ+Dat:найӧлы # ;
+Der/йӧ+Instr:найӧӧн # ;
+Der/йӧ+Com:найӧкӧд # ;
+Cmpl+Com:накӧдыс # ;
+Der/йӧ+Car:найӧтӧг # ;
+Cmpl+Car+PxSg3:натӧгыс # ;
+Cmpl+PxSg3+Cns:наысла # ;
+Cmpl+Ela+PxSg3:насьыс # ;
+Cmpl+PxSg3+Apr:наыслань # ;
+Cmpl+Apr+PxSg3:наланьыс # ;
+Cmpl+PxSg3+Egr:наыссянь # ;
+Cmpl+Egr+PxSg3:насяньыс # ;
+Cmpl+Tra+PxSg3:натіыс # ;
+Cmpl+Ter+PxSg3:наӧдзыс # ;


LEXICON perssg 
+Instr:ӧн # ;
+Com:кӧд # ;
+Car:тӧг # ;
+Cns:ла # ;
+Ine:ын # ;
+Ill:ӧ # ; !
+Ela:ысь # ;
+Apr:лань # ;
+Egr:сянь # ;
+Prl:ӧд # ;
+Tra:ті # ;
+Ter:ӧдз # ;
+Abl:лысь # ; !This is not attested for Sg1 and Sg2 in the 2000 Grammar



LEXICON persplobl
+Abl:лысь # ;
+Dat:лы # ;
+Acc:ӧс # ;

+Instr:ӧн # ;
+Com:кӧд # ;
+Car:тӧг # ;
+Cns:ла # ;
+Comp:ся # ;
+Ine:ын # ;
+Ela:ысь # ;
+Ill:ӧ # ;
+Apr:лань # ;
+Egr:сянь # ;
+Prl:ӧд # ;
+Tra:ті # ;
+Ter:ӧдз # ;
+Apr+Ine:ланьын # ;  ! New cases in last grammar  
+Apr+Ela:ланьысь # ;  ! 
+Apr+Ill:ланьӧ # ; ! 
+Apr+Egr:ланьсянь # ; ! 
+Apr+Prl:ланьӧд # ; !
+Apr+Tra:ланьті # ; !       
+Apr+Ter:ланьӧдз # ; ! 

! These ones to be removed:
!!! LEXICON PRON-PERS-PL1-ACC
!!! +Pron+Pers+Pl1+Acc: # ;
!!! LEXICON PRON-PERS-PL1-COM
!!! +Pron+Pers+Pl1+Com: # ;
!!! LEXICON PRON-PERS-PL1-DAT
!!! +Pron+Pers+Pl1+Dat: # ;
!!! LEXICON PRON-PERS-PL1-GEN
!!! +Pron+Pers+Pl1+Gen: # ;
!!! LEXICON PRON-PERS-PL1-NOM
!!! +Pron+Pers+Pl1+Nom: # ;
!!! LEXICON PRON-PERS-PL2-ACC
!!! +Pron+Pers+Pl2+Acc: # ;
!!! LEXICON PRON-PERS-PL2-COM
!!! +Pron+Pers+Pl2+Com: # ;
!!! LEXICON PRON-PERS-PL2-DAT
!!! +Pron+Pers+Pl2+Dat: # ;
!!! LEXICON PRON-PERS-PL2-GEN
!!! +Pron+Pers+Pl2+Gen: # ;
!!! LEXICON PRON-PERS-PL2-NOM
!!! +Pron+Pers+Pl2+Nom: # ;
!!! LEXICON PRON-PERS-PL3-ACC
!!! +Pron+Pers+Pl3+Acc: # ;
!!! LEXICON PRON-PERS-PL3-COM
!!! +Pron+Pers+Pl3+Com: # ;
!!! LEXICON PRON-PERS-PL3-DAT
!!! +Pron+Pers+Pl3+Dat: # ;
!!! LEXICON PRON-PERS-PL3-ELA
!!! +Pron+Pers+Pl3+Ela: # ;
!!! LEXICON PRON-PERS-PL3-GEN
!!! +Pron+Pers+Pl3+Gen: # ;
!!! LEXICON PRON-PERS-PL3-NOM
!!! +Pron+Pers+Pl3+Nom: # ;
!!! LEXICON PRON-PERS-SG1-ACC
!!! +Pron+Pers+Sg1+Acc: # ;
!!! LEXICON PRON-PERS-SG1-DAT
!!! +Pron+Pers+Sg1+Dat: # ;
!!! LEXICON PRON-PERS-SG1-ELA
!!! +Pron+Pers+Sg1+Ela: # ;
!!! LEXICON PRON-PERS-SG1-GEN
!!! +Pron+Pers+Sg1+Gen: # ;
!!! LEXICON PRON-PERS-SG1-NOM ! tähän tulee ме
!!! +Pron+Pers+Sg1+Nom: # ;
!!! LEXICON PRON-PERS-SG2-ACC
!!! +Pron+Pers+Sg2+Acc: # ;
!!! LEXICON PRON-PERS-SG2-COM
!!! +Pron+Pers+Sg2+Com: # ;
!!! LEXICON PRON-PERS-SG2-DAT
!!! +Pron+Pers+Sg2+Dat: # ;
!!! LEXICON PRON-PERS-SG2-ELA
!!! +Pron+Pers+Sg2+Ela: # ;
!!! LEXICON PRON-PERS-SG2-GEN
!!! +Pron+Pers+Sg2+Gen: # ;
!!! LEXICON PRON-PERS-SG2-NOM
!!! +Pron+Pers+Sg2+Nom: # ;
!!! LEXICON PRON-PERS-SG2-ABL
!!! +Pron+Pers+Sg2+Abl: # ;
!!! LEXICON PRON-PERS-SG3-ACC
!!! +Pron+Pers+Sg3+Acc: # ;
!!! LEXICON PRON-PERS-SG3-COM
!!! +Pron+Pers+Sg3+Com: # ;
!!! LEXICON PRON-PERS-SG3-DAT
!!! +Pron+Pers+Sg3+Dat: # ;
!!! LEXICON PRON-PERS-SG3-ELA
!!! +Pron+Pers+Sg3+Ela: # ;
!!! LEXICON PRON-PERS-SG3-GEN
!!! +Pron+Pers+Sg3+Gen: # ;
!!! LEXICON PRON-PERS-SG3-NOM
!!! +Pron+Pers+Sg3+Nom: # ;
!!! LEXICON PRON-PERS-SG3-ABL
!!! +Pron+Pers+Sg3+Abl: # ;
!!! LEXICON PRON-PERS-SG3-OBL
!!! +Pron+Pers+Sg3+Dat:%>лы # ;
!!! +Pron+Pers+Sg3+Abl:%>лысь # ;


!Interrogative pronouns serve as the base of interrogative, relative, indefinite and distributional pronouns.

LEXICON INTERR
кин+Pron+Hum+Nom:кин INTERR-REL ; ! 2011-11-09 This will need cases
кодар+Pron+A:кодар INTERR-REL-INDEF ;
кодар+Pron+A+Der/MWN:кодар PRON-HUM-OBL ;
кодар+Pron+A+Der/ладор:кодарладор PRON-HUM-OBL ;
 !
коді:код PRON-HUM ;

кодыр+Pron+Adv+Temp:кодыр INTERR-REL-INDEF ;
кор+Pron+Adv+Temp:кор INTERR-REL-INDEF_COMPARISON ;
кор+Pron+Adv+Temp+Pl+Ill:коръясӧ INTERR-REL-INDEF ;
коръя+Pron+Adv+Temp:коръя INTERR-REL-INDEF ;
кудз+Pron+Adv+Manner+Use/Sub:кудз INTERR-REL-INDEF_COMPARISON ;
кутшӧм+Pron+A:кутшӧм INTERR-REL-INDEF_COMPARISON ; ! This needs to be 
кутшӧма+Pron+Adv+Manner:кутшӧма INTERR-REL-INDEF ; ! This needs to be 
кутшӧми+Pron+A:кутшӧми INTERR-REL-INDEF ; ! This needs to be 
кыдз+Pron+Adv+Manner:кыдз INTERR-REL-INDEF_COMPARISON ;
кыдзи+Pron+Adv+Manner:кыдзи INTERR-REL-INDEF_COMPARISON ;
кыдзик+Pron+Adv+Manner:кыдзик INTERR-REL-INDEF_COMPARISON ;
кыдзик+Pron+Adv+Manner+Instr:кыдзикӧн INTERR-REL-INDEF ;
кымын+Pron+Qnt:кымын INTERR-REL-INDEF_COMPARISON ; ! Quantifier info required
кымын+Pron+Qnt+Iter:кымынысь INTERR-REL-INDEF_COMPARISON ; ! Quantifier info required
кымын+Pron+Qnt+Ord:кымынӧд INTERR-REL-INDEF_COMPARISON ; ! Quantifier info required
кымын+Pron+Qnt+Ord+Iter:кымынӧдысь INTERR-REL-INDEF_COMPARISON ; ! Quantifier info required
кысь+Pron+Spat+Ela:кысь INTERR-REL-INDEF_COMPARISON ;
кытчӧ+Pron+Spat+Ill:кытчӧ INTERR-REL-INDEF_COMPARISON ;
кытчӧдз+Pron+Spat+Ter:кытчӧдз INTERR-REL-INDEF_COMPARISON ;
кытысь+Pron+Spat+Ela:кытысь INTERR-REL-INDEF_COMPARISON ;
кыті+Pron+Spat+Tra:кыті INTERR-REL-INDEF_COMPARISON ;
кытӧн+Pron+Spat+Ine:кытӧн INTERR-REL-INDEF_COMPARISON ;
кытӧні+Pron+Spat+Ine:кытӧні INTERR-REL ;
 !
кӧні+Pron+Adv+Spat+Ine+Pred+Sg:кӧні INTERR-REL-INDEF_COMPARISON ;
кӧні+Pron+Adv+Spat+Ine+Pred+Pl:кӧнӧсь INTERR-REL ;
кӧні+Pron+Adv+Spat+Ine:кӧн PRON-INDEF ;
мый+Pron+NonHum:мый PRON-NONHUM ;
мыйда+Pron+NonHum:мыйда INTERR-REL-INDEF ;
мыйта+Pron+NonHum:мыйта INTERR-REL-INDEF ;
мыйтӧм+Pron+NonHum:мыйтӧм INTERR-REL-INDEF ;
мыйӧс+Pron+NonHum+Use/Sub:мыйӧс INTERR-REL-INDEF ;
!Here are some negative pronouns out of place 2011-11-22
некысь+Pron+Neg+Spat+Ela:некысь K ;
некысянь+Pron+Neg+Spat+Egr:некысянь K ;
некыт+Pron+Neg+Spat+Tra:некыт K ;
некытчӧ+Pron+Neg+Spat+Ill:некытчӧ K ;
некытчӧдз+Pron+Neg+Spat+Ter:некытчӧдз K ;
некытысь+Pron+Neg+Spat+Ela:некытысь K ;
некыті+Pron+Neg+Spat+Tra:некыті K ;
некытӧн+Pron+Neg+Spat+Ine:некытӧн K ;
некӧн+Pron+Neg+Spat+Ine:некӧн K ;



LEXICON PRON-HUM
+Pron+Sg+Nom:і INTERR-REL ;
+Pron+Pl+Nom:ъяс INTERR-REL-INDEF ;
+Pron+Sg+Nom: INTERR-REL-INDEF ;
+Pron+Sg: PRON-HUM-OBL ;
+Pron+Pl:ъяс PRON-HUM-OBL ;
+Pron: PRON-HUM-COLL ;
!код, коді

LEXICON PRON-HUM-OBL
! 2000: 187
+Cmpl: INTERR-REL-INDEF ;
+Gen:лӧн INTERR-REL-INDEF ;
+Abl:лысь INTERR-REL-INDEF ; !
+Dat:лы INTERR-REL-INDEF ;
+Acc:ӧс INTERR-REL-INDEF ;
+Acc3:сӧ INTERR-REL-INDEF ;
+Instr:ӧн INTERR-REL-INDEF ;
+Com:кӧд INTERR-REL-INDEF ;
+Car:тӧг INTERR-REL-INDEF ;
+Cns:ла INTERR-REL-INDEF ;
+Ine:ын INTERR-REL-INDEF_COMPARISON ;
+Ela:ысь INTERR-REL-INDEF_COMPARISON ; 
+Ill:ӧ INTERR-REL-INDEF_COMPARISON ; !
+Apr:лань INTERR-REL-INDEF ;
+Egr:сянь INTERR-REL-INDEF ;
+Tra:ті INTERR-REL-INDEF ;
+Ter:ӧдз INTERR-REL-INDEF ;

LEXICON PRON-HUM-COLL
+Coll:нан PRON-HUM-COLL-PERS ;
+ZeroColl: PRON-HUM-COLL-PERS ;
!
+ZeroColl+PxPl2+Acc2:ныдтӧ INTERR-REL-INDEF ;
+Coll+PxPl2+Acc2:наннытӧ INTERR-REL-INDEF ;

LEXICON PRON-HUM-COLL-PERS
+PxPl1:ным PRON-HUM-COLL-CASES ;
+PxPl2:ныд PRON-HUM-COLL-CASES ;
+PxPl3:ныс PRON-HUM-COLL-CASES ;
!
+PxPl1+Acc:нымӧс INTERR-REL-INDEF ;
+PxPl2+Acc2:нытӧ INTERR-REL-INDEF ;
+PxPl3+Acc3:ныссӧ INTERR-REL-INDEF ;

LEXICON PRON-HUM-COLL-CASES
+Nom: INTERR-REL-INDEF ;
 PRON-HUM-OBL ;

LEXICON PRON-NONHUM
+Nom: INTERR-REL-INDEF ;
+Gen:лӧн INTERR-REL-INDEF ; !
+Abl:лысь INTERR-REL-INDEF ; !
+Dat:лы INTERR-REL-INDEF ; !
+ZeroAcc: INTERR-REL-INDEF ;
+Acc3:сӧ INTERR-REL-INDEF ;
+Instr:ӧн INTERR-REL-INDEF ;
+Com:кӧд INTERR-REL-INDEF ;
+Car:тӧг INTERR-REL-INDEF ;
+Comp:ся INTERR-REL-INDEF ;
+Cns:ла INTERR-REL-INDEF ;
+Ine:ын INTERR-REL-INDEF_COMPARISON ;
+Ela:ысь INTERR-REL-INDEF_COMPARISON ; 
+Ill:ӧ INTERR-REL-INDEF_COMPARISON ; !
+Apr:лань INTERR-REL-INDEF ;
+Egr:сянь INTERR-REL-INDEF ;
+Prl:ӧд INTERR-REL-INDEF ;
+Tra:ті INTERR-REL-INDEF ;
+Ter:ӧдз INTERR-REL-INDEF ;

LEXICON INTERR-REL-INDEF_COMPARISON
+Compar:%>джык INTERR-REL ;
+NoComparison:%>джык INTERR-REL ;

LEXICON INTERR-REL-INDEF ! 3 types of pronouns are congregated here
 PRON-INDEF ; ! This leads to a variety of indefinite pronouns
 INTERR-REL ; ! This leads to a split between relative and interrogative pronouns

LEXICON INTERR-REL
+Interr: COMPARATIVE ;
+Rel: K ;

LEXICON PRON-INDEF
+Der/кӧ+Indef:%>кӧ K ;
+Der/сюрӧ+Indef:%>сюрӧ K ;


! Indefinite pronouns
 
LEXICON PRONINDEFTAG
+Pron+Indef: # ;

LEXICON PRONINDEFTAG-KO
+Pron: PRONINDEF-PERS-KO-CX ;

LEXICON PRONINDEF-PERS-KO-CX
+Nom: PRONINDEF-PERS-KO-END ;
+Acc:%>ӧс PRONINDEF-PERS-KO-END ;
+Gen:%>лӧн PRONINDEF-PERS-KO-END ;
+Abl:%>лысь PRONINDEF-PERS-KO-END ;
+Dat:%>лы PRONINDEF-PERS-KO-END ;
+Instr:%>ӧн PRONINDEF-PERS-KO-END ;
+Ela:%>ысь PRONINDEF-PERS-KO-END ; !2011-10-19 It might be that this only has to do with мый _what_ and мыйыськӧ _for some reason_
+Com:%>кӧд PRONINDEF-PERS-KO-END ;
+Egr:%>сянь PRONINDEF-PERS-KO-END ;

LEXICON PRONINDEF-PERS-KO-END
+Der/кӧ+Indef:%>кӧ K ;
+Der/сюрӧ+Indef:%>сюрӧ K ; !Basically both endings can be used for deriving 
                     !indefinite pronouns from the interrogative/relative pronouns.

!!LEXICON PRONINDEF-PERS-END
!!+Der/сюрӧ:%>сюрӧ # ;

!!LEXICON PRONINDEF-PERS-KO
!!+Nom: PRONINDEF-PERS-KO-END ;
!!+Acc:%>ӧс PRONINDEF-PERS-KO-END ;
!!+Gen:%>лӧн PRONINDEF-PERS-KO-END ;
!!+Dat:%>лы PRONINDEF-PERS-KO-END ;
!!!!+Acc:%>ӧс PRONINDEF-PERS-KO-END ;


LEXICON PRON-SPAT
+Pron+Spat: COMPARATIVE ;

LEXICON PRON-RECIPR
+Pron+Recipr: PRON-RECIPR-CASE ;

LEXICON PRON-RECIPR-CASE
+Cmpl: # ;
+PxSg2+Cmpl:ыд ;
+PxSg2+Cmpl:ыс ;
+PxPl1+Cmpl:ным ;
+PxPl2+Cmpl:ныд ;
+PxPl3+Cmpl:ныс ;
+Gen:лӧн ;
+PxSg3+Abl:ыслысь ;
+PxPl3+Abl:ныслысь ;
+Dat:лы ;
+PxSg3+Dat:ыслы ;
+PxPl3+Dat:ныслы ;
+Acc:ӧс ;
+PxPl1+Acc:нымӧс ;
+PxPl2+Acc2:нытӧ ;
+PxPl3+Acc3:ныссӧ ;
+Acc3:сӧ ;
+PxSg3+Com:ыскӧд ;
+PxPl2+Com:ныдкӧд ;
+PxPl3+Com:ныскӧд ;
+Car+PxSg3:тӧгыс ;
+Ela:ысь ;
+Ela+PxSg3:сьыс ;
+Ela+PxPl3:сьыныс ;
+PxSg3+Ill:ас ; ! Here is a good problem: Should there be a special tag
                ! for the illative-inessive forms
+PxPl3+Ill:нысӧ ;


LEXICON PRON-REL
+Pron+Rel: # ;

LEXICON PRON-REFL-IN_LADOR
+Pron+Refl+Der/ладор: Noun1 ; !2011-11-22

LEXICON EndRefl
+Pron+Refl+Abs: # ; !ас is an absolute-form modifer
 PRON-REFL-OBL-CASES ;

LEXICON PRON-REFL-OBL-CASES
+Ela+PxSg1:сьым K ;
+Ela+PxSg2:сьыд K ;
+Ela+PxSg3:сьыс K ;
+Ela+PxPl1:сьыным K ;
+Ela+PxPl1+Abl:сьынымлысь K ;
+Ela+PxPl1+Ela:сьынымысь K ;
+Ela+PxPl2:сьыныд K ;
+Ela+PxPl3:сьыныс K ;
+Dat+PxSg1:лым K ;
+Dat+PxSg2:лыд K ;
+Dat+PxSg3:лыс K ;
+Dat+PxPl1:лыным K ;
+Dat+PxPl2:лыныд K ;
+Dat+PxPl2+Dat:лыныдлы K ;
+Dat+PxPl3:лыныс K ;
+Dat+PxPl3+Dat:лыныслы K ;
+PxPl3+Dat:ныслы K ;
+Sg1+Instr:ӧн K ; !There is no indication of person. = Sg1, Pl3
+Pl3+Instr:ӧн K ; !There is no indication of person. = Sg1, Pl3
+Instr+PxSg1+Instr:намӧн K ;
+Instr+PxSg2:над K ;
+Instr+PxSg3:нас K ;
+Instr+PxPl1:наным K ;
+Instr+PxPl2:наныд K ;
+Instr+PxPl3:наныс K ;
+Sg1+Com:кӧд K ; !There is no indication of person. = Sg1
+Com+PxSg2:кӧдыд K ; !
+Com+PxSg3:кӧдыс K ; !
+Com+PxPl1:кӧдным K ; !
+Com+PxPl2:кӧдныд K ; !
+PxPl2+Com:ныдкӧд K ; !
+Com+PxPl3:кӧдныс K ; !
+PxPl3+Com:ныскӧд K ; !
+Sg1+Car:тӧг K ; !There is no indication of person. = Sg1
+Car+PxSg2:тӧгыд K ; !
+Car+PxSg3:тӧгыс K ; !
+Car+PxPl1:тӧгным K ; !
+Car+PxPl2:тӧгныд K ; !
+PxPl2+Car:ныдтӧг K ; !
+Car+PxPl3:тӧгныс K ; !
+PxPl3+Car:ныстӧг K ; !
+Sg1+Cons:ла K ; !There is no indication of person. = Sg1
+Cons+PxSg2:лаыд K ;
+PxSg2+Cons:ыдла K ;
+PxSg3+Cons:ысла K ;
+PxPl2+Cons:ныдла K ;
+PxPl3+Cons:нысла K ;
+Ine+PxSg2:ад K ;
+Ine+PxSg3:ас K ;
+Ine+PxPl1:аным K ;
+Ine+PxPl2:аныд K ;
+Ine+PxPl3:аныс K ;
+Sg1+Ill:ӧ K ; !There is no indication of person. = Sg1
+Ill+PxSg1:ам K ; !Ӧнія коми кыв gives this for illative only
+Ill+PxSg1:ад K ; !
+Ill+PxSg1:ас K ; !
+Ill+PxSg1:аным K ; !
+Ill+PxSg1:аныд K ; !
+Ill+PxSg1:аныс K ; !
+Sg1+Apr:лань K ; !There is no indication of person. = Sg1
+Apr+PxSg2:ланьыд K ;
+Apr+PxSg3:ланьыс K ;
+Apr+PxPl1:ланьным K ;
+Apr+PxPl2:ланьныд K ;
+Apr+PxPl3:ланьныс K ;
+Sg1+Egr:сянь K ; !There is no indication of person. = Sg1
+Egr+PxSg2:сяньыд K ;
+Egr+PxSg3:сяньыс K ;
+Egr+PxPl1:сяньным K ;
+Egr+PxPl2:сяньныд K ;
+Egr+PxPl3:сяньныс K ;
+Sg1+Tra:ті K ; !There is no indication of person. = Sg1
+Tra+PxSg2:тіыд K ;
+Tra+PxSg3:тіыс K ;
! Pl1 and Pl2 are not attested in Ӧнія коми кыв
+Tra+PxPl3:тіныс K ;
+Sg1+Ter:ӧдз K ; !There is no indication of person. = Sg1
+Ter+PxSg2:ӧдзыд K ;
+Ter+PxSg3:ӧдзыс K ;
+PxPl1+Ter:нымӧдз K ;
+PxPl2+Ter:ныдӧдз K ;
+PxPl3+Ter:нысӧдз K ;
!Extras 2011-10-07


LEXICON PRON-REFL-SG1-NOM
+Pron+Refl+Sg1+PxSg1+Nom: # ;
+Pron+Refl+Sg1+PxSg1+Acc:ӧс # ;
+Pron+Refl+Sg1+PxSg1+Instr:ӧн # ;
+Pron+Refl+Sg1+PxSg1+Com:кӧд # ;
+Pron+Refl+Sg1+PxSg1+Car:тӧг # ;
+Pron+Refl+Sg1+PxSg1+Ine:ын # ;
+Pron+Refl+Sg1+PxSg1+Ela:ысь # ;
+Pron+Refl+Sg1+PxSg1+Ill:ӧ # ;


LEXICON PRON-REFL-SG1-GEN
+Pron+Refl+Gen+PxSg1: # ;
+Pron+Refl+Gen+PxSg1+Com:кӧд # ;
+Pron+Refl+Gen+PxSg1+Egr:сянь # ;

LEXICON PRON-REFL-SG2-NOM
+Pron+Refl+Nom+PxSg2: # ;
+Pron+Refl+Nom+PxSg2+Car:тӧг # ;
+Pron+Refl+Nom+PxSg2+Ela:ысь # ;

LEXICON PRON-REFL-SG2-GEN
+Pron+Refl+Gen+PxSg2: # ;
+Pron+Refl+Gen+PxSg2+Erg:сянь # ;

LEXICON PRON-REFL-SG2-ACC
+Pron+Refl+Sg2+Acc2: # ;

LEXICON PRON-REFL-SG3-NOM
+Pron+Refl+Sg3+Nom: # ;
+Pron+Refl+Sg2+PxSg2+Instr:ӧн # ;
+Pron+Refl+Sg2+PxSg2+Com:кӧд # ;
+Pron+Refl+Sg2+PxSg2+Car:тӧг # ;
+Pron+Refl+Sg1+PxSg2+Ela:ысь # ;

LEXICON PRON-REFL-SG3-GEN
+Pron+Refl+Gen+PxSg3: # ;
+Pron+Refl+Gen+PxSg3+Gen:лӧн # ;
+Pron+Refl+Gen+PxSg3+Ine:ӧ # ;

LEXICON PRON-REFL-SG3-ACC
+Pron+Refl+Sg3+Acc3: # ;

LEXICON PRON-REFL-PL1-NOM
+Pron+Refl+Nom+PxPl1: # ;
+Pron+Refl+PxPl1+Acc:ӧс # ;
+Pron+Refl+PxPl1+Com:кӧд # ;
+Pron+Refl+PxPl1+Car:тӧг # ;
+Pron+Refl+PxPl1+Ela:ысь # ;
+Pron+Refl+PxPl1+Ter:ӧдз # ;

LEXICON PRON-REFL-PL1-GEN
+Pron+Refl+Gen+PxPl1: # ;
+Pron+Refl+Gen+PxPl1+Abl:лысь # ;
+Pron+Refl+Gen+PxPl1+Der/ладор:%>ладор Noun1 ;

LEXICON PRON-REFL-PL2-NOM
+Pron+Refl+Nom+PxPl2: # ;
+Pron+Refl+Nom+PxPl2+Acc2:тӧ # ;
+Pron+Refl+Nom+PxPl2+Cns:ла # ;
+Pron+Refl+Nom+PxPl2+Car:тӧг # ;
+Pron+Refl+Nom+PxPl2+Ela:ысь # ;

LEXICON PRON-REFL-PL2-GEN
+Pron+Refl+Gen+PxPl2: # ;
+Pron+Refl+Gen+PxPl2+Gen: # ;
+Pron+Refl+Gen+PxPl2+Der/ладор:%>ладор Noun1 ;


LEXICON PRON-REFL-PL2-ACC
+Pron+Refl+Pl2+Acc2: # ;

LEXICON PRON-REFL-PL3-NOM
+Pron+Refl+Nom+PxPl3: # ;
+Pron+Refl+Nom+PxPl3+Cns:ла # ;

LEXICON PRON-REFL-PL3-GEN
+Pron+Refl+Gen+PxPl3: # ;
+Pron+Refl+Gen+PxPl3+Gen:лӧн # ;
+Pron+Refl+Gen+PxPl3+Prl:ӧд # ;
+Pron+Refl+Gen+PxPl3+Der/ладор:%>ладор Noun1 ;

LEXICON PRON-REFL-PL3-ACC
+Pron+Refl+Pl3+Acc3: # ;


LEXICON PRONDEMTAG
+Pron+Dem: # ; ! sg/pl?

LEXICON PRONDEMTAG-ES
+Pron+Dem+Sg+Nom:%>ійӧ K ;
+Pron+Dem+Sg+Acc:%>ійӧс K ;
+Pron+Dem+Sg+Instr:%>ійӧн K ;
+Pron+Dem+Sg:%>ы PRONDEMTAGSG ;
+Pron+Dem+Pl:%>ійӧяс # ;
+Pron+Dem+Pl:%>ійяс # ;
+Pron+Dem+Pl:%>ійӧян # ;
+Pron+Dem+Pl:%>ійян # ;

LEXICON PRONDEMTAGSG-TA
+Pron+Dem+Sg+Nom:%>йӧ K ;
+Pron+Dem+Sg+Acc:%>йӧс K ;
+Pron+Dem+Sg+Instr:%>йӧн K ;
+Pron+Dem+Sg: PRONDEMTAGSG ;
+Pron+Dem+Pl:%>йӧяс PRONDEMTAGPL ;
+Pron+Dem+Pl:%>йяс PRONDEMTAGPL ;
+Pron+Dem+Pl:%>йӧян PRONDEMTAGPL ;
+Pron+Dem+Pl:%>йян PRONDEMTAGPL ;

LEXICON PRONDEMTAGSG 
+Cmpl: # ;
+Instr:ӧн K ;
+Com:кӧд K ;
+Car:тӧг K ;
+Cns:ла K ;
+Ine:ын K ;
+Ela:ысь K ; 
+Ill:ӧ K ; !This is questioned in the 2000 Grammar
+Abl:лысь K ; !
 PRONDEMTAG-APR ;
 PRONDEMTAG-EGR ;
 PRONDEMTAG-PRL ;
 PRONDEMTAG-TRA ;
 PRONDEMTAG-TER ;


LEXICON PRONDEMTAGPL
!+Pron+Dem+Pl: # ;
+Nom: # ;
+Gen:лӧн # ;
+Acc:ӧс # ;
+Instr:ӧн K ;
+Com:кӧд K ;
+Car:тӧг K ;
+Cns:ла K ;
+Ine:ын K ;
+Ela:ысь K ; 
+Ill:ӧ K ; !
+Abl:лысь K ; !
 PRONDEMTAG-APR ;
 PRONDEMTAG-APR-EGR ;
 PRONDEMTAG-APR-ELA ;
 PRONDEMTAG-APR-ILL ;
 PRONDEMTAG-APR-INE ;
 PRONDEMTAG-EGR ;
 PRONDEMTAG-PRL ;
 PRONDEMTAG-TRA ;
 PRONDEMTAG-TER ;

LEXICON PRONDEMTAG-ELA
+Ela:ысь K ;

LEXICON PRONDEMTAG-APR
+Apr:лань K ;

LEXICON PRONDEMTAG-APR-EGR
+Apr:ланьсянь K ;

LEXICON PRONDEMTAG-APR-ELA
+Apr:ланьысь K ;

LEXICON PRONDEMTAG-APR-ILL
+Apr:ланьӧ K ;

LEXICON PRONDEMTAG-APR-INE
+Apr:ланьын K ;

LEXICON PRONDEMTAG-EGR
+Egr:сянь K ;

LEXICON PRONDEMTAG-PRL
+Prl:ӧд K ;

LEXICON PRONDEMTAG-TRA
+Tra:ті K ;

LEXICON PRONDEMTAG-TER
+Ter:ӧдз K ;



LEXICON PRON-INTER-NONHUM
+Pron+Interr+Nom: # ;
+Pron+Interr+Gen:лӧн K ; !
+Pron+Interr+Abl:лысь K ; !
+Pron+Interr+Dat:лы K ; !
+Pron+Interr+ZeroAcc: # ;
+Pron+Interr+Acc3:сӧ # ;
+Pron+Interr+Instr:ӧн K ;
+Pron+Interr+Com:кӧд K ;
+Pron+Interr+Car:тӧг K ;
+Pron+Interr+Cns:ла K ;
+Pron+Interr+Ine:ын K ;
+Pron+Interr+Ela:ысь K ; 
+Pron+Interr+Ill:ӧ K ; !
+Pron+Interr+Apr:лань K ;
+Pron+Interr+Egr:сянь K ;
+Pron+Interr+Prl:ӧд K ;
+Pron+Interr+Tra:ті K ;
+Pron+Interr+Ter:ӧдз K ;

LEXICON PRON-NEG-HUM
+Pron+Neg+Sg+Nom: # ;
+Pron+Neg+Sg: PRON-NEG-OBL ;

LEXICON PRON-DUAL
+Pron+Du: PRON-NEG-OBL ;

!!! LEXICON PRON-INTER-REL-HUM-COLL
!!! +Coll:нан PRON-INTER-REL-HUM-COLL-PERS ;
!!! +ZeroColl: PRON-INTER-REL-HUM-COLL-PERS ;
!!! +ZeroColl+PxPl2+Acc2:ныдтӧ PRON-INTER-REL-HUM-COLL-INDEF ;
!!! +Coll+PxPl2+Acc2:наннытӧ PRON-INTER-REL-HUM-COLL-INDEF ;
!!! 
!!! LEXICON PRON-INTER-REL-HUM-COLL-PERS
!!! +PxPl1:ным PRON-INTER-REL-HUM-COLL-CASES ;
!!! +PxPl2:ныд PRON-INTER-REL-HUM-COLL-CASES ;
!!! +PxPl3:ныс PRON-INTER-REL-HUM-COLL-CASES ;
!!! +PxPl1+Acc:нымӧс PRON-INTER-REL-HUM-COLL-INDEF ;
!!! +PxPl3+Acc3:ныссӧ PRON-INTER-REL-HUM-COLL-INDEF ;
!!! 
!!! LEXICON PRON-INTER-REL-HUM-COLL-CASES
!!! +Nom: PRON-INTER-REL-HUM-COLL-INDEF ;
!!! +Gen:лӧн PRON-INTER-REL-HUM-COLL-INDEF ;
!!! +Abl:лысь PRON-INTER-REL-HUM-COLL-INDEF ;
!!! +Dat:лы PRON-INTER-REL-HUM-COLL-INDEF ;
!!! +Instr:ӧн PRON-INTER-REL-HUM-COLL-INDEF ;
!!! +Com:кӧд PRON-INTER-REL-HUM-COLL-INDEF ;
!!! +Car:тӧг PRON-INTER-REL-HUM-COLL-INDEF ;
!!! +Cns:ла PRON-INTER-REL-HUM-COLL-INDEF ;
!!! +Ine:ын PRON-INTER-REL-HUM-COLL-INDEF ;
!!! +Ela:ысь PRON-INTER-REL-HUM-COLL-INDEF ;
!!! +Ill:ӧ PRON-INTER-REL-HUM-COLL-INDEF ;
!!! +Apr:лань PRON-INTER-REL-HUM-COLL-INDEF ;
!!! +Egr:сянь PRON-INTER-REL-HUM-COLL-INDEF ;
!!! +Tra:ті PRON-INTER-REL-HUM-COLL-INDEF ;
!!! +Ter:ӧдз PRON-INTER-REL-HUM-COLL-INDEF ;
!!! 
!!! LEXICON PRON-INTER-REL-HUM-COLL-INDEF
!!! +Der/кӧ+Indef:кӧ K ;
!!!  K ;

!Actually there might be reason to consider +Der/MWN
LEXICON PRONQ-MUKOD
+Pron+Qnt+Abs: # ;
+Pron+Qnt+Sg+Nom: K ;
+Pron+Qnt+Sg: PRONQ-STAV-CASES ;
+Pron+Qnt+Pl+Nom:ъяс K ;
+Pron+Qnt+Pl:ъяс PRONQ-STAV-CASES ;

LEXICON PRONQ-STAV
+Pron+Qnt+Abs: # ;
+Pron+Qnt+Sg+Nom: K ;
+Pron+Qnt+Sg+ZeroAcc: K ;
+Pron+Qnt+Sg: PRONQ-STAV-CASES ;

LEXICON PRONQ-STAV-CASES
+Nom+PxSg2:ыд K ;
+Nom+PxSg3:ыс K ;
+Nom+PxPl1:ным K ;
+Nom+PxPl2:ныд K ;
+Nom+PxPl3:ныс K ;

+Instr:ӧн K ;
+Instr+PxSg1:нам K ;
+Instr+PxSg2:над K ;
+Instr+PxSg3:нас K ;
+PxPl1+Instr:нымӧн K ; ! Should there be a Der/ӧн tag?
+PxPl2+Instr:ныдӧн K ; ! Should there be a Der/ӧн tag?
+PxPl3+Instr:нысӧн K ; ! Should there be a Der/ӧн tag?

+Ela:ысь K ;
+Ela+PxSg3:сьыс K ;
+Ela+PxPl1:сьыным K ;
+PxPl1+Ela:нымысь K ;
+Ela+PxPl2:сьыныд K ;
+PxPl2+Ela:ныдысь K ;
+Ela+PxPl3:сьыныс K ;
+PxPl3+Ela:нысысь K ;

+Gen:лӧн K ;
+PxSg3+Gen:ыслӧн K ;
+PxPl1+Gen:нымлӧн K ;
+PxPl2+Gen:ныдлӧн K ;
+PxPl3+Gen:ныслӧн K ;

+Acc:ӧс K ;
+PxSg1+Acc:ӧс K ;
+PxSg2+Acc2:тӧ K ;
+PxSg3+Acc3:сӧ K ;
+PxPl1+Acc:нымӧс K ;
+PxPl2+Acc2:нытӧ K ;
+PxPl3+Acc3:нысӧ K ;

+PxSg2+Dat:ыдлы K ;
+PxSg3+Dat:ыслы K ;
+PxPl1+Dat:нымлы K ;
+PxPl2+Dat:ныдлы K ;
+PxPl3+Dat:ныслы K ;

+Abl:лысь K ;
+PxSg3+Abl:ыслысь K ;
+PxPl1+Abl:нымлысь K ;
+PxPl2+Abl:ныдлысь K ;
+PxPl3+Abl:ныслысь K ;

+Com:кӧд K ;
+PxSg3+Com:ыскӧд K ;
+PxPl1+Com:нымкӧд K ;
+PxPl2+Com:ныдкӧд K ;
+PxPl3+Com:ныскӧд K ;

+Car:тӧг K ;
+PxSg3+Car:ыстӧг K ;
+PxPl1+Car:нымтӧг K ;
+PxPl2+Car:ныдтӧг K ;
+PxPl3+Car:ныстӧг K ;

+Cns:ла K ;
+PxSg3+Cns:ысла K ;
+PxPl1+Cns:нымла K ;
+PxPl2+Cns:ныдла K ;
+PxPl3+Cns:нысла K ;

+Comp+PxSg3:сяыс K ;

+Ine:ын K ;
+PxPl1+Ine:нымын K ;
+PxPl2+Ine:ныдын K ;
+PxPl3+Ine:нысын K ;

+Ill:ӧ K ;
+PxPl1+Ill:нымӧ K ;
+PxPl2+Ill:ныдӧ K ;
+PxPl3+Ill:нысӧ K ;

+Egr:сянь K ;
+Egr+PxSg3:сяньыс K ;
+PxSg3+Egr:ыссянь K ;
+Egr+PxPl1:сяньным K ;
+PxPl1+Egr:нымсянь K ;
+Egr+PxPl2:сяньныд K ;
+PxPl2+Egr:ныдсянь K ;
+PxPl3+Egr:ныссянь K ;

+Apr:лань K ;
+Apr+PxSg3:ланьыс K ;
+PxSg3+Apr:ыслань K ;
+PxPl1+Apr:нымлань K ;
+PxPl2+Apr:ныдлань K ;
+PxPl3+Apr:ныслань K ;

+Tra:ті K ;
+Tra+PxSg3:тіыс K ;
+PxSg3+Tra:ысті K ;
+PxPl1+Tra:нымті K ;
+PxPl2+Tra:ныдті K ;
+PxPl3+Tra:нысті K ;

+Ter:ӧдз K ;
+PxSg3+Ter:ысӧдз K ;
+PxPl1+Ter:нымӧдз K ;
+PxPl2+Ter:ныдӧдз K ;
+PxPl3+Ter:нысӧдз K ;



! COMPARATIVE ; ! sg/pl?

LEXICON PRONQ-COLL
+Pron+Qnt+Coll+Sg: PRONQ-COLL-CASE ; ! sg/pl?

! быдӧн, ӧтнасӧн, ӧтнамӧн, ӧтнадӧн, ставӧн, кымынӧн, унаӧн, кыкӧн

LEXICON PRONQ-COLL-CASE
!ӧнія коми кыв 2000: 183
+Nom: K ;
+Gen:лӧн K ;
+PxSg2+Gen:ыдлӧн K ;
+PxSg3+Gen:ыслӧн K ;
+Abl:лысь K ;
+PxSg2+Abl:ыдлысь K ;
+PxSg3+Abl:ыслысь K ;
+Dat:лы K ;
+PxSg2+Dat:ыдлысь K ;
+PxSg3+Dat:ыслысь K ;
+Acc:ӧс K ;
+PxSg2+Acc2:тӧ K ;
+PxSg3+Acc3:сӧ K ;
+Instr:ӧн K ;
+Com:кӧд K ;
+PxSg2+Com:ыдкӧд K ;
+PxSg3+Com:ыскӧд K ;
+Car:тӧг K ;
+Car+PxSg2:тӧгыд K ;
+Car+PxSg3:тӧгыс K ;
+Cns:ла K ;
+PxSg2+Cns:ыдла K ;
+PxSg3+Cns:ысла K ;
+Ine:ын K ;
+Ela:ысь K ;
+Ela+PxSg2:сьыд K ;
+Ela+PxSg3:сьыс K ;
+Ill:ӧ K ;
+Apr:лань K ;
+Apr+PxSg2:ланьыд K ;
+Apr+PxSg3:ланьыс K ;
+Egr:сянь K ;
+Egr+PxSg2:сяньыд K ;
+Egr+PxSg3:сяньыс K ;
+Tra:ті K ;
+Ter:ӧдз K ;
+Ter+PxSg2:ӧдзыд K ;
+Ter+PxSg3:ӧдзыс K ;

LEXICON PRONQ-ATAG
+Pron+Qnt+A: K ;
+Pron+Qnt+A+Der/MWN: Noun1 ;

LEXICON PRONQTAG
+Pron+Qnt: COMPARATIVE ; ! sg/pl?

LEXICON PRONQTAGPL
+Pron+Qnt+Pl: # ;

LEXICON PRON-NEG-End
+Pron+Neg: K ;

LEXICON PRON-NEG-A
+Pron+Neg+A: # ;
+Pron+Neg+A+Der/MWN+N+Sg+Nom: # ;
+Pron+Neg+A+Der/MWN+N+Sg: PRON-NEG-OBL ;

LEXICON PRON-NEG-ADV
+Pron+Neg+Adv: COMPARATIVE ;

LEXICON PRON-NEG-IN_LA
+Pron+Neg+Der/ла+Spat: PRON-NEG-SPAT ;

LEXICON PRON-NEG-ITER
+Pron+Neg+Der/ысь+Iter: K ;

LEXICON PRON-NEG
+Pron+Neg+Sg+Nom: # ;
+Pron+Neg+Sg: PRON-NEG-OBL ;


LEXICON PRON-NEG-COLL
+Coll:нан PRON-NEG-COLL-PERS ;
+ZeroColl: PRON-NEG-COLL-PERS ;
!
+ZeroColl+PxPl2+Acc2:ныдтӧ K ;
+Coll+PxPl2+Acc2:наннытӧ K ;

LEXICON PRON-NEG-COLL-PERS
+PxPl1:ным PRON-NEG-COLL-CASES ;
+PxPl2:ныд PRON-NEG-COLL-CASES ;
+PxPl3:ныс PRON-NEG-COLL-CASES ;
!
+PxPl1+Acc:нымӧс K ;
+PxPl2+Acc2:нытӧ K ;
+PxPl3+Acc3:ныссӧ K ;

LEXICON PRON-NEG-COLL-CASES
+Nom: K ;
 PRON-NEG-OBL ;

LEXICON PRON-NEG-OBL
! 2011-11-15 This is used by PRON-NEG
! also тэа-мэа inclusive 1Du, сыа-мэа exclusive 1Du
+Cmpl: # ;
+Gen:лӧн # ;
+Abl:лысь K ; !
+Dat:лы # ;
+Acc:ӧс # ;
+Acc3:сӧ # ;
+Instr:ӧн K ;
+Com:кӧд K ;
+Car:тӧг K ;
+Cns:ла K ;
 PRON-NEG-SPAT ;

LEXICON PRON-NEG-SPAT
+Ine:ын K ;
+Ela:ысь K ; 
+Ill:ӧ K ; !
+Apr:лань K ;
+Egr:сянь K ;
+Tra:ті K ;
+Ter:ӧдз K ;
 
